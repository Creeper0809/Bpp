// gen_stmt.b - Statement code generation
//
// Generates x86-64 assembly for statements:
// - cg_stmt: generate code for a single statement
// - cg_block: generate code for a block of statements

import std.io;
import std.vec;
import std.util;
import std.str;
import types;
import ast;
import compiler;
import emitter.emitter;
import emitter.symtab;
import emitter.typeinfo;
import emitter.gen_expr;

// ============================================
// Block Codegen
// ============================================

var g_defer_emit_mode;

func cg_defer_stack_len() -> u64 {
    var stack: *Vec<*AstNode> = emitter_get_defer_stack();
    return stack.?len();
}

func cg_defer_mark_block_begin() -> u64 {
    var markers: *Vec<u64> = emitter_get_defer_markers();
    markers.push(cg_defer_stack_len());
    return 0;
}

func cg_defer_emit_to(marker: u64, pop_after_emit: u64) -> u64 {
    var stack: *Vec<*AstNode> = emitter_get_defer_stack();
    if (pop_after_emit != 0) {
        while (stack.len() > marker) {
            var stmt: *AstNode = stack.get(stack.len() - 1);
            stack.pop();
            var prev_mode: u64 = g_defer_emit_mode;
            g_defer_emit_mode = 1;
            cg_stmt((u64)stmt);
            g_defer_emit_mode = prev_mode;
        }
        return 0;
    }

    if (stack.len() <= marker) { return 0; }
    var idx: i64 = (i64)stack.len() - 1;
    var min_idx: i64 = (i64)marker;
    while (idx >= min_idx) {
        var stmt2: *AstNode = stack.get((u64)idx);
        var prev_mode2: u64 = g_defer_emit_mode;
        g_defer_emit_mode = 1;
        cg_stmt((u64)stmt2);
        g_defer_emit_mode = prev_mode2;
        idx = idx - 1;
    }
    return 0;
}

func cg_defer_mark_block_end() -> u64 {
    var markers: *Vec<u64> = emitter_get_defer_markers();
    if (markers.len() == 0) { return 0; }
    var marker: u64 = markers.get(markers.len() - 1);
    markers.pop();
    cg_defer_emit_to(marker, 1);
    return 0;
}

func cg_defer_push_loop_marker() -> u64 {
    var loop_markers: *Vec<u64> = emitter_get_defer_loop_markers();
    loop_markers.push(cg_defer_stack_len());
    return 0;
}

func cg_defer_pop_loop_marker() -> u64 {
    var loop_markers: *Vec<u64> = emitter_get_defer_loop_markers();
    if (loop_markers.len() > 0) { loop_markers.pop(); }
    return 0;
}

func cg_defer_emit_to_loop_marker() -> u64 {
    var loop_markers: *Vec<u64> = emitter_get_defer_loop_markers();
    if (loop_markers.len() == 0) { return 0; }
    var marker: u64 = loop_markers.get(loop_markers.len() - 1);
    cg_defer_emit_to(marker, 0);
    return 0;
}

func cg_block(node: u64) -> u64 {
    var block: *AstBlock = (*AstBlock)node;
    var stmts: *Vec<u64> = block.stmts_vec;
    var len: u64 = stmts.len();
    cg_defer_mark_block_begin();
    for(var i: u64 = 0; i < len;i++){
        cg_stmt(stmts.get(i));
   }
    cg_defer_mark_block_end();
}

// ============================================
// Statement Codegen
// ============================================

func cg_stmt(node: u64) -> u64 {
    var kind: u64 = ast_kind(node);
    var symtab: *Symtab = emitter_get_symtab();
    var g_structs_vec: *Vec<*AstStructDef> = typeinfo_get_structs();

    switch (kind) {
        case AST_RETURN:
            cg_return_stmt(node, symtab);
            return;
        case AST_VAR_DECL:
            cg_var_decl_stmt(node, symtab, g_structs_vec);
            return;
        case AST_ASSIGN:
            cg_assign_stmt(node, symtab);
            return;
        case AST_EXPR_STMT:
            var expr_stmt: *AstExprStmt = (*AstExprStmt)node;
            var expr: u64 = expr_stmt.expr;
            if (expr != 0) {
                var expr_kind: u64 = ast_kind(expr);
                if (expr_kind == AST_CALL) {
                    var call_node: *AstCall = (*AstCall)expr;
                    var fn: *AstFunc = typeinfo_find_func(call_node.name_ptr, call_node.name_len);
                    if (fn != 0) {
                        if (fn.ret_type == TYPE_STRUCT && fn.ret_ptr_depth == 0) {
                            var sret_size: u64 = sizeof_type(TYPE_STRUCT, 0, fn.ret_struct_name_ptr, fn.ret_struct_name_len);
                            if (sret_size > 16) {
                                var tmp_offset: u64 = cg_alloc_tmp_struct(symtab, sret_size);
                                cg_call_sret(call_node, tmp_offset, symtab);
                                return;
                            }
                        }
                    }
                } else if (expr_kind == AST_METHOD_CALL) {
                    var mc: *AstMethodCall = (*AstMethodCall)expr;
                    var mc_sret_size: u64 = cg_method_call_ret_struct_size(mc, symtab);
                    if (mc_sret_size > 16) {
                        var tmp_offset2: u64 = cg_alloc_tmp_struct(symtab, mc_sret_size);
                        cg_method_call_sret_to_offset(mc, tmp_offset2, symtab);
                        return;
                    }
                }
            }
            cg_expr(expr);
            return;
        case AST_IF:
            cg_if_stmt(node);
            return;
        case AST_WHILE:
            cg_while_stmt(node);
            return;
        case AST_FOR:
            cg_for_stmt(node);
            return;
        case AST_SWITCH:
            cg_switch_stmt(node);
            return;
        case AST_BREAK:
            var g_loop_labels: *Vec<u64> = emitter_get_loop_labels();
            var len: u64 = g_loop_labels.len();
            if (len == 0) {
                emit("    ; ERROR: break outside loop\n", 29);
                panic("Codegen error");
            }
            cg_defer_emit_to_loop_marker();
            var label: u64 = g_loop_labels.get(len - 1);
            emit("    jmp ", 8);
            emit_label(label);
            emit_nl();
            return;
        case AST_CONTINUE:
            var g_loop_continue_labels: *Vec<u64> = emitter_get_continue_labels();
            var len2: u64 = g_loop_continue_labels.len();
            if (len2 == 0) {
                emit("    ; ERROR: continue outside loop\n", 32);
                panic("Codegen error");
            }
            cg_defer_emit_to_loop_marker();
            var label2: u64 = g_loop_continue_labels.get(len2 - 1);
            emit("    jmp ", 8);
            emit_label(label2);
            emit_nl();
            return;
        case AST_ASM:
            cg_asm_stmt(node);
            return;
        case AST_ALIAS:
            var al: *AstAlias = (*AstAlias)node;
            compiler_reg_alias_set(al.reg_ptr, al.reg_len, al.name_ptr, al.name_len);
            return;
        case AST_DEFER:
            var df: *AstDefer = (*AstDefer)node;
            if (g_defer_emit_mode != 0) {
                cg_stmt((u64)df.stmt);
            } else {
                var defer_stack: *Vec<*AstNode> = emitter_get_defer_stack();
                defer_stack.push(df.stmt);
            }
            return;
        case AST_DELETE:
            cg_delete_stmt(node, symtab);
            return;
        case AST_BLOCK:
            cg_block(node);
            return;
    }

}

func emit_call_resolved(name_ptr: u64, name_len: u64) -> u64 {
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        resolved_ptr = resolved.ptr;
        resolved_len = resolved.len;
    }
    emit("    call ", 9);
    emit(resolved_ptr, resolved_len);
    emit_nl();
    return 0;
}

func cg_call_sret(call: *AstCall, dest_offset: u64, symtab: *Symtab) -> u64 {
    var args: *Vec<*AstNode> = call.args_vec;
    var nargs: u64 = 0;
    if (args != 0) { nargs = args.len(); }
    var total_arg_words: u64 = 0;
    if (nargs > 0) {
        var i: u64 = nargs;
        while (i > 0) {
            i = i - 1;
            var arg: *AstNode = args.get(i);
            total_arg_words = total_arg_words + _cg_sysv_push_call_arg(arg, symtab);
        }
    }

    emit("    lea rdi, [rbp", 18);
    if (dest_offset < 0) { emit_i64(dest_offset); }
    else { emit("+", 1); emit_u64(dest_offset); }
    emit("]\n", 2);

    var stack_words: u64 = _cg_sysv_pop_arg_regs_sret(total_arg_words);
    emit_call_resolved(call.name_ptr, call.name_len);
    if (stack_words > 0) {
        emit("    add rsp, ", 13);
        emit_u64(stack_words * 8);
        emit_nl();
    }
    return 0;
}

func cg_ensure_heap_brk_global() -> u64 {
    var globals: *Vec<*GlobalInfo> = emitter_get_globals();
    if (globals == 0) { return 0; }
    if (is_global_var("__cg_heap_brk", 13) != 0) { return 0; }
    var ginfo: *GlobalInfo = new GlobalInfo();
    ginfo.name_ptr = "__cg_heap_brk";
    ginfo.name_len = 13;
    ginfo.typeinfo_ptr = typeinfo_make(TYPE_U64, 0);
        ginfo.init_kind = GLOBAL_INIT_NONE;
        ginfo.init_value = 0;
        ginfo.init_name_ptr = 0;
        ginfo.init_name_len = 0;
        ginfo.init_expr = 0;
        globals.push(ginfo);
    return 0;
}

// ============================================
// Return Statement
// ============================================

func cg_return_stmt(node: u64, symtab: *Symtab) -> u64 {
    var ret: *AstReturn = (*AstReturn)node;
    var expr: u64 = ret.expr;

    cg_defer_emit_to(0, 0);
    
    if (expr != 0) {
        var ret_type: u64 = emitter_get_ret_type();
        var ret_ptr_depth: u64 = emitter_get_ret_ptr_depth();
        var ret_struct_name_ptr: u64 = emitter_get_ret_struct_name_ptr();
        var ret_struct_name_len: u64 = emitter_get_ret_struct_name_len();
        
        // Check if return type is struct (value, not pointer)
        if (ret_type == TYPE_STRUCT && ret_ptr_depth == 0) {
            var expr_kind: u64 = ast_kind(expr);
            
            // Find struct size
            var struct_size: u64 = sizeof_type(ret_type, 0, ret_struct_name_ptr, ret_struct_name_len);
            
            // Large structs: return via hidden sret pointer in rdi
            if (struct_size > 16) {
                var sret_ptr_name: u64 = (u64)"__sret_ptr";
                var sret_ptr_len: u64 = 10;
                var sret_offset: u64 = symtab_find(symtab, sret_ptr_name, sret_ptr_len);
                if (sret_offset == 0) {
                    emit_stderr("[ERROR] sret pointer not saved for large struct return\n", 67);
                    panic("Codegen error");
                }
                emit("    mov rdi, [rbp", 20);
                if (sret_offset < 0) { emit_i64(sret_offset); }
                else { emit("+", 1); emit_u64(sret_offset); }
                emit("]\n", 2);
                if (expr_kind == AST_STRUCT_LITERAL) {
                    var lit2: *AstStructLiteral = (*AstStructLiteral)expr;
                    var values2: *Vec<u64> = lit2.values_vec;
                    var vcount2: u64 = 0;
                    if (values2 != 0) { vcount2 = values2.len(); }
                    var off: u64 = 0;
                    for (var i2: u64 = 0; i2 < vcount2; i2++) {
                        emit("    push rdi\n", 13);
                        cg_expr(values2.get(i2));
                        emit("    pop rdi\n", 12);
                        emit("    mov [rdi", 12);
                        if (off != 0) { emit("+", 1); emit_u64(off); }
                        emit("], rax\n", 7);
                        off = off + 8;
                    }
                    if (off < struct_size) {
                        emit("    xor eax, eax\n", 17);
                        for (var fill_off: u64 = off; fill_off < struct_size; fill_off = fill_off + 8) {
                            emit("    mov [rdi", 12);
                            if (fill_off != 0) { emit("+", 1); emit_u64(fill_off); }
                            emit("], rax\n", 7);
                        }
                    }
                } else if (expr_kind == AST_CALL) {
                    var call_expr: *AstCall = (*AstCall)expr;
                    var args_call: *Vec<*AstNode> = call_expr.args_vec;
                    var nargs_call: u64 = 0;
                    if (args_call != 0) { nargs_call = args_call.len(); }

                    emit("    push rdi\n", 13);
                    var total_arg_words_call: u64 = 0;
                    if (nargs_call > 0) {
                        var ci: u64 = nargs_call;
                        while (ci > 0) {
                            ci = ci - 1;
                            var carg: *AstNode = args_call.get(ci);
                            total_arg_words_call = total_arg_words_call + _cg_sysv_push_call_arg(carg, symtab);
                        }
                    }
                    var stack_words_call: u64 = _cg_sysv_pop_arg_regs_sret(total_arg_words_call);
                    emit("    pop rdi\n", 12);
                    emit_call_resolved(call_expr.name_ptr, call_expr.name_len);
                    if (stack_words_call > 0) {
                        emit("    add rsp, ", 13);
                        emit_u64(stack_words_call * 8);
                        emit_nl();
                    }
                } else if (expr_kind == AST_STACK_CTOR) {
                    emit("    mov r12, rdi\n", 19);
                    cg_stack_ctor_call_at_r12((*AstStackCtor)expr, symtab);
                } else {
                    emit("    push rdi\n", 13);
                    cg_lvalue(expr);
                    emit("    mov rbx, rax\n", 19);
                    emit("    pop rdi\n", 12);
                    for (var off2: u64 = 0; off2 < struct_size; off2 = off2 + 8) {
                        emit("    mov rax, [rbx", 18);
                        if (off2 != 0) { emit("+", 1); emit_u64(off2); }
                        emit("]\n", 2);
                        emit("    mov [rdi", 12);
                        if (off2 != 0) { emit("+", 1); emit_u64(off2); }
                        emit("], rax\n", 7);
                    }
                }
                emit("    mov rsp, rbp\n", 17);
                emit("    pop rbp\n", 12);
                emit("    ret\n", 8);
                return 0;
            }
            
            // If expression is a function call, it already returns struct in rax/rdx
            if (expr_kind == AST_CALL) {
                cg_expr(expr);
                // rax and rdx already contain the struct value
            } else if (expr_kind == AST_STACK_CTOR) {
                var tmp_offset_ret: u64 = cg_alloc_tmp_struct(symtab, struct_size);
                cg_stack_ctor_init_to_offset((*AstStackCtor)expr, tmp_offset_ret, symtab);
                emit("    mov rax, [rbp", 19);
                if (tmp_offset_ret < 0) { emit_i64(tmp_offset_ret); }
                else { emit("+", 1); emit_u64(tmp_offset_ret); }
                emit("]\n", 2);
                if (struct_size > 8) {
                    emit("    mov rdx, [rbp", 19);
                    var tmp_ret_hi: u64 = tmp_offset_ret + 8;
                    if (tmp_ret_hi < 0) { emit_i64(tmp_ret_hi); }
                    else { emit("+", 1); emit_u64(tmp_ret_hi); }
                    emit("]\n", 2);
                }
            } else if (expr_kind == AST_STRUCT_LITERAL) {
                // For struct literals, evaluate fields and place in rax/rdx
                var lit: *AstStructLiteral = (*AstStructLiteral)expr;
                var values: *Vec<u64> = lit.values_vec;
                if (values == 0) {
                    emit("    xor eax, eax\n", 17);
                    emit("    xor edx, edx\n", 17);
                } else {
                    var vcount: u64 = values.len();
                    // First field -> rax
                    if (vcount > 0) {
                        cg_expr(values.get(0));
                        // rax now has first field
                    } else {
                        emit("    xor eax, eax\n", 17);
                    }
                    // Second field -> rdx (if struct > 8 bytes)
                    if (struct_size > 8) {
                        if (vcount > 1) {
                            emit("    push rax\n", 13);
                            cg_expr(values.get(1));
                            emit("    mov rdx, rax\n", 17);
                            emit("    pop rax\n", 12);
                        } else {
                            emit("    xor edx, edx\n", 17);
                        }
                    }
                }
            } else {
                // For other expressions (AST_IDENT, etc), get address and load
                cg_lvalue(expr);

                // rax now contains address of struct
                // Load struct content into rax (and rdx if needed)
                emit("    mov r10, rax\n", 17);   // Save struct address in r10
                emit("    mov rax, [r10]\n", 19); // Load first 8 bytes into rax
                if (struct_size > 8) {
                    emit("    mov rdx, [r10+8]\n", 21); // Load next 8 bytes into rdx
                }
            }
        } else if (ret_type == TYPE_SLICE && ret_ptr_depth == 0) {
            var expr_kind2: u64 = ast_kind(expr);
            if (expr_kind2 == AST_SLICE) {
                var slice_node: *AstSlice = (*AstSlice)expr;
                var handled: u64 = 0;
                var ptr_kind: u64 = ast_kind(slice_node.ptr_expr);
                if (ptr_kind == AST_IDENT) {
                    var idn: *AstIdent = (*AstIdent)slice_node.ptr_expr;
                    var ti2: *TypeInfo = symtab_get_type(symtab, idn.name_ptr, idn.name_len);
                    if (ti2 != 0) {
                        if (ti2.type_kind == TYPE_ARRAY && ti2.ptr_depth == 0) {
                            cg_ensure_heap_brk_global();
                            var elem_size: u64 = sizeof_type(ti2.elem_type_kind, ti2.elem_ptr_depth, ti2.struct_name_ptr, ti2.struct_name_len);
                            cg_expr(slice_node.len_expr);
                            emit("    mov r12, rax\n", 19);
                            emit("    mov r8, rax\n", 18);
                            emit("    mov rax, ", 13);
                            emit_u64(elem_size);
                            emit_nl();
                            emit("    imul r8, rax\n", 20);
                            var init_label: u64 = new_label();
                            var ok_label: u64 = new_label();
                            var fail_label: u64 = new_label();
                            emit("    mov rax, [_gvar___cg_heap_brk]\n", 35);
                            emit("    cmp rax, 0\n", 16);
                            emit("    jne ", 8);
                            emit_label(init_label);
                            emit_nl();
                            emit("    mov rax, 12\n", 18);
                            emit("    xor rdi, rdi\n", 19);
                            emit("    syscall\n", 13);
                            emit("    mov [_gvar___cg_heap_brk], rax\n", 39);
                            emit_label_def(init_label);
                            emit("    mov rbx, [_gvar___cg_heap_brk]\n", 35);
                            emit("    mov rdi, rbx\n", 19);
                            emit("    add rdi, r8\n", 18);
                            emit("    mov rax, 12\n", 18);
                            emit("    syscall\n", 13);
                            emit("    cmp rax, rdi\n", 18);
                            emit("    jb ", 7);
                            emit_label(fail_label);
                            emit_nl();
                            emit("    mov [_gvar___cg_heap_brk], rax\n", 39);
                            emit("    jmp ", 8);
                            emit_label(ok_label);
                            emit_nl();
                            emit_label_def(fail_label);
                            emit("    xor rbx, rbx\n", 19);
                            emit_label_def(ok_label);
                            emit("    mov rdx, r12\n", 19);
                            cg_expr(slice_node.ptr_expr);
                            emit("    mov rsi, rax\n", 19);
                            emit("    mov rdi, rbx\n", 19);
                            emit("    xor r9, r9\n", 17);
                            var loop_label: u64 = new_label();
                            var end_label: u64 = new_label();
                            emit_label_def(loop_label);
                            emit("    cmp r9, r8\n", 19);
                            emit("    jge ", 8);
                            emit_label(end_label);
                            emit_nl();
                            emit("    mov r10b, [rsi+r9]\n", 30);
                            emit("    mov [rdi+r9], r10b\n", 30);
                            emit("    add r9, 1\n", 16);
                            emit("    jmp ", 8);
                            emit_label(loop_label);
                            emit_nl();
                            emit_label_def(end_label);
                            emit("    mov rax, rbx\n", 19);
                            handled = 1;
                        }
                    }
                }
                if (handled == 0) {
                    cg_expr(slice_node.ptr_expr);
                    emit("    mov rbx, rax\n", 19);
                    cg_expr(slice_node.len_expr);
                    emit("    mov rdx, rax\n", 19);
                    emit("    mov rax, rbx\n", 19);
                }
            } else if (expr_kind2 == AST_CALL) {
                cg_expr(expr);
                // rax/rdx already set by callee
            } else {
                cg_lvalue(expr);
                emit("    mov rbx, [rax]\n", 19);
                emit("    mov rdx, [rax+8]\n", 21);
                emit("    mov rax, rbx\n", 19);
            }
        } else {
            // Normal return (non-struct or pointer to struct)
            cg_expr(expr);
        }
    } else {
        emit("    xor eax, eax\n", 17);
    }
    emit("    mov rsp, rbp\n", 17);
    emit("    pop rbp\n", 12);
    emit("    ret\n", 8);
}

// ============================================
// Variable Declaration Statement
// ============================================

func cg_var_decl_stmt(node: u64, symtab: *Symtab, g_structs_vec: *Vec<*AstStructDef>) -> u64 {
    var decl: *AstVarDecl = (*AstVarDecl)node;
    var name_ptr: u64 = decl.name_ptr;
    var name_len: u64 = decl.name_len;
    var type_kind: u64 = decl.type_kind;
    var ptr_depth: u64 = decl.ptr_depth;
    var is_tagged: u64 = decl.is_tagged;
    var init: u64 = decl.init_expr;
    var struct_name_ptr: u64 = decl.struct_name_ptr;
    var struct_name_len: u64 = decl.struct_name_len;
    var tag_layout_ptr: u64 = decl.tag_layout_ptr;
    var tag_layout_len: u64 = decl.tag_layout_len;
    var elem_type_kind: u64 = decl.elem_type_kind;
    var elem_ptr_depth: u64 = decl.elem_ptr_depth;
    var array_len: u64 = decl.array_len;
    
    // Calculate size based on type
    var size: u64 = 0;
    if (type_kind == TYPE_ARRAY) {
        var elem_size: u64 = sizeof_type(elem_type_kind, elem_ptr_depth, struct_name_ptr, struct_name_len);
        size = elem_size * array_len;
    } else if (type_kind == TYPE_SLICE) {
        size = 16;
    } else {
        size = sizeof_type(type_kind, ptr_depth, struct_name_ptr, struct_name_len);
        if (type_kind == TYPE_STRUCT && ptr_depth == 0) {
            var align_rem: u64 = size % 8;
            if (align_rem != 0) {
                size = size + (8 - align_rem);
            }
        }
    }
    
    var offset: u64 = symtab_add(symtab, name_ptr, name_len, type_kind, ptr_depth, size);
    var ti0: *TypeInfo = symtab_get_type(symtab, name_ptr, name_len);
    ti0.is_tagged = is_tagged;
    if (is_tagged == 1 && struct_name_ptr != 0 && type_kind != TYPE_STRUCT && type_kind != TYPE_ARRAY && type_kind != TYPE_SLICE) {
        ti0.struct_name_ptr = struct_name_ptr;
        ti0.struct_name_len = struct_name_len;
    }
    if (is_tagged == 1 && tag_layout_ptr != 0) {
        ti0.tag_layout_ptr = tag_layout_ptr;
        ti0.tag_layout_len = tag_layout_len;
    }
    
    // If base type is struct, find struct_def and store pointer in type_info
    if (type_kind == TYPE_STRUCT) {
        var struct_def: *AstStructDef = 0;
        if (g_structs_vec != 0) {
            var num_structs: u64 = g_structs_vec.len();
            for (var i: u64 = 0; i < num_structs; i++) {
                var sd: *AstStructDef = g_structs_vec.get(i);
                var sname_ptr: u64 = sd.name_ptr;
                var sname_len: u64 = sd.name_len;
                if (str_eq(sname_ptr, sname_len, struct_name_ptr, struct_name_len)) {
                    struct_def = sd;
                    break;
                }
            }
        }
        var ti: *TypeInfo = symtab_get_type(symtab, name_ptr, name_len);
        ti.struct_def = struct_def;
        ti.struct_name_ptr = struct_name_ptr;
        ti.struct_name_len = struct_name_len;
        ti.elem_type_kind = 0;
        ti.elem_ptr_depth = 0;
        ti.array_len = 0;
    }

    if (type_kind == TYPE_TRAIT) {
        var ti_trait: *TypeInfo = symtab_get_type(symtab, name_ptr, name_len);
        ti_trait.struct_name_ptr = struct_name_ptr;
        ti_trait.struct_name_len = struct_name_len;
        ti_trait.struct_def = 0;
        ti_trait.elem_type_kind = 0;
        ti_trait.elem_ptr_depth = 0;
        ti_trait.array_len = 0;
    }

    // If array/slice, store element type info in symtab
    if (type_kind == TYPE_ARRAY || type_kind == TYPE_SLICE) {
        var ti2: *TypeInfo = symtab_get_type(symtab, name_ptr, name_len);
        ti2.elem_type_kind = elem_type_kind;
        ti2.elem_ptr_depth = elem_ptr_depth;
        ti2.array_len = array_len;
        ti2.struct_name_ptr = struct_name_ptr;
        ti2.struct_name_len = struct_name_len;
        if (elem_type_kind == TYPE_STRUCT && g_structs_vec != 0 && struct_name_ptr != 0) {
            var num_structs2: u64 = g_structs_vec.len();
            var struct_def2: *AstStructDef = 0;
            for (var si2: u64 = 0; si2 < num_structs2; si2++) {
                var sd2: *AstStructDef = g_structs_vec.get(si2);
                if (str_eq(sd2.name_ptr, sd2.name_len, struct_name_ptr, struct_name_len)) {
                    struct_def2 = sd2;
                    break;
                }
            }
            ti2.struct_def = struct_def2;
        }
    }
    
    if (init != 0) {
        if (type_kind == TYPE_ARRAY) {
            emit_stderr("[ERROR] Array initialization is not supported\n", 46);
            panic("Codegen error");
        }
        var init_kind: u64 = ast_kind(init);

        if (init_kind == AST_STACK_CTOR) {
            cg_stack_ctor_init_to_offset((*AstStackCtor)init, offset, symtab);
            return;
        }

        if (type_kind == TYPE_STRUCT && ptr_depth == 0) {
            var struct_size0: u64 = sizeof_type(type_kind, ptr_depth, struct_name_ptr, struct_name_len);
            if (struct_size0 > 16) {
                if (init_kind == AST_CALL) {
                    cg_call_sret((*AstCall)init, offset, symtab);
                    return;
                }
                if (init_kind == AST_METHOD_CALL) {
                    cg_method_call_sret_to_offset((*AstMethodCall)init, offset, symtab);
                    return;
                }
            }
        }
        
        // Handle struct literal initialization specially
        if (init_kind == AST_STRUCT_LITERAL) {
            cg_struct_literal_init(init, offset);
            return;
        }
        
        if (type_kind != 0) {
            var it_info: *TypeInfo = get_expr_type_with_symtab((*AstNode)init, symtab);
            if (it_info != 0) {
                var it_base: u64 = it_info.type_kind;
                var it_depth: u64 = it_info.ptr_depth;
                var it_tagged: u64 = it_info.is_tagged;
                check_type_compat(it_base, it_depth, it_tagged, it_info.tag_layout_ptr, it_info.tag_layout_len, type_kind, ptr_depth, decl.is_tagged, decl.tag_layout_ptr, decl.tag_layout_len);
            }
        }
        
        // Slice initialization: slice(ptr, len) or copy from another slice
        if (type_kind == TYPE_SLICE) {
            if (init_kind == AST_SLICE) {
                var slice_node: *AstSlice = (*AstSlice)init;
                cg_expr(slice_node.ptr_expr);
                emit("    mov [rbp", 12);
                if (offset < 0) { emit_i64(offset); }
                else { emit("+", 1); emit_u64(offset); }
                emit("], rax\n", 7);
                cg_expr(slice_node.len_expr);
                emit("    mov [rbp", 12);
                var off2: u64 = offset + 8;
                if (off2 < 0) { emit_i64(off2); }
                else { emit("+", 1); emit_u64(off2); }
                emit("], rax\n", 7);
                return;
            }
            if (init_kind == AST_CALL) {
                cg_expr(init);
                emit("    mov [rbp", 12);
                if (offset < 0) { emit_i64(offset); }
                else { emit("+", 1); emit_u64(offset); }
                emit("], rax\n", 7);
                emit("    mov [rbp", 12);
                var offc: u64 = offset + 8;
                if (offc < 0) { emit_i64(offc); }
                else { emit("+", 1); emit_u64(offc); }
                emit("], rdx\n", 7);
                return;
            }
            // Copy from another slice value
            cg_lvalue(init);
            emit("    mov rbx, [rax]\n", 19);
            emit("    mov rcx, [rax+8]\n", 21);
            emit("    mov [rbp", 12);
            if (offset < 0) { emit_i64(offset); }
            else { emit("+", 1); emit_u64(offset); }
            emit("], rbx\n", 7);
            emit("    mov [rbp", 12);
            var off3: u64 = offset + 8;
            if (off3 < 0) { emit_i64(off3); }
            else { emit("+", 1); emit_u64(off3); }
            emit("], rcx\n", 7);
            return;
        }

        if (type_kind == TYPE_STRUCT && ptr_depth == 0 && init_kind != AST_CALL && init_kind != AST_METHOD_CALL && init_kind != AST_CALL_PTR && init_kind != AST_STRUCT_LITERAL) {
            var struct_size_copy: u64 = sizeof_type(type_kind, ptr_depth, struct_name_ptr, struct_name_len);
            cg_lvalue(init);
            emit("    mov rbx, rax\n", 19);
            emit("    lea r8, [rbp", 17);
            if (offset < 0) { emit_i64(offset); }
            else { emit("+", 1); emit_u64(offset); }
            emit("]\n", 2);

            var off_copy: u64 = 0;
            while (off_copy + 8 <= struct_size_copy) {
                emit("    mov rcx, [rbx", 18);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("]\n", 2);
                emit("    mov [r8", 11);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("], rcx\n", 7);
                off_copy = off_copy + 8;
            }
            var rem_copy: u64 = struct_size_copy - off_copy;
            if (rem_copy >= 4) {
                emit("    mov ecx, [rbx", 18);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("]\n", 2);
                emit("    mov [r8", 11);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("], ecx\n", 7);
                off_copy = off_copy + 4;
                rem_copy = rem_copy - 4;
            }
            if (rem_copy >= 2) {
                emit("    mov cx, [rbx", 16);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("]\n", 2);
                emit("    mov [r8", 11);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("], cx\n", 6);
                off_copy = off_copy + 2;
                rem_copy = rem_copy - 2;
            }
            if (rem_copy >= 1) {
                emit("    mov cl, [rbx", 16);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("]\n", 2);
                emit("    mov [r8", 11);
                if (off_copy != 0) { emit("+", 1); emit_u64(off_copy); }
                emit("], cl\n", 6);
            }
            return;
        }

        cg_expr(init);
        if (is_tagged == 1 && ptr_depth > 0) {
            var it2: *TypeInfo = get_expr_type_with_symtab((*AstNode)init, symtab);
            if (it2 != 0) {
                if (!(it2.ptr_depth > 0 && it2.is_tagged == 1)) {
                    emit_tagged_mask();
                }
            } else {
                emit_tagged_mask();
            }
        }
        
        // Store small integer types with correct width (u8/u16/u32)
        if (ptr_depth == 0) {
            if (type_kind == TYPE_U8) {
                emit("    mov [rbp", 12);
                if (offset < 0) { emit_i64(offset); }
                else { emit("+", 1); emit_u64(offset); }
                emit("], al\n", 6);
                return;
            }
            if (type_kind == TYPE_U16) {
                emit("    mov [rbp", 12);
                if (offset < 0) { emit_i64(offset); }
                else { emit("+", 1); emit_u64(offset); }
                emit("], ax\n", 6);
                return;
            }
            if (type_kind == TYPE_U32) {
                emit("    mov [rbp", 12);
                if (offset < 0) { emit_i64(offset); }
                else { emit("+", 1); emit_u64(offset); }
                emit("], eax\n", 7);
                return;
            }
        }
        
        // Check if initializing struct by value (e.g., var p: Point = Point_new(...))
        if (type_kind == TYPE_STRUCT && ptr_depth == 0) {
            // Struct returned in rax/rdx registers
            emit("    mov [rbp", 12);
            if (offset < 0) { emit_i64(offset); }
            else { emit("+", 1); emit_u64(offset); }
            emit("], rax\n", 7);
            
            // Check if struct is larger than 8 bytes
            var struct_size: u64 = sizeof_type(type_kind, ptr_depth, struct_name_ptr, struct_name_len);
            if (struct_size > 8) {
                emit("    mov [rbp", 12);
                var offset2: u64 = offset + 8;
                if (offset2 < 0) { emit_i64(offset2); }
                else { emit("+", 1); emit_u64(offset2); }
                emit("], rdx\n", 7);
            }
        } else {
            // Normal case: single value in rax
            emit("    mov [rbp", 12);
            if (offset < 0) { emit_i64(offset); }
            else { emit("+", 1); emit_u64(offset); }
            emit("], rax\n", 7);
        }
    }

    if (type_kind == TYPE_STRUCT && ptr_depth == 0) {
        emit("    lea r12, [rbp", 18);
        if (offset < 0) { emit_i64(offset); }
        else { emit("+", 1); emit_u64(offset); }
        emit("]\n", 2);
        cg_emit_trait_vptr_init_at_r12(struct_name_ptr, struct_name_len);
    }
}

func cg_struct_literal_init(init: u64, offset: u64) -> u64 {
    var lit: *AstStructLiteral = (*AstStructLiteral)init;
    var values: *Vec<u64> = lit.values_vec;
    var num_values: u64 = values.len();
    var fields: *Vec<*FieldDesc> = lit.struct_def.fields_vec;
    var num_fields: u64 = fields.len();

    var parent_total: u64 = 0;
    if (lit.struct_def.parents_vec != 0) {
        var parents: *Vec<*ParentDesc> = lit.struct_def.parents_vec;
        var pn: u64 = parents.len();
        for (var pi: u64 = 0; pi < pn; pi++) {
            var parent_desc: *ParentDesc = parents.get(pi);
            var parent_def: *AstStructDef = parent_desc.struct_def;
            if (parent_def == 0) {
                parent_def = get_struct_def(parent_desc.name_ptr, parent_desc.name_len);
                if (parent_def == 0) {
                    emit("[ERROR] Parent struct not found\n", 34);
                    panic("Codegen error");
                }
                parent_desc.struct_def = parent_def;
            }
            var parent_size: u64 = sizeof_type(TYPE_STRUCT, 0, parent_desc.name_ptr, parent_desc.name_len);
            parent_total = parent_total + parent_size;
        }
    }

    var impls: *Vec<*TraitImpl> = compiler_get_trait_impls();
    if (impls != 0) {
        var ni: u64 = impls.len();
        for (var ti: u64 = 0; ti < ni; ti++) {
            var impl_info: *TraitImpl = impls.get(ti);
            if (!str_eq(impl_info.struct_ptr, impl_info.struct_len, lit.struct_def.name_ptr, lit.struct_def.name_len)) { continue; }
            var vptr_name: *NameInfo = compiler_build_vptr_field_name(impl_info.trait_ptr, impl_info.trait_len);
            var field_offset_v: u64 = 0;
            var field_desc_v: *FieldDesc = 0;
            var found_v: u64 = struct_find_field_desc_scoped(lit.struct_def, 0, 0, vptr_name.ptr, vptr_name.len, &field_offset_v, &field_desc_v);
            if (found_v == 0) {
                emit_stderr("[ERROR] vptr field not found for struct literal init\n", 58);
                panic("Codegen error");
            }
            emit("    lea rax, [rel _gvar_", 28);
            emit(impl_info.vtable_global_ptr, impl_info.vtable_global_len);
            emit("]\n", 2);
            emit("    mov [rbp", 12);
            var total_off_v: u64 = offset + field_offset_v;
            if (total_off_v < 0) { emit_i64(total_off_v); }
            else { emit("+", 1); emit_u64(total_off_v); }
            emit("], rax\n", 7);
        }
    }
    
    // Initialize each field
    var field_offset: u64 = parent_total;
    var value_index: u64 = 0;
    for (var fi: u64 = 0; fi < num_fields && value_index < num_values; fi++) {
        var field: *FieldDesc = fields.get(fi);
        var field_size: u64 = sizeof_field_desc(field);
        if (compiler_is_vptr_field_name(field.name_ptr, field.name_len) != 0) {
            field_offset = field_offset + field_size;
            continue;
        }

        cg_expr(values.get(value_index));

        emit("    mov [rbp", 12);
        var total_offset: u64 = offset + field_offset;
        if (total_offset < 0) { emit_i64(total_offset); }
        else { emit("+", 1); emit_u64(total_offset); }
        emit("], rax\n", 7);

        field_offset = field_offset + field_size;
        value_index = value_index + 1;
    }
}

// ============================================
// Assignment Statement
// ============================================

func cg_assign_stmt(node: u64, symtab: *Symtab) -> u64 {
    var assign: *AstAssign = (*AstAssign)node;
    var target: u64 = assign.target;
    var value: u64 = assign.value;
    
    var target_kind: u64 = ast_kind(target);
    var value_kind: u64 = ast_kind(value);
    if (target_kind == AST_SAFE_MEMBER_ACCESS) {
        emit_stderr("[ERROR] Safe member access cannot be used in assignment\n", 64);
        panic("Codegen error");
    }
    if (value_kind == AST_STACK_CTOR) {
        var target_ti: *TypeInfo = get_expr_type_with_symtab((*AstNode)target, symtab);
        if (target_ti == 0 || target_ti.type_kind != TYPE_STRUCT || target_ti.ptr_depth != 0) {
            emit_stderr("[ERROR] stack constructor assignment requires struct lvalue\n", 64);
            panic("Codegen error");
        }
        cg_lvalue(target);
        emit("    mov r12, rax\n", 19);
        cg_stack_ctor_call_at_r12((*AstStackCtor)value, symtab);
        return;
    }
    if (target_kind == AST_MEMBER_ACCESS) {
        var m: *AstMemberAccess = (*AstMemberAccess)target;
        var obj: u64 = m.object;
        var member_ptr: u64 = m.member_ptr;
        var member_len: u64 = m.member_len;
        var ot: *TypeInfo = get_expr_type_with_symtab((*AstNode)obj, symtab);
        if (ot != 0) {
            if (ot.ptr_depth > 0 && ot.is_tagged == 1 && ot.tag_layout_ptr != 0) {
                var layout_info: *AstStructDef = get_struct_def(ot.tag_layout_ptr, ot.tag_layout_len);
                if (layout_info == 0) {
                    emit("[ERROR] Tagged layout struct not found\n", 41);
                    panic("Codegen error");
                }
                var packed_flag: u64 = layout_info.is_packed;
                if (packed_flag == 0) {
                    emit("[ERROR] Tagged layout must be packed struct\n", 49);
                    panic("Codegen error");
                }
                if (ast_kind(obj) != AST_IDENT) {
                    emit("[ERROR] Tagged layout assignment requires identifier base\n", 62);
                    panic("Codegen error");
                }

                var total_bits: u64 = get_packed_layout_total_bits(layout_info);
                var field_offset: u64 = get_packed_field_bit_offset(layout_info, member_ptr, member_len);
                var field_width: u64 = get_packed_field_bit_width(layout_info, member_ptr, member_len);
                var start_bit: u64 = 64 - total_bits;
                var shift_bits: u64 = start_bit + field_offset;

                cg_expr(value);
                emit("    mov r9, rax\n", 16);
                cg_expr(obj);
                emit("    mov rbx, rax\n", 17);

                if (field_width < 64) {
                    emit_mask_to_rdx(field_width);
                    emit("    and r9, rdx\n", 16);
                }
                if (shift_bits > 0) {
                    emit("    mov rcx, ", 13);
                    emit_u64(shift_bits);
                    emit_nl();
                    emit("    shl r9, cl\n", 15);
                }

                if (field_width < 64) {
                    emit_mask_to_rdx(field_width);
                    if (shift_bits > 0) {
                        emit("    mov r10, rdx\n", 17);
                        emit("    mov rcx, ", 13);
                        emit_u64(shift_bits);
                        emit_nl();
                        emit("    shl r10, cl\n", 16);
                    } else {
                        emit("    mov r10, rdx\n", 17);
                    }
                    emit("    not r10\n", 12);
                    emit("    and rbx, r10\n", 17);
                } else {
                    emit("    xor rbx, rbx\n", 17);
                }

                emit("    or rbx, r9\n", 15);
                cg_lvalue(obj);
                emit("    mov [rax], rbx\n", 19);
                return;
            }
            if (ot.ptr_depth == 0 && ot.type_kind == TYPE_STRUCT && ot.struct_def != 0) {
                var struct_info: *AstStructDef = ot.struct_def;
                var packed_flag2: u64 = struct_info.is_packed;
                if (packed_flag2 == 1) {
                    var total_bits2: u64 = get_packed_layout_total_bits(struct_info);
                    var field_offset2: u64 = get_packed_field_bit_offset(struct_info, member_ptr, member_len);
                    var field_width2: u64 = get_packed_field_bit_width(struct_info, member_ptr, member_len);
                    if (total_bits2 > 64 || (field_offset2 + field_width2) > 64) {
                        var byte_offset2: u64 = field_offset2 / 8;
                        var bit_shift2: u64 = field_offset2 % 8;
                        var byte_width2: u64 = (field_width2 + 7) / 8;

                        if (bit_shift2 != 0) {
                            emit("[ERROR] Packed field write requires byte alignment\n", 56);
                            panic("Codegen error");
                        }

                        cg_expr(value);
                        emit("    mov r9, rax\n", 16);
                        cg_lvalue(obj);
                        if (byte_offset2 > 0) {
                            emit("    add rax, ", 13);
                            emit_u64(byte_offset2);
                            emit("\n", 1);
                        }

                        if (byte_width2 == 1) {
                            emit("    mov [rax], r9b\n", 22);
                        } else if (byte_width2 == 2) {
                            emit("    mov [rax], r9w\n", 22);
                        } else if (byte_width2 == 4) {
                            emit("    mov [rax], r9d\n", 22);
                        } else {
                            emit("    mov [rax], r9\n", 21);
                        }
                        return;
                    }
                    var shift_bits2: u64 = field_offset2;
                    var size_bytes2: u64 = (total_bits2 + 7) / 8;

                    cg_expr(value);
                    emit("    mov r9, rax\n", 16);
                    cg_lvalue(obj);
                    if (size_bytes2 == 1) {
                        emit("    movzx rbx, byte [rax]\n", 30);
                    } else if (size_bytes2 == 2) {
                        emit("    movzx rbx, word [rax]\n", 30);
                    } else if (size_bytes2 == 4) {
                        emit("    mov ebx, [rax]\n", 19);
                    } else {
                        emit("    mov rbx, [rax]\n", 19);
                    }

                    if (field_width2 < 64) {
                        emit_mask_to_rdx(field_width2);
                        emit("    and r9, rdx\n", 16);
                    }
                    if (shift_bits2 > 0) {
                        emit("    mov rcx, ", 13);
                        emit_u64(shift_bits2);
                        emit_nl();
                        emit("    shl r9, cl\n", 15);
                    }

                    if (field_width2 < 64) {
                        emit_mask_to_rdx(field_width2);
                        if (shift_bits2 > 0) {
                            emit("    mov r10, rdx\n", 17);
                            emit("    mov rcx, ", 13);
                            emit_u64(shift_bits2);
                            emit_nl();
                            emit("    shl r10, cl\n", 16);
                        } else {
                            emit("    mov r10, rdx\n", 17);
                        }
                        emit("    not r10\n", 12);
                        emit("    and rbx, r10\n", 17);
                    } else {
                        emit("    xor rbx, rbx\n", 17);
                    }

                    emit("    or rbx, r9\n", 15);
                    cg_lvalue(obj);
                    if (size_bytes2 == 1) {
                        emit("    mov [rax], bl\n", 18);
                    } else if (size_bytes2 == 2) {
                        emit("    mov [rax], bx\n", 18);
                    } else if (size_bytes2 == 4) {
                        emit("    mov [rax], ebx\n", 19);
                    } else {
                        emit("    mov [rax], rbx\n", 19);
                    }
                    return;
                }
            }
        }
    }
    if (target_kind == AST_IDENT) {
        var ident: *AstIdent = (*AstIdent)target;
        var name_ptr: u64 = ident.name_ptr;
        var name_len: u64 = ident.name_len;
        
        var target_type: *TypeInfo = symtab_get_type(symtab, name_ptr, name_len);
        var vt_info: *TypeInfo = get_expr_type_with_symtab((*AstNode)value, symtab);
        
        if (target_type != 0 && vt_info != 0) {
            var vt_depth: u64 = vt_info.ptr_depth;
            if (vt_depth > 0) {
                var vt_base: u64 = vt_info.type_kind;
                symtab_update_type(symtab, name_ptr, name_len, vt_base, vt_depth);
            }
        }
    }

    var target_type_info: *TypeInfo = 0;
    if (target_kind == AST_IDENT) {
        var ident3: *AstIdent = (*AstIdent)target;
        target_type_info = symtab_get_type(symtab, ident3.name_ptr, ident3.name_len);
    }
    var value_type_info: *TypeInfo = get_expr_type_with_symtab((*AstNode)value, symtab);

    // Slice assignment: copy ptr+len
    if (target_kind == AST_IDENT || target_kind == AST_MEMBER_ACCESS) {
        var tt: *TypeInfo = get_expr_type_with_symtab((*AstNode)target, symtab);
        if (tt != 0) {
            if (tt.type_kind == TYPE_SLICE && tt.ptr_depth == 0) {
                if (value_kind == AST_SLICE) {
                    var slice_node: *AstSlice = (*AstSlice)value;
                    cg_expr(slice_node.ptr_expr);
                    emit("    mov rbx, rax\n", 17);
                    cg_expr(slice_node.len_expr);
                    emit("    mov rcx, rax\n", 17);
                    cg_lvalue(target);
                    emit("    mov [rax], rbx\n", 19);
                    emit("    mov [rax+8], rcx\n", 21);
                    return;
                }
                if (value_kind == AST_CALL) {
                    cg_expr(value);
                    emit("    mov rbx, rax\n", 17);
                    emit("    mov rcx, rdx\n", 17);
                    cg_lvalue(target);
                    emit("    mov [rax], rbx\n", 19);
                    emit("    mov [rax+8], rcx\n", 21);
                    return;
                }
                cg_lvalue(value);
                emit("    mov rbx, [rax]\n", 19);
                emit("    mov rcx, [rax+8]\n", 21);
                cg_lvalue(target);
                emit("    mov [rax], rbx\n", 19);
                emit("    mov [rax+8], rcx\n", 21);
                return;
            }
        }
    }
    
    cg_expr(value);
    if (target_type_info != 0) {
        var tt2: *TypeInfo = target_type_info;
        if (tt2.ptr_depth > 0 && tt2.is_tagged == 1) {
            if (value_type_info != 0) {
                var vt2: *TypeInfo = (*TypeInfo)value_type_info;
                if (!(vt2.ptr_depth > 0 && vt2.is_tagged == 1)) {
                    emit_tagged_mask();
                }
            } else {
                emit_tagged_mask();
            }
        }
    }
    emit("    push rax\n", 13);
    cg_lvalue(target);
    emit("    pop rbx\n", 12);
    
    if (target_kind == AST_DEREF) {
        var deref: *AstDeref = (*AstDeref)target;
        var deref_operand: u64 = deref.operand;
        var ti: *TypeInfo = get_expr_type_with_symtab((*AstNode)deref_operand, symtab);
        var base_type: u64 = ti.type_kind;
        var ptr_depth: u64 = ti.ptr_depth;
        
        if (ptr_depth == 1) {
            if (base_type == TYPE_U8) {
                emit("    mov [rax], bl\n", 18);
                return;
            }
            if (base_type == TYPE_U16) {
                emit("    mov [rax], bx\n", 18);
                return;
            }
            if (base_type == TYPE_U32) {
                emit("    mov [rax], ebx\n", 19);
                return;
            }
        }
    }
    
    if (target_kind == AST_DEREF8) {
        emit("    mov [rax], bl\n", 18);
        return;
    }

    if (target_kind == AST_INDEX) {
        var it: *TypeInfo = get_expr_type_with_symtab((*AstNode)target, symtab);
        if (it != 0) {
            if (it.ptr_depth == 0) {
                if (it.type_kind == TYPE_U8) {
                    emit("    mov [rax], bl\n", 18);
                    return;
                }
                if (it.type_kind == TYPE_U16) {
                    emit("    mov [rax], bx\n", 18);
                    return;
                }
                if (it.type_kind == TYPE_U32) {
                    emit("    mov [rax], ebx\n", 19);
                    return;
                }
            }
        }
        emit("    mov [rax], rbx\n", 19);
        return;
    }
    
    if (target_kind == AST_IDENT) {
        var ident: *AstIdent = (*AstIdent)target;
        var name_ptr: u64 = ident.name_ptr;
        var name_len: u64 = ident.name_len;
        var tt: *TypeInfo = get_expr_type_with_symtab((*AstNode)target, symtab);
        if (tt != 0) {
            if (tt.ptr_depth == 0) {
                if (tt.type_kind == TYPE_U8) {
                    emit("    mov [rax], bl\n", 18);
                    return;
                }
                if (tt.type_kind == TYPE_U16) {
                    emit("    mov [rax], bx\n", 18);
                    return;
                }
                if (tt.type_kind == TYPE_U32) {
                    emit("    mov [rax], ebx\n", 19);
                    return;
                }
            }
        }
    }
    
    // Check if this is a struct-to-struct copy
    var tt_info: *TypeInfo = get_expr_type_with_symtab((*AstNode)target, symtab);
    if (tt_info != 0) {
        var tt_base: u64 = tt_info.type_kind;
        var tt_depth: u64 = tt_info.ptr_depth;
        
        // If it's a direct struct (not pointer), do multi-qword copy
        if (tt_base == TYPE_STRUCT && tt_depth == 0) {
            var struct_def: *AstStructDef = tt_info.struct_def;
            if (struct_def != 0) {
                // Get struct name to calculate size
                var sd: *AstStructDef = struct_def;
                var struct_name_ptr: u64 = sd.name_ptr;
                var struct_name_len: u64 = sd.name_len;
                var struct_size: u64 = sizeof_type(TYPE_STRUCT, 0, struct_name_ptr, struct_name_len);
                
                // rax = dest address, rbx = value (but we need lvalue!)
                emit("    mov r8, rax  ; save dest addr\n", 34);
                
                // Get source address by evaluating value as lvalue
                cg_lvalue(value);
                
                // Now: rax = source address, r8 = dest address
                // Copy struct_size bytes (8 bytes at a time)
                var off: u64 = 0;
                while (off + 8 <= struct_size) {
                    emit("    mov rcx, [rax", 17);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("]\n", 2);
                    emit("    mov [r8", 11);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("], rcx\n", 7);
                    off = off + 8;
                }
                var rem: u64 = struct_size - off;
                if (rem >= 4) {
                    emit("    mov ecx, [rax", 17);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("]\n", 2);
                    emit("    mov [r8", 11);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("], ecx\n", 7);
                    off = off + 4;
                    rem = rem - 4;
                }
                if (rem >= 2) {
                    emit("    mov cx, [rax", 16);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("]\n", 2);
                    emit("    mov [r8", 11);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("], cx\n", 6);
                    off = off + 2;
                    rem = rem - 2;
                }
                if (rem >= 1) {
                    emit("    mov cl, [rax", 16);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("]\n", 2);
                    emit("    mov [r8", 11);
                    if (off > 0) {
                        emit("+", 1);
                        emit_u64(off);
                    }
                    emit("], cl\n", 6);
                }
                return;
            }
        }
    }
    
    emit("    mov [rax], rbx\n", 19);
}

// ============================================
// Delete Statement
// ============================================

func cg_delete_stmt(node: u64, symtab: *Symtab) -> u64 {
    var dl: *AstDelete = (*AstDelete)node;
    var expr: u64 = dl.expr;
    if (expr == 0) { return 0; }

    cg_expr(expr);
    emit("    test rax, rax\n", 20);
    var label_end: u64 = new_label();
    emit("    je ", 7);
    emit_label(label_end);
    emit_nl();

    emit("    mov rbx, rax\n", 19);

    var ti: *TypeInfo = get_expr_type_with_symtab((*AstNode)expr, symtab);
    if (ti != 0 && ti.type_kind == TYPE_STRUCT && ti.ptr_depth > 0) {
        var method_ptr: u64 = (u64)"destructor";
        var method_len: u64 = 10;
        var name_info: *NameInfo = compiler_build_method_name(ti.struct_name_ptr, ti.struct_name_len, method_ptr, method_len);
        var resolved_ptr: u64 = name_info.ptr;
        var resolved_len: u64 = name_info.len;
        var resolved: *NameInfo = resolve_name(name_info.ptr, name_info.len);
        if (resolved != 0) {
            resolved_ptr = resolved.ptr;
            resolved_len = resolved.len;
        }

        if (compiler_func_exists(name_info.ptr, name_info.len) != 0) {
            emit("    push rbx\n", 14);
            var stack_words: u64 = _cg_sysv_pop_arg_regs(1);
            emit("    call ", 9);
            emit(resolved_ptr, resolved_len);
            emit_nl();
            if (stack_words > 0) {
                emit("    add rsp, ", 13);
                emit_u64(stack_words * 8);
                emit_nl();
            }
        }
    }

    var free_ptr: u64 = (u64)"free";
    var free_len: u64 = 4;
    var free_res: *NameInfo = resolve_name(free_ptr, free_len);
    if (free_res != 0) {
        free_ptr = free_res.ptr;
        free_len = free_res.len;
    }

    emit("    mov rdi, rbx\n", 19);
    emit("    call ", 9);
    emit(free_ptr, free_len);
    emit_nl();

    emit_label_def(label_end);
    return 0;
}

// ============================================
// Control Flow Statements
// ============================================

func cg_if_stmt(node: u64) -> u64 {
    var if_stmt: *AstIf = (*AstIf)node;
    var cond: u64 = if_stmt.cond;
    var then_blk: u64 = if_stmt.then_block;
    var else_blk: u64 = if_stmt.else_block;
    
    var else_label: u64 = new_label();
    var end_label: u64 = new_label();
    
    cg_expr(cond);
    emit("    test rax, rax\n", 18);
    emit("    jz ", 7);
    emit_label(else_label);
    emit_nl();
    
    cg_block(then_blk);
    
    if (else_blk != 0) {
        emit("    jmp ", 8);
        emit_label(end_label);
        emit_nl();
    }
    
    emit_label_def(else_label);
    
    if (else_blk != 0) {
        cg_block(else_blk);
        emit_label_def(end_label);
    }
}

func cg_while_stmt(node: u64) -> u64 {
    var while_stmt: *AstWhile = (*AstWhile)node;
    var cond: u64 = while_stmt.cond;
    var body: u64 = while_stmt.body;
    
    var start_label: u64 = new_label();
    var end_label: u64 = new_label();
    
    emit_label_def(start_label);
    
    cg_expr(cond);
    emit("    test rax, rax\n", 18);
    emit("    jz ", 7);
    emit_label(end_label);
    emit_nl();
    
    var g_loop_labels: *Vec<u64> = emitter_get_loop_labels();
    var g_loop_continue_labels: *Vec<u64> = emitter_get_continue_labels();
    g_loop_labels.push(end_label);
    g_loop_continue_labels.push(start_label);
    cg_defer_push_loop_marker();
    
    cg_block(body);
    
    cg_defer_pop_loop_marker();
    g_loop_labels.pop();
    g_loop_continue_labels.pop();
    
    emit("    jmp ", 8);
    emit_label(start_label);
    emit_nl();
    
    emit_label_def(end_label);
}

func cg_for_stmt(node: u64) -> u64 {
    var for_stmt: *AstFor = (*AstFor)node;
    var init: u64 = for_stmt.init;
    var cond: u64 = for_stmt.cond;
    var update: u64 = for_stmt.update;
    var body: u64 = for_stmt.body;
    
    if (init != 0) { cg_stmt(init); }
    
    var start_label: u64 = new_label();
    var update_label: u64 = new_label();
    var end_label: u64 = new_label();
    
    emit_label_def(start_label);
    
    if (cond != 0) {
        cg_expr(cond);
        emit("    test rax, rax\n", 18);
        emit("    jz ", 7);
        emit_label(end_label);
        emit_nl();
    }
    
    var g_loop_labels: *Vec<u64> = emitter_get_loop_labels();
    var g_loop_continue_labels: *Vec<u64> = emitter_get_continue_labels();
    g_loop_labels.push(end_label);
    g_loop_continue_labels.push(update_label);
    cg_defer_push_loop_marker();
    
    cg_block(body);
    
    cg_defer_pop_loop_marker();
    g_loop_labels.pop();
    g_loop_continue_labels.pop();
    
    emit_label_def(update_label);
    
    if (update != 0) { cg_stmt(update); }
    
    emit("    jmp ", 8);
    emit_label(start_label);
    emit_nl();
    
    emit_label_def(end_label);
}

func cg_switch_stmt(node: u64) -> u64 {
    var switch_stmt: *AstSwitch = (*AstSwitch)node;
    var expr: u64 = switch_stmt.expr;
    var cases: *Vec<u64> = switch_stmt.cases_vec;
    
    // Evaluate switch expression once
    cg_expr(expr);
    
    var end_label: u64 = new_label();
    var default_label: u64 = 0;
    var has_default: u64 = 0;
    
    // Push end_label to g_loop_labels so that break works
    var g_loop_labels: *Vec<u64> = emitter_get_loop_labels();
    g_loop_labels.push(end_label);
    cg_defer_push_loop_marker();
    
    var num_cases: u64 = cases.len();
    
    // Analyze cases to decide optimization strategy
    var use_jump_table: u64 = 0;
    var min_val: i64 = 0;
    var max_val: i64 = 0;
    var case_count: u64 = 0;
    
    // Count non-default cases and find min/max values
    for (var i: u64 = 0; i < num_cases; i++) {
        var case_node: u64 = cases.get(i);
        var case_stmt: *AstCase = (*AstCase)case_node;
        
        if (case_stmt.is_default == 0) {
            // Try to evaluate case value as constant
            var value_node: u64 = case_stmt.value;
            var value_kind: u64 = ast_kind(value_node);
            
            if (value_kind == AST_LITERAL) {
                var lit: *AstLiteral = (*AstLiteral)value_node;
                var val: i64 = (i64)(lit.value);
                
                if (case_count == 0) {
                    min_val = val;
                    max_val = val;
                } else {
                    if (val < min_val) { min_val = val; }
                    if (val > max_val) { max_val = val; }
                }
                case_count = case_count + 1;
            }
        } else {
            has_default = 1;
        }
        
    }
    
    // Use jump table if: 3+ cases, range <= 256, density > 40%
    var range: i64 = max_val - min_val + 1;
    if (case_count >= 3 && range > 0 && range <= 256) {
        var range_u64: u64 = (u64)range;
        var density: u64 = (case_count * 100) / range_u64;
        if (density >= 40) {
            use_jump_table = 1;
        }
    }
    
    if (use_jump_table == 1) {
        cg_switch_jump_table(cases, min_val, max_val, end_label, has_default);
    } else {
        cg_switch_linear(cases, end_label, has_default);
    }
    
    cg_defer_pop_loop_marker();
    // Pop end_label from g_loop_labels
    g_loop_labels.pop();
    
    emit_label_def(end_label);
}

// Linear comparison switch (original behavior, improved)
func cg_switch_linear(cases: *Vec<u64>, end_label: u64, has_default: u64) -> u64 {
    emitln("    push rax    ; switch value");
    
    var num_cases: u64 = cases.len();
    var default_label: u64 = 0;
    var symtab: *Symtab = emitter_get_symtab();
    
    // First pass: generate comparisons and find default
    var case_labels: *Vec<u64> = new Vec<u64>(num_cases);
    for (var i: u64 = 0; i < num_cases; i++) {
        var case_node: u64 = cases.get(i);
        var case_stmt: *AstCase = (*AstCase)case_node;
        var is_default: u64 = case_stmt.is_default;
        
        var case_label: u64 = new_label();
        case_labels.push(case_label);
        
        if (is_default == 1) {
            default_label = case_label;
        } else {
            // Generate comparison for this case
            var value: u64 = case_stmt.value;
            var value_kind: u64 = ast_kind(value);
            
            // Check if we're comparing strings (AST_STRING or pointer type)
            var is_string_compare: u64 = 0;
            if (value_kind == AST_STRING) {
                is_string_compare = 1;
            }
            
            if (is_string_compare == 1) {
                // String comparison using str_eq(s1, len1, s2, len2)
                // Use System V ABI registers with a small temp spill area.
                
                // Compute s2 and len2
                cg_expr(value);
                emitln("    sub rsp, 16");
                emitln("    mov [rsp], rax    ; s2");
                emitln("    mov rdi, rax");
                emit_call_resolved("str_len", 7);
                emitln("    mov [rsp+8], rax    ; len2");
                
                // Compute len1 from switch value at [rsp+16]
                emitln("    mov rdi, [rsp+16]    ; s1");
                emit_call_resolved("str_len", 7);
                
                // Call str_eq(s1, len1, s2, len2)
                emitln("    mov rsi, rax    ; len1");
                emitln("    mov rdi, [rsp+16]    ; s1");
                emitln("    mov rdx, [rsp]    ; s2");
                emitln("    mov rcx, [rsp+8]    ; len2");
                emit_call_resolved("str_eq", 6);
                emitln("    add rsp, 16");
                emitln("    test rax, rax");
                emit("    jnz ", 8);
                emit_label(case_label);
                emit_nl();
            } else {
                // Integer comparison
                emitln("    mov rax, [rsp]    ; reload switch value");
                emitln("    push rax");
                cg_expr(value);
                emit("    mov rbx, rax\n", 17);
                emit("    pop rax\n", 12);
                emit("    cmp rax, rbx\n", 17);
                emit("    je ", 7);
                emit_label(case_label);
                emit_nl();
            }
        }
        
    }
    
    // If no match, jump to default or end
    if (has_default == 1) {
        emit("    jmp ", 8);
        emit_label(default_label);
        emit_nl();
    } else {
        emit("    jmp ", 8);
        emit_label(end_label);
        emit_nl();
    }
    
    // Second pass: generate case bodies
    for (var i: u64 = 0; i < num_cases; i++) {
        var case_node: u64 = cases.get(i);
        var case_stmt: *AstCase = (*AstCase)case_node;
        var body: u64 = case_stmt.body;
        var case_label: u64 = case_labels.get(i);
        
        emit_label_def(case_label);
        cg_block(body);
        
    }
    
    emit("    add rsp, 8    ; pop switch value\n", 37);
}

// Jump table switch (optimized for dense integer ranges)
func cg_switch_jump_table(cases: *Vec<u64>, min_val: i64, max_val: i64, end_label: u64, has_default: u64) -> u64 {
    var num_cases: u64 = cases.len();
    var range: i64 = max_val - min_val + 1;
    var range_u64: u64 = (u64)range;
    var table_label: u64 = new_label();
    var default_label: u64 = end_label;
    
    // Create case label mapping
    var case_labels: *Vec<u64> = new Vec<u64>(num_cases);
    var value_to_label: *Vec<u64> = new Vec<u64>(range_u64);
    
    // Initialize all table entries to default
    for (var j: u64 = 0; j < range_u64; j++) {
        value_to_label.push(end_label);
    }
    
    // First pass: create labels and build value.label mapping
    for (var i: u64 = 0; i < num_cases; i++) {
        var case_node: u64 = cases.get(i);
        var case_stmt: *AstCase = (*AstCase)case_node;
        var case_label: u64 = new_label();
        case_labels.push(case_label);
        
        if (case_stmt.is_default == 1) {
            default_label = case_label;
            // Update all unassigned entries to point to default
            for (var j: u64 = 0; j < range_u64; j++) {
                if (value_to_label.get(j) == end_label) {
                    value_to_label.set(j, default_label);
                }
            }
        } else {
            var value_node: u64 = case_stmt.value;
            var value_kind: u64 = ast_kind(value_node);
            if (value_kind == AST_LITERAL) {
                var lit: *AstLiteral = (*AstLiteral)value_node;
                var val: i64 = (i64)(lit.value);
                var idx: u64 = (u64)(val - min_val);
                value_to_label.set(idx, case_label);
            }
        }
        
    }
    
    // Generate bounds check and jump table lookup
    emit("    ; Jump table switch (range: ", 30);
    emit_i64(min_val);
    emit(" to ", 4);
    emit_i64(max_val);
    emit(")\n", 2);
    
    // Check if value is in range [min_val, max_val]
    if (min_val != 0) {
        emit("    sub rax, ", 13);
        emit_i64(min_val);
        emit("    ; normalize to 0-based\n", 27);
    }
    emit("    cmp rax, ", 13);
    emit_u64(range_u64);
    emit_nl();
    emit("    jae ", 8);
    emit_label(default_label);
    emit("    ; out of range\n", 19);
    
    // Jump via table: jmp [table + rax*8]
    emit("    lea rbx, [rel ", 18);
    emit(" ", 1);
    emit_label(table_label);
    emit("]\n", 2);
    emit("    jmp [rbx + rax*8]\n", 21);
    
    // Emit jump table
    emit_nl();
    emit_label_def(table_label);
    for (var j: u64 = 0; j < range_u64; j++) {
        emit("    dq ", 7);
        emit_label(value_to_label.get(j));
        emit_nl();
    }
    emit_nl();
    
    // Second pass: generate case bodies
    for (var i: u64 = 0; i < num_cases; i++) {
        var case_node: u64 = cases.get(i);
        var case_stmt: *AstCase = (*AstCase)case_node;
        var body: u64 = case_stmt.body;
        var case_label: u64 = case_labels.get(i);
        
        emit_label_def(case_label);
        cg_block(body);
        
    }
}

func cg_asm_stmt(node: u64) -> u64 {
    var asm_stmt: *AstAsm = (*AstAsm)node;
    var text_vec: *Vec<u64> = asm_stmt.text_vec;
    emit_asm_text_with_alias(text_vec);
}
