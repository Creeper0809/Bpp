// io.b - I/O helpers and memory allocation
// Low-level I/O + heap helpers
import std.str;
import std.os;

var heap_inited;
var heap_brk;
var heap_reserved_end;
var g_out_fd;
var g_print_u64_buf;

func sys_brk(addr) { return os_sys_brk(addr); }
func sys_write(fd, buf, count) { return os_sys_write(fd, buf, count); }
func sys_read(fd, buf, count) { return os_sys_read(fd, buf, count); }
func sys_open(path, flags, mode) { return os_sys_open(path, flags, mode); }
func sys_close(fd) { return os_sys_close(fd); }
func sys_fstat(fd, statbuf) { return os_sys_fstat(fd, statbuf); }
func sys_exit(code) { return os_sys_exit(code); }

func io_set_output_fd(fd: u64) -> u64 {
    g_out_fd = fd;
    return 0;
}

func io_get_output_fd() -> u64 {
    if (g_out_fd == 0) { return 1; }
    return g_out_fd;
}

func heap_alloc(size) {
    if (size == 0) {
        return 0;
    }

    // Align allocation size to 8 bytes to keep u64 stores within mapped pages.
    var align_mask: u64 = 0;
    align_mask = align_mask - 8; // ~7 via wrap-around
    var aligned_size: u64 = size + 7;
    aligned_size = aligned_size & align_mask;
    
    if (heap_inited == 0) {
        var cur_brk: u64 = os_sys_brk(0);
        heap_brk = cur_brk;
        heap_reserved_end = cur_brk;
        heap_inited = 1;
    }

    var aligned_brk: u64 = heap_brk + 7;
    aligned_brk = aligned_brk & align_mask;

    var p: u64 = aligned_brk;
    var new_brk: u64 = p + aligned_size;
    if (new_brk > heap_reserved_end) {
        var grow: u64 = aligned_size;
        if (grow < 1048576) { grow = 1048576; } // Reserve 1 MiB chunks.
        var target_end: u64 = new_brk + grow;
        var res: u64 = os_sys_brk(target_end);
        if (res < target_end) {
            return 0;
        }
        heap_reserved_end = target_end;
    }
    heap_brk = new_brk;
    return p;
}

func emitln(s: u64) {
    var len: u64 = str_len(s);
    var fd: u64 = io_get_output_fd();
    os_sys_write(fd, s, len);
    os_sys_write(fd, "\n", 1);
}

func emit(s: u64) {
    if (s == 0) { return; }
    emit_len(s, str_len(s));
}

func emit_len(s: u64, len: u64) {
    if (s == 0) { return; }
    var fd2: u64 = io_get_output_fd();
    os_sys_write(fd2, s, len);
}

func print(s, len) {
    emit_len(s, len);
}

func print_nl() {
    var fd4: u64 = io_get_output_fd();
    os_sys_write(fd4, "\n", 1);
}

func println(s, len) {
    var fd5: u64 = io_get_output_fd();
    os_sys_write(fd5, s, len);
    os_sys_write(fd5, "\n", 1);
}

func io_u64_to_ascii(buf: u64, n: u64) -> u64 {
    var buf_u8: *u8 = (*u8)buf;
    if (n == 0) {
        buf_u8[0] = 48;
        return 1;
    }

    var i: u64 = 0;
    var value: u64 = n;
    for (; value > 0; ) {
        var digit: u64 = value % 10;
        buf_u8[i] = digit + 48;
        value = value / 10;
        i = i + 1;
    }
    var half: u64 = i / 2;
    for (var k: u64 = 0; k < half; k++) {
        var rk: u64 = i - 1 - k;
        var tmp: u8 = buf_u8[k];
        buf_u8[k] = buf_u8[rk];
        buf_u8[rk] = tmp;
    }
    return i;
}

func print_u64(n: u64) {
    if (g_print_u64_buf == 0) {
        g_print_u64_buf = heap_alloc(32 * sizeof(u8));
    }
    var buf = g_print_u64_buf;
    var i: u64 = io_u64_to_ascii(buf, n);
    var fd7: u64 = io_get_output_fd();
    os_sys_write(fd7, buf, i);
}

func print_i64(n: i64) {
    if (n < 0) {
        var fd8: u64 = io_get_output_fd();
        os_sys_write(fd8, "-", 1);
        print_u64((u64)(0 - n));
    } else {
        print_u64((u64)n);
    }
}
