// str.b - String utilities for v3.8
import std.io;
func str_eq(s1, len1, s2, len2) {
    if (len1 != len2) { return false; }
    if (s1 == s2) { return true; }

    var words: u64 = len1 / 8;
    var s1_u64: *u64 = (*u64)s1;
    var s2_u64: *u64 = (*u64)s2;
    for (var i: u64 = 0; i < words; i++) {
        if (s1_u64[i] != s2_u64[i]) { return false; }
    }

    var base: u64 = words * 8;
    var s1_u8: *u8 = (*u8)s1;
    var s2_u8: *u8 = (*u8)s2;
    for (var j: u64 = base; j < len1; j++) {
         if (s1_u8[j] != s2_u8[j]) { return false; }
    }
    return true;
}

func str_copy(dst, src, len) {
    var dst_u8: *u8 = (*u8)dst;
    var src_u8: *u8 = (*u8)src;
    for(var i = 0; i < len; i++){
        dst_u8[i] = src_u8[i];
    }
}

func str_len(s) {
    var s_u8: *u8 = (*u8)s;
    var i = 0;
    for (; s_u8[i] != 0; i++) {
    }
    return i;
}

func str_concat(s1, len1, s2, len2) {
    var result = heap_alloc((len1 + len2 + 1) * sizeof(u8));
    var result_u8: *u8 = (*u8)result;
    str_copy(result, s1, len1);
    str_copy(result + len1, s2, len2);
    result_u8[len1 + len2] = 0;
    return result;
}

func str_concat3(s1, len1, s2, len2, s3, len3) {
    var result = heap_alloc((len1 + len2 + len3 + 1) * sizeof(u8));
    var result_u8: *u8 = (*u8)result;
    str_copy(result, s1, len1);
    str_copy(result + len1, s2, len2);
    str_copy(result + len1 + len2, s3, len3);
    result_u8[len1 + len2 + len3] = 0;
    return result;
}
