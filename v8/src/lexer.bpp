// lexer.b - Lexer implementation for v3.8

import std.io;
import std.str;
import std.util;
import types;
import std.vec;
import std.char;

// ============================================
// ASCII Character Constants
// ============================================
const CHAR_NEWLINE = 10;
const CHAR_SPACE = 32;
const CHAR_EXCLAIM = 33;
const CHAR_QUOTE = 34;
const CHAR_PERCENT = 37;
const CHAR_AMPERSAND = 38;
const CHAR_LPAREN = 40;
const CHAR_RPAREN = 41;
const CHAR_STAR = 42;
const CHAR_PLUS = 43;
const CHAR_COMMA = 44;
const CHAR_MINUS = 45;
const CHAR_DOT = 46;
const CHAR_SLASH = 47;
const CHAR_COLON = 58;
const CHAR_SEMICOLON = 59;
const CHAR_LT = 60;
const CHAR_EQ = 61;
const CHAR_GT = 62;
const CHAR_QUESTION = 63;
const CHAR_LBRACKET = 91;
const CHAR_BACKSLASH = 92;
const CHAR_RBRACKET = 93;
const CHAR_CARET = 94;
const CHAR_LBRACE = 123;
const CHAR_PIPE = 124;
const CHAR_RBRACE = 125;
const CHAR_TILDE = 126;

var g_tok_pool_ptr: u64 = 0;
var g_tok_pool_len: u64 = 0;
var g_tok_pool_cap: u64 = 0;

func tok_pool_begin(estimated_tokens: u64) -> u64 {
    var cap: u64 = estimated_tokens;
    if (cap < 256) { cap = 256; }
    g_tok_pool_ptr = heap_alloc(cap * sizeof(Token));
    g_tok_pool_cap = cap;
    g_tok_pool_len = 0;
    return 0;
}

func tok_pool_alloc() -> *Token {
    if (g_tok_pool_ptr == 0 || g_tok_pool_len >= g_tok_pool_cap) {
        var cap2: u64 = g_tok_pool_cap * 2;
        if (cap2 < 256) { cap2 = 256; }
        g_tok_pool_ptr = heap_alloc(cap2 * sizeof(Token));
        g_tok_pool_cap = cap2;
        g_tok_pool_len = 0;
    }

    var idx: u64 = g_tok_pool_len;
    g_tok_pool_len = g_tok_pool_len + 1;
    return (*Token)(g_tok_pool_ptr + idx * sizeof(Token));
}

// Lexer structure: [src_ptr, src_len, pos, line, col]

func lex_new(src: u64, len: u64) -> *Lexer {
    var l: *Lexer = (*Lexer)heap_alloc(sizeof(Lexer));
    l.src_ptr = src;
    l.src_len = len;
    l.pos = 0;
    l.line = 1;
    l.col = 1;
    return l;
}

func lex_at_end(lex: *Lexer) -> u64 {
    if (lex.pos >= lex.src_len) { return true; }
    return false;
}

func lex_peek(lex: *Lexer) -> u64 {
    if (lex_at_end(lex)) { return 0; }
    var src_u8: *u8 = (*u8)lex.src_ptr;
    return src_u8[lex.pos];
}

func lex_peek_next(lex: *Lexer) -> u64 {
    if (lex.pos + 1 >= lex.src_len) { return 0; }
    var src_u8: *u8 = (*u8)lex.src_ptr;
    return src_u8[lex.pos + 1];
}

func lex_advance(lex: *Lexer) -> u64 {
    var c: u64 = lex_peek(lex);
    lex.pos = lex.pos + 1;
    if (c == CHAR_NEWLINE) {
        lex.line = lex.line + 1;
        lex.col = 1;
    } else {
        lex.col = lex.col + 1;
    }
    return c;
}

func lex_skip_ws(lex: *Lexer) -> u64 {
    while (!lex_at_end(lex)) {
        var c: u64 = lex_peek(lex);
        if (!is_whitespace(c)) { break; }
        lex_advance(lex);
    }
}

func lex_skip_comment(lex: *Lexer) -> u64 {
    if (lex_peek(lex) == CHAR_SLASH) {
        if (lex_peek_next(lex) == CHAR_SLASH) {
            lex_advance(lex);
            lex_advance(lex);
            while (!lex_at_end(lex)) {
                var c: u64 = lex_peek(lex);
                if (c == CHAR_NEWLINE) {
                    lex_advance(lex);
                    break;
                }
                lex_advance(lex);
            }
        }
    }
}

func lex_skip_ws_and_comments(lex: *Lexer) -> u64 {
    while (!lex_at_end(lex)) {
        lex_skip_ws(lex);
        var c: u64 = lex_peek(lex);
        if (c == CHAR_SLASH) {
            if (lex_peek_next(lex) == CHAR_SLASH) {
                lex_skip_comment(lex);
            } else {
                break;
            }
        } else {
            break;
        }
    }
}

// ============================================
// Keyword Lookup Table
// ============================================

// Keyword table entry: [keyword_ptr, keyword_len, token_kind]
// Using a simple linear search table for keyword recognition

func lex_check_keyword(ptr: u64, len: u64) -> u64 {
    var s: *u8 = (*u8)ptr;
    if (len == 2) {
        var c0: u64 = s[0];
        var c1: u64 = s[1];
        if (c0 == 105 && c1 == 102) { return TOKEN_IF; } // if
        if (c0 == 97 && c1 == 115) { return TOKEN_AS; }  // as
        if (c0 == 117 && c1 == 56) { return TOKEN_U8; }  // u8
        return TOKEN_IDENTIFIER;
    }

    if (len == 3) {
        var c0_3: u64 = s[0];
        var c1_3: u64 = s[1];
        var c2_3: u64 = s[2];
        if (c0_3 == 110 && c1_3 == 101 && c2_3 == 119) { return TOKEN_NEW; } // new
        if (c0_3 == 118 && c1_3 == 97 && c2_3 == 114) { return TOKEN_VAR; }   // var
        if (c0_3 == 102 && c1_3 == 111 && c2_3 == 114) { return TOKEN_FOR; }  // for
        if (c0_3 == 97 && c1_3 == 115 && c2_3 == 109) { return TOKEN_ASM; }   // asm
        if (c0_3 == 117 && c1_3 == 49 && c2_3 == 54) { return TOKEN_U16; }    // u16
        if (c0_3 == 117 && c1_3 == 51 && c2_3 == 50) { return TOKEN_U32; }    // u32
        if (c0_3 == 117 && c1_3 == 54 && c2_3 == 52) { return TOKEN_U64; }    // u64
        if (c0_3 == 105 && c1_3 == 54 && c2_3 == 52) { return TOKEN_I64; }    // i64
        if (c0_3 == 102 && c1_3 == 54 && c2_3 == 52) { return TOKEN_F64; }    // f64
        return TOKEN_IDENTIFIER;
    }

    if (len == 4) {
        var c0_4: u64 = s[0];
        var c1_4: u64 = s[1];
        var c2_4: u64 = s[2];
        var c3_4: u64 = s[3];
        if (c0_4 == 102 && c1_4 == 117 && c2_4 == 110 && c3_4 == 99) { return TOKEN_FUNC; }  // func
        if (c0_4 == 97 && c1_4 == 98 && c2_4 == 115 && c3_4 == 116) { return TOKEN_ABST; }   // abst
        if (c0_4 == 116 && c1_4 == 111 && c2_4 == 100 && c3_4 == 111) { return TOKEN_TODO; } // todo
        if (c0_4 == 101 && c1_4 == 108 && c2_4 == 115 && c3_4 == 101) { return TOKEN_ELSE; } // else
        if (c0_4 == 116 && c1_4 == 114 && c2_4 == 117 && c3_4 == 101) { return TOKEN_TRUE; } // true
        if (c0_4 == 110 && c1_4 == 117 && c2_4 == 108 && c3_4 == 108) { return TOKEN_NULL; } // null
        if (c0_4 == 101 && c1_4 == 110 && c2_4 == 117 && c3_4 == 109) { return TOKEN_ENUM; } // enum
        if (c0_4 == 105 && c1_4 == 109 && c2_4 == 112 && c3_4 == 108) { return TOKEN_IMPL; } // impl
        if (c0_4 == 99 && c1_4 == 97 && c2_4 == 115 && c3_4 == 101) { return TOKEN_CASE; }   // case
        if (c0_4 == 99 && c1_4 == 104 && c2_4 == 97 && c3_4 == 114) { return TOKEN_CHAR; }   // char
        if (c0_4 == 102 && c1_4 == 114 && c2_4 == 111 && c3_4 == 109) { return TOKEN_FROM; } // from
        return TOKEN_IDENTIFIER;
    }

    if (len == 5) {
        var c0_5: u64 = s[0];
        var c1_5: u64 = s[1];
        var c2_5: u64 = s[2];
        var c3_5: u64 = s[3];
        var c4_5: u64 = s[4];
        if (c0_5 == 99 && c1_5 == 111 && c2_5 == 110 && c3_5 == 115 && c4_5 == 116) { return TOKEN_CONST; } // const
        if (c0_5 == 102 && c1_5 == 97 && c2_5 == 108 && c3_5 == 115 && c4_5 == 101) { return TOKEN_FALSE; } // false
        if (c0_5 == 119 && c1_5 == 104 && c2_5 == 105 && c3_5 == 108 && c4_5 == 101) { return TOKEN_WHILE; } // while
        if (c0_5 == 98 && c1_5 == 114 && c2_5 == 101 && c3_5 == 97 && c4_5 == 107) { return TOKEN_BREAK; } // break
        if (c0_5 == 97 && c1_5 == 108 && c2_5 == 105 && c3_5 == 97 && c4_5 == 115) { return TOKEN_ALIAS; } // alias
        if (c0_5 == 100 && c1_5 == 101 && c2_5 == 102 && c3_5 == 101 && c4_5 == 114) { return TOKEN_DEFER; } // defer
        if (c0_5 == 116 && c1_5 == 114 && c2_5 == 97 && c3_5 == 105 && c4_5 == 116) { return TOKEN_TRAIT; } // trait
        return TOKEN_IDENTIFIER;
    }

    if (len == 6) {
        var c0_6: u64 = s[0];
        var c1_6: u64 = s[1];
        var c2_6: u64 = s[2];
        var c3_6: u64 = s[3];
        var c4_6: u64 = s[4];
        var c5_6: u64 = s[5];
        if (c0_6 == 114 && c1_6 == 101 && c2_6 == 116 && c3_6 == 117 && c4_6 == 114 && c5_6 == 110) { return TOKEN_RETURN; } // return
        if (c0_6 == 115 && c1_6 == 116 && c2_6 == 114 && c3_6 == 117 && c4_6 == 99 && c5_6 == 116) { return TOKEN_STRUCT; } // struct
        if (c0_6 == 115 && c1_6 == 119 && c2_6 == 105 && c3_6 == 116 && c4_6 == 99 && c5_6 == 104) { return TOKEN_SWITCH; } // switch
        if (c0_6 == 105 && c1_6 == 109 && c2_6 == 112 && c3_6 == 111 && c4_6 == 114 && c5_6 == 116) { return TOKEN_IMPORT; } // import
        if (c0_6 == 115 && c1_6 == 105 && c2_6 == 122 && c3_6 == 101 && c4_6 == 111 && c5_6 == 102) { return TOKEN_SIZEOF; } // sizeof
        if (c0_6 == 115 && c1_6 == 116 && c2_6 == 97 && c3_6 == 116 && c4_6 == 105 && c5_6 == 99) { return TOKEN_STATIC; } // static
        if (c0_6 == 116 && c1_6 == 97 && c2_6 == 103 && c3_6 == 103 && c4_6 == 101 && c5_6 == 100) { return TOKEN_TAGGED; } // tagged
        if (c0_6 == 112 && c1_6 == 97 && c2_6 == 99 && c3_6 == 107 && c4_6 == 101 && c5_6 == 100) { return TOKEN_PACKED; } // packed
        if (c0_6 == 100 && c1_6 == 101 && c2_6 == 108 && c3_6 == 101 && c4_6 == 116 && c5_6 == 101) { return TOKEN_DELETE; } // delete
        if (c0_6 == 97 && c1_6 == 115 && c2_6 == 115 && c3_6 == 101 && c4_6 == 114 && c5_6 == 116) { return TOKEN_ASSERT; } // assert
        return TOKEN_IDENTIFIER;
    }

    if (len == 7) {
        if (s[0] == 100 && s[1] == 101 && s[2] == 102 && s[3] == 97 && s[4] == 117 && s[5] == 108 && s[6] == 116) { return TOKEN_DEFAULT; } // default
        return TOKEN_IDENTIFIER;
    }

    if (len == 8) {
        if (s[0] == 99 && s[1] == 111 && s[2] == 110 && s[3] == 116 && s[4] == 105 && s[5] == 110 && s[6] == 117 && s[7] == 101) { return TOKEN_CONTINUE; } // continue
        if (s[0] == 95 && s[1] == 95 && s[2] == 76 && s[3] == 73 && s[4] == 78 && s[5] == 69 && s[6] == 95 && s[7] == 95) { return TOKEN_LINE_MACRO; } // __LINE__
        return TOKEN_IDENTIFIER;
    }

    if (len == 10) {
        if (s[0] == 100 && s[1] == 101 && s[2] == 115 && s[3] == 116 && s[4] == 114 && s[5] == 117 && s[6] == 99 && s[7] == 116 && s[8] == 111 && s[9] == 114) { return TOKEN_DESTRUCTOR; } // destructor
        return TOKEN_IDENTIFIER;
    }

    if (len == 11) {
        if (s[0] == 99 && s[1] == 111 && s[2] == 110 && s[3] == 115 && s[4] == 116 && s[5] == 114 && s[6] == 117 && s[7] == 99 && s[8] == 116 && s[9] == 111 && s[10] == 114) { return TOKEN_CONSTRUCTOR; } // constructor
        if (s[0] == 117 && s[1] == 110 && s[2] == 114 && s[3] == 101 && s[4] == 97 && s[5] == 99 && s[6] == 104 && s[7] == 97 && s[8] == 98 && s[9] == 108 && s[10] == 101) { return TOKEN_UNREACHABLE; } // unreachable
        return TOKEN_IDENTIFIER;
    }

    return TOKEN_IDENTIFIER;
}

// Token structure: [kind, ptr, len, line, col]

func tok_new(kind: u64, ptr: u64, len: u64, line: u64, col: u64) -> *Token {
    var t: *Token = tok_pool_alloc();
    t.kind = kind;
    t.ptr = ptr;
    t.len = len;
    t.line = line;
    t.col = col;
    return t;
}



func lex_next(lex: *Lexer) -> *Token {
    lex_skip_ws_and_comments(lex);
    
    var line: u64 = lex.line;
    var col: u64 = lex.col;
    
    if (lex_at_end(lex)) {
        return tok_new(TOKEN_EOF, 0, 0, line, col);
    }
    
    var start: u64 = lex.pos;
    var c: u64 = lex_advance(lex);
    var src: u64 = lex.src_ptr;
    
    // Identifier or keyword
    if (is_alpha(c)) {
        while (!lex_at_end(lex)) {
            if (is_alnum(lex_peek(lex))) {
                lex_advance(lex);
            } else {
                break;
            }
        }
        var len: u64  = lex.pos - start;
        var kind: u64 = lex_check_keyword(src + start, len);
        return tok_new(kind, src + start, len, line, col);
    }
    
    // Number
    if (is_digit(c)) {
        while (!lex_at_end(lex)) {
            if (is_digit(lex_peek(lex))) {
                lex_advance(lex);
            } else {
                break;
            }
        }
        if (lex_peek(lex) == CHAR_DOT && is_digit(lex_peek_next(lex))) {
            lex_advance(lex);
            while (!lex_at_end(lex)) {
                if (is_digit(lex_peek(lex))) {
                    lex_advance(lex);
                } else {
                    break;
                }
            }
            var f_len: u64 = lex.pos - start;
            return tok_new(TOKEN_FLOAT, src + start, f_len, line, col);
        }
        var len: u64 = lex.pos - start;
        return tok_new(TOKEN_NUMBER, src + start, len, line, col);
    }
    
    // String literal
    if (c == CHAR_QUOTE) {
        while (!lex_at_end(lex)) {
            var ch: u64 = lex_peek(lex);
            if (ch == CHAR_QUOTE) {
                lex_advance(lex);
                break;
            }
            if (ch == CHAR_BACKSLASH) {
                lex_advance(lex);
                if (!lex_at_end(lex)) {
                    lex_advance(lex);
                }
            } else {
                lex_advance(lex);
            }
        }
        var len: u64 = lex.pos - start;
        return tok_new(TOKEN_STRING, src + start, len, line, col);
    }
    
    // Two-char operators
    if (c == CHAR_EQ) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_EQEQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_EQ, src + start, 1, line, col);
    }
    if (c == CHAR_EXCLAIM) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_BANGEQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_BANG, src + start, 1, line, col);
    }
    if (c == CHAR_LT) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_LTEQ, src + start, 2, line, col);
        }
        if (lex_peek(lex) == CHAR_LT) {
            lex_advance(lex);
            return tok_new(TOKEN_LSHIFT, src + start, 2, line, col);
        }
        return tok_new(TOKEN_LT, src + start, 1, line, col);
    }
    if (c == CHAR_GT) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_GTEQ, src + start, 2, line, col);
        }
        if (lex_peek(lex) == CHAR_GT) {
            lex_advance(lex);
            return tok_new(TOKEN_RSHIFT, src + start, 2, line, col);
        }
        return tok_new(TOKEN_GT, src + start, 1, line, col);
    }

    if (c == CHAR_AMPERSAND) {
        if (lex_peek(lex) == CHAR_AMPERSAND) {
            lex_advance(lex);
            return tok_new(TOKEN_ANDAND, src + start, 2, line, col);
        }
        return tok_new(TOKEN_AMPERSAND, src + start, 1, line, col);
    }

    if (c == CHAR_PIPE) {
        if (lex_peek(lex) == CHAR_PIPE) {
            lex_advance(lex);
            return tok_new(TOKEN_OROR, src + start, 2, line, col);
        }
        return tok_new(TOKEN_PIPE, src + start, 1, line, col);
    }
    
    // Single-char tokens
    if (c == CHAR_LPAREN) { return tok_new(TOKEN_LPAREN, src + start, 1, line, col); }
    if (c == CHAR_RPAREN) { return tok_new(TOKEN_RPAREN, src + start, 1, line, col); }
    if (c == CHAR_LBRACE) { return tok_new(TOKEN_LBRACE, src + start, 1, line, col); }
    if (c == CHAR_RBRACE) { return tok_new(TOKEN_RBRACE, src + start, 1, line, col); }
    if (c == CHAR_LBRACKET) { return tok_new(TOKEN_LBRACKET, src + start, 1, line, col); }
    if (c == CHAR_RBRACKET) { return tok_new(TOKEN_RBRACKET, src + start, 1, line, col); }
    if (c == CHAR_SEMICOLON) { return tok_new(TOKEN_SEMICOLON, src + start, 1, line, col); }
    if (c == CHAR_COLON) { return tok_new(TOKEN_COLON, src + start, 1, line, col); }
    if (c == CHAR_COMMA) { return tok_new(TOKEN_COMMA, src + start, 1, line, col); }
    if (c == CHAR_DOT) {
        if (lex_peek(lex) == CHAR_QUESTION) {
            lex_advance(lex);
            return tok_new(TOKEN_DOT_Q, src + start, 2, line, col);
        }
        return tok_new(TOKEN_DOT, src + start, 1, line, col);
    }
    if (c == CHAR_QUESTION) { return tok_new(TOKEN_QUESTION, src + start, 1, line, col); }
    if (c == CHAR_PLUS) {
        if (lex_peek(lex) == CHAR_PLUS) {
            lex_advance(lex);
            return tok_new(TOKEN_PLUSPLUS, src + start, 2, line, col);
        }
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_PLUS_EQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_PLUS, src + start, 1, line, col);
    }
    if (c == CHAR_MINUS) {
        if (lex_peek(lex) == CHAR_MINUS) {
            lex_advance(lex);
            return tok_new(TOKEN_MINUSMINUS, src + start, 2, line, col);
        }
        if (lex_peek(lex) == CHAR_GT) {
            lex_advance(lex);
            return tok_new(TOKEN_ARROW, src + start, 2, line, col);
        }
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_MINUS_EQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_MINUS, src + start, 1, line, col);
    }
    if (c == CHAR_STAR) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_STAR_EQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_STAR, src + start, 1, line, col);
    }
    if (c == CHAR_SLASH) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_SLASH_EQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_SLASH, src + start, 1, line, col);
    }
    if (c == CHAR_PERCENT) {
        if (lex_peek(lex) == CHAR_EQ) {
            lex_advance(lex);
            return tok_new(TOKEN_PERCENT_EQ, src + start, 2, line, col);
        }
        return tok_new(TOKEN_PERCENT, src + start, 1, line, col);
    }
    if (c == CHAR_TILDE) { return tok_new(TOKEN_TILDE, src + start, 1, line, col); }
    if (c == CHAR_CARET) { return tok_new(TOKEN_CARET, src + start, 1, line, col); }
    // NOTE: '&' and '|' are handled above to support &&/||.
    
    return tok_new(TOKEN_EOF, 0, 0, line, col);
}

// Lexer-only source normalization.
// Keeps parser and compiler pipeline logic simple by fixing tokenization edge
// cases directly at lexer boundary.
func lex_preprocess_source(source_ptr: u64, source_len: u64) -> *NameInfo {
    if (source_ptr == 0 || source_len < 2) { return 0; }
    var source_u8: []u8 = slice(source_ptr, source_len + 1);

    var has_shift_pair: u64 = 0;
    for (var i0: u64 = 0; i0 + 1 < source_len; i0++) {
        if (source_u8[i0] == 62 && source_u8[i0 + 1] == 62) {
            has_shift_pair = 1;
            break;
        }
    }
    if (has_shift_pair == 0) { return 0; }

    var repl_count: u64 = 0;
    var i: u64 = 0;
    var line_start: u64 = 1;
    var in_global_var: u64 = 0;
    while (i + 1 < source_len) {
        var c: u64 = source_u8[i];
        if (line_start != 0) {
            if (c == 118 && i + 3 < source_len && source_u8[i + 1] == 97 && source_u8[i + 2] == 114 &&
                (source_u8[i + 3] == 32 || source_u8[i + 3] == 9)) {
                in_global_var = 1;
                line_start = 0;
            } else if (c == 10) {
                in_global_var = 0;
                line_start = 1;
            } else if (c == 32 || c == 9) {
                line_start = 0;
                in_global_var = 0;
            } else {
                in_global_var = 0;
                line_start = 0;
            }
        }
        if (c == 10) {
            in_global_var = 0;
            line_start = 1;
            i = i + 1;
            continue;
        }
        if (in_global_var != 0 && c == 62 && source_u8[i + 1] == 62) {
            repl_count = repl_count + 1;
            i = i + 2;
            continue;
        }
        i = i + 1;
    }

    if (repl_count == 0) { return 0; }

    var new_size: u64 = source_len + repl_count;
    var new_buf: u64 = heap_alloc((new_size + 1) * sizeof(u8));
    var new_u8: []u8 = slice(new_buf, new_size + 1);
    var si: u64 = 0;
    var di: u64 = 0;
    line_start = 1;
    in_global_var = 0;
    while (si < source_len) {
        var ch: u64 = source_u8[si];
        if (line_start != 0) {
            if (ch == 118 && si + 3 < source_len && source_u8[si + 1] == 97 && source_u8[si + 2] == 114 &&
                (source_u8[si + 3] == 32 || source_u8[si + 3] == 9)) {
                in_global_var = 1;
                line_start = 0;
            } else if (ch == 10) {
                in_global_var = 0;
                line_start = 1;
            } else if (ch == 32 || ch == 9) {
                line_start = 0;
                in_global_var = 0;
            } else {
                in_global_var = 0;
                line_start = 0;
            }
        }
        if (ch == 10) {
            new_u8[di] = ch;
            di = di + 1;
            si = si + 1;
            in_global_var = 0;
            line_start = 1;
            continue;
        }
        if (in_global_var != 0 && ch == 62 && si + 1 < source_len && source_u8[si + 1] == 62) {
            new_u8[di] = 62;
            new_u8[di + 1] = 32;
            new_u8[di + 2] = 62;
            di = di + 3;
            si = si + 2;
            continue;
        }
        new_u8[di] = ch;
        di = di + 1;
        si = si + 1;
    }
    new_u8[new_size] = 0;
    return new NameInfo{new_buf, new_size};
}

func lex_all(src: u64, len: u64) -> *Vec<*Token> {
    push_trace("lex_all", "lexer.b", __LINE__);
    defer pop_trace();
    
    // Approximate token count to reduce allocator churn in hot lexing path.
    tok_pool_begin((len / 2) + 16);

    var l: *Lexer = lex_new(src, len);
    var tokens: *Vec<*Token> = new Vec<*Token>(256);
    while (1) {
        var tok: *Token = lex_next(l);
        tokens.push(tok);
        if (tok.kind == TOKEN_EOF) { break; }
    }
    
    return tokens;
}
