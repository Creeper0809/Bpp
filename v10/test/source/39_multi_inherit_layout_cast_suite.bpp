// Covers: multiple inheritance layout/scoped field + pointer cast adjustment (old 39, 65)
// Expect exit code: 0

struct Animal {
    public hp: u64;
}

struct Flyer {
    public altitude: u64;
}

struct Dragon : Animal, Flyer {
    public fire_power: u64;
}

struct Lion {
    public roar: u64;
}

struct Tiger {
    public roar: u64;
}

struct Liger : Lion, Tiger {
    public power: u64;
}

struct Mammal : Animal {
    public fur: u64;
}

struct Dog : Mammal {
    public tail: u64;
}

struct Left {
    public l: u64;
}

struct Right {
    public r: u64;
}

struct CastChild : Left, Right {
    public c: u64;
}

impl Right {
    public func read(self: *Right) -> u64 {
        return self.r;
    }
}

impl CastChild {
    public func sum(self: *CastChild) -> u64 {
        return self.l + self.r + self.c;
    }
}

func sum_dragon(d: *Dragon) -> u64 {
    return d.hp + d.altitude + d.fire_power;
}

func sum_liger(l: *Liger) -> u64 {
    return l.<Lion>roar + l.<Tiger>roar + l.power;
}

func read_left(p: *Left) -> u64 {
    return p.l;
}

func read_right(p: *Right) -> u64 {
    return p.r;
}

func main() -> u64 {
    // old 39 path
    var d: Dragon;
    d.hp = 100;
    d.altitude = 200;
    d.fire_power = 300;
    if (sum_dragon(&d) != 600) { return 1; }

    var x: Liger;
    x.<Lion>roar = 10;
    x.<Tiger>roar = 20;
    x.power = 30;
    if (sum_liger(&x) != 60) { return 2; }

    var p: *Liger = &x;
    p.<Lion>roar = 11;
    p.<Tiger>roar = 22;
    p.power = 33;
    if (sum_liger(p) != 66) { return 3; }

    var dog: Dog;
    dog.<Animal>hp = 7;
    dog.fur = 9;
    dog.tail = 11;
    if (dog.<Animal>hp + dog.fur + dog.tail != 27) { return 4; }

    // old 65 path
    var c: CastChild;
    c.l = 10;
    c.r = 20;
    c.c = 30;

    var rp: *Right = (*Right)&c;
    if (rp.r != 20) { return 101; }
    if (rp.read() != 20) { return 102; }

    var lp: *Left = (*Left)&c;
    if (lp.l != 10) { return 103; }

    if (c.sum() != 60) { return 104; }

    var cp: *CastChild = &c;
    if (read_left(cp) != 10) { return 105; }
    if (read_right(cp) != 20) { return 106; }

    return 0;
}
