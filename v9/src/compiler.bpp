// compiler.b - Compiler globals and driver helpers

import std.io;
import std.str;
import std.path;
import std.vec;
import std.hashmap;
import std.util;
import types;
import lexer;
import ast;
import parser.util;
import parser.decl;
import module_utils;

const GENERIC_DEBUG = 0;
const TRAIT_LOOKUP_DEBUG = 0;
const TRAIT_LOOKUP_LEGACY_M_PREFIX = 1;
const COMPILER_NOT_FOUND_U64 = 9223372036854775807;
const COMPILER_TRY_KIND_INVALID = 0;
const COMPILER_TRY_KIND_OPTION = 1;
const COMPILER_TRY_KIND_RESULT = 2;
const COMPILER_SAFE_RECV_INVALID = 0;
const COMPILER_SAFE_RECV_STRUCT_PTR = 1;
const COMPILER_SAFE_RECV_TRAIT_PTR = 2;

// ============================================
// Global Module State
// ============================================
var g_loaded_modules: *HashMap<u64, u64> = new HashMap<u64, u64>(64);          // HashMap: path -> 1 (tracks fully loaded files)
var g_loaded_modules_pass1: *HashMap<u64, u64> = new HashMap<u64, u64>(64);    // HashMap: path -> 1 (tracks pass1 signature scan)
var g_module_imports_pass1: *HashMap<u64, *Vec<*AstImport>> = new HashMap<u64, *Vec<*AstImport>>(64);    // HashMap: path -> imports vec from pass1
var g_module_imports_pass2: *HashMap<u64, *Vec<*AstImport>> = new HashMap<u64, *Vec<*AstImport>>(64);    // HashMap: path -> imports vec from pass2 (stable copies)
var g_loaded_modules_vec: *Vec<*NameInfo> = new Vec<*NameInfo>(64);               // Vec<NameInfo> of fully loaded module paths
var g_all_funcs: *Vec<*AstFunc> = new Vec<*AstFunc>(64);         // Vec of all function ASTs
var g_all_func_sigs: *Vec<*AstFunc> = new Vec<*AstFunc>(64);     // Vec of all function signatures (pass 1)
var g_all_consts: *Vec<*AstConstDecl> = new Vec<*AstConstDecl>(128);        // Vec of all const ASTs
var g_all_globals: *Vec<*GlobalInfo> = new Vec<*GlobalInfo>(64);       // Vec of all global var info
var g_all_funcs_map: *HashMap<u64, *AstFunc> = 0;
var g_all_funcs_map_src: *Vec<*AstFunc> = 0;
var g_all_funcs_map_src_len: u64 = 0;
var g_all_consts_map2: *HashMap<u64, *AstConstDecl> = 0;
var g_all_consts_map2_src: *Vec<*AstConstDecl> = 0;
var g_all_consts_map2_src_len: u64 = 0;
var g_all_globals_map2: *HashMap<u64, *GlobalInfo> = 0;
var g_all_globals_map2_src: *Vec<*GlobalInfo> = 0;
var g_all_globals_map2_src_len: u64 = 0;
var g_all_structs: *HashMap<u64, *AstStructDef> = new HashMap<u64, *AstStructDef>(64);       // HashMap: struct_name -> struct_def
var g_all_structs_vec: *Vec<*AstStructDef> = new Vec<*AstStructDef>(16);   // Vec of all struct_defs (for codegen)
var g_all_traits: *HashMap<u64, *TraitDef> = new HashMap<u64, *TraitDef>(64);       // HashMap: trait_name -> trait_def
var g_all_traits_vec: *Vec<*TraitDef> = new Vec<*TraitDef>(16);   // Vec of all trait_defs
var g_all_trait_impls_vec: *Vec<*TraitImpl> = new Vec<*TraitImpl>(32);   // Vec of all trait impls
var g_abst_methods: *HashMap<u64, *Vec<*NameInfo>> = 0;                 // HashMap: struct name -> abstract methods
var g_base_dir = 0;          // Base directory for imports
var g_base_dir_len = 0;
var g_lib_dir = 0;           // Library root directory for compiler/runtime modules
var g_lib_dir_len = 0;

// Generic templates and instantiations
var g_generic_funcs: *HashMap<u64, *GenericFuncTemplate> = new HashMap<u64, *GenericFuncTemplate>(64);          // HashMap: base name -> GenericFuncTemplate
var g_generic_funcs_mangled: *HashMap<u64, *GenericFuncTemplate> = new HashMap<u64, *GenericFuncTemplate>(64);  // HashMap: mangled name -> GenericFuncTemplate
var g_generic_func_ptrs: *Vec<*GenericFuncTemplate> = new Vec<*GenericFuncTemplate>(16);      // Vec of generic template pointers
var g_generic_instantiations: *HashMap<u64, u64> = new HashMap<u64, u64>(64); // HashMap: mangled instantiation name -> 1
var g_generic_structs: *HashMap<u64, *GenericStructTemplate> = new HashMap<u64, *GenericStructTemplate>(64);         // HashMap: base name -> GenericStructTemplate
var g_generic_struct_ptrs: *Vec<*GenericStructTemplate> = new Vec<*GenericStructTemplate>(16);     // Vec of generic struct template pointers
var g_generic_struct_instantiations: *HashMap<u64, *AstStructDef> = new HashMap<u64, *AstStructDef>(64); // HashMap: instantiation name -> struct_def
var g_generic_struct_bindings: *HashMap<u64, *Vec<*GenericBinding>> = new HashMap<u64, *Vec<*GenericBinding>>(64); // HashMap: instantiation name -> bindings

var g_file_ptr = 0;
var g_file_len = 0;
var g_module_source_cache: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(64);
var g_module_tokens_cache: *HashMap<u64, *Vec<*Token>> = new HashMap<u64, *Vec<*Token>>(64);
var g_resolved_module_path_cache: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(256);
var g_module_aliases: *HashMap<u64, *HashMap<u64, *NameInfo>> = new HashMap<u64, *HashMap<u64, *NameInfo>>(128);     // HashMap: module_id -> alias_map
var g_prelude_aliases: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);    // HashMap: alias -> [mangled_ptr, mangled_len]
var g_prelude_miss_cache: *HashMap<u64, u64> = new HashMap<u64, u64>(256);              // HashMap: alias -> 1 when confirmed not in prelude
var g_prelude_loaded: u64 = 0;
var g_module_exports: *HashMap<u64, *Vec<*ExportEntry>> = new HashMap<u64, *Vec<*ExportEntry>>(64);     // HashMap: module_id -> exports vec
var g_module_export_maps: *HashMap<u64, *HashMap<u64, *NameInfo>> = new HashMap<u64, *HashMap<u64, *NameInfo>>(64);  // HashMap: module_id -> export name map
var g_func_module_map: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);    // HashMap: mangled func -> module_id
var g_global_module_map: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);  // HashMap: mangled global -> module_id
var g_current_module_ptr = 0;
var g_current_module_len = 0;
var g_impl_prefix_ptr = 0;
var g_impl_prefix_len = 0;
var g_impl_methods: *HashMap<u64, u64> = new HashMap<u64, u64>(64);       // HashMap: impl-lowered name -> 1
var g_impl_method_access: *HashMap<u64, u64> = new HashMap<u64, u64>(64); // HashMap: impl-lowered name -> access modifier
var g_impl_method_owner: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(64); // HashMap: impl-lowered name -> owner struct
var g_impl_method_funcs: *HashMap<u64, *AstFunc> = new HashMap<u64, *AstFunc>(64); // HashMap: impl-lowered name -> func pointer
var g_abst_methods_access: *HashMap<u64, *HashMap<u64, u64>> = 0; // HashMap: struct name -> (method -> access)
var g_abst_methods_sigs: *HashMap<u64, *HashMap<u64, *AstFunc>> = 0; // HashMap: struct name -> (method -> signature)
var g_virtual_dispatch_traits: *HashMap<u64, *HashMap<u64, *NameInfo>> = 0; // HashMap: struct -> (method -> virtual trait)
var g_current_access_struct_ptr: u64 = 0;
var g_current_access_struct_len: u64 = 0;
var g_resolve_name_hits: *HashMap<u64, *HashMap<u64, *NameInfo>> = 0;     // HashMap: module_id -> (name -> resolved)
var g_resolve_name_misses: *HashMap<u64, *HashMap<u64, u64>> = 0;          // HashMap: module_id -> (name -> miss=1)
var g_reg_aliases: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(64);   // HashMap: alias name -> register name
var g_const_lookup_result_ptr: *ConstResult = 0;

const PRELUDE_MODULE_COUNT = 7;

// Generic function template (48 bytes)
struct GenericFuncTemplate {
    public name_ptr: u64;
    public name_len: u64;
    public mangled_ptr: u64;
    public mangled_len: u64;
    public params_vec: *Vec<*GenericParam>;
    public func_sig: *AstFunc;
    public func_def: *AstFunc;
}

impl GenericFuncTemplate {
    public constructor() {
        self.name_ptr = 0;
        self.name_len = 0;
        self.mangled_ptr = 0;
        self.mangled_len = 0;
        self.params_vec = 0;
        self.func_sig = 0;
        self.func_def = 0;
    }
}

// Generic struct template (24 bytes)
struct GenericStructTemplate {
    public name_ptr: u64;
    public name_len: u64;
    public params_vec: *Vec<*GenericParam>;
    public struct_def: *AstStructDef;
}

impl GenericStructTemplate {
    public constructor() {
        self.name_ptr = 0;
        self.name_len = 0;
        self.params_vec = 0;
        self.struct_def = 0;
    }
}

// Generic binding (40 bytes)
struct GenericBinding {
    public kind: u64;
    public name_ptr: u64;
    public name_len: u64;
    public type: *TypeInfo;
    public value: u64;
}

impl GenericBinding {
    public constructor() {
        self.kind = 0;
        self.name_ptr = 0;
        self.name_len = 0;
        self.type = 0;
        self.value = 0;
    }
}

struct Stat {
    public pad0: u64;
    public pad1: u64;
    public pad2: u64;
    public pad3: u64;
    public pad4: u64;
    public pad5: u64;
    public size: u64;
}

// ============================================
// File Reading
// ============================================

func read_entire_file(path: u64) -> u64 {
    var fd_i: i64 = (i64)sys_open(path, 0, 0);
    if (fd_i < 0) { return 0; }
    var fd: u64 = (u64)fd_i;

    var statbuf_raw: u64 = heap_alloc(144 * sizeof(u8));
    var statbuf: *Stat = (*Stat)statbuf_raw;
    var st_res: i64 = (i64)sys_fstat(fd, statbuf_raw);
    if (st_res < 0) {
        sys_close(fd);
        return 0;
    }
    var size: u64 = statbuf.size;

    var buf: u64 = heap_alloc((size + 1) * sizeof(u8));

    var total: u64 = 0;
    for (; total < size; ) {
        var n: i64 = (i64)sys_read(fd, buf + total, size - total);
        if (n < 0) {
            sys_close(fd);
            return 0;
        }
        if (n == 0) { break; }
        total = total + (u64)n;
    }

    var buf_u8: []u8 = slice(buf, size + 1);
    buf_u8[total] = 0;

    sys_close(fd);

    g_file_ptr = buf;
    g_file_len = total;

    return buf;
}

func compiler_get_source_ptr() -> u64 {
    return g_file_ptr;
}

func compiler_get_source_len() -> u64 {
    return g_file_len;
}

func compiler_report_module_open_error(file_path: u64, file_path_len: u64) -> u64 {
    emit_stderr("[ERROR] Cannot open module: ");
    var file_path_u8: []u8 = slice(file_path, file_path_len);
    for (var i: u64 = 0; i < file_path_len; i++) {
        emit_char(file_path_u8[i]);
    }
    emit_nl();
    return 0;
}

func compiler_get_cached_module_source(file_path: u64, file_path_len: u64) -> *NameInfo {
    if (g_module_source_cache == 0) {
        g_module_source_cache = new HashMap<u64, *NameInfo>(64);
    }
    var cached: *NameInfo = g_module_source_cache.get(file_path, file_path_len);
    if (cached != 0) {
        g_file_ptr = cached.ptr;
        g_file_len = cached.len;
        return cached;
    }

    var content: u64 = read_entire_file(file_path);
    if (content == 0) { return 0; }

    var source_info: *NameInfo = new NameInfo{g_file_ptr, g_file_len};
    g_module_source_cache.put(file_path, file_path_len, source_info);
    return source_info;
}

func compiler_set_current_source(file_path: u64, file_path_len: u64) -> u64 {
    var source_info: *NameInfo = compiler_get_cached_module_source(file_path, file_path_len);
    if (source_info == 0) { return false; }
    return true;
}

func compiler_get_cached_module_tokens(file_path: u64, file_path_len: u64) -> *Vec<*Token> {
    if (g_module_tokens_cache == 0) {
        g_module_tokens_cache = new HashMap<u64, *Vec<*Token>>(64);
    }

    var tokens_cached: *Vec<*Token> = g_module_tokens_cache.get(file_path, file_path_len);
    if (tokens_cached != 0) { return tokens_cached; }

    var source_info: *NameInfo = compiler_get_cached_module_source(file_path, file_path_len);
    if (source_info == 0) { return 0; }
    var lex_ptr: u64 = source_info.ptr;
    var lex_len: u64 = source_info.len;
    var preprocessed: *NameInfo = lex_preprocess_source(lex_ptr, lex_len);
    if (preprocessed != 0) {
        lex_ptr = preprocessed.ptr;
        lex_len = preprocessed.len;
    }

    var tokens: *Vec<*Token> = lex_all(lex_ptr, lex_len);
    g_module_tokens_cache.put(file_path, file_path_len, tokens);
    return tokens;
}

func clone_name_info(src_ptr: u64, src_len: u64) -> *NameInfo {
    return module_util_clone_name_info(src_ptr, src_len);
}

func prelude_module_info(index: u64, out_ptr: *u64, out_len: *u64) -> u64 {
    return module_util_prelude_module_info(index, out_ptr, out_len);
}

// ============================================
// Config File Parsing
// ============================================

// Read config.ini and extract VERSION value
func read_version_from_config(config_path: u64) -> u64 {
    var content: u64 = read_entire_file(config_path);
    if (content == 0) {
        return 0;
    }

    return module_util_find_line_starting_with(content, g_file_len, "VERSION=", 8);
}

// ============================================
// Module Id + Mangling Helpers
// ============================================

// Returns [ptr,len] struct (16 bytes)
func module_id_from_path(path: u64, path_len: u64) -> *NameInfo {
    return module_util_module_id_from_path(path, path_len, g_lib_dir, g_lib_dir_len, g_base_dir, g_base_dir_len);
}

// ============================================
// Module Loading
// ============================================

func compiler_resolve_parent_def(parent_desc: *ParentDesc, panic_context: u64) -> *AstStructDef {
    if (parent_desc == 0) { return 0; }
    if (parent_desc.struct_def != 0) { return parent_desc.struct_def; }
    var parent_def: *AstStructDef = get_struct_def(parent_desc.name_ptr, parent_desc.name_len);
    if (parent_def == 0) {
        emit_stderr("[ERROR] Parent struct not found: ");
        emit_stderr_len(parent_desc.name_ptr, parent_desc.name_len);
        emit_stderr("\n");
        panic(panic_context);
    }
    parent_desc.struct_def = parent_def;
    return parent_def;
}

func compiler_is_not_found_u64(value: u64) -> u64 {
    if (value == COMPILER_NOT_FOUND_U64) { return 1; }
    return 0;
}

func resolve_module_path_cache_put(module_path: u64, module_len: u64, resolved_path: u64) -> u64 {
    g_resolved_module_path_cache.put(module_path, module_len, new NameInfo{resolved_path, str_len(resolved_path)});
    return resolved_path;
}

func resolve_module_path_try_cache(module_path: u64, module_len: u64, candidate: u64) -> u64 {
    if (module_util_file_exists(candidate) == 0) { return 0; }
    return resolve_module_path_cache_put(module_path, module_len, candidate);
}

func resolve_module_path_join_root(root_ptr: u64, root_len: u64, rel_ptr: u64, rel_len: u64) -> u64 {
    return str_concat3(root_ptr, root_len, "/", 1, rel_ptr, rel_len);
}

func resolve_module_path_panic_not_found(module_path: u64, module_len: u64, last_candidate: u64) -> u64 {
    emit_stderr("[ERROR] Cannot resolve module path: ");
    emit_stderr_len(module_path, module_len);
    emit_stderr("\n");
    if (last_candidate != 0) {
        emit_stderr("[ERROR] Last tried path: ");
        emit_stderr(last_candidate);
        emit_stderr("\n");
    }
    panic("Parse error");
    return 0;
}

func resolve_module_path(module_path: u64, module_len: u64) -> u64 {
    if (g_resolved_module_path_cache == 0) {
        g_resolved_module_path_cache = new HashMap<u64, *NameInfo>(256);
    }
    var cached_info: *NameInfo = g_resolved_module_path_cache.get(module_path, module_len);
    if (cached_info != 0) { return cached_info.ptr; }

    var eff_path: u64 = module_path;
    var eff_len: u64 = module_len;

    var prefer_lib: u64 = 0;

    if (module_util_is_std_alias(module_path, module_len)) {
        eff_path = module_util_std_alias_to_module_path(module_path, module_len);
        eff_len = str_len(eff_path);
        prefer_lib = 1;
    }

    if (module_util_is_std_path(eff_path, eff_len)) {
        prefer_lib = 1;
    }

    var with_ext: u64 = str_concat(eff_path, eff_len, ".b", 2);
    var with_ext_len: u64 = eff_len + 2;

    var with_ext_bpp: u64 = str_concat(eff_path, eff_len, ".bpp", 4);
    var with_ext_bpp_len: u64 = eff_len + 4;

    var full: u64 = 0;
    var fallback: u64 = 0;
    if (prefer_lib) {
        full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_lib_dir, g_lib_dir_len, with_ext_bpp, with_ext_bpp_len));
        if (full != 0) { return full; }
        full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_base_dir, g_base_dir_len, with_ext_bpp, with_ext_bpp_len));
        if (full != 0) { return full; }
        full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_lib_dir, g_lib_dir_len, with_ext, with_ext_len));
        if (full != 0) { return full; }
        fallback = resolve_module_path_join_root(g_base_dir, g_base_dir_len, with_ext, with_ext_len);
        full = resolve_module_path_try_cache(module_path, module_len, fallback);
        if (full != 0) { return full; }
        return resolve_module_path_panic_not_found(module_path, module_len, fallback);
    }

    full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_base_dir, g_base_dir_len, with_ext_bpp, with_ext_bpp_len));
    if (full != 0) { return full; }
    full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_lib_dir, g_lib_dir_len, with_ext_bpp, with_ext_bpp_len));
    if (full != 0) { return full; }
    full = resolve_module_path_try_cache(module_path, module_len, resolve_module_path_join_root(g_base_dir, g_base_dir_len, with_ext, with_ext_len));
    if (full != 0) { return full; }
    fallback = resolve_module_path_join_root(g_lib_dir, g_lib_dir_len, with_ext, with_ext_len);
    full = resolve_module_path_try_cache(module_path, module_len, fallback);
    if (full != 0) { return full; }
    return resolve_module_path_panic_not_found(module_path, module_len, fallback);
}

// ============================================
// Import Alias + Export Table
// ============================================

// Export entry: [name_ptr, name_len, mangled_ptr, mangled_len] = 32 bytes
func module_exports_get(module_ptr: u64, module_len: u64) -> *Vec<*ExportEntry> {
    return g_module_exports.?get(module_ptr, module_len);
}

func module_export_map_get(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    return g_module_export_maps.?get(module_ptr, module_len);
}

func module_export_map_ensure(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_module_export_maps == 0) {
        g_module_export_maps = new HashMap<u64, *HashMap<u64, *NameInfo>>(64);
    }
    var existing: *HashMap<u64, *NameInfo> = g_module_export_maps.get(module_ptr, module_len);
    if (existing != 0) { return existing; }
    var map: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);
    g_module_export_maps.put(module_ptr, module_len, map);
    return map;
}

func module_exports_ensure(module_ptr: u64, module_len: u64) -> *Vec<*ExportEntry> {
    if (g_module_exports == 0) {
        g_module_exports = new HashMap<u64, *Vec<*ExportEntry>>(64);
    }
    var existing: *Vec<*ExportEntry> = g_module_exports.get(module_ptr, module_len);
    if (existing != 0) { return existing; }
    var vec: *Vec<*ExportEntry> = new Vec<*ExportEntry>(32);
    g_module_exports.put(module_ptr, module_len, vec);
    return vec;
}

func add_module_export(module_ptr: u64, module_len: u64, name_ptr: u64, name_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_ensure(module_ptr, module_len);
    var entry: *ExportEntry = new ExportEntry{name_ptr, name_len, mangled_ptr, mangled_len};
    exports.push(entry);
    var export_map: *HashMap<u64, *NameInfo> = module_export_map_ensure(module_ptr, module_len);
    if (export_map.has(name_ptr, name_len) == 0) {
        var info: *NameInfo = new NameInfo{mangled_ptr, mangled_len};
        export_map.put(name_ptr, name_len, info);
    }
    compiler_invalidate_resolve_name_cache();
    return 0;
}

func module_aliases_get(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    return g_module_aliases.?get(module_ptr, module_len);
}

func module_aliases_ensure(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_module_aliases == 0) {
        g_module_aliases = new HashMap<u64, *HashMap<u64, *NameInfo>>(128);
    }
    var existing: *HashMap<u64, *NameInfo> = g_module_aliases.get(module_ptr, module_len);
    if (existing != 0) { return existing; }
    var map: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);
    g_module_aliases.put(module_ptr, module_len, map);
    return map;
}

func add_import_alias(module_ptr: u64, module_len: u64, alias_ptr: u64, alias_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    var alias_map: *HashMap<u64, *NameInfo> = module_aliases_ensure(module_ptr, module_len);
    var existing: *NameInfo = alias_map.get(alias_ptr, alias_len);
    if (existing != 0) {
        var ex_ptr: u64 = existing.ptr;
        var ex_len: u64 = existing.len;
        if (ex_ptr == mangled_ptr && ex_len == mangled_len) {
            return 0;
        }
        emit_stderr("[ERROR] import alias conflict: ");
        emit_stderr_len(alias_ptr, alias_len);
        emit_stderr_nl();
        panic("Parse error");
    }
    var info: *NameInfo = new NameInfo{mangled_ptr, mangled_len};
    alias_map.put(alias_ptr, alias_len, info);
    compiler_invalidate_resolve_name_cache();
    return 0;
}

func add_prelude_alias(alias_ptr: u64, alias_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    if (g_prelude_aliases == 0) {
        g_prelude_aliases = new HashMap<u64, *NameInfo>(128);
    }
    var existing: *NameInfo = g_prelude_aliases.get(alias_ptr, alias_len);
    if (existing != 0) {
        var ex_ptr: u64 = existing.ptr;
        var ex_len: u64 = existing.len;
        if (ex_ptr == mangled_ptr && ex_len == mangled_len) {
            return 0;
        }
        emit_stderr("[ERROR] prelude alias conflict: ");
        emit_stderr_len(alias_ptr, alias_len);
        emit_stderr_nl();
        panic("Parse error");
    }
    var info: *NameInfo = new NameInfo{mangled_ptr, mangled_len};
    g_prelude_aliases.put(alias_ptr, alias_len, info);
    // Clear any previous negative-cache mark for this symbol.
    if (g_prelude_miss_cache != 0) {
        g_prelude_miss_cache.put(alias_ptr, alias_len, 0);
    }
    compiler_invalidate_resolve_name_cache();
    return 0;
}

func resolve_import_alias(module_ptr: u64, module_len: u64, name_ptr: u64, name_len: u64) -> *NameInfo {
    var alias_map: *HashMap<u64, *NameInfo> = module_aliases_get(module_ptr, module_len);
    return alias_map.?get(name_ptr, name_len);
}

func resolve_prelude_alias(name_ptr: u64, name_len: u64) -> *NameInfo {
    return g_prelude_aliases.?get(name_ptr, name_len);
}

func compiler_invalidate_resolve_name_cache() -> u64 {
    g_resolve_name_hits = 0;
    g_resolve_name_misses = 0;
    return 0;
}

func resolve_name_hits_bucket_get(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_resolve_name_hits == 0) { return 0; }
    return g_resolve_name_hits.get(module_ptr, module_len);
}

func resolve_name_hits_bucket_ensure(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_resolve_name_hits == 0) {
        g_resolve_name_hits = new HashMap<u64, *HashMap<u64, *NameInfo>>(64);
    }
    var bucket: *HashMap<u64, *NameInfo> = g_resolve_name_hits.get(module_ptr, module_len);
    if (bucket != 0) { return bucket; }
    var out: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(128);
    g_resolve_name_hits.put(module_ptr, module_len, out);
    return out;
}

func resolve_name_misses_bucket_get(module_ptr: u64, module_len: u64) -> *HashMap<u64, u64> {
    if (g_resolve_name_misses == 0) { return 0; }
    return g_resolve_name_misses.get(module_ptr, module_len);
}

func resolve_name_misses_bucket_ensure(module_ptr: u64, module_len: u64) -> *HashMap<u64, u64> {
    if (g_resolve_name_misses == 0) {
        g_resolve_name_misses = new HashMap<u64, *HashMap<u64, u64>>(64);
    }
    var bucket: *HashMap<u64, u64> = g_resolve_name_misses.get(module_ptr, module_len);
    if (bucket != 0) { return bucket; }
    var out: *HashMap<u64, u64> = new HashMap<u64, u64>(128);
    g_resolve_name_misses.put(module_ptr, module_len, out);
    return out;
}

func compiler_rebuild_func_lookup() -> u64 {
    if (g_all_funcs == 0) {
        g_all_funcs_map = new HashMap<u64, *AstFunc>(16);
        g_all_funcs_map_src = 0;
        g_all_funcs_map_src_len = 0;
        return 0;
    }
    var n: u64 = g_all_funcs.len();
    var cap: u64 = 16;
    if (n > 0) {
        cap = n * 2;
        if (cap < 16) { cap = 16; }
    }
    g_all_funcs_map = new HashMap<u64, *AstFunc>(cap);
    for (var i: u64 = 0; i < n; i++) {
        var fn: *AstFunc = g_all_funcs.get(i);
        if (fn == 0) { continue; }
        if (g_all_funcs_map.has(fn.name_ptr, fn.name_len) != 0) { continue; }
        g_all_funcs_map.put(fn.name_ptr, fn.name_len, fn);
    }
    g_all_funcs_map_src = g_all_funcs;
    g_all_funcs_map_src_len = n;
    return 0;
}

func compiler_extend_func_lookup(from_idx: u64, to_idx: u64) -> u64 {
    if (g_all_funcs_map == 0 || g_all_funcs == 0) { return 0; }
    for (var i: u64 = from_idx; i < to_idx; i++) {
        var fn: *AstFunc = g_all_funcs.get(i);
        if (fn == 0) { continue; }
        if (g_all_funcs_map.has(fn.name_ptr, fn.name_len) != 0) { continue; }
        g_all_funcs_map.put(fn.name_ptr, fn.name_len, fn);
    }
    return 0;
}

func compiler_ensure_func_lookup() -> u64 {
    if (g_all_funcs == 0) {
        if (g_all_funcs_map == 0 || g_all_funcs_map_src != 0 || g_all_funcs_map_src_len != 0) {
            compiler_rebuild_func_lookup();
        }
        return 0;
    }
    var n: u64 = g_all_funcs.len();
    if (g_all_funcs_map == 0 || g_all_funcs_map_src != g_all_funcs || g_all_funcs_map_src_len > n) {
        compiler_rebuild_func_lookup();
        return 0;
    }
    if (g_all_funcs_map_src_len < n) {
        compiler_extend_func_lookup(g_all_funcs_map_src_len, n);
        g_all_funcs_map_src_len = n;
    }
    return 0;
}

func compiler_rebuild_const_lookup() -> u64 {
    if (g_all_consts == 0) {
        g_all_consts_map2 = new HashMap<u64, *AstConstDecl>(16);
        g_all_consts_map2_src = 0;
        g_all_consts_map2_src_len = 0;
        return 0;
    }
    var n: u64 = g_all_consts.len();
    var cap: u64 = 16;
    if (n > 0) {
        cap = n * 2;
        if (cap < 16) { cap = 16; }
    }
    g_all_consts_map2 = new HashMap<u64, *AstConstDecl>(cap);
    for (var i: u64 = 0; i < n; i++) {
        var c: *AstConstDecl = g_all_consts.get(i);
        if (c == 0) { continue; }
        if (g_all_consts_map2.has(c.name_ptr, c.name_len) != 0) { continue; }
        g_all_consts_map2.put(c.name_ptr, c.name_len, c);
    }
    g_all_consts_map2_src = g_all_consts;
    g_all_consts_map2_src_len = n;
    return 0;
}

func compiler_extend_const_lookup(from_idx: u64, to_idx: u64) -> u64 {
    if (g_all_consts_map2 == 0 || g_all_consts == 0) { return 0; }
    for (var i: u64 = from_idx; i < to_idx; i++) {
        var c: *AstConstDecl = g_all_consts.get(i);
        if (c == 0) { continue; }
        if (g_all_consts_map2.has(c.name_ptr, c.name_len) != 0) { continue; }
        g_all_consts_map2.put(c.name_ptr, c.name_len, c);
    }
    return 0;
}

func compiler_ensure_const_lookup() -> u64 {
    if (g_all_consts == 0) {
        if (g_all_consts_map2 == 0 || g_all_consts_map2_src != 0 || g_all_consts_map2_src_len != 0) {
            compiler_rebuild_const_lookup();
        }
        return 0;
    }
    var n: u64 = g_all_consts.len();
    if (g_all_consts_map2 == 0 || g_all_consts_map2_src != g_all_consts || g_all_consts_map2_src_len > n) {
        compiler_rebuild_const_lookup();
        return 0;
    }
    if (g_all_consts_map2_src_len < n) {
        compiler_extend_const_lookup(g_all_consts_map2_src_len, n);
        g_all_consts_map2_src_len = n;
    }
    return 0;
}

func compiler_rebuild_global_lookup() -> u64 {
    if (g_all_globals == 0) {
        g_all_globals_map2 = new HashMap<u64, *GlobalInfo>(16);
        g_all_globals_map2_src = 0;
        g_all_globals_map2_src_len = 0;
        return 0;
    }
    var n: u64 = g_all_globals.len();
    var cap: u64 = 16;
    if (n > 0) {
        cap = n * 2;
        if (cap < 16) { cap = 16; }
    }
    g_all_globals_map2 = new HashMap<u64, *GlobalInfo>(cap);
    for (var i: u64 = 0; i < n; i++) {
        var ginfo: *GlobalInfo = g_all_globals.get(i);
        if (ginfo == 0) { continue; }
        if (g_all_globals_map2.has(ginfo.name_ptr, ginfo.name_len) != 0) { continue; }
        g_all_globals_map2.put(ginfo.name_ptr, ginfo.name_len, ginfo);
    }
    g_all_globals_map2_src = g_all_globals;
    g_all_globals_map2_src_len = n;
    return 0;
}

func compiler_extend_global_lookup(from_idx: u64, to_idx: u64) -> u64 {
    if (g_all_globals_map2 == 0 || g_all_globals == 0) { return 0; }
    for (var i: u64 = from_idx; i < to_idx; i++) {
        var ginfo: *GlobalInfo = g_all_globals.get(i);
        if (ginfo == 0) { continue; }
        if (g_all_globals_map2.has(ginfo.name_ptr, ginfo.name_len) != 0) { continue; }
        g_all_globals_map2.put(ginfo.name_ptr, ginfo.name_len, ginfo);
    }
    return 0;
}

func compiler_ensure_global_lookup() -> u64 {
    if (g_all_globals == 0) {
        if (g_all_globals_map2 == 0 || g_all_globals_map2_src != 0 || g_all_globals_map2_src_len != 0) {
            compiler_rebuild_global_lookup();
        }
        return 0;
    }
    var n: u64 = g_all_globals.len();
    if (g_all_globals_map2 == 0 || g_all_globals_map2_src != g_all_globals || g_all_globals_map2_src_len > n) {
        compiler_rebuild_global_lookup();
        return 0;
    }
    if (g_all_globals_map2_src_len < n) {
        compiler_extend_global_lookup(g_all_globals_map2_src_len, n);
        g_all_globals_map2_src_len = n;
    }
    return 0;
}

func compiler_invalidate_func_lookup() -> u64 {
    g_all_funcs_map_src = 0;
    g_all_funcs_map_src_len = 0;
    return 0;
}

func compiler_invalidate_const_lookup() -> u64 {
    g_all_consts_map2_src = 0;
    g_all_consts_map2_src_len = 0;
    return 0;
}

func compiler_invalidate_global_lookup() -> u64 {
    g_all_globals_map2_src = 0;
    g_all_globals_map2_src_len = 0;
    return 0;
}

func compiler_func_exists(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_funcs == 0) { return false; }
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        resolved_ptr = resolved.ptr;
        resolved_len = resolved.len;
    }
    compiler_ensure_func_lookup();
    if (g_all_funcs_map != 0 && g_all_funcs_map.has(resolved_ptr, resolved_len) != 0) {
        return true;
    }
    return false;
}

func compiler_get_func(name_ptr: u64, name_len: u64) -> *AstFunc {
    if (g_all_funcs == 0) { return 0; }
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        resolved_ptr = resolved.ptr;
        resolved_len = resolved.len;
    }
    compiler_ensure_func_lookup();
    return g_all_funcs_map.?get(resolved_ptr, resolved_len);
}

func compiler_global_exists(name_ptr: u64, name_len: u64) -> u64 {
    compiler_ensure_global_lookup();
    if (g_all_globals_map2 != 0 && g_all_globals_map2.has(name_ptr, name_len) != 0) {
        return true;
    }
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0 && g_all_globals_map2 != 0) {
        if (g_all_globals_map2.has(resolved.ptr, resolved.len) != 0) { return true; }
    }
    return false;
}

func compiler_find_global_info(name_ptr: u64, name_len: u64) -> *GlobalInfo {
    compiler_ensure_global_lookup();
    var ginfo: *GlobalInfo = g_all_globals_map2.?get(name_ptr, name_len);
    if (ginfo != 0) { return ginfo; }
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        return g_all_globals_map2.?get(resolved.ptr, resolved.len);
    }
    return 0;
}

func compiler_set_all_globals(globals: *Vec<*GlobalInfo>) -> u64 {
    g_all_globals = globals;
    compiler_invalidate_global_lookup();
    return 0;
}

func prelude_try_symbol(module_path: u64, module_len: u64, name_ptr: u64, name_len: u64) -> *NameInfo {
    var mod_id: *NameInfo = module_util_module_id_from_import(module_path, module_len);
    if (mod_id == 0) { return 0; }
    var mod_ptr: u64 = mod_id.ptr;
    var mod_len: u64 = mod_id.len;

    var export_map: *HashMap<u64, *NameInfo> = module_export_map_get(mod_ptr, mod_len);
    if (export_map != 0) {
        var mapped: *NameInfo = export_map.get(name_ptr, name_len);
        if (mapped != 0) {
            add_prelude_alias(name_ptr, name_len, mapped.ptr, mapped.len);
            return mapped;
        }
    }

    var exports: *Vec<*ExportEntry> = module_exports_get(mod_ptr, mod_len);
    if (exports == 0) { return 0; }

    var n: u64 = exports.len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = exports.get(i);
        var sym_ptr: u64 = e.name_ptr;
        var sym_len: u64 = e.name_len;
        if (str_eq(sym_ptr, sym_len, name_ptr, name_len) != 0) {
            var mangled_ptr: u64 = e.mangled_ptr;
            var mangled_len: u64 = e.mangled_len;
            add_prelude_alias(name_ptr, name_len, mangled_ptr, mangled_len);
            var info: *NameInfo = new NameInfo{mangled_ptr, mangled_len};
            return info;
        }
    }
    return 0;
}

func resolve_prelude_alias_lazy(name_ptr: u64, name_len: u64) -> *NameInfo {
    // Fast path: most prelude symbols are already preloaded via load_std_prelude().
    var cached: *NameInfo = resolve_prelude_alias(name_ptr, name_len);
    if (cached != 0) { return cached; }

    // Miss cache avoids repeated export scans for non-prelude identifiers.
    if (g_prelude_miss_cache != 0) {
        var miss: u64 = g_prelude_miss_cache.get(name_ptr, name_len);
        if (miss == 1) { return 0; }
    }

    // Once prelude is fully loaded, a cache miss means definitive miss.
    if (g_prelude_loaded != 0) {
        if (g_prelude_miss_cache == 0) {
            g_prelude_miss_cache = new HashMap<u64, u64>(256);
        }
        g_prelude_miss_cache.put(name_ptr, name_len, 1);
        return 0;
    }

    for (var i: u64 = 0; i < PRELUDE_MODULE_COUNT; i++) {
        var module_ptr: u64 = 0;
        var module_len: u64 = 0;
        if (prelude_module_info(i, &module_ptr, &module_len) == 0) { continue; }
        var info: *NameInfo = prelude_try_symbol(module_ptr, module_len, name_ptr, name_len);
        if (info != 0) { return info; }
    }
    if (g_prelude_miss_cache == 0) {
        g_prelude_miss_cache = new HashMap<u64, u64>(256);
    }
    g_prelude_miss_cache.put(name_ptr, name_len, 1);
    return 0;
}

func resolve_name(name_ptr: u64, name_len: u64) -> *NameInfo {
    var module_ptr: u64 = 0;
    var module_len: u64 = 0;
    if (g_current_module_ptr != 0 && g_current_module_len != 0) {
        module_ptr = g_current_module_ptr;
        module_len = g_current_module_len;
    }

    var hit_bucket: *HashMap<u64, *NameInfo> = resolve_name_hits_bucket_get(module_ptr, module_len);
    if (hit_bucket != 0) {
        var cached: *NameInfo = hit_bucket.get(name_ptr, name_len);
        if (cached != 0) { return cached; }
    }

    var miss_bucket: *HashMap<u64, u64> = resolve_name_misses_bucket_get(module_ptr, module_len);
    if (miss_bucket != 0) {
        if (miss_bucket.get(name_ptr, name_len) == 1) { return 0; }
    }

    var result: *NameInfo = 0;
    var suffix_idx: u64 = compiler_find_generic_suffix_index(name_ptr, name_len);
    if (suffix_idx < name_len) {
        var base_ptr: u64 = name_ptr;
        var base_len: u64 = suffix_idx;
        var resolved_base: *NameInfo = resolve_name(base_ptr, base_len);
        if (resolved_base != 0) {
            var suffix_len: u64 = name_len - suffix_idx;
            var combined_len: u64 = resolved_base.len + suffix_len;
            var combined_ptr: u64 = heap_alloc((combined_len + 1) * sizeof(u8));
            str_copy(combined_ptr, resolved_base.ptr, resolved_base.len);
            str_copy(combined_ptr + resolved_base.len, name_ptr + suffix_idx, suffix_len);
            var combined_u8: []u8 = slice(combined_ptr, combined_len + 1);
            combined_u8[combined_len] = 0;
            result = new NameInfo{combined_ptr, combined_len};
        }
    }
    if (result == 0 && g_current_module_ptr != 0 && g_current_module_len != 0) {
        var import_alias: *NameInfo = resolve_import_alias(g_current_module_ptr, g_current_module_len, name_ptr, name_len);
        if (import_alias != 0) { result = import_alias; }
    }

    if (result == 0 && g_current_module_ptr != 0 && g_current_module_len != 0) {
        var export_map: *HashMap<u64, *NameInfo> = module_export_map_get(g_current_module_ptr, g_current_module_len);
        if (export_map != 0) {
            var mapped: *NameInfo = export_map.get(name_ptr, name_len);
            if (mapped != 0) { result = mapped; }
        }
        if (result == 0 && export_map == 0) {
            var exports: *Vec<*ExportEntry> = module_exports_get(g_current_module_ptr, g_current_module_len);
            if (exports != 0 && exports.len() != 0) {
                emit_stderr("[ERROR] module export map missing for loaded module\n");
                panic("Compiler state error");
            }
        }
    }

    if (result == 0) {
        var prelude_info: *NameInfo = resolve_prelude_alias_lazy(name_ptr, name_len);
        if (prelude_info != 0) { result = prelude_info; }
    }

    if (result != 0) {
        var hit_bucket_w: *HashMap<u64, *NameInfo> = resolve_name_hits_bucket_ensure(module_ptr, module_len);
        hit_bucket_w.put(name_ptr, name_len, result);
        var miss_bucket_w: *HashMap<u64, u64> = resolve_name_misses_bucket_get(module_ptr, module_len);
        if (miss_bucket_w != 0) {
            miss_bucket_w.put(name_ptr, name_len, 0);
        }
        return result;
    }

    var miss_bucket_new: *HashMap<u64, u64> = resolve_name_misses_bucket_ensure(module_ptr, module_len);
    miss_bucket_new.put(name_ptr, name_len, 1);
    return 0;
}

// ============================================
// ASM Register Alias
// ============================================

func compiler_reg_alias_reset() -> u64 {
    g_reg_aliases = new HashMap<u64, *NameInfo>(64);
    return 0;
}

func compiler_reg_alias_set(reg_ptr: u64, reg_len: u64, alias_ptr: u64, alias_len: u64) -> u64 {
    if (g_reg_aliases == 0) { g_reg_aliases = new HashMap<u64, *NameInfo>(64); }
    var info: *NameInfo = new NameInfo{reg_ptr, reg_len};
    g_reg_aliases.put(alias_ptr, alias_len, info);
    return 0;
}

func compiler_reg_alias_get(alias_ptr: u64, alias_len: u64) -> *NameInfo {
    return g_reg_aliases.?get(alias_ptr, alias_len);
}

// ============================================
// Const Lookup (SSA/Codegen Shared)
// ============================================

func compiler_const_lookup_result() -> *ConstResult {
    if (g_const_lookup_result_ptr == 0) {
        g_const_lookup_result_ptr = new ConstResult{0, 0};
    }
    return g_const_lookup_result_ptr;
}

func compiler_find_const(name_ptr: u64, name_len: u64) -> u64 {
    var res: *ConstResult = compiler_const_lookup_result();
    res.found = 0;
    res.value = 0;
    var result: u64 = (u64)res;

    if (g_all_consts == 0) { return result; }
    compiler_ensure_const_lookup();

    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        resolved_ptr = resolved.ptr;
        resolved_len = resolved.len;
    }

    var c: *AstConstDecl = g_all_consts_map2.?get(resolved_ptr, resolved_len);
    if (c != 0) {
        res.found = 1;
        res.value = c.value;
    }
    return result;
}

func import_all_from_module(importer_ptr: u64, importer_len: u64, module_ptr: u64, module_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return false; }
    var n: u64 = exports.len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = exports.get(i);
        var name_ptr: u64 = e.name_ptr;
        var name_len: u64 = e.name_len;
        var mangled_ptr: u64 = e.mangled_ptr;
        var mangled_len: u64 = e.mangled_len;
        add_import_alias(importer_ptr, importer_len, name_ptr, name_len, mangled_ptr, mangled_len);
    }
    return true;
}

func import_symbol_from_module(importer_ptr: u64, importer_len: u64, module_ptr: u64, module_len: u64, symbol_ptr: u64, symbol_len: u64, alias_ptr: u64, alias_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return false; }
    var n: u64 = exports.len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = exports.get(i);
        var name_ptr: u64 = e.name_ptr;
        var name_len: u64 = e.name_len;
        if (str_eq(name_ptr, name_len, symbol_ptr, symbol_len)) {
            var mangled_ptr: u64 = e.mangled_ptr;
            var mangled_len: u64 = e.mangled_len;
            add_import_alias(importer_ptr, importer_len, alias_ptr, alias_len, mangled_ptr, mangled_len);
            return true;
        }
    }
    emit_stderr("[ERROR] import symbol not found: ");
    emit_stderr_len(symbol_ptr, symbol_len);
    emit_stderr_nl();
    panic("Parse error");
    return false;
}

func prelude_import_all_from_module(module_ptr: u64, module_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return false; }
    var n: u64 = exports.len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = exports.get(i);
        var name_ptr: u64 = e.name_ptr;
        var name_len: u64 = e.name_len;
        var mangled_ptr: u64 = e.mangled_ptr;
        var mangled_len: u64 = e.mangled_len;
        add_prelude_alias(name_ptr, name_len, mangled_ptr, mangled_len);
    }
    return true;
}

// After all modules are loaded, resolve import aliases using stored import lists.
func finalize_import_aliases() -> u64 {
    if (g_loaded_modules_vec == 0) { return false; }
    var n: u64 = g_loaded_modules_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var info: *NameInfo = g_loaded_modules_vec.get(i);
        var module_path: u64 = info.ptr;
        var module_path_len: u64 = info.len;
        var module_id: *NameInfo = module_id_from_path(module_path, module_path_len);
        var module_ptr: u64 = module_id.ptr;
        var module_len: u64 = module_id.len;

        var imports: *Vec<*AstImport> = g_module_imports_pass2.get(module_path, module_path_len);
        if (imports == 0) { continue; }
        var num_imports: u64 = imports.len();
        for (var ii: u64 = 0; ii < num_imports; ii++) {
            var imp: *AstImport = imports.get(ii);
            var imp_path: u64 = imp.path_ptr;
            var imp_len: u64 = imp.path_len;
            var imp_sym_ptr: u64 = imp.symbol_ptr;
            var imp_sym_len: u64 = imp.symbol_len;
            var imp_alias_ptr: u64 = imp.alias_ptr;
            var imp_alias_len: u64 = imp.alias_len;

            var imp_mod_id: *NameInfo = module_util_module_id_from_import(imp_path, imp_len);
            var imp_mod_ptr: u64 = imp_mod_id.ptr;
            var imp_mod_len: u64 = imp_mod_id.len;

            if (imp_sym_ptr == 0) {
                import_all_from_module(module_ptr, module_len, imp_mod_ptr, imp_mod_len);
            } else {
                import_symbol_from_module(module_ptr, module_len, imp_mod_ptr, imp_mod_len, imp_sym_ptr, imp_sym_len, imp_alias_ptr, imp_alias_len);
            }
        }
    }
    return true;
}

func set_current_module_for_func(func_ptr: u64, func_len: u64) -> u64 {
    g_current_module_ptr = 0;
    g_current_module_len = 0;
    if (g_func_module_map == 0) { return 0; }
    var info: *NameInfo = g_func_module_map.get(func_ptr, func_len);
    if (info != 0) {
        g_current_module_ptr = info.ptr;
        g_current_module_len = info.len;
    }
    return 0;
}

func set_current_module_for_global(name_ptr: u64, name_len: u64) -> u64 {
    g_current_module_ptr = 0;
    g_current_module_len = 0;
    if (g_global_module_map == 0) { return 0; }
    var info: *NameInfo = g_global_module_map.get(name_ptr, name_len);
    if (info != 0) {
        g_current_module_ptr = info.ptr;
        g_current_module_len = info.len;
    }
    return 0;
}

// ============================================
// Export Registration + Mangling
// ============================================

func compiler_mangle_symbol_name(orig_ptr: u64, orig_len: u64, prefix_ptr: u64, prefix_len: u64, preserve_main: u64) -> *NameInfo {
    if (prefix_len == 0) { return new NameInfo{orig_ptr, orig_len}; }
    if (preserve_main != 0 && orig_len == 4 && str_eq(orig_ptr, orig_len, "main", 4)) {
        return new NameInfo{orig_ptr, orig_len};
    }
    return module_util_mangle_name(prefix_ptr, prefix_len, orig_ptr, orig_len);
}

func compiler_ensure_func_module_map() -> *HashMap<u64, *NameInfo> {
    if (g_func_module_map == 0) {
        g_func_module_map = new HashMap<u64, *NameInfo>(128);
    }
    return g_func_module_map;
}

func compiler_ensure_global_module_map() -> *HashMap<u64, *NameInfo> {
    if (g_global_module_map == 0) {
        g_global_module_map = new HashMap<u64, *NameInfo>(128);
    }
    return g_global_module_map;
}

func compiler_copy_impl_method_metadata_if_needed(orig_ptr: u64, orig_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    if (compiler_is_impl_method(orig_ptr, orig_len) == 0) { return 0; }
    var access: u64 = compiler_get_impl_method_access(orig_ptr, orig_len);
    var owner: *NameInfo = compiler_get_impl_method_owner(orig_ptr, orig_len);
    var fn: *AstFunc = compiler_get_impl_method_func(orig_ptr, orig_len);
    if (owner != 0) {
        compiler_register_impl_method_ex(mangled_ptr, mangled_len, owner.ptr, owner.len, access);
    } else {
        compiler_register_impl_method_ex(mangled_ptr, mangled_len, 0, 0, access);
    }
    if (fn != 0) {
        compiler_register_impl_method_func(mangled_ptr, mangled_len, fn);
    }
    return 0;
}

func compiler_register_module_exports_funcs(funcs: *Vec<*AstFunc>, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64, module_info: *NameInfo) -> u64 {
    if (funcs == 0) { return 0; }
    var func_map: *HashMap<u64, *NameInfo> = compiler_ensure_func_module_map();
    var num_funcs: u64 = funcs.len();
    for (var i: u64 = 0; i < num_funcs; i++) {
        var fn: *AstFunc = funcs.get(i);
        compiler_finalize_impl_method_name_if_needed(fn);
        compiler_register_finalized_impl_method(fn);

        var orig_ptr: u64 = fn.name_ptr;
        var orig_len: u64 = fn.name_len;
        var mangled_info: *NameInfo = compiler_mangle_symbol_name(orig_ptr, orig_len, prefix_ptr, prefix_len, 1);
        fn.name_ptr = mangled_info.ptr;
        fn.name_len = mangled_info.len;
        compiler_copy_impl_method_metadata_if_needed(orig_ptr, orig_len, mangled_info.ptr, mangled_info.len);

        add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_info.ptr, mangled_info.len);

        if (compiler_is_generic_template(fn) != 0) {
            var tpl: *GenericFuncTemplate = compiler_find_generic_template_by_func_ptr(fn);
            if (tpl != 0) {
                tpl.mangled_ptr = mangled_info.ptr;
                tpl.mangled_len = mangled_info.len;
                if (g_generic_funcs == 0) { g_generic_funcs = new HashMap<u64, *GenericFuncTemplate>(64); }
                g_generic_funcs.put(mangled_info.ptr, mangled_info.len, tpl);
            }
        }

        func_map.put(mangled_info.ptr, mangled_info.len, module_info);
    }
    return 0;
}

func compiler_register_module_exports_consts(consts: *Vec<*AstConstDecl>, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64) -> u64 {
    if (consts == 0) { return 0; }
    var num_consts: u64 = consts.len();
    for (var ci: u64 = 0; ci < num_consts; ci++) {
        var c: *AstConstDecl = consts.get(ci);
        var orig_ptr: u64 = c.name_ptr;
        var orig_len: u64 = c.name_len;
        var mangled_info: *NameInfo = compiler_mangle_symbol_name(orig_ptr, orig_len, prefix_ptr, prefix_len, 0);
        c.name_ptr = mangled_info.ptr;
        c.name_len = mangled_info.len;
        add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_info.ptr, mangled_info.len);
    }
    return 0;
}

func compiler_register_module_exports_globals(globals: *Vec<*GlobalInfo>, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64, module_info: *NameInfo) -> u64 {
    if (globals == 0) { return 0; }
    var global_map: *HashMap<u64, *NameInfo> = compiler_ensure_global_module_map();
    var num_globals: u64 = globals.len();
    for (var gi: u64 = 0; gi < num_globals; gi++) {
        var ginfo: *GlobalInfo = globals.get(gi);
        var orig_ptr: u64 = ginfo.name_ptr;
        var orig_len: u64 = ginfo.name_len;
        var mangled_info: *NameInfo = compiler_mangle_symbol_name(orig_ptr, orig_len, prefix_ptr, prefix_len, 0);
        ginfo.name_ptr = mangled_info.ptr;
        ginfo.name_len = mangled_info.len;
        add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_info.ptr, mangled_info.len);
        global_map.put(mangled_info.ptr, mangled_info.len, module_info);
    }
    return 0;
}

func register_module_exports(prog: u64, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64) -> u64 {
    var prog_info: *AstProgram = (*AstProgram)prog;
    var module_info: *NameInfo = new NameInfo{module_ptr, module_len};
    compiler_register_module_exports_funcs(prog_info.funcs_vec, module_ptr, module_len, prefix_ptr, prefix_len, module_info);
    compiler_register_module_exports_consts(prog_info.consts_vec, module_ptr, module_len, prefix_ptr, prefix_len);
    compiler_register_module_exports_globals(prog_info.globals_vec, module_ptr, module_len, prefix_ptr, prefix_len, module_info);
    return 0;
}

func load_module_by_name(module_path: u64, module_len: u64) -> u64 {
    var resolved: u64 = resolve_module_path(module_path, module_len);
    var resolved_len: u64 = str_len(resolved);
    return load_module(resolved, resolved_len);
}

func compiler_import_panic_missing_module() -> u64 {
    emit_stderr("[ERROR] Expected module identifier in import\n");
    panic("Parse error");
    return 0;
}

func compiler_import_expect_module_tok(tokens: *Vec<*Token>, idx: *u64, n: u64) -> *Token {
    if (*idx >= n) {
        compiler_import_panic_missing_module();
    }
    var tok: *Token = tokens.get(*idx);
    var kind: u64 = tok.kind;
    if (kind != TOKEN_IDENTIFIER && kind != TOKEN_CHAR) {
        compiler_import_panic_missing_module();
    }
    *idx = *idx + 1;
    return tok;
}

func compiler_import_parse_path_suffix(tokens: *Vec<*Token>, idx: *u64, n: u64, start_tok: *Token) -> *NameInfo {
    var path_ptr: u64 = start_tok.ptr;
    var path_len: u64 = start_tok.len;
    while (*idx < n && tokens.get(*idx).kind == TOKEN_DOT) {
        *idx = *idx + 1;
        var next_tok: *Token = compiler_import_expect_module_tok(tokens, idx, n);
        var joined_path: u64 = str_concat(path_ptr, path_len, "/", 1);
        path_ptr = str_concat(joined_path, path_len + 1, next_tok.ptr, next_tok.len);
        path_len = path_len + 1 + next_tok.len;
    }
    return new NameInfo{path_ptr, path_len};
}

func compiler_import_expect_semicolon(tokens: *Vec<*Token>, idx: *u64, n: u64) -> u64 {
    if (*idx >= n || tokens.get(*idx).kind != TOKEN_SEMICOLON) {
        emit_stderr("[ERROR] Expected ';' after import\n");
        panic("Parse error");
    }
    *idx = *idx + 1;
    return 0;
}

func compiler_scan_imports_from_tokens(tokens: *Vec<*Token>) -> *Vec<*AstImport> {
    var imports: *Vec<*AstImport> = new Vec<*AstImport>(4);
    if (tokens == 0) { return imports; }

    var n: u64 = tokens.len();
    var i: u64 = 0;
    while (i < n) {
        var tok: *Token = tokens.get(i);
        if (tok.kind != TOKEN_IMPORT) {
            i = i + 1;
            continue;
        }
        i = i + 1;
        if (i >= n) { break; }

        var first_tok: *Token = compiler_import_expect_module_tok(tokens, &i, n);

        if (i < n && (tokens.get(i).kind == TOKEN_AS || tokens.get(i).kind == TOKEN_FROM)) {
            var symbol_ptr: u64 = first_tok.ptr;
            var symbol_len: u64 = first_tok.len;
            var alias_ptr: u64 = symbol_ptr;
            var alias_len: u64 = symbol_len;

            if (tokens.get(i).kind == TOKEN_AS) {
                i = i + 1;
                if (i >= n || tokens.get(i).kind != TOKEN_IDENTIFIER) {
                    emit_stderr("[ERROR] Expected alias identifier in import\n");
                    panic("Parse error");
                }
                var alias_tok: *Token = tokens.get(i);
                alias_ptr = alias_tok.ptr;
                alias_len = alias_tok.len;
                i = i + 1;
            }

            if (i >= n || tokens.get(i).kind != TOKEN_FROM) {
                emit_stderr("[ERROR] Expected 'from' in import\n");
                panic("Parse error");
            }
            i = i + 1;

            var mod_tok: *Token = compiler_import_expect_module_tok(tokens, &i, n);
            var path_info: *NameInfo = compiler_import_parse_path_suffix(tokens, &i, n, mod_tok);
            compiler_import_expect_semicolon(tokens, &i, n);

            imports.push(ast_import(path_info.ptr, path_info.len, symbol_ptr, symbol_len, alias_ptr, alias_len));
            continue;
        }

        var path_info2: *NameInfo = compiler_import_parse_path_suffix(tokens, &i, n, first_tok);
        compiler_import_expect_semicolon(tokens, &i, n);
        imports.push(ast_import(path_info2.ptr, path_info2.len, 0, 0, 0, 0));
    }

    return imports;
}

func compiler_pass1_load_imports_recursive(imports_copy: *Vec<*AstImport>) -> u64 {
    if (imports_copy == 0) { return true; }
    var num_imports: u64 = imports_copy.len();
    for (var i: u64 = 0; i < num_imports; i++) {
        var imp: *AstImport = imports_copy.get(i);
        var resolved: u64 = resolve_module_path(imp.path_ptr, imp.path_len);
        var resolved_len: u64 = str_len(resolved);
        if (!load_module_pass1(resolved, resolved_len)) {
            return false;
        }
    }
    return true;
}

func compiler_pass1_register_func_sigs(prog_sig: *AstProgram) -> u64 {
    if (prog_sig == 0) { return 0; }
    var sig_funcs: *Vec<*AstFunc> = prog_sig.funcs_vec;
    var num_sig_funcs: u64 = sig_funcs.len();
    for (var i: u64 = 0; i < num_sig_funcs; i++) {
        var sig_fn: *AstFunc = sig_funcs.get(i);
        compiler_finalize_impl_method_name_if_needed(sig_fn);
        compiler_register_finalized_impl_method(sig_fn);
        g_all_func_sigs.push(sig_fn);
    }
    return 0;
}

// Pass1: scan signatures and register generic templates before full parse.
func load_module_pass1(file_path: u64, file_path_len: u64) -> u64 {
    if (g_loaded_modules_pass1.has(file_path, file_path_len)) {
        return true;
    }
    g_loaded_modules_pass1.put(file_path, file_path_len, 1);

    var tokens: *Vec<*Token> = compiler_get_cached_module_tokens(file_path, file_path_len);
    if (tokens == 0) {
        compiler_report_module_open_error(file_path, file_path_len);
        return false;
    }

    // Pre-scan imports from token stream without parser round-trip.
    var imports_copy: *Vec<*AstImport> = compiler_scan_imports_from_tokens(tokens);
    g_module_imports_pass1.put(file_path, file_path_len, imports_copy);

    if (!compiler_pass1_load_imports_recursive(imports_copy)) { return false; }

    if (compiler_set_current_source(file_path, file_path_len) == 0) {
        compiler_report_module_open_error(file_path, file_path_len);
        return false;
    }
    var p1: *Parser = parse_new(tokens);
    var prog_sig: *AstProgram = parse_program_pass1(p1);
    compiler_pass1_register_func_sigs(prog_sig);
    return true;
}

func load_std_prelude() -> u64 {
    g_prelude_loaded = 0;
    g_prelude_miss_cache = new HashMap<u64, u64>(256);
    compiler_invalidate_resolve_name_cache();

    for (var i: u64 = 0; i < PRELUDE_MODULE_COUNT; i++) {
        var module_ptr: u64 = 0;
        var module_len: u64 = 0;
        if (prelude_module_info(i, &module_ptr, &module_len) == 0) { continue; }
        if (!load_module_by_name(module_ptr, module_len)) { return false; }
        var module_id: *NameInfo = module_util_module_id_from_import(module_ptr, module_len);
        prelude_import_all_from_module(module_id.ptr, module_id.len);
    }
    g_prelude_loaded = 1;
    return true;
}

func compiler_mark_module_loaded(file_path: u64, file_path_len: u64) -> u64 {
    g_loaded_modules.put(file_path, file_path_len, 1);
    var path_info: *NameInfo = clone_name_info(file_path, file_path_len);
    g_loaded_modules_vec.push(path_info);
    return 0;
}

func compiler_load_module_import_deps_pass1(file_path: u64, file_path_len: u64) -> u64 {
    if (g_module_imports_pass1 == 0) { return true; }
    var imports_p1: *Vec<*AstImport> = g_module_imports_pass1.get(file_path, file_path_len);
    if (imports_p1 == 0) { return true; }

    var num_imports_p1: u64 = imports_p1.len();
    for (var pi: u64 = 0; pi < num_imports_p1; pi++) {
        var imp_p1: *AstImport = imports_p1.get(pi);
        var resolved_p1: u64 = resolve_module_path(imp_p1.path_ptr, imp_p1.path_len);
        var resolved_len_p1: u64 = str_len(resolved_p1);
        if (!load_module(resolved_p1, resolved_len_p1)) {
            return false;
        }
    }
    return true;
}

func compiler_module_identity(file_path: u64, file_path_len: u64, module_ptr_out: *u64, module_len_out: *u64, prefix_ptr_out: *u64, prefix_len_out: *u64) -> u64 {
    var module_id: *NameInfo = module_id_from_path(file_path, file_path_len);
    var module_prefix: *NameInfo = module_util_module_prefix_from_id(module_id.ptr, module_id.len);
    *module_ptr_out = module_id.ptr;
    *module_len_out = module_id.len;
    *prefix_ptr_out = module_prefix.ptr;
    *prefix_len_out = module_prefix.len;
    return 0;
}

func compiler_merge_program_consts(prog: *AstProgram) -> u64 {
    var consts: *Vec<*AstConstDecl> = prog.consts_vec;
    var num_consts: u64 = consts.len();
    for (var ci: u64 = 0; ci < num_consts; ci++) {
        g_all_consts.push(consts.get(ci));
    }
    if (num_consts != 0) { compiler_invalidate_const_lookup(); }
    return 0;
}

func compiler_merge_program_funcs(prog: *AstProgram) -> u64 {
    var funcs: *Vec<*AstFunc> = prog.funcs_vec;
    var num_funcs: u64 = funcs.len();
    for (var fi: u64 = 0; fi < num_funcs; fi++) {
        g_all_funcs.push(funcs.get(fi));
    }
    if (num_funcs != 0) { compiler_invalidate_func_lookup(); }
    return 0;
}

func compiler_merge_program_globals(prog: *AstProgram) -> u64 {
    var globals: *Vec<*GlobalInfo> = prog.globals_vec;
    if (globals == 0) { return 0; }

    var num_globals: u64 = globals.len();
    for (var gi: u64 = 0; gi < num_globals; gi++) {
        g_all_globals.push(globals.get(gi));
    }
    if (num_globals != 0) { compiler_invalidate_global_lookup(); }
    return 0;
}

func compiler_register_program_structs(prog: *AstProgram) -> u64 {
    var structs: *Vec<*AstStructDef> = prog.structs_vec;
    if (structs == 0) { return 0; }

    var num_structs: u64 = structs.len();
    for (var si: u64 = 0; si < num_structs; si++) {
        var struct_info: *AstStructDef = structs.get(si);
        g_all_structs.put(struct_info.name_ptr, struct_info.name_len, struct_info);
    }
    return 0;
}

func compiler_merge_module_program(file_path: u64, file_path_len: u64, prog: *AstProgram, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64) -> u64 {
    register_module_exports(prog, module_ptr, module_len, prefix_ptr, prefix_len);
    compiler_generic_update_mangled_names(prog);

    g_module_imports_pass2.put(file_path, file_path_len, prog.imports_vec);
    compiler_merge_program_consts(prog);
    compiler_merge_program_funcs(prog);
    compiler_merge_program_globals(prog);
    compiler_register_program_structs(prog);
    return 0;
}

func load_module(file_path: u64, file_path_len: u64) -> u64 {
    if (g_loaded_modules.has(file_path, file_path_len)) {
        return true;
    }

    if (!load_module_pass1(file_path, file_path_len)) {
        return false;
    }

    compiler_mark_module_loaded(file_path, file_path_len);
    if (!compiler_load_module_import_deps_pass1(file_path, file_path_len)) { return false; }

    var tokens: *Vec<*Token> = compiler_get_cached_module_tokens(file_path, file_path_len);
    if (tokens == 0) {
        compiler_report_module_open_error(file_path, file_path_len);
        return false;
    }
    if (compiler_set_current_source(file_path, file_path_len) == 0) {
        compiler_report_module_open_error(file_path, file_path_len);
        return false;
    }
    
    // Module identity for mangling
    var module_ptr: u64 = 0;
    var module_len: u64 = 0;
    var prefix_ptr: u64 = 0;
    var prefix_len: u64 = 0;
    compiler_module_identity(file_path, file_path_len, &module_ptr, &module_len, &prefix_ptr, &prefix_len);
    
    // Pass 2: full parse with bodies
    var p: *Parser = parse_new(tokens);
    var prog: *AstProgram = parse_program(p);
    compiler_merge_module_program(file_path, file_path_len, prog, module_ptr, module_len, prefix_ptr, prefix_len);

    return true;
}

// ============================================
// Helper functions for parser
// ============================================

// Check if a name is a registered struct type
func is_struct_type(name_ptr: u64, name_len: u64) -> u64 {
    var struct_def: *AstStructDef = g_all_structs.?get(name_ptr, name_len);
    if (struct_def == 0) { return false; }
    return true;
}

// Get struct definition by name
func get_struct_def(name_ptr: u64, name_len: u64) -> *AstStructDef {
    return g_all_structs.?get(name_ptr, name_len);
}

// Register a struct type during parsing
func register_struct_type(struct_def: *AstStructDef) -> u64 {
    if (g_all_structs == 0) {
        g_all_structs = new HashMap<u64, *AstStructDef>(64);
    }
    if (g_all_structs_vec == 0) {
        g_all_structs_vec = new Vec<*AstStructDef>(16);
    }
    var struct_info: *AstStructDef = struct_def;
    var struct_name_ptr: u64 = struct_info.name_ptr;
    var struct_name_len: u64 = struct_info.name_len;
    if (compiler_is_trait_name(struct_name_ptr, struct_name_len) != 0) {
        emit_stderr("[ERROR] Struct name conflicts with trait: ");
        emit_stderr_len(struct_name_ptr, struct_name_len);
        emit_stderr("\n");
        panic("Parse error");
    }
    g_all_structs.put(struct_name_ptr, struct_name_len, struct_info);
    g_all_structs_vec.push(struct_info);
}

// ============================================
// Trait Registry
// ============================================

func compiler_register_trait_def(trait_def: *TraitDef) -> u64 {
    if (trait_def == 0) { return 0; }
    if (g_all_traits == 0) {
        g_all_traits = new HashMap<u64, *TraitDef>(64);
    }
    if (g_all_traits_vec == 0) {
        g_all_traits_vec = new Vec<*TraitDef>(16);
    }
    var name_ptr: u64 = trait_def.name_ptr;
    var name_len: u64 = trait_def.name_len;
    var existing: *TraitDef = g_all_traits.get(name_ptr, name_len);
    if (existing != 0) {
        existing.name_ptr = name_ptr;
        existing.name_len = name_len;
        existing.methods_vec = trait_def.methods_vec;
        return 0;
    }
    if (g_all_structs != 0) {
        var struct_def: *AstStructDef = g_all_structs.?get(name_ptr, name_len);
        if (struct_def != 0) {
            emit_stderr("[ERROR] Trait name conflicts with struct: ");
            emit_stderr_len(name_ptr, name_len);
            emit_stderr("\n");
            panic("Parse error");
        }
    }
    g_all_traits.put(name_ptr, name_len, trait_def);
    g_all_traits_vec.push(trait_def);
    return 0;
}

func compiler_get_trait_def(name_ptr: u64, name_len: u64) -> *TraitDef {
    if (g_all_traits == 0) { return 0; }
    return g_all_traits.?get(name_ptr, name_len);
}

func compiler_is_trait_name(name_ptr: u64, name_len: u64) -> u64 {
    var td: *TraitDef = compiler_get_trait_def(name_ptr, name_len);
    if (td == 0) { return false; }
    return true;
}

func compiler_register_trait_impl(trait_impl: *TraitImpl) -> u64 {
    if (trait_impl == 0) { return 0; }
    if (g_all_trait_impls_vec == 0) {
        g_all_trait_impls_vec = new Vec<*TraitImpl>(32);
    }
    var existing: *TraitImpl = compiler_find_trait_impl(trait_impl.trait_ptr, trait_impl.trait_len, trait_impl.struct_ptr, trait_impl.struct_len);
    if (existing != 0) {
        if (trait_impl.methods_vec != 0) {
            if (existing.methods_vec == 0) {
                existing.methods_vec = trait_impl.methods_vec;
            } else {
                var n: u64 = trait_impl.methods_vec.len();
                for (var i: u64 = 0; i < n; i++) {
                    var m: *TraitImplMethod = trait_impl.methods_vec.get(i);
                    if (compiler_find_trait_impl_method(existing, m.name_ptr, m.name_len) == 0) {
                        existing.methods_vec.push(m);
                    }
                }
            }
        }
        return 0;
    }
    g_all_trait_impls_vec.push(trait_impl);
    return 0;
}

func compiler_get_trait_impls() -> *Vec<*TraitImpl> {
    return g_all_trait_impls_vec;
}

// ============================================
// Generic Template Registry
// ============================================

func compiler_get_or_create_generic_template(name_ptr: u64, name_len: u64, params_vec: *Vec<*GenericParam>) -> *GenericFuncTemplate {
    if (g_generic_funcs == 0) { g_generic_funcs = new HashMap<u64, *GenericFuncTemplate>(64); }
    var existing: *GenericFuncTemplate = g_generic_funcs.get(name_ptr, name_len);
    if (existing != 0) { return existing; }

    var tpl: *GenericFuncTemplate = new GenericFuncTemplate();
    tpl.name_ptr = name_ptr;
    tpl.name_len = name_len;
    tpl.mangled_ptr = 0;
    tpl.mangled_len = 0;
    tpl.params_vec = params_vec;
    tpl.func_sig = 0;
    tpl.func_def = 0;
    g_generic_funcs.put(name_ptr, name_len, tpl);
    return tpl;
}

func compiler_get_or_create_generic_struct_template(name_ptr: u64, name_len: u64, params_vec: *Vec<*GenericParam>) -> *GenericStructTemplate {
    if (g_generic_structs == 0) { g_generic_structs = new HashMap<u64, *GenericStructTemplate>(64); }
    var existing: *GenericStructTemplate = g_generic_structs.get(name_ptr, name_len);
    if (existing != 0) { return existing; }

    var tpl: *GenericStructTemplate = new GenericStructTemplate();
    tpl.name_ptr = name_ptr;
    tpl.name_len = name_len;
    tpl.params_vec = params_vec;
    tpl.struct_def = 0;
    g_generic_structs.put(name_ptr, name_len, tpl);
    return tpl;
}

func compiler_register_generic_struct_def(name_ptr: u64, name_len: u64, params_vec: *Vec<*GenericParam>, struct_def: *AstStructDef) -> u64 {
    if (params_vec == 0 || params_vec.len() == 0) { return 0; }
    var tpl: *GenericStructTemplate = compiler_get_or_create_generic_struct_template(name_ptr, name_len, params_vec);
    tpl.params_vec = params_vec;
    tpl.struct_def = struct_def;
    if (g_generic_struct_ptrs == 0) { g_generic_struct_ptrs = new Vec<*GenericStructTemplate>(16); }
    g_generic_struct_ptrs.push(tpl);
    if (GENERIC_DEBUG != 0) {
        emit_stderr("[DEBUG] generic struct template: ");
        emit_stderr_len(name_ptr, name_len);
        emit_stderr("\n");
    }
    return 0;
}

func compiler_find_generic_struct_template_by_name(name_ptr: u64, name_len: u64) -> *GenericStructTemplate {
    return g_generic_structs.?get(name_ptr, name_len);
}

func compiler_is_generic_struct_def(struct_def: *AstStructDef) -> u64 {
    if (g_generic_struct_ptrs == 0) { return false; }
    var n: u64 = g_generic_struct_ptrs.len();
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericStructTemplate = g_generic_struct_ptrs.get(i);
        if (tpl.struct_def == struct_def) { return true; }
    }
    return false;
}

func compiler_register_generic_struct_bindings(inst_ptr: u64, inst_len: u64, bindings: *Vec<*GenericBinding>) -> u64 {
    if (inst_ptr == 0 || inst_len == 0 || bindings == 0) { return 0; }
    if (g_generic_struct_bindings == 0) { g_generic_struct_bindings = new HashMap<u64, *Vec<*GenericBinding>>(64); }
    if (g_generic_struct_bindings.has(inst_ptr, inst_len) != 0) { return 0; }
    g_generic_struct_bindings.put(inst_ptr, inst_len, bindings);
    return 0;
}

func compiler_get_generic_struct_bindings(inst_ptr: u64, inst_len: u64) -> *Vec<*GenericBinding> {
    if (g_generic_struct_bindings == 0) { return 0; }
    return g_generic_struct_bindings.get(inst_ptr, inst_len);
}

func compiler_find_generic_suffix_index(name_ptr: u64, name_len: u64) -> u64 {
    if (name_ptr == 0 || name_len < 5) { return name_len; }
    for (var i: u64 = 0; i + 5 <= name_len; i++) {
        if (str_eq(name_ptr + i, 5, "__G__", 5)) { return i; }
    }
    return name_len;
}

func compiler_try_kind_from_typeinfo(ti: *TypeInfo) -> u64 {
    if (ti == 0) { return COMPILER_TRY_KIND_INVALID; }
    if (ti.type_kind != TYPE_STRUCT || ti.ptr_depth != 0) { return COMPILER_TRY_KIND_INVALID; }
    var base_len: u64 = compiler_find_generic_suffix_index(ti.struct_name_ptr, ti.struct_name_len);
    if (str_eq(ti.struct_name_ptr, base_len, "Option", 6)) { return COMPILER_TRY_KIND_OPTION; }
    if (str_eq(ti.struct_name_ptr, base_len, "Result", 6)) { return COMPILER_TRY_KIND_RESULT; }
    return COMPILER_TRY_KIND_INVALID;
}

func compiler_try_field_names(kind: u64, out_check_ptr: *u64, out_check_len: *u64, out_value_ptr: *u64, out_value_len: *u64) -> u64 {
    if (kind == COMPILER_TRY_KIND_OPTION) {
        *out_check_ptr = (u64)"is_some";
        *out_check_len = 7;
        *out_value_ptr = (u64)"value";
        *out_value_len = 5;
        return true;
    }
    if (kind == COMPILER_TRY_KIND_RESULT) {
        *out_check_ptr = (u64)"is_ok";
        *out_check_len = 5;
        *out_value_ptr = (u64)"ok";
        *out_value_len = 2;
        return true;
    }
    *out_check_ptr = 0;
    *out_check_len = 0;
    *out_value_ptr = 0;
    *out_value_len = 0;
    return false;
}

func compiler_try_return_matches_type(value_ti: *TypeInfo, ret_type: u64, ret_ptr_depth: u64, ret_struct_name_ptr: u64, ret_struct_name_len: u64) -> u64 {
    if (compiler_try_kind_from_typeinfo(value_ti) == COMPILER_TRY_KIND_INVALID) { return false; }
    if (ret_type != TYPE_STRUCT || ret_ptr_depth != 0) { return false; }
    if (str_eq(ret_struct_name_ptr, ret_struct_name_len, value_ti.struct_name_ptr, value_ti.struct_name_len) == 0) { return false; }
    return true;
}

func compiler_try_resolve_struct_def(value_ti: *TypeInfo) -> *AstStructDef {
    if (value_ti == 0) { return 0; }
    var struct_def: *AstStructDef = value_ti.struct_def;
    if (struct_def == 0) {
        struct_def = get_struct_def(value_ti.struct_name_ptr, value_ti.struct_name_len);
    }
    return struct_def;
}

func compiler_safe_receiver_mode(recv_ti: *TypeInfo) -> u64 {
    if (recv_ti == 0) { return COMPILER_SAFE_RECV_INVALID; }
    if (recv_ti.ptr_depth == 0) { return COMPILER_SAFE_RECV_INVALID; }
    if (recv_ti.type_kind == TYPE_TRAIT || compiler_is_trait_name(recv_ti.struct_name_ptr, recv_ti.struct_name_len) != 0) {
        return COMPILER_SAFE_RECV_TRAIT_PTR;
    }
    if (recv_ti.type_kind == TYPE_STRUCT) { return COMPILER_SAFE_RECV_STRUCT_PTR; }
    return COMPILER_SAFE_RECV_INVALID;
}

func compiler_generic_adopt_impl_params(params_vec: *Vec<*GenericParam>, name_ptr: u64, name_len: u64, is_def: u64) -> *Vec<*GenericParam> {
    if (params_vec != 0 && params_vec.len() != 0) { return params_vec; }
    if (compiler_is_impl_context() == 0) { return params_vec; }

    var tpl: *GenericStructTemplate = compiler_find_generic_struct_template_by_name(g_impl_prefix_ptr, g_impl_prefix_len);
    if (tpl == 0 || tpl.params_vec == 0 || tpl.params_vec.len() == 0) { return params_vec; }

    if (GENERIC_DEBUG != 0) {
        if (is_def != 0) {
            emit_stderr("[DEBUG] impl generic params adopted for func def: ");
        } else {
            emit_stderr("[DEBUG] impl generic params adopted for func sig: ");
        }
        emit_stderr_len(name_ptr, name_len);
        emit_stderr("\n");
    }
    return tpl.params_vec;
}

func compiler_generic_register_name(name_ptr: u64, name_len: u64) -> *NameInfo {
    if (g_impl_prefix_ptr == 0 || g_impl_prefix_len == 0) {
        return new NameInfo{name_ptr, name_len};
    }
    return module_util_build_joined_name(g_impl_prefix_ptr, g_impl_prefix_len, name_ptr, name_len);
}

func compiler_register_generic_func_sig(name_ptr: u64, name_len: u64, params_vec: *Vec<*GenericParam>, func_sig: *AstFunc) -> u64 {
    params_vec = compiler_generic_adopt_impl_params(params_vec, name_ptr, name_len, 0);
    if (params_vec == 0 || params_vec.len() == 0) { return 0; }
    var reg_name: *NameInfo = compiler_generic_register_name(name_ptr, name_len);
    var tpl: *GenericFuncTemplate = compiler_get_or_create_generic_template(reg_name.ptr, reg_name.len, params_vec);
    tpl.params_vec = params_vec;
    tpl.func_sig = func_sig;
    return 0;
}

func compiler_register_generic_func_def(name_ptr: u64, name_len: u64, params_vec: *Vec<*GenericParam>, func_def: *AstFunc) -> u64 {
    params_vec = compiler_generic_adopt_impl_params(params_vec, name_ptr, name_len, 1);
    if (params_vec == 0 || params_vec.len() == 0) { return 0; }
    var reg_name: *NameInfo = compiler_generic_register_name(name_ptr, name_len);
    var tpl: *GenericFuncTemplate = compiler_get_or_create_generic_template(reg_name.ptr, reg_name.len, params_vec);
    tpl.params_vec = params_vec;
    tpl.func_def = func_def;
    if (g_generic_func_ptrs == 0) { g_generic_func_ptrs = new Vec<*GenericFuncTemplate>(16); }
    g_generic_func_ptrs.push(tpl);
    if (GENERIC_DEBUG != 0) {
        emit_stderr("[DEBUG] generic template: ");
        emit_stderr_len(name_ptr, name_len);
        emit_stderr("\n");
    }
    return 0;
}

func compiler_set_impl_prefix(prefix_ptr: u64, prefix_len: u64) -> u64 {
    g_impl_prefix_ptr = prefix_ptr;
    g_impl_prefix_len = prefix_len;
    return 0;
}

func compiler_clear_impl_prefix() -> u64 {
    g_impl_prefix_ptr = 0;
    g_impl_prefix_len = 0;
    return 0;
}

func compiler_is_impl_context() -> u64 {
    if (g_impl_prefix_ptr != 0 && g_impl_prefix_len != 0) { return true; }
    return false;
}

func compiler_impl_has_generic_param(name_ptr: u64, name_len: u64) -> u64 {
    if (compiler_is_impl_context() == 0) { return false; }
    var tpl: *GenericStructTemplate = compiler_find_generic_struct_template_by_name(g_impl_prefix_ptr, g_impl_prefix_len);
    if (tpl == 0 || tpl.params_vec == 0) { return false; }
    var n: u64 = tpl.params_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var gp: *GenericParam = tpl.params_vec.get(i);
        if (str_eq(gp.name_ptr, gp.name_len, name_ptr, name_len) != 0) {
            return true;
        }
    }
    return false;
}

func compiler_get_impl_prefix_ptr() -> u64 {
    return g_impl_prefix_ptr;
}

func compiler_get_impl_prefix_len() -> u64 {
    return g_impl_prefix_len;
}

func compiler_set_current_access_struct(struct_ptr: u64, struct_len: u64) -> u64 {
    g_current_access_struct_ptr = struct_ptr;
    g_current_access_struct_len = struct_len;
    return 0;
}

func compiler_clear_current_access_struct() -> u64 {
    g_current_access_struct_ptr = 0;
    g_current_access_struct_len = 0;
    return 0;
}

func compiler_get_current_access_struct_ptr() -> u64 {
    return g_current_access_struct_ptr;
}

func compiler_get_current_access_struct_len() -> u64 {
    return g_current_access_struct_len;
}

func compiler_set_current_access_struct_from_func(func_ptr: u64, func_len: u64) -> u64 {
    compiler_clear_current_access_struct();
    if (g_impl_method_owner == 0) { return 0; }
    var owner: *NameInfo = g_impl_method_owner.get(func_ptr, func_len);
    if (owner == 0) { return 0; }
    compiler_set_current_access_struct(owner.ptr, owner.len);
    return 0;
}

func compiler_register_impl_method_ex(name_ptr: u64, name_len: u64, owner_struct_ptr: u64, owner_struct_len: u64, access: u64) -> u64 {
    if (name_ptr == 0 || name_len == 0) { return 0; }
    if (g_impl_methods == 0) { g_impl_methods = new HashMap<u64, u64>(64); }
    if (g_impl_method_access == 0) { g_impl_method_access = new HashMap<u64, u64>(64); }
    if (g_impl_method_owner == 0) { g_impl_method_owner = new HashMap<u64, *NameInfo>(64); }
    g_impl_methods.put(name_ptr, name_len, 1);
    g_impl_method_access.put(name_ptr, name_len, access);
    if (owner_struct_ptr != 0 && owner_struct_len != 0) {
        g_impl_method_owner.put(name_ptr, name_len, new NameInfo{owner_struct_ptr, owner_struct_len});
    }
    return 0;
}

func compiler_register_impl_method_func(name_ptr: u64, name_len: u64, fn: *AstFunc) -> u64 {
    if (name_ptr == 0 || name_len == 0 || fn == 0) { return 0; }
    if (g_impl_method_funcs == 0) { g_impl_method_funcs = new HashMap<u64, *AstFunc>(64); }
    g_impl_method_funcs.put(name_ptr, name_len, fn);
    return 0;
}

func compiler_register_impl_method(name_ptr: u64, name_len: u64) -> u64 {
    return compiler_register_impl_method_ex(name_ptr, name_len, 0, 0, ACCESS_PUBLIC);
}

func compiler_finalize_impl_method_name_if_needed(fn: *AstFunc) -> u64 {
    if (fn == 0) { return 0; }
    if (fn.impl_owner_ptr == 0 || fn.impl_owner_len == 0) { return 0; }
    if (fn.impl_name_finalized != 0) { return 0; }

    var original_name_ptr: u64 = fn.impl_original_name_ptr;
    var original_name_len: u64 = fn.impl_original_name_len;
    if (original_name_ptr == 0 || original_name_len == 0) {
        original_name_ptr = fn.name_ptr;
        original_name_len = fn.name_len;
        fn.impl_original_name_ptr = original_name_ptr;
        fn.impl_original_name_len = original_name_len;
    }

    var name_info: *NameInfo = compiler_build_method_name(fn.impl_owner_ptr, fn.impl_owner_len, original_name_ptr, original_name_len);
    fn.name_ptr = name_info.ptr;
    fn.name_len = name_info.len;
    fn.impl_name_finalized = 1;
    return 0;
}

func compiler_register_finalized_impl_method(fn: *AstFunc) -> u64 {
    if (fn == 0) { return 0; }
    if (fn.impl_owner_ptr == 0 || fn.impl_owner_len == 0) { return 0; }
    compiler_register_impl_method_ex(fn.name_ptr, fn.name_len, fn.impl_owner_ptr, fn.impl_owner_len, fn.impl_method_access);
    compiler_register_impl_method_func(fn.name_ptr, fn.name_len, fn);
    compiler_alias_generic_template_by_func_ptr(fn, fn.name_ptr, fn.name_len);

    var resolved_method: *NameInfo = resolve_name(fn.name_ptr, fn.name_len);
    if (resolved_method != 0) {
        compiler_alias_generic_template_by_func_ptr(fn, resolved_method.ptr, resolved_method.len);
    }
    return 0;
}

func compiler_get_impl_method_access(name_ptr: u64, name_len: u64) -> u64 {
    if (g_impl_method_access == 0) { return ACCESS_PUBLIC; }
    if (g_impl_method_access.has(name_ptr, name_len) == 0) { return ACCESS_PUBLIC; }
    return g_impl_method_access.get(name_ptr, name_len);
}

func compiler_get_impl_method_owner(name_ptr: u64, name_len: u64) -> *NameInfo {
    if (g_impl_method_owner == 0) { return 0; }
    return g_impl_method_owner.get(name_ptr, name_len);
}

func compiler_get_impl_method_func(name_ptr: u64, name_len: u64) -> *AstFunc {
    if (g_impl_method_funcs == 0) { return 0; }
    return g_impl_method_funcs.get(name_ptr, name_len);
}

func compiler_abst_method_access_map_ensure(struct_ptr: u64, struct_len: u64) -> *HashMap<u64, u64> {
    if (g_abst_methods_access == 0) {
        g_abst_methods_access = new HashMap<u64, *HashMap<u64, u64>>(32);
    }
    var m: *HashMap<u64, u64> = g_abst_methods_access.get(struct_ptr, struct_len);
    if (m != 0) { return m; }
    var out: *HashMap<u64, u64> = new HashMap<u64, u64>(16);
    g_abst_methods_access.put(struct_ptr, struct_len, out);
    return out;
}

func compiler_abst_method_sigs_map_ensure(struct_ptr: u64, struct_len: u64) -> *HashMap<u64, *AstFunc> {
    if (g_abst_methods_sigs == 0) {
        g_abst_methods_sigs = new HashMap<u64, *HashMap<u64, *AstFunc>>(32);
    }
    var m: *HashMap<u64, *AstFunc> = g_abst_methods_sigs.get(struct_ptr, struct_len);
    if (m != 0) { return m; }
    var out: *HashMap<u64, *AstFunc> = new HashMap<u64, *AstFunc>(16);
    g_abst_methods_sigs.put(struct_ptr, struct_len, out);
    return out;
}

func compiler_register_abst_method_ex(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64, sig: *AstFunc, access: u64) -> u64 {
    if (struct_ptr == 0 || struct_len == 0 || method_ptr == 0 || method_len == 0) { return 0; }
    if (g_abst_methods == 0) { g_abst_methods = new HashMap<u64, *Vec<*NameInfo>>(32); }
    var list: *Vec<*NameInfo> = g_abst_methods.get(struct_ptr, struct_len);
    if (list == 0) {
        list = new Vec<*NameInfo>(4);
        g_abst_methods.put(struct_ptr, struct_len, list);
    }
    var n: u64 = list.len();
    for (var i: u64 = 0; i < n; i++) {
        var ni: *NameInfo = list.get(i);
        if (str_eq(ni.ptr, ni.len, method_ptr, method_len)) { return 0; }
    }
    var name_info: *NameInfo = new NameInfo{method_ptr, method_len};
    list.push(name_info);

    var access_map: *HashMap<u64, u64> = compiler_abst_method_access_map_ensure(struct_ptr, struct_len);
    access_map.put(method_ptr, method_len, access);

    if (sig != 0) {
        var sig_map: *HashMap<u64, *AstFunc> = compiler_abst_method_sigs_map_ensure(struct_ptr, struct_len);
        sig_map.put(method_ptr, method_len, sig);
    }
    return 0;
}

func compiler_register_abst_method(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> u64 {
    return compiler_register_abst_method_ex(struct_ptr, struct_len, method_ptr, method_len, 0, ACCESS_PRIVATE);
}

func compiler_get_abst_methods(struct_ptr: u64, struct_len: u64) -> *Vec<*NameInfo> {
    if (g_abst_methods == 0) { return 0; }
    return g_abst_methods.get(struct_ptr, struct_len);
}

func compiler_get_abst_method_access(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> u64 {
    if (g_abst_methods_access == 0) { return ACCESS_PRIVATE; }
    var access_map: *HashMap<u64, u64> = g_abst_methods_access.get(struct_ptr, struct_len);
    if (access_map == 0) { return ACCESS_PRIVATE; }
    if (access_map.has(method_ptr, method_len) == 0) { return ACCESS_PRIVATE; }
    return access_map.get(method_ptr, method_len);
}

func compiler_get_abst_method_sig(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> *AstFunc {
    if (g_abst_methods_sigs == 0) { return 0; }
    var sig_map: *HashMap<u64, *AstFunc> = g_abst_methods_sigs.get(struct_ptr, struct_len);
    if (sig_map == 0) { return 0; }
    return sig_map.get(method_ptr, method_len);
}

func compiler_is_abst_method(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> u64 {
    var list: *Vec<*NameInfo> = compiler_get_abst_methods(struct_ptr, struct_len);
    if (list == 0) { return false; }
    var n: u64 = list.len();
    for (var i: u64 = 0; i < n; i++) {
        var ni: *NameInfo = list.get(i);
        if (str_eq(ni.ptr, ni.len, method_ptr, method_len)) { return true; }
    }
    return false;
}

func compiler_is_impl_method(name_ptr: u64, name_len: u64) -> u64 {
    return g_impl_methods.?has(name_ptr, name_len);
}

func compiler_can_access_private_member_from(caller_struct_ptr: u64, caller_struct_len: u64, owner_struct_ptr: u64, owner_struct_len: u64) -> u64 {
    if (owner_struct_ptr == 0 || owner_struct_len == 0) { return false; }
    if (caller_struct_ptr == 0 || caller_struct_len == 0) { return false; }
    if (str_eq(owner_struct_ptr, owner_struct_len, caller_struct_ptr, caller_struct_len) != 0) { return true; }

    // Treat generic template and instantiated names as the same owner.
    // Example: Vec <-> Vec__G__T_u64
    var owner_base_len: u64 = compiler_find_generic_suffix_index(owner_struct_ptr, owner_struct_len);
    var caller_base_len: u64 = compiler_find_generic_suffix_index(caller_struct_ptr, caller_struct_len);
    if (str_eq(owner_struct_ptr, owner_base_len, caller_struct_ptr, caller_base_len) != 0) { return true; }
    return false;
}

func compiler_can_access_protected_member_from(caller_struct_ptr: u64, caller_struct_len: u64, owner_struct_ptr: u64, owner_struct_len: u64) -> u64 {
    if (compiler_can_access_private_member_from(caller_struct_ptr, caller_struct_len, owner_struct_ptr, owner_struct_len) != 0) {
        return true;
    }

    if (compiler_struct_is_same_or_descendant(caller_struct_ptr, caller_struct_len, owner_struct_ptr, owner_struct_len) != 0) {
        return true;
    }

    var owner_base_len: u64 = compiler_find_generic_suffix_index(owner_struct_ptr, owner_struct_len);
    var caller_base_len: u64 = compiler_find_generic_suffix_index(caller_struct_ptr, caller_struct_len);
    if (compiler_struct_is_same_or_descendant(caller_struct_ptr, caller_struct_len, owner_struct_ptr, owner_base_len) != 0) {
        return true;
    }
    if (compiler_struct_is_same_or_descendant(caller_struct_ptr, caller_base_len, owner_struct_ptr, owner_struct_len) != 0) {
        return true;
    }
    if (compiler_struct_is_same_or_descendant(caller_struct_ptr, caller_base_len, owner_struct_ptr, owner_base_len) != 0) {
        return true;
    }
    return false;
}

func compiler_can_access_member_from(caller_struct_ptr: u64, caller_struct_len: u64, owner_struct_ptr: u64, owner_struct_len: u64, access: u64) -> u64 {
    if (access == ACCESS_PUBLIC) { return true; }
    if (access == ACCESS_PRIVATE) {
        return compiler_can_access_private_member_from(caller_struct_ptr, caller_struct_len, owner_struct_ptr, owner_struct_len);
    }
    if (access == ACCESS_PROTECTED) {
        return compiler_can_access_protected_member_from(caller_struct_ptr, caller_struct_len, owner_struct_ptr, owner_struct_len);
    }
    return false;
}

func compiler_can_access_private_member(owner_struct_ptr: u64, owner_struct_len: u64) -> u64 {
    return compiler_can_access_private_member_from(g_current_access_struct_ptr, g_current_access_struct_len, owner_struct_ptr, owner_struct_len);
}

func compiler_can_access_protected_member(owner_struct_ptr: u64, owner_struct_len: u64) -> u64 {
    return compiler_can_access_protected_member_from(g_current_access_struct_ptr, g_current_access_struct_len, owner_struct_ptr, owner_struct_len);
}

func compiler_can_access_member(owner_struct_ptr: u64, owner_struct_len: u64, access: u64) -> u64 {
    return compiler_can_access_member_from(g_current_access_struct_ptr, g_current_access_struct_len, owner_struct_ptr, owner_struct_len, access);
}

func compiler_is_generic_func_ptr(fn: *AstFunc) -> u64 {
    if (fn == 0) { return false; }
    var n: u64 = g_generic_func_ptrs.?len();
    if (n == 0) { return false; }
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericFuncTemplate = g_generic_func_ptrs.get(i);
        if (tpl.func_def == fn) { return true; }
    }
    return false;
}

func compiler_find_generic_template_by_name(name_ptr: u64, name_len: u64) -> *GenericFuncTemplate {
    return g_generic_funcs.?get(name_ptr, name_len);
}

func compiler_find_generic_template_by_mangled(name_ptr: u64, name_len: u64) -> *GenericFuncTemplate {
    return g_generic_funcs_mangled.?get(name_ptr, name_len);
}

func compiler_find_generic_template_by_func_ptr(fn_ptr: *AstFunc) -> *GenericFuncTemplate {
    var n: u64 = g_generic_func_ptrs.?len();
    if (n == 0) { return 0; }
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericFuncTemplate = g_generic_func_ptrs.get(i);
        if (tpl.func_def == fn_ptr) { return tpl; }
        if (tpl.func_sig == fn_ptr) { return tpl; }
    }
    return 0;
}

func compiler_alias_generic_template_by_func_ptr(fn_ptr: *AstFunc, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var tpl_ptr: *GenericFuncTemplate = compiler_find_generic_template_by_func_ptr(fn_ptr);
    if (tpl_ptr == 0) { return 0; }
    if (g_generic_funcs == 0) { g_generic_funcs = new HashMap<u64, *GenericFuncTemplate>(64); }
    var tpl: *GenericFuncTemplate = tpl_ptr;
    g_generic_funcs.put(new_name_ptr, new_name_len, tpl);
    return 0;
}

func compiler_generic_update_mangled_names(prog: *AstProgram) -> u64 {
    if (g_generic_func_ptrs == 0) { return 0; }
    var funcs: *Vec<*AstFunc> = prog.funcs_vec;
    var n: u64 = funcs.?len();
    if (n == 0) { return 0; }
    for (var i: u64 = 0; i < n; i++) {
        var fn: *AstFunc = funcs.get(i);
        var tpl_ptr: *GenericFuncTemplate = compiler_find_generic_template_by_func_ptr(fn);
        if (tpl_ptr == 0) { continue; }
        var tpl: *GenericFuncTemplate = tpl_ptr;
        tpl.mangled_ptr = fn.name_ptr;
        tpl.mangled_len = fn.name_len;
        if (g_generic_funcs_mangled == 0) { g_generic_funcs_mangled = new HashMap<u64, *GenericFuncTemplate>(64); }
        g_generic_funcs_mangled.put(tpl.mangled_ptr, tpl.mangled_len, tpl);
    }
    return 0;
}

// ============================================
// Generic Monomorphization
// ============================================

// Append a string to a vec<u8>
func generic_vec_append_str(v: *Vec<u64>, s_ptr: u64, s_len: u64) -> u64 {
    var s_u8: []u8 = slice(s_ptr, s_len);
    for (var i: u64 = 0; i < s_len; i++) {
        v.push(s_u8[i]);
    }
    return 0;
}

// Append unsigned integer to vec as decimal
func generic_vec_append_u64(v: *Vec<u64>, value: u64) -> u64 {
    if (value == 0) {
        v.push(48);
        return 0;
    }
    var buf: u64 = heap_alloc(32 * sizeof(u8));
    var buf_u8: []u8 = slice(buf, 32);
    var idx: u64 = 0;
    var x: u64 = value;
    while (x > 0) {
        var digit: u64 = x % 10;
        buf_u8[idx] = 48 + digit;
        idx = idx + 1;
        x = x / 10;
    }
    for (var i: u64 = 0; i < idx; i++) {
        v.push(buf_u8[idx - 1 - i]);
    }
    return 0;
}

func generic_type_equal(ta: *TypeInfo, tb: *TypeInfo) -> u64 {
    if (ta.type_kind != tb.type_kind) { return false; }
    if (ta.ptr_depth != tb.ptr_depth) { return false; }
    if (ta.type_kind == TYPE_STRUCT) {
        if (!str_eq(ta.struct_name_ptr, ta.struct_name_len, tb.struct_name_ptr, tb.struct_name_len)) { return false; }
    }
    if (ta.type_kind == TYPE_ARRAY || ta.type_kind == TYPE_SLICE) {
        if (ta.elem_type_kind != tb.elem_type_kind) { return false; }
        if (ta.elem_ptr_depth != tb.elem_ptr_depth) { return false; }
        if (ta.type_kind == TYPE_ARRAY && ta.array_len != tb.array_len) { return false; }
        if (!str_eq(ta.struct_name_ptr, ta.struct_name_len, tb.struct_name_ptr, tb.struct_name_len)) { return false; }
    }
    return true;
}

func generic_find_binding(bindings: *Vec<*GenericBinding>, kind: u64, name_ptr: u64, name_len: u64) -> *GenericBinding {
    if (bindings == 0) { return 0; }
    var n: u64 = bindings.len();
    for (var i: u64 = 0; i < n; i++) {
        var b: *GenericBinding = bindings.get(i);
        if (b.kind == kind && str_eq(b.name_ptr, b.name_len, name_ptr, name_len)) {
            return b;
        }
    }
    return 0;
}

func generic_bind_type(bindings: *Vec<*GenericBinding>, name_ptr: u64, name_len: u64, type_ptr: *TypeInfo) -> u64 {
    var existing: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_TYPE, name_ptr, name_len);
    if (existing != 0) {
        if (!generic_type_equal(existing.type, type_ptr)) {
            return true;
        }
        return false;
    }
    var nb: *GenericBinding = new GenericBinding();
    nb.kind = GENERIC_KIND_TYPE;
    nb.name_ptr = name_ptr;
    nb.name_len = name_len;
    nb.type = type_ptr;
    nb.value = 0;
    bindings.push(nb);
    return false;
}

func generic_bind_value(bindings: *Vec<*GenericBinding>, name_ptr: u64, name_len: u64, value: u64) -> u64 {
    var existing: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_VALUE, name_ptr, name_len);
    if (existing != 0) {
        if (existing.value != value) {
            return true;
        }
        return false;
    }
    var nb: *GenericBinding = new GenericBinding();
    nb.kind = GENERIC_KIND_VALUE;
    nb.name_ptr = name_ptr;
    nb.name_len = name_len;
    nb.type = 0;
    nb.value = value;
    bindings.push(nb);
    return false;
}

func compiler_typeinfo_new(type_kind: u64, ptr_depth: u64) -> *TypeInfo {
    var ti: *TypeInfo = (*TypeInfo)heap_alloc(sizeof(TypeInfo));
    ti.type_kind = type_kind;
    ti.ptr_depth = ptr_depth;
    ti.is_tagged = 0;
    ti.struct_name_ptr = 0;
    ti.struct_name_len = 0;
    ti.tag_layout_ptr = 0;
    ti.tag_layout_len = 0;
    ti.struct_def = 0;
    ti.elem_type_kind = 0;
    ti.elem_ptr_depth = 0;
    ti.array_len = 0;
    ti.array_len_is_param = 0;
    ti.array_len_param_ptr = 0;
    ti.array_len_param_len = 0;
    return ti;
}

func generic_make_typeinfo_from_fields(type_kind: u64, ptr_depth: u64, is_tagged: u64, struct_name_ptr: u64, struct_name_len: u64, tag_layout_ptr: u64, tag_layout_len: u64, elem_type_kind: u64, elem_ptr_depth: u64, array_len: u64, array_len_is_param: u64, array_len_param_ptr: u64, array_len_param_len: u64) -> *TypeInfo {
    var ti: *TypeInfo = compiler_typeinfo_new(type_kind, ptr_depth);
    ti.is_tagged = is_tagged;
    ti.struct_name_ptr = struct_name_ptr;
    ti.struct_name_len = struct_name_len;
    ti.tag_layout_ptr = tag_layout_ptr;
    ti.tag_layout_len = tag_layout_len;
    ti.struct_def = 0;
    ti.elem_type_kind = elem_type_kind;
    ti.elem_ptr_depth = elem_ptr_depth;
    ti.array_len = array_len;
    ti.array_len_is_param = array_len_is_param;
    ti.array_len_param_ptr = array_len_param_ptr;
    ti.array_len_param_len = array_len_param_len;
    return ti;
}

func generic_subst_typeinfo(type_kind: u64, ptr_depth: u64, is_tagged: u64, struct_name_ptr: u64, struct_name_len: u64, tag_layout_ptr: u64, tag_layout_len: u64, elem_type_kind: u64, elem_ptr_depth: u64, array_len: u64, array_len_is_param: u64, array_len_param_ptr: u64, array_len_param_len: u64, bindings: *Vec<*GenericBinding>) -> *TypeInfo {
    if (type_kind == TYPE_GENERIC) {
        if (struct_name_ptr == 0 || struct_name_len == 0) {
            return generic_make_typeinfo_from_fields(TYPE_GENERIC, ptr_depth, is_tagged, struct_name_ptr, struct_name_len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
        }
        var b_ptr: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_TYPE, struct_name_ptr, struct_name_len);
        if (b_ptr == 0) {
            emit_stderr("[ERROR] Missing generic type binding: ");
            emit_stderr_len(struct_name_ptr, struct_name_len);
            emit_stderr(" len=");
            print_u64(struct_name_len);
            emit_stderr(" ptr=");
            print_u64(struct_name_ptr);
            emit_stderr("\n");
            panic("Parse error");
        }
        var bt: *TypeInfo = b_ptr.type;
        var merged_ptr_depth: u64 = bt.ptr_depth + ptr_depth;
        return generic_make_typeinfo_from_fields(bt.type_kind, merged_ptr_depth, bt.is_tagged, bt.struct_name_ptr, bt.struct_name_len, bt.tag_layout_ptr, bt.tag_layout_len, bt.elem_type_kind, bt.elem_ptr_depth, bt.array_len, bt.array_len_is_param, bt.array_len_param_ptr, bt.array_len_param_len);
    }

    if (bindings != 0 && type_kind == TYPE_STRUCT && struct_name_ptr != 0 && struct_name_len != 0) {
        var b_ptr2: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_TYPE, struct_name_ptr, struct_name_len);
        if (b_ptr2 != 0) {
            var bt2: *TypeInfo = b_ptr2.type;
            var merged_ptr_depth2: u64 = bt2.ptr_depth + ptr_depth;
            return generic_make_typeinfo_from_fields(bt2.type_kind, merged_ptr_depth2, bt2.is_tagged, bt2.struct_name_ptr, bt2.struct_name_len, bt2.tag_layout_ptr, bt2.tag_layout_len, bt2.elem_type_kind, bt2.elem_ptr_depth, bt2.array_len, bt2.array_len_is_param, bt2.array_len_param_ptr, bt2.array_len_param_len);
        }
    }

    if (type_kind == TYPE_STRUCT && bindings != 0) {
        var base_len: u64 = compiler_find_generic_suffix_index(struct_name_ptr, struct_name_len);
        if (base_len < struct_name_len) {
            var tpl_ptr: *GenericStructTemplate = compiler_find_generic_struct_template_by_name(struct_name_ptr, base_len);
            if (tpl_ptr != 0) {
                var name_info: *NameInfo = generic_struct_get_or_create_instance(tpl_ptr, bindings);
                return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, name_info.ptr, name_info.len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
            }
        }
    }

    if (type_kind == TYPE_ARRAY || type_kind == TYPE_SLICE) {
        var new_array_len: u64 = array_len;
        var new_array_len_is_param: u64 = array_len_is_param;
        var new_array_param_ptr: u64 = array_len_param_ptr;
        var new_array_param_len: u64 = array_len_param_len;
        if (array_len_is_param != 0) {
            var vb_ptr: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_VALUE, array_len_param_ptr, array_len_param_len);
            if (vb_ptr == 0) {
                emit_stderr("[ERROR] Missing generic value binding\n");
                panic("Parse error");
            }
            new_array_len = vb_ptr.value;
            new_array_len_is_param = 0;
            new_array_param_ptr = 0;
            new_array_param_len = 0;
        }

        var et: *TypeInfo = generic_subst_typeinfo(elem_type_kind, elem_ptr_depth, 0, struct_name_ptr, struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0, bindings);
        return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, et.struct_name_ptr, et.struct_name_len, tag_layout_ptr, tag_layout_len, et.type_kind, et.ptr_depth, new_array_len, new_array_len_is_param, new_array_param_ptr, new_array_param_len);
    }

    return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, struct_name_ptr, struct_name_len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
}

func generic_append_type_repr(v: *Vec<u64>, ti: *TypeInfo) -> u64 {
    if (ti.ptr_depth > 0) {
        v.push(112); // 'p'
        generic_vec_append_u64(v, ti.ptr_depth);
        v.push(95); // '_'
        var base: *TypeInfo = generic_make_typeinfo_from_fields(ti.type_kind, 0, ti.is_tagged, ti.struct_name_ptr, ti.struct_name_len, ti.tag_layout_ptr, ti.tag_layout_len, ti.elem_type_kind, ti.elem_ptr_depth, ti.array_len, ti.array_len_is_param, ti.array_len_param_ptr, ti.array_len_param_len);
        return generic_append_type_repr(v, base);
    }

    switch (ti.type_kind) {
        case TYPE_U8: generic_vec_append_str(v, "u8", 2); break;
        case TYPE_U16: generic_vec_append_str(v, "u16", 3); break;
        case TYPE_U32: generic_vec_append_str(v, "u32", 3); break;
        case TYPE_U64: generic_vec_append_str(v, "u64", 3); break;
        case TYPE_I64: generic_vec_append_str(v, "i64", 3); break;
        case TYPE_STRUCT:
            v.push(83); // 'S'
            var struct_name_u8: []u8 = slice(ti.struct_name_ptr, ti.struct_name_len);
            for (var i: u64 = 0; i < ti.struct_name_len; i++) {
                var c: u64 = struct_name_u8[i];
                if (c == 47 || c == 46) { c = 95; }
                v.push(c);
            }
            break;
        case TYPE_ARRAY:
            v.push(97); // 'a'
            generic_vec_append_u64(v, ti.array_len);
            v.push(95);
            var elem: *TypeInfo = generic_make_typeinfo_from_fields(ti.elem_type_kind, ti.elem_ptr_depth, 0, ti.struct_name_ptr, ti.struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            generic_append_type_repr(v, elem);
            break;
        case TYPE_SLICE:
            v.push(115); // 's'
            v.push(95);
            var elem2: *TypeInfo = generic_make_typeinfo_from_fields(ti.elem_type_kind, ti.elem_ptr_depth, 0, ti.struct_name_ptr, ti.struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            generic_append_type_repr(v, elem2);
            break;
        default:
            generic_vec_append_str(v, "t", 1);
            break;
    }
    return 0;
}

func generic_mangle_name(base_ptr: u64, base_len: u64, params_vec: *Vec<*GenericParam>, bindings: *Vec<*GenericBinding>) -> *NameInfo {
    var v: *Vec<u64> = new Vec<u64>(base_len + 32);
    generic_vec_append_str(v, base_ptr, base_len);
    generic_vec_append_str(v, "__G__", 5);
    var n: u64 = params_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var gp: *GenericParam = params_vec.get(i);
        if (gp.kind == GENERIC_KIND_TYPE) {
            v.push(84); // 'T'
            v.push(95);
            var b_ptr: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_TYPE, gp.name_ptr, gp.name_len);
            if (b_ptr == 0) {
                emit_stderr("[ERROR] Missing generic type binding\n");
                panic("Parse error");
            }
            generic_append_type_repr(v, b_ptr.type);
        } else {
            v.push(86); // 'V'
            v.push(95);
            var b2_ptr: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_VALUE, gp.name_ptr, gp.name_len);
            if (b2_ptr == 0) {
                emit_stderr("[ERROR] Missing generic value binding\n");
                panic("Parse error");
            }
            generic_vec_append_u64(v, b2_ptr.value);
        }

        if (i + 1 < n) { v.push(95); }
    }

    var out_len: u64 = v.len();
    var out_ptr: u64 = heap_alloc((out_len + 1) * sizeof(u8));
    var out_u8: []u8 = slice(out_ptr, out_len + 1);
    for (var i2: u64 = 0; i2 < out_len; i2++) {
        out_u8[i2] = v.get(i2);
    }
    out_u8[out_len] = 0;
    var info: *NameInfo = new NameInfo{out_ptr, out_len};
    return info;
}

func generic_make_typeinfo_from_param(param: *Param) -> *TypeInfo {
    return generic_make_typeinfo_from_fields(param.type_kind, param.ptr_depth, param.is_tagged, param.struct_name_ptr, param.struct_name_len, param.tag_layout_ptr, param.tag_layout_len, param.elem_type_kind, param.elem_ptr_depth, param.array_len, param.array_len_is_param, param.array_len_param_ptr, param.array_len_param_len);
}

func generic_make_typeinfo_from_vardecl(decl: *AstVarDecl) -> *TypeInfo {
    return generic_make_typeinfo_from_fields(decl.type_kind, decl.ptr_depth, decl.is_tagged, decl.struct_name_ptr, decl.struct_name_len, decl.tag_layout_ptr, decl.tag_layout_len, decl.elem_type_kind, decl.elem_ptr_depth, decl.array_len, decl.array_len_is_param, decl.array_len_param_ptr, decl.array_len_param_len);
}

func generic_infer_from_typeinfo(tk: u64, pd: u64, sn_ptr: u64, sn_len: u64, elem_k: u64, elem_pd: u64, array_len: u64, array_len_is_param: u64, array_param_ptr: u64, array_param_len: u64, arg_ti: *TypeInfo, bindings: *Vec<*GenericBinding>) -> u64 {
    if (arg_ti == 0) { return true; }
    if (arg_ti.type_kind == TYPE_VOID && arg_ti.ptr_depth == 0 && arg_ti.struct_name_ptr == 0 && arg_ti.struct_name_len == 0 &&
        arg_ti.elem_type_kind == 0 && arg_ti.elem_ptr_depth == 0 && arg_ti.array_len == 0) {
        return true;
    }

    if (tk == TYPE_GENERIC) {
        if (arg_ti.ptr_depth < pd) {
            return true;
        }
        var adj_ptr_depth: u64 = arg_ti.ptr_depth - pd;
        var bound: *TypeInfo = generic_make_typeinfo_from_fields(arg_ti.type_kind, adj_ptr_depth, arg_ti.is_tagged, arg_ti.struct_name_ptr, arg_ti.struct_name_len, arg_ti.tag_layout_ptr, arg_ti.tag_layout_len, arg_ti.elem_type_kind, arg_ti.elem_ptr_depth, arg_ti.array_len, arg_ti.array_len_is_param, arg_ti.array_len_param_ptr, arg_ti.array_len_param_len);
        if (generic_bind_type(bindings, sn_ptr, sn_len, bound) != 0) { return true; }
        return false;
    }

    if (tk == TYPE_ARRAY || tk == TYPE_SLICE) {
        if (arg_ti.type_kind != tk) {
            return true;
        }
        if (array_len_is_param != 0) {
            if (generic_bind_value(bindings, array_param_ptr, array_param_len, arg_ti.array_len) != 0) { return true; }
        } else if (tk == TYPE_ARRAY && array_len != arg_ti.array_len) {
            return true;
        }

        return generic_infer_from_typeinfo(elem_k, elem_pd, sn_ptr, sn_len, 0, 0, 0, 0, 0, 0, generic_make_typeinfo_from_fields(arg_ti.elem_type_kind, arg_ti.elem_ptr_depth, 0, arg_ti.struct_name_ptr, arg_ti.struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0), bindings);
    }

    if (tk != arg_ti.type_kind || pd != arg_ti.ptr_depth) {
        return true;
    }
    if (tk == TYPE_STRUCT && !str_eq(sn_ptr, sn_len, arg_ti.struct_name_ptr, arg_ti.struct_name_len)) {
        return true;
    }
    return false;
}

func _compiler_find_parent_def(struct_def: *AstStructDef, parent_ptr: u64, parent_len: u64, depth: u64) -> *AstStructDef {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }
    if (str_eq(struct_def.name_ptr, struct_def.name_len, parent_ptr, parent_len)) { return struct_def; }
    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    for (var i: u64 = 0; i < pn; i++) {
        var parent_desc: *ParentDesc = parents.get(i);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var found: *AstStructDef = _compiler_find_parent_def(parent_def, parent_ptr, parent_len, depth + 1);
        if (found != 0) { return found; }
    }
    return 0;
}

func compiler_virtual_dispatch_bucket_ensure(struct_ptr: u64, struct_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_virtual_dispatch_traits == 0) {
        g_virtual_dispatch_traits = new HashMap<u64, *HashMap<u64, *NameInfo>>(32);
    }
    var bucket: *HashMap<u64, *NameInfo> = g_virtual_dispatch_traits.get(struct_ptr, struct_len);
    if (bucket != 0) { return bucket; }
    var out: *HashMap<u64, *NameInfo> = new HashMap<u64, *NameInfo>(16);
    g_virtual_dispatch_traits.put(struct_ptr, struct_len, out);
    return out;
}

func compiler_register_virtual_dispatch_trait(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64, trait_ptr: u64, trait_len: u64) -> u64 {
    if (struct_ptr == 0 || struct_len == 0 || method_ptr == 0 || method_len == 0 || trait_ptr == 0 || trait_len == 0) { return 0; }
    var bucket: *HashMap<u64, *NameInfo> = compiler_virtual_dispatch_bucket_ensure(struct_ptr, struct_len);
    bucket.put(method_ptr, method_len, new NameInfo{trait_ptr, trait_len});
    return 0;
}

func _compiler_find_virtual_dispatch_trait_in_struct(struct_def: *AstStructDef, method_ptr: u64, method_len: u64, depth: u64) -> *NameInfo {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }

    if (g_virtual_dispatch_traits != 0) {
        var bucket: *HashMap<u64, *NameInfo> = g_virtual_dispatch_traits.get(struct_def.name_ptr, struct_def.name_len);
        if (bucket != 0) {
            var found: *NameInfo = bucket.get(method_ptr, method_len);
            if (found != 0) { return found; }
        }
    }

    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    for (var i: u64 = 0; i < pn; i++) {
        var parent_desc: *ParentDesc = parents.get(i);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var found_parent: *NameInfo = _compiler_find_virtual_dispatch_trait_in_struct(parent_def, method_ptr, method_len, depth + 1);
        if (found_parent != 0) { return found_parent; }
    }
    return 0;
}

func compiler_find_virtual_dispatch_trait(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> *NameInfo {
    var sd: *AstStructDef = get_struct_def(struct_ptr, struct_len);
    if (sd == 0) { return 0; }
    return _compiler_find_virtual_dispatch_trait_in_struct(sd, method_ptr, method_len, 0);
}

func _compiler_find_method_func_in_hierarchy(struct_def: *AstStructDef, method_ptr: u64, method_len: u64, depth: u64) -> *AstFunc {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }

    var method_name: *NameInfo = compiler_build_method_name(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
    var fn: *AstFunc = compiler_get_impl_method_func(method_name.ptr, method_name.len);
    if (fn == 0) {
        fn = compiler_get_func(method_name.ptr, method_name.len);
    }
    if (fn != 0) { return fn; }

    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    var found_fn: *AstFunc = 0;
    var found_count: u64 = 0;
    for (var i: u64 = 0; i < pn; i++) {
        var parent_desc: *ParentDesc = parents.get(i);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var cand: *AstFunc = _compiler_find_method_func_in_hierarchy(parent_def, method_ptr, method_len, depth + 1);
        if (cand != 0) {
            found_count = found_count + 1;
            if (found_count == 1) { found_fn = cand; }
        }
    }
    if (found_count > 1) {
        emit_stderr("[ERROR] Ambiguous method resolution in inheritance: ");
        emit_stderr_len(struct_def.name_ptr, struct_def.name_len);
        emit_stderr(".");
        emit_stderr_len(method_ptr, method_len);
        emit_stderr("\n");
        panic("Compiler error");
    }
    return found_fn;
}

func compiler_find_method_func_in_hierarchy(struct_def: *AstStructDef, method_ptr: u64, method_len: u64) -> *AstFunc {
    return _compiler_find_method_func_in_hierarchy(struct_def, method_ptr, method_len, 0);
}

func compiler_struct_is_same_or_descendant(child_ptr: u64, child_len: u64, ancestor_ptr: u64, ancestor_len: u64) -> u64 {
    if (str_eq(child_ptr, child_len, ancestor_ptr, ancestor_len) != 0) { return true; }
    var child_def: *AstStructDef = get_struct_def(child_ptr, child_len);
    if (child_def == 0) { return false; }
    var found: *AstStructDef = _compiler_find_parent_def(child_def, ancestor_ptr, ancestor_len, 0);
    if (found != 0) { return true; }
    return false;
}

func _compiler_find_method_decl_in_hierarchy(struct_def: *AstStructDef, method_ptr: u64, method_len: u64, depth: u64, owner_ptr_out: *u64, owner_len_out: *u64, access_out: *u64, sig_out: **AstFunc) -> u64 {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }

    var method_name: *NameInfo = compiler_build_method_name(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
    if (compiler_is_impl_method(method_name.ptr, method_name.len) != 0) {
        *owner_ptr_out = struct_def.name_ptr;
        *owner_len_out = struct_def.name_len;
        *access_out = compiler_get_impl_method_access(method_name.ptr, method_name.len);
        *sig_out = compiler_get_func(method_name.ptr, method_name.len);
        return 1;
    }

    if (compiler_is_abst_method(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len) != 0) {
        *owner_ptr_out = struct_def.name_ptr;
        *owner_len_out = struct_def.name_len;
        *access_out = compiler_get_abst_method_access(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
        *sig_out = compiler_get_abst_method_sig(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
        return 1;
    }

    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    var found: u64 = 0;
    var owner_ptr_tmp: u64 = 0;
    var owner_len_tmp: u64 = 0;
    var access_tmp: u64 = ACCESS_PUBLIC;
    var sig_tmp: *AstFunc = 0;
    for (var i: u64 = 0; i < pn; i++) {
        var parent_desc: *ParentDesc = parents.get(i);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var owner_ptr_c: u64 = 0;
        var owner_len_c: u64 = 0;
        var access_c: u64 = ACCESS_PUBLIC;
        var sig_c: *AstFunc = 0;
        if (_compiler_find_method_decl_in_hierarchy(parent_def, method_ptr, method_len, depth + 1, &owner_ptr_c, &owner_len_c, &access_c, &sig_c) != 0) {
            found = found + 1;
            if (found == 1) {
                owner_ptr_tmp = owner_ptr_c;
                owner_len_tmp = owner_len_c;
                access_tmp = access_c;
                sig_tmp = sig_c;
            }
        }
    }
    if (found > 1) {
        emit_stderr("[ERROR] Ambiguous method declaration in inheritance: ");
        emit_stderr_len(struct_def.name_ptr, struct_def.name_len);
        emit_stderr(".");
        emit_stderr_len(method_ptr, method_len);
        emit_stderr("\n");
        panic("Compiler error");
    }
    if (found == 0) { return 0; }

    *owner_ptr_out = owner_ptr_tmp;
    *owner_len_out = owner_len_tmp;
    *access_out = access_tmp;
    *sig_out = sig_tmp;
    return 1;
}

func compiler_find_method_decl_in_hierarchy(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64, owner_ptr_out: *u64, owner_len_out: *u64, access_out: *u64, sig_out: **AstFunc) -> u64 {
    var sd: *AstStructDef = get_struct_def(struct_ptr, struct_len);
    if (sd == 0) { return 0; }
    return _compiler_find_method_decl_in_hierarchy(sd, method_ptr, method_len, 0, owner_ptr_out, owner_len_out, access_out, sig_out);
}

func compiler_check_field_access(field_desc: *FieldDesc) -> u64 {
    if (field_desc == 0) { return 0; }
    return compiler_can_access_member(field_desc.owner_struct_ptr, field_desc.owner_struct_len, field_desc.access);
}

func compiler_check_method_access(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> u64 {
    var owner_ptr: u64 = 0;
    var owner_len: u64 = 0;
    var access: u64 = ACCESS_PUBLIC;
    var sig: *AstFunc = 0;
    if (compiler_find_method_decl_in_hierarchy(struct_ptr, struct_len, method_ptr, method_len, &owner_ptr, &owner_len, &access, &sig) == 0) {
        return 1;
    }
    return compiler_can_access_member(owner_ptr, owner_len, access);
}

func compiler_is_virtual_trait_name(name_ptr: u64, name_len: u64) -> u64 {
    if (name_len < 7) { return false; }
    if (str_eq(name_ptr, 7, "__Virt_", 7) != 0) { return true; }
    return false;
}

func _compiler_find_field_desc_unscoped(struct_def: *AstStructDef, member_ptr: u64, member_len: u64, depth: u64) -> *FieldDesc {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }
    var fields: *Vec<*FieldDesc> = struct_def.fields_vec;
    var n: u64 = fields.len();
    for (var i: u64 = 0; i < n; i++) {
        var f: *FieldDesc = fields.get(i);
        if (str_eq(f.name_ptr, f.name_len, member_ptr, member_len)) { return f; }
    }
    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    var match_desc: *FieldDesc = 0;
    var match_count: u64 = 0;
    for (var j: u64 = 0; j < pn; j++) {
        var parent_desc: *ParentDesc = parents.get(j);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var cand: *FieldDesc = _compiler_find_field_desc_unscoped(parent_def, member_ptr, member_len, depth + 1);
        if (cand != 0) {
            match_count = match_count + 1;
            if (match_count == 1) { match_desc = cand; }
        }
    }
    if (match_count > 1) {
        emit_stderr("[ERROR] Ambiguous field access: ");
        emit_stderr_len(struct_def.name_ptr, struct_def.name_len);
        emit_stderr(".");
        emit_stderr_len(member_ptr, member_len);
        emit_stderr("\n");
        panic("Compiler error");
    }
    return match_desc;
}

func compiler_find_struct_field_desc_scoped(struct_def: *AstStructDef, parent_ptr: u64, parent_len: u64, member_ptr: u64, member_len: u64) -> *FieldDesc {
    if (struct_def == 0) { return 0; }
    if (parent_ptr != 0) {
        var parent_def: *AstStructDef = _compiler_find_parent_def(struct_def, parent_ptr, parent_len, 0);
        if (parent_def == 0) {
            emit_stderr("[ERROR] Parent scope not found: ");
            emit_stderr_len(parent_ptr, parent_len);
            emit_stderr("\n");
            panic("Compiler error");
        }
        var desc: *FieldDesc = _compiler_find_field_desc_unscoped(parent_def, member_ptr, member_len, 0);
        if (desc == 0) {
            emit_stderr("[ERROR] Field not found in parent scope: ");
            emit_stderr_len(parent_ptr, parent_len);
            emit_stderr(".");
            emit_stderr_len(member_ptr, member_len);
            emit_stderr("\n");
            panic("Compiler error");
        }
        return desc;
    }
    return _compiler_find_field_desc_unscoped(struct_def, member_ptr, member_len, 0);
}

func generic_find_struct_field_type(struct_name_ptr: u64, struct_name_len: u64, parent_ptr: u64, parent_len: u64, member_ptr: u64, member_len: u64) -> *TypeInfo {
    if (struct_name_ptr == 0 || struct_name_len == 0) { return 0; }
    var sd: *AstStructDef = get_struct_def(struct_name_ptr, struct_name_len);
    if (sd == 0) {
        if (GENERIC_DEBUG != 0) {
            emit_stderr("[DEBUG] Missing struct def for member access: ");
            emit_stderr_len(struct_name_ptr, struct_name_len);
            emit_stderr("\n");
        }
        return 0;
    }
    var field_desc: *FieldDesc = compiler_find_struct_field_desc_scoped(sd, parent_ptr, parent_len, member_ptr, member_len);
    if (field_desc != 0) {
        var f: *FieldDesc = field_desc;
        return generic_make_typeinfo_from_fields(f.type_kind, f.ptr_depth, f.is_tagged, f.struct_name_ptr, f.struct_name_len, f.tag_layout_ptr, f.tag_layout_len, f.elem_type_kind, f.elem_ptr_depth, f.array_len, 0, 0, 0);
    }

    if (GENERIC_DEBUG != 0) {
        emit_stderr("[DEBUG] Missing struct field: ");
        emit_stderr_len(struct_name_ptr, struct_name_len);
        emit_stderr(".");
        emit_stderr_len(member_ptr, member_len);
        emit_stderr("\n");
    }
    return 0;
}

func generic_type_u64() -> *TypeInfo {
    return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
}

func generic_type_unknown() -> *TypeInfo {
    return generic_make_typeinfo_from_fields(TYPE_VOID, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
}

func generic_type_u8_ptr() -> *TypeInfo {
    return generic_make_typeinfo_from_fields(TYPE_U8, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
}

func generic_type_f64() -> *TypeInfo {
    return generic_make_typeinfo_from_fields(TYPE_F64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
}

func generic_infer_member_access_type(member: *AstMemberAccess, symtab: *HashMap<u64, *TypeInfo>) -> *TypeInfo {
    var obj_ti: *TypeInfo = generic_infer_expr_type(member.object, symtab);
    if (obj_ti == 0 || obj_ti.type_kind != TYPE_STRUCT) { return generic_type_u64(); }
    var field_ti: *TypeInfo = generic_find_struct_field_type(obj_ti.struct_name_ptr, obj_ti.struct_name_len, member.parent_ptr, member.parent_len, member.member_ptr, member.member_len);
    if (field_ti != 0) { return field_ti; }
    return generic_type_u64();
}

func generic_infer_try_type(tr: *AstTry, symtab: *HashMap<u64, *TypeInfo>) -> *TypeInfo {
    var opt_ti: *TypeInfo = generic_infer_expr_type(tr.operand, symtab);
    if (opt_ti == 0) { return generic_type_u64(); }
    if (opt_ti.type_kind != TYPE_STRUCT || opt_ti.ptr_depth != 0) { return generic_type_u64(); }
    var base_len: u64 = compiler_find_generic_suffix_index(opt_ti.struct_name_ptr, opt_ti.struct_name_len);
    if (str_eq(opt_ti.struct_name_ptr, base_len, "Option", 6)) {
        var vt: *TypeInfo = generic_find_struct_field_type(opt_ti.struct_name_ptr, opt_ti.struct_name_len, 0, 0, (u64)"value", 5);
        if (vt != 0) { return vt; }
    }
    if (str_eq(opt_ti.struct_name_ptr, base_len, "Result", 6)) {
        var ok_t: *TypeInfo = generic_find_struct_field_type(opt_ti.struct_name_ptr, opt_ti.struct_name_len, 0, 0, (u64)"ok", 2);
        if (ok_t != 0) { return ok_t; }
    }
    return generic_type_u64();
}

func generic_infer_call_return_type(call: *AstCall) -> *TypeInfo {
    var resolved_ptr: u64 = call.name_ptr;
    var resolved_len: u64 = call.name_len;
    var resolved: *NameInfo = resolve_name(call.name_ptr, call.name_len);
    if (resolved != 0) {
        resolved_ptr = resolved.ptr;
        resolved_len = resolved.len;
    }
    if (g_all_func_sigs != 0) {
        var n: u64 = g_all_func_sigs.len();
        for (var i: u64 = 0; i < n; i++) {
            var fn: *AstFunc = g_all_func_sigs.get(i);
            if (str_eq(fn.name_ptr, fn.name_len, resolved_ptr, resolved_len)) {
                return generic_make_typeinfo_from_fields(fn.ret_type, fn.ret_ptr_depth, fn.ret_is_tagged, fn.ret_struct_name_ptr, fn.ret_struct_name_len, fn.ret_tag_layout_ptr, fn.ret_tag_layout_len, 0, 0, 0, 0, 0, 0);
            }
        }
    }
    return generic_type_u64();
}

func generic_infer_expr_type(expr: *AstNode, symtab: *HashMap<u64, *TypeInfo>) -> *TypeInfo {
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_LITERAL:
            return generic_type_u64();
        case AST_STRING:
            return generic_type_u8_ptr();
        case AST_IDENT:
            var id: *AstIdent = (*AstIdent)expr;
            var t_ptr: *TypeInfo = 0;
            if (symtab != 0) {
                t_ptr = symtab.get(id.name_ptr, id.name_len);
            }
            if (t_ptr != 0) { return t_ptr; }
            var ginfo: *GlobalInfo = compiler_find_global_info(id.name_ptr, id.name_len);
            if (ginfo == 0) {
                var resolved: *NameInfo = resolve_name(id.name_ptr, id.name_len);
                if (resolved != 0) {
                    ginfo = compiler_find_global_info(resolved.ptr, resolved.len);
                }
            }
            if (ginfo != 0) {
                if (ginfo.typeinfo_ptr != 0) { return ginfo.typeinfo_ptr; }
            }
            var const_res: *ConstResult = (*ConstResult)compiler_find_const(id.name_ptr, id.name_len);
            if (const_res != 0 && const_res.found != 0) {
                return generic_type_u64();
            }
            if (compiler_func_exists(id.name_ptr, id.name_len) != 0) {
                return generic_type_u8_ptr();
            }
            return generic_type_unknown();
        case AST_SUPER:
            return generic_type_u64();
        case AST_ADDR_OF:
            var a: *AstAddrOf = (*AstAddrOf)expr;
            var base_t: *TypeInfo = generic_infer_expr_type(a.operand, symtab);
            return generic_make_typeinfo_from_fields(base_t.type_kind, base_t.ptr_depth + 1, base_t.is_tagged, base_t.struct_name_ptr, base_t.struct_name_len, base_t.tag_layout_ptr, base_t.tag_layout_len, base_t.elem_type_kind, base_t.elem_ptr_depth, base_t.array_len, base_t.array_len_is_param, base_t.array_len_param_ptr, base_t.array_len_param_len);
        case AST_DEREF:
            var d: *AstDeref = (*AstDeref)expr;
            var dt: *TypeInfo = generic_infer_expr_type(d.operand, symtab);
            if (dt.ptr_depth == 0) { return generic_type_u64(); }
            return generic_make_typeinfo_from_fields(dt.type_kind, dt.ptr_depth - 1, dt.is_tagged, dt.struct_name_ptr, dt.struct_name_len, dt.tag_layout_ptr, dt.tag_layout_len, dt.elem_type_kind, dt.elem_ptr_depth, dt.array_len, dt.array_len_is_param, dt.array_len_param_ptr, dt.array_len_param_len);
        case AST_CAST:
            var c: *AstCast = (*AstCast)expr;
            return generic_make_typeinfo_from_fields(c.target_type, c.target_ptr_depth, c.target_is_tagged, c.struct_name_ptr, c.struct_name_len, c.tag_layout_ptr, c.tag_layout_len, 0, 0, 0, 0, 0, 0);
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            var bt: *TypeInfo = generic_infer_expr_type(ix.base, symtab);
            if (bt.type_kind == TYPE_ARRAY || bt.type_kind == TYPE_SLICE) {
                return generic_make_typeinfo_from_fields(bt.elem_type_kind, bt.elem_ptr_depth, 0, bt.struct_name_ptr, bt.struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            }
            if (bt.ptr_depth > 0) {
                return generic_make_typeinfo_from_fields(bt.type_kind, bt.ptr_depth - 1, bt.is_tagged, bt.struct_name_ptr, bt.struct_name_len, bt.tag_layout_ptr, bt.tag_layout_len, bt.elem_type_kind, bt.elem_ptr_depth, bt.array_len, bt.array_len_is_param, bt.array_len_param_ptr, bt.array_len_param_len);
            }
            return generic_type_u64();
        case AST_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)expr;
            return generic_infer_member_access_type(ma, symtab);
        case AST_SAFE_MEMBER_ACCESS:
            var sma: *AstMemberAccess = (*AstMemberAccess)expr;
            return generic_infer_member_access_type(sma, symtab);
        case AST_TRY:
            var tr: *AstTry = (*AstTry)expr;
            return generic_infer_try_type(tr, symtab);
        case AST_FLOAT:
            return generic_type_f64();
        case AST_STACK_CTOR:
            var sc3: *AstStackCtor = (*AstStackCtor)expr;
            return generic_make_typeinfo_from_fields(sc3.type_kind, sc3.ptr_depth, sc3.is_tagged, sc3.struct_name_ptr, sc3.struct_name_len, sc3.tag_layout_ptr, sc3.tag_layout_len,
                                                     sc3.elem_type_kind, sc3.elem_ptr_depth, sc3.array_len, sc3.array_len_is_param, sc3.array_len_param_ptr, sc3.array_len_param_len);
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)expr;
            var tpl: *GenericStructTemplate = 0;
            if (compiler_try_resolve_generic_struct_template(gsl.name_ptr, gsl.name_len, &tpl) == 0) {
                return generic_type_u64();
            }
            var bindings_gsl: *Vec<*GenericBinding> = generic_build_bindings_for_struct(tpl, gsl.generic_args_vec);
            var inst_name: *NameInfo = generic_struct_get_or_create_instance(tpl, bindings_gsl);
            return generic_make_typeinfo_from_fields(TYPE_STRUCT, 0, 0, inst_name.ptr, inst_name.len, 0, 0, 0, 0, 0, 0, 0, 0);
        case AST_CALL:
            return generic_infer_call_return_type((*AstCall)expr);
        default:
            return generic_type_u64();
    }
}

func generic_collect_var_decls(stmt: *AstNode, symtab: *HashMap<u64, *TypeInfo>) -> u64 {
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            var t_ptr: *TypeInfo = generic_make_typeinfo_from_vardecl(vd);
            symtab.put(vd.name_ptr, vd.name_len, t_ptr);
            break;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            var stmts: *Vec<*AstNode> = blk.stmts_vec;
            var n: u64 = stmts.len();
            for (var i: u64 = 0; i < n; i++) {
                generic_collect_var_decls(stmts.get(i), symtab);
            }
            break;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            if (ifs.then_block != 0) { generic_collect_var_decls(ifs.then_block, symtab); }
            if (ifs.else_block != 0) { generic_collect_var_decls(ifs.else_block, symtab); }
            break;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            if (wl.body != 0) { generic_collect_var_decls(wl.body, symtab); }
            break;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            if (fr.init != 0) { generic_collect_var_decls(fr.init, symtab); }
            if (fr.body != 0) { generic_collect_var_decls(fr.body, symtab); }
            break;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            var cases: *Vec<*AstNode> = sw.cases_vec;
            var cn: u64 = cases.len();
            for (var j: u64 = 0; j < cn; j++) {
                generic_collect_var_decls(cases.get(j), symtab);
            }
            break;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            if (cs.body != 0) { generic_collect_var_decls(cs.body, symtab); }
            break;
        default:
            break;
    }
    return 0;
}

func generic_build_symtab_for_func(fn: *AstFunc) -> *HashMap<u64, *TypeInfo> {
    var symtab: *HashMap<u64, *TypeInfo> = new HashMap<u64, *TypeInfo>(64);
    var params: *Vec<*Param> = fn.params_vec;
    if (params != 0) {
        var n: u64 = params.len();
        for (var i: u64 = 0; i < n; i++) {
            var p: *Param = params.get(i);
            var t_ptr: *TypeInfo = generic_make_typeinfo_from_param(p);
            symtab.put(p.name_ptr, p.name_len, t_ptr);
        }
    }
    if (fn.body != 0) {
        generic_collect_var_decls(fn.body, symtab);
    }
    return symtab;
}

func generic_clone_args_vec(args: *Vec<*AstNode>, bindings: *Vec<*GenericBinding>) -> *Vec<*AstNode> {
    if (args == 0) { return 0; }
    var out: *Vec<*AstNode> = new Vec<*AstNode>(8);
    var n: u64 = args.len();
    for (var i: u64 = 0; i < n; i++) {
        var expr: *AstNode = args.get(i);
        out.push(generic_clone_expr(expr, bindings));
    }
    return out;
}

func generic_clone_maybe_instantiate_ctor_template(struct_name_ptr: u64, struct_name_len: u64, args_vec: *Vec<*AstNode>) -> u64 {
    if (args_vec == 0) { return 0; }
    var struct_base_len: u64 = compiler_find_generic_suffix_index(struct_name_ptr, struct_name_len);
    var ctor_name: *NameInfo = _compiler_build_constructor_name(struct_name_ptr, struct_base_len);
    var tpl_ptr: *GenericFuncTemplate = compiler_find_generic_template_by_name(ctor_name.ptr, ctor_name.len);
    if (tpl_ptr != 0 && tpl_ptr.params_vec != 0 && tpl_ptr.params_vec.len() > 0) {
        var ctor_bindings: *Vec<*GenericBinding> = compiler_get_generic_struct_bindings(struct_name_ptr, struct_name_len);
        if (ctor_bindings != 0) {
            generic_get_or_create_instance(tpl_ptr, ctor_bindings);
        }
    }
    return 0;
}

func generic_clone_expr_generic_call(gc: *AstGenericCall, bindings: *Vec<*GenericBinding>) -> *AstNode {
    var gargs: *Vec<*GenericArg> = new Vec<*GenericArg>(4);
    var gn: u64 = gc.generic_args_vec.len();
    for (var i: u64 = 0; i < gn; i++) {
        var ga: *GenericArg = gc.generic_args_vec.get(i);
        var ng: *GenericArg = new GenericArg();
        ng.kind = ga.kind;
        ng.value = ga.value;
        if (ga.kind == GENERIC_KIND_TYPE) {
            var ti: *TypeInfo = ga.type_ptr;
            if (ti.type_kind == TYPE_GENERIC) {
                var chk: *GenericBinding = generic_find_binding(bindings, GENERIC_KIND_TYPE, ti.struct_name_ptr, ti.struct_name_len);
                if (chk == 0 && GENERIC_DEBUG != 0) {
                    emit_stderr("[DEBUG] Missing binding for generic arg in call: ");
                    emit_stderr_len(gc.name_ptr, gc.name_len);
                    emit_stderr(" arg=");
                    emit_stderr_len(ti.struct_name_ptr, ti.struct_name_len);
                    emit_stderr("\n");
                }
            }
            ng.type_ptr = generic_subst_typeinfo(ti.type_kind, ti.ptr_depth, ti.is_tagged, ti.struct_name_ptr, ti.struct_name_len, ti.tag_layout_ptr, ti.tag_layout_len, ti.elem_type_kind, ti.elem_ptr_depth, ti.array_len, ti.array_len_is_param, ti.array_len_param_ptr, ti.array_len_param_len, bindings);
        } else {
            ng.type_ptr = 0;
        }
        gargs.push(ng);
    }
    var args_g: *Vec<*AstNode> = generic_clone_args_vec(gc.args_vec, bindings);
    return ast_generic_call_with_origin(gc.name_ptr, gc.name_len, gargs, args_g, gc.origin);
}

func generic_clone_expr_new(nw: *AstNew, bindings: *Vec<*GenericBinding>) -> *AstNode {
    var sub3: *TypeInfo = generic_subst_typeinfo(nw.type_kind, nw.ptr_depth, nw.is_tagged, nw.struct_name_ptr, nw.struct_name_len, nw.tag_layout_ptr, nw.tag_layout_len,
                                                nw.elem_type_kind, nw.elem_ptr_depth, nw.array_len, nw.array_len_is_param, nw.array_len_param_ptr, nw.array_len_param_len, bindings);
    var lit2: *AstNode = generic_clone_expr(nw.literal_expr, bindings);
    var args4: *Vec<*AstNode> = generic_clone_args_vec(nw.ctor_args_vec, bindings);
    if (sub3.type_kind == TYPE_STRUCT) {
        generic_clone_maybe_instantiate_ctor_template(sub3.struct_name_ptr, sub3.struct_name_len, args4);
    }
    return (*AstNode)ast_new(sub3.type_kind, sub3.ptr_depth, sub3.is_tagged, sub3.struct_name_ptr, sub3.struct_name_len, sub3.tag_layout_ptr, sub3.tag_layout_len,
                             sub3.elem_type_kind, sub3.elem_ptr_depth, sub3.array_len, sub3.array_len_is_param, sub3.array_len_param_ptr, sub3.array_len_param_len,
                             lit2, args4);
}

func generic_clone_expr_stack_ctor(sc: *AstStackCtor, bindings: *Vec<*GenericBinding>) -> *AstNode {
    var sub_sc: *TypeInfo = generic_subst_typeinfo(sc.type_kind, sc.ptr_depth, sc.is_tagged, sc.struct_name_ptr, sc.struct_name_len, sc.tag_layout_ptr, sc.tag_layout_len,
                                                  sc.elem_type_kind, sc.elem_ptr_depth, sc.array_len, sc.array_len_is_param, sc.array_len_param_ptr, sc.array_len_param_len, bindings);
    var args_sc: *Vec<*AstNode> = generic_clone_args_vec(sc.ctor_args_vec, bindings);
    if (sub_sc.type_kind == TYPE_STRUCT) {
        generic_clone_maybe_instantiate_ctor_template(sub_sc.struct_name_ptr, sub_sc.struct_name_len, args_sc);
    }
    return (*AstNode)ast_stack_ctor(sub_sc.type_kind, sub_sc.ptr_depth, sub_sc.is_tagged, sub_sc.struct_name_ptr, sub_sc.struct_name_len, sub_sc.tag_layout_ptr, sub_sc.tag_layout_len,
                                    sub_sc.elem_type_kind, sub_sc.elem_ptr_depth, sub_sc.array_len, sub_sc.array_len_is_param, sub_sc.array_len_param_ptr, sub_sc.array_len_param_len,
                                    args_sc);
}

func generic_clone_expr(node: *AstNode, bindings: *Vec<*GenericBinding>) -> *AstNode {
    if (node == 0) { return 0; }
    var kind: u64 = ast_kind(node);
    switch (kind) {
        case AST_LITERAL:
            var lit: *AstLiteral = (*AstLiteral)node;
            return ast_literal(lit.value);
        case AST_FLOAT:
            var fl: *AstFloat = (*AstFloat)node;
            return ast_float(fl.str_ptr, fl.str_len);
        case AST_IDENT:
            var id: *AstIdent = (*AstIdent)node;
            return ast_ident(id.name_ptr, id.name_len);
        case AST_SUPER:
            return (*AstNode)ast_super();
        case AST_STRING:
            var st: *AstString = (*AstString)node;
            return ast_string(st.str_ptr, st.str_len);
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)node;
            return ast_binary(bin.op, generic_clone_expr(bin.left, bindings), generic_clone_expr(bin.right, bindings));
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)node;
            return ast_unary(un.op, generic_clone_expr(un.operand, bindings));
        case AST_CALL:
            var call: *AstCall = (*AstCall)node;
            var args: *Vec<*AstNode> = generic_clone_args_vec(call.args_vec, bindings);
            return ast_call_with_origin(call.name_ptr, call.name_len, args, call.origin);
        case AST_GENERIC_CALL:
            return generic_clone_expr_generic_call((*AstGenericCall)node, bindings);
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)node;
            var gargs2: *Vec<*GenericArg> = new Vec<*GenericArg>(4);
            var gn2: u64 = gsl.generic_args_vec.len();
            for (var gi: u64 = 0; gi < gn2; gi++) {
                var ga2: *GenericArg = gsl.generic_args_vec.get(gi);
                var ng2: *GenericArg = new GenericArg();
                ng2.kind = ga2.kind;
                ng2.value = ga2.value;
                if (ga2.kind == GENERIC_KIND_TYPE) {
                    var ti3: *TypeInfo = ga2.type_ptr;
                    ng2.type_ptr = generic_subst_typeinfo(ti3.type_kind, ti3.ptr_depth, ti3.is_tagged, ti3.struct_name_ptr, ti3.struct_name_len, ti3.tag_layout_ptr, ti3.tag_layout_len, ti3.elem_type_kind, ti3.elem_ptr_depth, ti3.array_len, ti3.array_len_is_param, ti3.array_len_param_ptr, ti3.array_len_param_len, bindings);
                } else {
                    ng2.type_ptr = 0;
                }
                gargs2.push(ng2);
            }
            var vals2: *Vec<*AstNode> = generic_clone_args_vec(gsl.values_vec, bindings);
            return (*AstNode)ast_generic_struct_literal(gsl.name_ptr, gsl.name_len, gargs2, vals2);
        case AST_STATIC_GENERIC_CALL:
            var sgc: *AstStaticGenericCall = (*AstStaticGenericCall)node;
            var sg_gargs: *Vec<*GenericArg> = new Vec<*GenericArg>(4);
            var sg_n: u64 = sgc.generic_args_vec.len();
            for (var sg_i: u64 = 0; sg_i < sg_n; sg_i++) {
                var sg_ga: *GenericArg = sgc.generic_args_vec.get(sg_i);
                var sg_ng: *GenericArg = new GenericArg();
                sg_ng.kind = sg_ga.kind;
                sg_ng.value = sg_ga.value;
                if (sg_ga.kind == GENERIC_KIND_TYPE) {
                    var sg_ti: *TypeInfo = sg_ga.type_ptr;
                    sg_ng.type_ptr = generic_subst_typeinfo(sg_ti.type_kind, sg_ti.ptr_depth, sg_ti.is_tagged, sg_ti.struct_name_ptr, sg_ti.struct_name_len, sg_ti.tag_layout_ptr, sg_ti.tag_layout_len, sg_ti.elem_type_kind, sg_ti.elem_ptr_depth, sg_ti.array_len, sg_ti.array_len_is_param, sg_ti.array_len_param_ptr, sg_ti.array_len_param_len, bindings);
                } else {
                    sg_ng.type_ptr = 0;
                }
                sg_gargs.push(sg_ng);
            }
            var sg_args: *Vec<*AstNode> = generic_clone_args_vec(sgc.args_vec, bindings);
            return (*AstNode)ast_static_generic_call(sgc.struct_name_ptr, sgc.struct_name_len, sgc.method_ptr, sgc.method_len, sg_gargs, sg_args);
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)node;
            return ast_addr_of(generic_clone_expr(ao.operand, bindings));
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)node;
            return ast_deref(generic_clone_expr(dr.operand, bindings));
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)node;
            return ast_deref8(generic_clone_expr(dr8.operand, bindings));
        case AST_CAST:
            var cs: *AstCast = (*AstCast)node;
            var sub: *TypeInfo = generic_subst_typeinfo(cs.target_type, cs.target_ptr_depth, cs.target_is_tagged, cs.struct_name_ptr, cs.struct_name_len, cs.tag_layout_ptr, cs.tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
            var ti: *TypeInfo = sub;
            return ast_cast_ex(generic_clone_expr(cs.expr, bindings), ti.type_kind, ti.ptr_depth, ti.is_tagged, ti.struct_name_ptr, ti.struct_name_len, ti.tag_layout_ptr, ti.tag_layout_len);
        case AST_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)node;
            if (ma.parent_ptr != 0) {
                return ast_member_access_scoped(generic_clone_expr(ma.object, bindings), ma.parent_ptr, ma.parent_len, ma.member_ptr, ma.member_len);
            }
            return ast_member_access(generic_clone_expr(ma.object, bindings), ma.member_ptr, ma.member_len);
        case AST_SAFE_MEMBER_ACCESS:
            var sma: *AstMemberAccess = (*AstMemberAccess)node;
            if (sma.parent_ptr != 0) {
                return ast_safe_member_access_scoped(generic_clone_expr(sma.object, bindings), sma.parent_ptr, sma.parent_len, sma.member_ptr, sma.member_len);
            }
            return ast_safe_member_access(generic_clone_expr(sma.object, bindings), sma.member_ptr, sma.member_len);
        case AST_METHOD_CALL:
            var mc: *AstMethodCall = (*AstMethodCall)node;
            var args2: *Vec<*AstNode> = generic_clone_args_vec(mc.args_vec, bindings);
            return ast_method_call(generic_clone_expr(mc.receiver, bindings), mc.method_ptr, mc.method_len, args2);
        case AST_SAFE_METHOD_CALL:
            var smc: *AstMethodCall = (*AstMethodCall)node;
            var sargs2: *Vec<*AstNode> = generic_clone_args_vec(smc.args_vec, bindings);
            return ast_safe_method_call(generic_clone_expr(smc.receiver, bindings), smc.method_ptr, smc.method_len, sargs2);
        case AST_TRY:
            var tr: *AstTry = (*AstTry)node;
            return ast_try(generic_clone_expr(tr.operand, bindings));
        case AST_STRUCT_LITERAL:
            var sl: *AstStructLiteral = (*AstStructLiteral)node;
            var vals: *Vec<*AstNode> = generic_clone_args_vec(sl.values_vec, bindings);
            return ast_struct_literal(sl.struct_def, vals);
        case AST_SIZEOF:
            var sz: *AstSizeof = (*AstSizeof)node;
            var sub2: *TypeInfo = generic_subst_typeinfo(sz.type_kind, sz.ptr_depth, 0, sz.struct_name_ptr, sz.struct_name_len, 0, 0, sz.elem_type_kind, sz.elem_ptr_depth, sz.array_len, sz.array_len_is_param, sz.array_len_param_ptr, sz.array_len_param_len, bindings);
            var ti2: *TypeInfo = sub2;
            return ast_sizeof(ti2.type_kind, ti2.ptr_depth, ti2.struct_name_ptr, ti2.struct_name_len, ti2.elem_type_kind, ti2.elem_ptr_depth, ti2.array_len, ti2.array_len_is_param, ti2.array_len_param_ptr, ti2.array_len_param_len);
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)node;
            return ast_sizeof_expr(generic_clone_expr(se.expr, bindings));
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)node;
            return ast_index(generic_clone_expr(ix.base, bindings), generic_clone_expr(ix.index, bindings));
        case AST_SLICE:
            var sl2: *AstSlice = (*AstSlice)node;
            return ast_slice(generic_clone_expr(sl2.ptr_expr, bindings), generic_clone_expr(sl2.len_expr, bindings));
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)node;
            var args3: *Vec<*AstNode> = generic_clone_args_vec(cp.args_vec, bindings);
            return ast_call_ptr(generic_clone_expr(cp.callee, bindings), args3);
        case AST_NEW:
            return generic_clone_expr_new((*AstNew)node, bindings);
        case AST_STACK_CTOR:
            return generic_clone_expr_stack_ctor((*AstStackCtor)node, bindings);
        default:
            return node;
    }
}

func generic_clone_stmt(node: *AstNode, bindings: *Vec<*GenericBinding>) -> *AstNode {
    if (node == 0) { return 0; }
    var kind: u64 = ast_kind(node);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)node;
            return ast_return(generic_clone_expr(r.expr, bindings));
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)node;
            var ti: *TypeInfo = generic_subst_typeinfo(vd.type_kind, vd.ptr_depth, vd.is_tagged, vd.struct_name_ptr, vd.struct_name_len, vd.tag_layout_ptr, vd.tag_layout_len, vd.elem_type_kind, vd.elem_ptr_depth, vd.array_len, vd.array_len_is_param, vd.array_len_param_ptr, vd.array_len_param_len, bindings);
            var decl: *AstVarDecl = ast_var_decl(vd.name_ptr, vd.name_len, ti.type_kind, ti.ptr_depth, generic_clone_expr(vd.init_expr, bindings));
            decl.is_tagged = ti.is_tagged;
            decl.struct_name_ptr = ti.struct_name_ptr;
            decl.struct_name_len = ti.struct_name_len;
            decl.tag_layout_ptr = ti.tag_layout_ptr;
            decl.tag_layout_len = ti.tag_layout_len;
            decl.elem_type_kind = ti.elem_type_kind;
            decl.elem_ptr_depth = ti.elem_ptr_depth;
            decl.array_len = ti.array_len;
            decl.array_len_is_param = ti.array_len_is_param;
            decl.array_len_param_ptr = ti.array_len_param_ptr;
            decl.array_len_param_len = ti.array_len_param_len;
            return (*AstNode)decl;
        case AST_ASSIGN:
            var assign_node: *AstAssign = (*AstAssign)node;
            return ast_assign(generic_clone_expr(assign_node.target, bindings), generic_clone_expr(assign_node.value, bindings));
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)node;
            return ast_expr_stmt(generic_clone_expr(es.expr, bindings));
        case AST_IF:
            var ifs: *AstIf = (*AstIf)node;
            return ast_if(generic_clone_expr(ifs.cond, bindings), generic_clone_stmt(ifs.then_block, bindings), generic_clone_stmt(ifs.else_block, bindings));
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)node;
            return ast_while(generic_clone_expr(wl.cond, bindings), generic_clone_stmt(wl.body, bindings));
        case AST_FOR:
            var fr: *AstFor = (*AstFor)node;
            return ast_for(generic_clone_stmt(fr.init, bindings), generic_clone_expr(fr.cond, bindings), generic_clone_expr(fr.update, bindings), generic_clone_stmt(fr.body, bindings));
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)node;
            var cases_out: *Vec<*AstNode> = new Vec<*AstNode>(4);
            var cn: u64 = sw.cases_vec.len();
            for (var i: u64 = 0; i < cn; i++) {
                cases_out.push(generic_clone_stmt(sw.cases_vec.get(i), bindings));
            }
            return ast_switch(generic_clone_expr(sw.expr, bindings), cases_out);
        case AST_CASE:
            var cs: *AstCase = (*AstCase)node;
            return ast_case(cs.value, generic_clone_stmt(cs.body, bindings), cs.is_default);
        case AST_BREAK:
            return ast_break();
        case AST_CONTINUE:
            return ast_continue();
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)node;
            var stmts_out: *Vec<*AstNode> = new Vec<*AstNode>(8);
            var n: u64 = blk.stmts_vec.len();
            for (var i: u64 = 0; i < n; i++) {
                stmts_out.push(generic_clone_stmt(blk.stmts_vec.get(i), bindings));
            }
            return ast_block(stmts_out);
        case AST_ASM:
            var asmnode: *AstAsm = (*AstAsm)node;
            var text_vec: *Vec<u64> = new Vec<u64>(16);
            var tn: u64 = asmnode.text_vec.len();
            for (var i2: u64 = 0; i2 < tn; i2++) { text_vec.push(asmnode.text_vec.get(i2)); }
            return ast_asm(text_vec);
        case AST_ALIAS:
            var al: *AstAlias = (*AstAlias)node;
            return (*AstNode)ast_alias(al.reg_ptr, al.reg_len, al.name_ptr, al.name_len);
        case AST_DEFER:
            var df: *AstDefer = (*AstDefer)node;
            return (*AstNode)ast_defer(generic_clone_stmt(df.stmt, bindings));
        case AST_DELETE:
            var dl: *AstDelete = (*AstDelete)node;
            return (*AstNode)ast_delete(generic_clone_expr(dl.expr, bindings));
        default:
            return node;
    }
}

func generic_clone_func(template_ptr: *AstFunc, bindings: *Vec<*GenericBinding>, new_name_ptr: u64, new_name_len: u64) -> *AstFunc {
    var fn: *AstFunc = template_ptr;
    var params_out: *Vec<*Param> = new Vec<*Param>(8);
    var pn: u64 = fn.params_vec.len();
    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = fn.params_vec.get(i);
        var ti: *TypeInfo = generic_subst_typeinfo(p.type_kind, p.ptr_depth, p.is_tagged, p.struct_name_ptr, p.struct_name_len, p.tag_layout_ptr, p.tag_layout_len, p.elem_type_kind, p.elem_ptr_depth, p.array_len, p.array_len_is_param, p.array_len_param_ptr, p.array_len_param_len, bindings);
        var np: *Param = new Param();
        np.name_ptr = p.name_ptr;
        np.name_len = p.name_len;
        np.type_kind = ti.type_kind;
        np.ptr_depth = ti.ptr_depth;
        np.is_tagged = ti.is_tagged;
        np.struct_name_ptr = ti.struct_name_ptr;
        np.struct_name_len = ti.struct_name_len;
        np.tag_layout_ptr = ti.tag_layout_ptr;
        np.tag_layout_len = ti.tag_layout_len;
        np.elem_type_kind = ti.elem_type_kind;
        np.elem_ptr_depth = ti.elem_ptr_depth;
        np.array_len = ti.array_len;
        np.array_len_is_param = ti.array_len_is_param;
        np.array_len_param_ptr = ti.array_len_param_ptr;
        np.array_len_param_len = ti.array_len_param_len;
        params_out.push(np);
    }

    var rt: *TypeInfo = generic_subst_typeinfo(fn.ret_type, fn.ret_ptr_depth, fn.ret_is_tagged, fn.ret_struct_name_ptr, fn.ret_struct_name_len, fn.ret_tag_layout_ptr, fn.ret_tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
    var body_out: *AstNode = generic_clone_stmt(fn.body, bindings);
    return ast_func_ex(new_name_ptr, new_name_len, params_out, rt.type_kind, rt.ptr_depth, rt.is_tagged, rt.struct_name_ptr, rt.struct_name_len, rt.tag_layout_ptr, rt.tag_layout_len, body_out);
}

func generic_clone_func_sig(template_ptr: *AstFunc, bindings: *Vec<*GenericBinding>, new_name_ptr: u64, new_name_len: u64) -> *AstFunc {
    var fn: *AstFunc = template_ptr;
    var params_out: *Vec<*Param> = new Vec<*Param>(8);
    var pn: u64 = fn.params_vec.len();
    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = fn.params_vec.get(i);
        var ti: *TypeInfo = generic_subst_typeinfo(p.type_kind, p.ptr_depth, p.is_tagged, p.struct_name_ptr, p.struct_name_len, p.tag_layout_ptr, p.tag_layout_len, p.elem_type_kind, p.elem_ptr_depth, p.array_len, p.array_len_is_param, p.array_len_param_ptr, p.array_len_param_len, bindings);
        var np: *Param = new Param();
        np.name_ptr = p.name_ptr;
        np.name_len = p.name_len;
        np.type_kind = ti.type_kind;
        np.ptr_depth = ti.ptr_depth;
        np.is_tagged = ti.is_tagged;
        np.struct_name_ptr = ti.struct_name_ptr;
        np.struct_name_len = ti.struct_name_len;
        np.tag_layout_ptr = ti.tag_layout_ptr;
        np.tag_layout_len = ti.tag_layout_len;
        np.elem_type_kind = ti.elem_type_kind;
        np.elem_ptr_depth = ti.elem_ptr_depth;
        np.array_len = ti.array_len;
        np.array_len_is_param = ti.array_len_is_param;
        np.array_len_param_ptr = ti.array_len_param_ptr;
        np.array_len_param_len = ti.array_len_param_len;
        params_out.push(np);
    }

    var rt: *TypeInfo = generic_subst_typeinfo(fn.ret_type, fn.ret_ptr_depth, fn.ret_is_tagged, fn.ret_struct_name_ptr, fn.ret_struct_name_len, fn.ret_tag_layout_ptr, fn.ret_tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
    return ast_func_ex(new_name_ptr, new_name_len, params_out, rt.type_kind, rt.ptr_depth, rt.is_tagged, rt.struct_name_ptr, rt.struct_name_len, rt.tag_layout_ptr, rt.tag_layout_len, 0);
}

func generic_get_or_create_instance(tpl_ptr: *GenericFuncTemplate, bindings: *Vec<*GenericBinding>) -> *NameInfo {
    var tpl: *GenericFuncTemplate = tpl_ptr;
    var base_ptr: u64 = tpl.mangled_ptr;
    var base_len: u64 = tpl.mangled_len;
    if (base_ptr == 0) {
        base_ptr = tpl.name_ptr;
        base_len = tpl.name_len;
    }

    var name_info: *NameInfo = generic_mangle_name(base_ptr, base_len, tpl.params_vec, bindings);
    var inst_ptr: u64 = name_info.ptr;
    var inst_len: u64 = name_info.len;

    if (g_generic_instantiations != 0) {
        var exists: u64 = g_generic_instantiations.get(inst_ptr, inst_len);
        if (exists != 0) { return name_info; }
    }

    var def_src: *AstFunc = tpl.func_def;
    if (def_src == 0 && tpl.func_sig != 0) { def_src = tpl.func_sig; }
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic function body\n");
        panic("Parse error");
    }

    var fn_new: *AstFunc = generic_clone_func(def_src, bindings, inst_ptr, inst_len);
    var sig_new: *AstFunc = generic_clone_func_sig(def_src, bindings, inst_ptr, inst_len);
    g_all_funcs.push(fn_new);
    g_all_func_sigs.push(sig_new);
    compiler_invalidate_func_lookup();

    // Preserve impl-method owner/access metadata for instantiated names.
    // Without this, private member access checks inside generic impl methods
    // lose their owner context during codegen.
    compiler_copy_impl_method_metadata_if_needed(base_ptr, base_len, inst_ptr, inst_len);
    if (compiler_is_impl_method(inst_ptr, inst_len) != 0) {
        compiler_register_impl_method_func(inst_ptr, inst_len, fn_new);
    }

    if (g_generic_instantiations == 0) { g_generic_instantiations = new HashMap<u64, u64>(64); }
    g_generic_instantiations.put(inst_ptr, inst_len, 1);

    // Map new function to template module
    if (g_func_module_map != 0 && tpl.mangled_ptr != 0) {
        var mod_info: *NameInfo = g_func_module_map.get(tpl.mangled_ptr, tpl.mangled_len);
        if (mod_info != 0) {
            g_func_module_map.put(inst_ptr, inst_len, mod_info);
        }
    }

    return name_info;
}

func generic_build_bindings_core(params_vec: *Vec<*GenericParam>, gargs: *Vec<*GenericArg>) -> *Vec<*GenericBinding> {
    if (params_vec == 0 || gargs == 0) {
        emit_stderr("[ERROR] Missing generic params/args\n");
        panic("Parse error");
    }
    var bindings: *Vec<*GenericBinding> = new Vec<*GenericBinding>(4);
    var pn: u64 = params_vec.len();
    var an: u64 = gargs.len();
    if (pn != an) {
        emit_stderr("[ERROR] Generic argument count mismatch\n");
        panic("Parse error");
    }
    for (var i: u64 = 0; i < pn; i++) {
        var gp: *GenericParam = params_vec.get(i);
        var ga: *GenericArg = gargs.get(i);
        if (gp.kind == GENERIC_KIND_TYPE) {
            if (ga.kind != GENERIC_KIND_TYPE) {
                emit_stderr("[ERROR] Expected type generic argument\n");
                panic("Parse error");
            }
            if (generic_bind_type(bindings, gp.name_ptr, gp.name_len, ga.type_ptr) != 0) {
                emit_stderr("[ERROR] Conflicting generic type bindings\n");
                panic("Parse error");
            }
        } else {
            if (ga.kind != GENERIC_KIND_VALUE) {
                emit_stderr("[ERROR] Expected value generic argument\n");
                panic("Parse error");
            }
            if (generic_bind_value(bindings, gp.name_ptr, gp.name_len, ga.value) != 0) {
                emit_stderr("[ERROR] Conflicting generic value bindings\n");
                panic("Parse error");
            }
        }
    }
    return bindings;
}

func generic_build_bindings_for_explicit(tpl_ptr: *GenericFuncTemplate, gargs: *Vec<*GenericArg>) -> *Vec<*GenericBinding> {
    var tpl: *GenericFuncTemplate = tpl_ptr;
    return generic_build_bindings_core(tpl.params_vec, gargs);
}

func generic_build_bindings_for_struct(tpl_ptr: *GenericStructTemplate, gargs: *Vec<*GenericArg>) -> *Vec<*GenericBinding> {
    var tpl: *GenericStructTemplate = tpl_ptr;
    return generic_build_bindings_core(tpl.params_vec, gargs);
}

func generic_clone_struct(template_def: *AstStructDef, bindings: *Vec<*GenericBinding>, new_name_ptr: u64, new_name_len: u64) -> *AstStructDef {
    var sd: *AstStructDef = template_def;
    var fields_in: *Vec<*FieldDesc> = sd.fields_vec;
    var fields_out: *Vec<*FieldDesc> = new Vec<*FieldDesc>(fields_in.len());
    var n: u64 = fields_in.len();
    for (var i: u64 = 0; i < n; i++) {
        var f: *FieldDesc = fields_in.get(i);
        var ti: *TypeInfo = generic_subst_typeinfo(f.type_kind, f.ptr_depth, f.is_tagged, f.struct_name_ptr, f.struct_name_len, f.tag_layout_ptr, f.tag_layout_len, f.elem_type_kind, f.elem_ptr_depth, f.array_len, 0, 0, 0, bindings);
        var nf: *FieldDesc = new FieldDesc();
        nf.name_ptr = f.name_ptr;
        nf.name_len = f.name_len;
        nf.type_kind = ti.type_kind;
        nf.struct_name_ptr = ti.struct_name_ptr;
        nf.struct_name_len = ti.struct_name_len;
        nf.ptr_depth = ti.ptr_depth;
        nf.is_tagged = ti.is_tagged;
        nf.tag_layout_ptr = ti.tag_layout_ptr;
        nf.tag_layout_len = ti.tag_layout_len;
        nf.bit_width = f.bit_width;
        nf.elem_type_kind = ti.elem_type_kind;
        nf.elem_ptr_depth = ti.elem_ptr_depth;
        nf.array_len = ti.array_len;
        nf.access = f.access;
        nf.owner_struct_ptr = new_name_ptr;
        nf.owner_struct_len = new_name_len;
        fields_out.push(nf);
    }
    var parents_out: *Vec<*ParentDesc> = 0;
    if (sd.parents_vec != 0) {
        var parents_in: *Vec<*ParentDesc> = sd.parents_vec;
        parents_out = new Vec<*ParentDesc>(parents_in.len());
        var pn: u64 = parents_in.len();
        for (var pi: u64 = 0; pi < pn; pi++) {
            var parent_in: *ParentDesc = parents_in.get(pi);
            var parent_out: *ParentDesc = new ParentDesc();
            parent_out.name_ptr = parent_in.name_ptr;
            parent_out.name_len = parent_in.name_len;
            parent_out.struct_def = parent_in.struct_def;
            parents_out.push(parent_out);
        }
    }
    var traits_out: *Vec<*TraitRef> = 0;
    if (sd.traits_vec != 0) {
        var traits_in: *Vec<*TraitRef> = sd.traits_vec;
        traits_out = new Vec<*TraitRef>(traits_in.len());
        var tn: u64 = traits_in.len();
        for (var ti: u64 = 0; ti < tn; ti++) {
            var t_in: *TraitRef = traits_in.get(ti);
            var t_out: *TraitRef = new TraitRef();
            t_out.name_ptr = t_in.name_ptr;
            t_out.name_len = t_in.name_len;
            t_out.trait_def = t_in.trait_def;
            traits_out.push(t_out);
        }
    }
    return ast_struct_def(new_name_ptr, new_name_len, parents_out, fields_out, sd.is_packed, traits_out);
}

func generic_struct_get_or_create_instance(tpl_ptr: *GenericStructTemplate, bindings: *Vec<*GenericBinding>) -> *NameInfo {
    var tpl: *GenericStructTemplate = tpl_ptr;
    var name_info: *NameInfo = generic_mangle_name(tpl.name_ptr, tpl.name_len, tpl.params_vec, bindings);
    var inst_ptr: u64 = name_info.ptr;
    var inst_len: u64 = name_info.len;

    if (g_generic_struct_instantiations != 0) {
        var exists: *AstStructDef = g_generic_struct_instantiations.get(inst_ptr, inst_len);
        if (exists != 0) {
            compiler_register_generic_struct_bindings(inst_ptr, inst_len, bindings);
            return name_info;
        }
    }

    var def_src: *AstStructDef = tpl.struct_def;
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic struct template\n");
        panic("Parse error");
    }

    var struct_new: *AstStructDef = generic_clone_struct(def_src, bindings, inst_ptr, inst_len);
    register_struct_type(struct_new);

    if (g_generic_struct_instantiations == 0) { g_generic_struct_instantiations = new HashMap<u64, *AstStructDef>(64); }
    g_generic_struct_instantiations.put(inst_ptr, inst_len, struct_new);
    compiler_register_generic_struct_bindings(inst_ptr, inst_len, bindings);
    return name_info;
}

func generic_build_bindings_by_infer(tpl_ptr: *GenericFuncTemplate, call_args: *Vec<*AstNode>, symtab: *HashMap<u64, *TypeInfo>) -> *Vec<*GenericBinding> {
    var tpl: *GenericFuncTemplate = tpl_ptr;
    var bindings: *Vec<*GenericBinding> = new Vec<*GenericBinding>(4);
    var def_src: *AstFunc = tpl.func_def;
    if (def_src == 0) { def_src = tpl.func_sig; }
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic signature for inference\n");
        panic("Parse error");
    }
    var fn: *AstFunc = def_src;
    var params: *Vec<*Param> = fn.params_vec;
    var pn: u64 = params.len();
    var an: u64 = call_args.len();
    if (pn != an) {
        emit_stderr("[ERROR] Generic inference arg count mismatch\n");
        panic("Parse error");
    }

    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = params.get(i);
        var arg_expr: *AstNode = call_args.get(i);
        var arg_ti: *TypeInfo = generic_infer_expr_type(arg_expr, symtab);
        if (generic_infer_from_typeinfo(p.type_kind, p.ptr_depth, p.struct_name_ptr, p.struct_name_len, p.elem_type_kind, p.elem_ptr_depth, p.array_len, p.array_len_is_param, p.array_len_param_ptr, p.array_len_param_len, arg_ti, bindings) != 0) {
            return 0;
        }
    }

    return bindings;
}

func generic_resolve_template(name_ptr: u64, name_len: u64) -> *GenericFuncTemplate {
    var tpl: *GenericFuncTemplate = compiler_find_generic_template_by_name(name_ptr, name_len);
    if (tpl != 0) {
        var t: *GenericFuncTemplate = tpl;
        if (t.params_vec != 0 && t.params_vec.len() > 0) { return tpl; }
    }
    var resolved: *NameInfo = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        tpl = compiler_find_generic_template_by_mangled(resolved.ptr, resolved.len);
        if (tpl != 0) {
            var t2: *GenericFuncTemplate = tpl;
            if (t2.params_vec != 0 && t2.params_vec.len() > 0) { return tpl; }
            tpl = 0;
        }
    }
    return tpl;
}

func generic_build_method_template_name(struct_ptr: u64, base_len: u64, method_ptr: u64, method_len: u64) -> *NameInfo {
    var full_len: u64 = base_len + 1 + method_len;
    var full_ptr: u64 = heap_alloc((full_len + 1) * sizeof(u8));
    var full_u8: []u8 = slice(full_ptr, full_len + 1);
    var struct_u8: []u8 = slice(struct_ptr, base_len);
    var method_u8: []u8 = slice(method_ptr, method_len);
    for (var i: u64 = 0; i < base_len; i++) {
        full_u8[i] = struct_u8[i];
    }
    full_u8[base_len] = 95;
    for (var j: u64 = 0; j < method_len; j++) {
        full_u8[base_len + 1 + j] = method_u8[j];
    }
    full_u8[full_len] = 0;
    return new NameInfo{full_ptr, full_len};
}

func generic_find_method_template_for_struct(struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> *GenericFuncTemplate {
    var base_len: u64 = compiler_find_generic_suffix_index(struct_ptr, struct_len);
    var full_name: *NameInfo = generic_build_method_template_name(struct_ptr, base_len, method_ptr, method_len);
    var tpl: *GenericFuncTemplate = compiler_find_generic_template_by_name(full_name.ptr, full_name.len);
    if (tpl != 0) { return tpl; }

    var base_u8: []u8 = slice(struct_ptr, base_len);
    var last_sep: u64 = base_len;
    for (var si: u64 = 0; si + 1 < base_len; si++) {
        if (base_u8[si] == 95 && base_u8[si + 1] == 95) { last_sep = si + 2; }
    }
    if (last_sep >= base_len) { return 0; }

    var short_base_len: u64 = base_len - last_sep;
    var short_base_ptr: u64 = struct_ptr + last_sep;
    var alt_name: *NameInfo = generic_build_method_template_name(short_base_ptr, short_base_len, method_ptr, method_len);
    return compiler_find_generic_template_by_name(alt_name.ptr, alt_name.len);
}

func generic_try_instantiate_method_template(mc: *AstMethodCall, args: *Vec<*AstNode>, recv_ti: *TypeInfo, symtab: *HashMap<u64, *TypeInfo>) -> u64 {
    if (recv_ti == 0 || recv_ti.type_kind != TYPE_STRUCT) { return 0; }

    var struct_ptr: u64 = recv_ti.struct_name_ptr;
    var struct_len: u64 = recv_ti.struct_name_len;
    var tpl_ptr: *GenericFuncTemplate = generic_find_method_template_for_struct(struct_ptr, struct_len, mc.method_ptr, mc.method_len);
    if (tpl_ptr == 0) { return 0; }

    var base_len: u64 = compiler_find_generic_suffix_index(struct_ptr, struct_len);
    var bindings: *Vec<*GenericBinding> = 0;
    if (base_len != struct_len) {
        bindings = compiler_get_generic_struct_bindings(struct_ptr, struct_len);
    }
    if (bindings == 0) {
        var an: u64 = args.len();
        var infer_args: *Vec<*AstNode> = new Vec<*AstNode>(an + 1);
        if (recv_ti.ptr_depth > 0) { infer_args.push(mc.receiver); }
        else { infer_args.push(ast_addr_of(mc.receiver)); }
        for (var ai: u64 = 0; ai < an; ai++) {
            infer_args.push(args.get(ai));
        }
        bindings = generic_build_bindings_by_infer(tpl_ptr, infer_args, symtab);
    }
    if (bindings != 0) {
        generic_get_or_create_instance(tpl_ptr, bindings);
    }
    return 0;
}

func generic_process_method_call_expr(mc: *AstMethodCall, symtab: *HashMap<u64, *TypeInfo>) -> u64 {
    mc.receiver = generic_process_expr(mc.receiver, symtab);
    var args: *Vec<*AstNode> = new Vec<*AstNode>(4);
    var an: u64 = mc.args_vec.?len();
    for (var i: u64 = 0; i < an; i++) {
        args.push(generic_process_expr(mc.args_vec.get(i), symtab));
    }
    mc.args_vec = args;
    var recv_ti: *TypeInfo = generic_infer_expr_type(mc.receiver, symtab);
    generic_try_instantiate_method_template(mc, args, recv_ti, symtab);
    return 0;
}

func generic_process_args_vec(args_in: *Vec<*AstNode>, symtab: *HashMap<u64, *TypeInfo>) -> *Vec<*AstNode> {
    if (args_in == 0) { return 0; }
    var args_out: *Vec<*AstNode> = new Vec<*AstNode>(4);
    var n: u64 = args_in.len();
    for (var i: u64 = 0; i < n; i++) {
        args_out.push(generic_process_expr(args_in.get(i), symtab));
    }
    return args_out;
}

func generic_try_instantiate_ctor_template(type_kind: u64, struct_name_ptr: u64, struct_name_len: u64, ctor_args_vec: *Vec<*AstNode>) -> u64 {
    if (ctor_args_vec == 0) { return 0; }
    if (type_kind != TYPE_STRUCT) { return 0; }

    var struct_base_len: u64 = compiler_find_generic_suffix_index(struct_name_ptr, struct_name_len);
    var ctor_name: *NameInfo = _compiler_build_constructor_name(struct_name_ptr, struct_base_len);
    var tpl_ptr: *GenericFuncTemplate = compiler_find_generic_template_by_name(ctor_name.ptr, ctor_name.len);
    if (tpl_ptr == 0 || tpl_ptr.params_vec == 0 || tpl_ptr.params_vec.len() == 0) { return 0; }

    var ctor_bindings: *Vec<*GenericBinding> = compiler_get_generic_struct_bindings(struct_name_ptr, struct_name_len);
    if (ctor_bindings != 0) {
        generic_get_or_create_instance(tpl_ptr, ctor_bindings);
    }
    return 0;
}

func generic_process_generic_call_expr(gc: *AstGenericCall, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    var tpl_ptr: *GenericFuncTemplate = generic_resolve_template(gc.name_ptr, gc.name_len);
    if (tpl_ptr == 0) {
        emit_stderr("[ERROR] Generic function not found\n");
        panic("Parse error");
    }
    var bindings: *Vec<*GenericBinding> = generic_build_bindings_for_explicit(tpl_ptr, gc.generic_args_vec);
    var name_info: *NameInfo = generic_get_or_create_instance(tpl_ptr, bindings);
    var args_new: *Vec<*AstNode> = generic_process_args_vec(gc.args_vec, symtab);
    return ast_call_with_origin(name_info.ptr, name_info.len, args_new, gc.origin);
}

func generic_process_call_expr(call: *AstCall, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    var tpl_ptr: *GenericFuncTemplate = generic_resolve_template(call.name_ptr, call.name_len);
    if (tpl_ptr == 0) {
        var args_new2: *Vec<*AstNode> = generic_process_args_vec(call.args_vec, symtab);
        return ast_call_with_origin(call.name_ptr, call.name_len, args_new2, call.origin);
    }
    var bindings2: *Vec<*GenericBinding> = generic_build_bindings_by_infer(tpl_ptr, call.args_vec, symtab);
    if (bindings2 == 0) {
        var args_fallback: *Vec<*AstNode> = generic_process_args_vec(call.args_vec, symtab);
        return ast_call_with_origin(call.name_ptr, call.name_len, args_fallback, call.origin);
    }
    var name_info2: *NameInfo = generic_get_or_create_instance(tpl_ptr, bindings2);
    var args_new3: *Vec<*AstNode> = generic_process_args_vec(call.args_vec, symtab);
    return ast_call_with_origin(name_info2.ptr, name_info2.len, args_new3, call.origin);
}

func generic_process_new_expr(nw: *AstNew, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    if (nw.literal_expr != 0) {
        nw.literal_expr = generic_process_expr(nw.literal_expr, symtab);
    }
    nw.ctor_args_vec = generic_process_args_vec(nw.ctor_args_vec, symtab);
    generic_try_instantiate_ctor_template(nw.type_kind, nw.struct_name_ptr, nw.struct_name_len, nw.ctor_args_vec);
    return (*AstNode)nw;
}

func generic_process_stack_ctor_expr(sc2: *AstStackCtor, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    sc2.ctor_args_vec = generic_process_args_vec(sc2.ctor_args_vec, symtab);
    generic_try_instantiate_ctor_template(sc2.type_kind, sc2.struct_name_ptr, sc2.struct_name_len, sc2.ctor_args_vec);
    return (*AstNode)sc2;
}

func generic_process_call_ptr_expr(cp: *AstCallPtr, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    cp.callee = generic_process_expr(cp.callee, symtab);
    cp.args_vec = generic_process_args_vec(cp.args_vec, symtab);
    return (*AstNode)cp;
}

func generic_process_index_expr(ix: *AstIndex, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    ix.base = generic_process_expr(ix.base, symtab);
    ix.index = generic_process_expr(ix.index, symtab);
    return (*AstNode)ix;
}

func generic_process_slice_expr(sl: *AstSlice, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    sl.ptr_expr = generic_process_expr(sl.ptr_expr, symtab);
    sl.len_expr = generic_process_expr(sl.len_expr, symtab);
    return (*AstNode)sl;
}

func generic_process_expr(expr: *AstNode, symtab: *HashMap<u64, *TypeInfo>) -> *AstNode {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_GENERIC_CALL:
            return generic_process_generic_call_expr((*AstGenericCall)expr, symtab);
        case AST_STATIC_GENERIC_CALL:
            var sgc: *AstStaticGenericCall = (*AstStaticGenericCall)expr;
            sgc.args_vec = generic_process_args_vec(sgc.args_vec, symtab);
            return generic_process_expr(compiler_lower_parse_sugar_static_generic_method_call(sgc), symtab);
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)expr;
            gsl.values_vec = generic_process_args_vec(gsl.values_vec, symtab);
            return compiler_lower_parse_sugar_generic_struct_literal(gsl);
        case AST_CALL:
            return generic_process_call_expr((*AstCall)expr, symtab);
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)expr;
            bin.left = generic_process_expr(bin.left, symtab);
            bin.right = generic_process_expr(bin.right, symtab);
            return expr;
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)expr;
            un.operand = generic_process_expr(un.operand, symtab);
            return expr;
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)expr;
            ao.operand = generic_process_expr(ao.operand, symtab);
            return expr;
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)expr;
            dr.operand = generic_process_expr(dr.operand, symtab);
            return expr;
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)expr;
            dr8.operand = generic_process_expr(dr8.operand, symtab);
            return expr;
        case AST_CAST:
            var cs: *AstCast = (*AstCast)expr;
            cs.expr = generic_process_expr(cs.expr, symtab);
            return expr;
        case AST_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)expr;
            ma.object = generic_process_expr(ma.object, symtab);
            return expr;
        case AST_SAFE_MEMBER_ACCESS:
            var sma: *AstMemberAccess = (*AstMemberAccess)expr;
            sma.object = generic_process_expr(sma.object, symtab);
            return expr;
        case AST_TRY:
            var tr2: *AstTry = (*AstTry)expr;
            tr2.operand = generic_process_expr(tr2.operand, symtab);
            return expr;
        case AST_FLOAT:
            return expr;
        case AST_SUPER:
            return expr;
        case AST_METHOD_CALL:
            generic_process_method_call_expr((*AstMethodCall)expr, symtab);
            return expr;
        case AST_SAFE_METHOD_CALL:
            generic_process_method_call_expr((*AstMethodCall)expr, symtab);
            return expr;
        case AST_NEW:
            return generic_process_new_expr((*AstNew)expr, symtab);
        case AST_STACK_CTOR:
            return generic_process_stack_ctor_expr((*AstStackCtor)expr, symtab);
        case AST_INDEX:
            return generic_process_index_expr((*AstIndex)expr, symtab);
        case AST_SLICE:
            return generic_process_slice_expr((*AstSlice)expr, symtab);
        case AST_CALL_PTR:
            return generic_process_call_ptr_expr((*AstCallPtr)expr, symtab);
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)expr;
            se.expr = generic_process_expr(se.expr, symtab);
            return expr;
        default:
            return expr;
    }
}

func generic_process_stmt_block(stmts: *Vec<*AstNode>, symtab: *HashMap<u64, *TypeInfo>) -> u64 {
    if (stmts == 0) { return 0; }
    var n: u64 = stmts.len();
    for (var i: u64 = 0; i < n; i++) {
        generic_process_stmt(stmts.get(i), symtab);
    }
    return 0;
}

func generic_process_stmt(stmt: *AstNode, symtab: *HashMap<u64, *TypeInfo>) -> u64 {
    if (stmt == 0) { return 0; }
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)stmt;
            r.expr = generic_process_expr(r.expr, symtab);
            return stmt;
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            vd.init_expr = generic_process_expr(vd.init_expr, symtab);
            return stmt;
        case AST_ASSIGN:
            var assign_stmt: *AstAssign = (*AstAssign)stmt;
            assign_stmt.target = generic_process_expr(assign_stmt.target, symtab);
            assign_stmt.value = generic_process_expr(assign_stmt.value, symtab);
            return stmt;
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)stmt;
            es.expr = generic_process_expr(es.expr, symtab);
            return stmt;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            ifs.cond = generic_process_expr(ifs.cond, symtab);
            generic_process_stmt(ifs.then_block, symtab);
            generic_process_stmt(ifs.else_block, symtab);
            return stmt;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            wl.cond = generic_process_expr(wl.cond, symtab);
            generic_process_stmt(wl.body, symtab);
            return stmt;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            generic_process_stmt(fr.init, symtab);
            fr.cond = generic_process_expr(fr.cond, symtab);
            fr.update = generic_process_expr(fr.update, symtab);
            generic_process_stmt(fr.body, symtab);
            return stmt;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            sw.expr = generic_process_expr(sw.expr, symtab);
            generic_process_stmt_block(sw.cases_vec, symtab);
            return stmt;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            generic_process_stmt(cs.body, symtab);
            return stmt;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            generic_process_stmt_block(blk.stmts_vec, symtab);
            return stmt;
        default:
            return stmt;
    }
}

func compiler_try_resolve_struct_name(name_ptr: u64, name_len: u64, resolved_ptr_out: *u64, resolved_len_out: *u64, struct_def_out: **AstStructDef) -> u64 {
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var struct_def: *AstStructDef = get_struct_def(resolved_ptr, resolved_len);
    if (struct_def == 0) {
        var resolved_name: *NameInfo = resolve_name(name_ptr, name_len);
        if (resolved_name != 0) {
            var resolved_def: *AstStructDef = get_struct_def(resolved_name.ptr, resolved_name.len);
            if (resolved_def != 0) {
                resolved_ptr = resolved_name.ptr;
                resolved_len = resolved_name.len;
                struct_def = resolved_def;
            }
        }
    }
    if (struct_def == 0) { return 0; }
    *resolved_ptr_out = resolved_ptr;
    *resolved_len_out = resolved_len;
    *struct_def_out = struct_def;
    return 1;
}

func compiler_try_resolve_generic_struct_template(name_ptr: u64, name_len: u64, tpl_out: **GenericStructTemplate) -> u64 {
    var tpl: *GenericStructTemplate = compiler_find_generic_struct_template_by_name(name_ptr, name_len);
    if (tpl == 0) {
        var resolved_name: *NameInfo = resolve_name(name_ptr, name_len);
        if (resolved_name != 0) {
            tpl = compiler_find_generic_struct_template_by_name(resolved_name.ptr, resolved_name.len);
        }
    }
    if (tpl == 0) { return 0; }
    *tpl_out = tpl;
    return 1;
}

func compiler_try_resolve_generic_struct_type_instance(name_ptr: u64, name_len: u64, generic_args: *Vec<*GenericArg>, out_name_info: **NameInfo) -> u64 {
    var tpl: *GenericStructTemplate = 0;
    if (compiler_try_resolve_generic_struct_template(name_ptr, name_len, &tpl) == 0) {
        return 0;
    }
    var bindings: *Vec<*GenericBinding> = generic_build_bindings_for_struct(tpl, generic_args);
    var name_info: *NameInfo = generic_struct_get_or_create_instance(tpl, bindings);
    if (name_info == 0) { return 0; }
    *out_name_info = name_info;
    return 1;
}

func compiler_resolve_generic_struct_type_instance(name_ptr: u64, name_len: u64, generic_args: *Vec<*GenericArg>) -> *NameInfo {
    var out: *NameInfo = 0;
    if (compiler_try_resolve_generic_struct_type_instance(name_ptr, name_len, generic_args, &out) != 0) {
        return out;
    }
    emit_stderr("[ERROR] Generic struct template not found: ");
    emit_stderr_len(name_ptr, name_len);
    emit_stderr("\n");
    panic("Compiler error");
}

func compiler_lower_parse_sugar_args(args: *Vec<*AstNode>) -> u64 {
    if (args == 0) { return 0; }
    var n: u64 = args.len();
    for (var i: u64 = 0; i < n; i++) {
        args.set(i, compiler_lower_parse_sugar_expr(args.get(i)));
    }
    return 0;
}

func compiler_lower_parse_sugar_ctor_call(call: *AstCall) -> *AstNode {
    if (call == 0) { return 0; }
    if (call.origin != AST_CALL_ORIGIN_DIRECT) { return (*AstNode)call; }

    var struct_ptr: u64 = 0;
    var struct_len: u64 = 0;
    var struct_def: *AstStructDef = 0;
    if (compiler_try_resolve_struct_name(call.name_ptr, call.name_len, &struct_ptr, &struct_len, &struct_def) == 0) {
        return (*AstNode)call;
    }

    return (*AstNode)ast_stack_ctor(TYPE_STRUCT, 0, 0, struct_ptr, struct_len, 0, 0, 0, 0, 0, 0, 0, 0, call.args_vec);
}

func compiler_lower_parse_sugar_generic_ctor_call(gcall: *AstGenericCall) -> *AstNode {
    if (gcall == 0) { return 0; }
    if (gcall.origin != AST_CALL_ORIGIN_DIRECT) { return (*AstNode)gcall; }

    var tpl: *GenericStructTemplate = 0;
    if (compiler_try_resolve_generic_struct_template(gcall.name_ptr, gcall.name_len, &tpl) == 0) {
        return (*AstNode)gcall;
    }

    var bindings: *Vec<*GenericBinding> = generic_build_bindings_for_struct(tpl, gcall.generic_args_vec);
    var name_info: *NameInfo = generic_struct_get_or_create_instance(tpl, bindings);
    return (*AstNode)ast_stack_ctor(TYPE_STRUCT, 0, 0, name_info.ptr, name_info.len, 0, 0, 0, 0, 0, 0, 0, 0, gcall.args_vec);
}

func compiler_lower_parse_sugar_generic_struct_literal(gsl: *AstGenericStructLiteral) -> *AstNode {
    if (gsl == 0) { return 0; }

    var tpl: *GenericStructTemplate = 0;
    if (compiler_try_resolve_generic_struct_template(gsl.name_ptr, gsl.name_len, &tpl) == 0) {
        emit_stderr("[ERROR] Generic struct template not found: ");
        emit_stderr_len(gsl.name_ptr, gsl.name_len);
        emit_stderr("\n");
        panic("Compiler error");
    }

    var bindings: *Vec<*GenericBinding> = generic_build_bindings_for_struct(tpl, gsl.generic_args_vec);
    var name_info: *NameInfo = generic_struct_get_or_create_instance(tpl, bindings);
    var struct_def: *AstStructDef = get_struct_def(name_info.ptr, name_info.len);
    if (struct_def == 0) {
        emit_stderr("[ERROR] Generic struct def missing\n");
        panic("Compiler error");
    }
    return (*AstNode)ast_struct_literal(struct_def, gsl.values_vec);
}

func compiler_lower_parse_sugar_static_generic_method_call(sgc: *AstStaticGenericCall) -> *AstNode {
    if (sgc == 0) { return 0; }

    var struct_ptr: u64 = 0;
    var struct_len: u64 = 0;
    var struct_def: *AstStructDef = 0;
    if (compiler_try_resolve_struct_name(sgc.struct_name_ptr, sgc.struct_name_len, &struct_ptr, &struct_len, &struct_def) == 0) {
        emit_stderr("[ERROR] static generic method owner type not found: ");
        emit_stderr_len(sgc.struct_name_ptr, sgc.struct_name_len);
        emit_stderr("\n");
        panic("Compiler error");
    }

    var lowered_name: *NameInfo = compiler_build_method_name(struct_ptr, struct_len, sgc.method_ptr, sgc.method_len);
    return (*AstNode)ast_generic_call_with_origin(lowered_name.ptr, lowered_name.len, sgc.generic_args_vec, sgc.args_vec, AST_CALL_ORIGIN_MEMBER_LOWERED);
}

func compiler_lower_parse_sugar_static_method_call(mc: *AstMethodCall, is_safe: u64) -> *AstNode {
    if (mc == 0 || mc.receiver == 0) { return (*AstNode)mc; }
    if (ast_kind(mc.receiver) != AST_IDENT) { return (*AstNode)mc; }

    var recv_id: *AstIdent = (*AstIdent)mc.receiver;
    var struct_ptr: u64 = 0;
    var struct_len: u64 = 0;
    var struct_def: *AstStructDef = 0;
    if (compiler_try_resolve_struct_name(recv_id.name_ptr, recv_id.name_len, &struct_ptr, &struct_len, &struct_def) == 0) {
        return (*AstNode)mc;
    }

    if (is_safe != 0) {
        emit_stderr("[ERROR] safe navigation is not allowed for static method call\n");
        panic("Compiler error");
    }

    var lowered_name: *NameInfo = compiler_build_method_name(struct_ptr, struct_len, mc.method_ptr, mc.method_len);
    return (*AstNode)ast_call_with_origin(lowered_name.ptr, lowered_name.len, mc.args_vec, AST_CALL_ORIGIN_MEMBER_LOWERED);
}

func compiler_lower_parse_sugar_expr(expr: *AstNode) -> *AstNode {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_CALL:
            var call: *AstCall = (*AstCall)expr;
            compiler_lower_parse_sugar_args(call.args_vec);
            return compiler_lower_parse_sugar_ctor_call(call);
        case AST_GENERIC_CALL:
            var gcall: *AstGenericCall = (*AstGenericCall)expr;
            compiler_lower_parse_sugar_args(gcall.args_vec);
            return compiler_lower_parse_sugar_generic_ctor_call(gcall);
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)expr;
            compiler_lower_parse_sugar_args(gsl.values_vec);
            return compiler_lower_parse_sugar_generic_struct_literal(gsl);
        case AST_STATIC_GENERIC_CALL:
            var sgc: *AstStaticGenericCall = (*AstStaticGenericCall)expr;
            compiler_lower_parse_sugar_args(sgc.args_vec);
            return compiler_lower_parse_sugar_static_generic_method_call(sgc);
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)expr;
            cp.callee = compiler_lower_parse_sugar_expr(cp.callee);
            compiler_lower_parse_sugar_args(cp.args_vec);
            return expr;
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)expr;
            bin.left = compiler_lower_parse_sugar_expr(bin.left);
            bin.right = compiler_lower_parse_sugar_expr(bin.right);
            return expr;
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)expr;
            un.operand = compiler_lower_parse_sugar_expr(un.operand);
            return expr;
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)expr;
            ao.operand = compiler_lower_parse_sugar_expr(ao.operand);
            return expr;
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)expr;
            dr.operand = compiler_lower_parse_sugar_expr(dr.operand);
            return expr;
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)expr;
            dr8.operand = compiler_lower_parse_sugar_expr(dr8.operand);
            return expr;
        case AST_CAST:
            var cs: *AstCast = (*AstCast)expr;
            cs.expr = compiler_lower_parse_sugar_expr(cs.expr);
            return expr;
        case AST_MEMBER_ACCESS:
        case AST_SAFE_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)expr;
            ma.object = compiler_lower_parse_sugar_expr(ma.object);
            return expr;
        case AST_METHOD_CALL:
            var mc: *AstMethodCall = (*AstMethodCall)expr;
            mc.receiver = compiler_lower_parse_sugar_expr(mc.receiver);
            compiler_lower_parse_sugar_args(mc.args_vec);
            return compiler_lower_parse_sugar_static_method_call(mc, 0);
        case AST_SAFE_METHOD_CALL:
            var smc: *AstMethodCall = (*AstMethodCall)expr;
            smc.receiver = compiler_lower_parse_sugar_expr(smc.receiver);
            compiler_lower_parse_sugar_args(smc.args_vec);
            return compiler_lower_parse_sugar_static_method_call(smc, 1);
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            ix.base = compiler_lower_parse_sugar_expr(ix.base);
            ix.index = compiler_lower_parse_sugar_expr(ix.index);
            return expr;
        case AST_SLICE:
            var sl: *AstSlice = (*AstSlice)expr;
            sl.ptr_expr = compiler_lower_parse_sugar_expr(sl.ptr_expr);
            sl.len_expr = compiler_lower_parse_sugar_expr(sl.len_expr);
            return expr;
        case AST_TRY:
            var tr: *AstTry = (*AstTry)expr;
            tr.operand = compiler_lower_parse_sugar_expr(tr.operand);
            return expr;
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)expr;
            se.expr = compiler_lower_parse_sugar_expr(se.expr);
            return expr;
        case AST_STRUCT_LITERAL:
            var lit: *AstStructLiteral = (*AstStructLiteral)expr;
            compiler_lower_parse_sugar_args(lit.values_vec);
            return expr;
        case AST_NEW:
            var nw: *AstNew = (*AstNew)expr;
            nw.literal_expr = compiler_lower_parse_sugar_expr(nw.literal_expr);
            compiler_lower_parse_sugar_args(nw.ctor_args_vec);
            return expr;
        case AST_STACK_CTOR:
            var sc: *AstStackCtor = (*AstStackCtor)expr;
            compiler_lower_parse_sugar_args(sc.ctor_args_vec);
            return expr;
        default:
            return expr;
    }
}

func compiler_lower_parse_sugar_stmt(stmt: *AstNode) -> u64 {
    if (stmt == 0) { return 0; }
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)stmt;
            r.expr = compiler_lower_parse_sugar_expr(r.expr);
            break;
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            vd.init_expr = compiler_lower_parse_sugar_expr(vd.init_expr);
            break;
        case AST_ASSIGN:
            var asn: *AstAssign = (*AstAssign)stmt;
            asn.target = compiler_lower_parse_sugar_expr(asn.target);
            asn.value = compiler_lower_parse_sugar_expr(asn.value);
            break;
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)stmt;
            es.expr = compiler_lower_parse_sugar_expr(es.expr);
            break;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            ifs.cond = compiler_lower_parse_sugar_expr(ifs.cond);
            compiler_lower_parse_sugar_stmt(ifs.then_block);
            compiler_lower_parse_sugar_stmt(ifs.else_block);
            break;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            wl.cond = compiler_lower_parse_sugar_expr(wl.cond);
            compiler_lower_parse_sugar_stmt(wl.body);
            break;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            compiler_lower_parse_sugar_stmt(fr.init);
            fr.cond = compiler_lower_parse_sugar_expr(fr.cond);
            fr.update = compiler_lower_parse_sugar_expr(fr.update);
            compiler_lower_parse_sugar_stmt(fr.body);
            break;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            sw.expr = compiler_lower_parse_sugar_expr(sw.expr);
            if (sw.cases_vec != 0) {
                var cn: u64 = sw.cases_vec.len();
                for (var ci: u64 = 0; ci < cn; ci++) {
                    compiler_lower_parse_sugar_stmt(sw.cases_vec.get(ci));
                }
            }
            break;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            cs.value = compiler_lower_parse_sugar_expr(cs.value);
            compiler_lower_parse_sugar_stmt(cs.body);
            break;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            if (blk.stmts_vec != 0) {
                var n: u64 = blk.stmts_vec.len();
                for (var i: u64 = 0; i < n; i++) {
                    compiler_lower_parse_sugar_stmt(blk.stmts_vec.get(i));
                }
            }
            break;
        case AST_DEFER:
            var df: *AstDefer = (*AstDefer)stmt;
            compiler_lower_parse_sugar_stmt(df.stmt);
            break;
        case AST_DELETE:
            var dl: *AstDelete = (*AstDelete)stmt;
            dl.expr = compiler_lower_parse_sugar_expr(dl.expr);
            break;
        default:
            break;
    }
    return 0;
}

func compiler_lower_parse_sugar() -> u64 {
    if (g_all_funcs != 0) {
        var fn_count: u64 = g_all_funcs.len();
        for (var i: u64 = 0; i < fn_count; i++) {
            var fn: *AstFunc = g_all_funcs.get(i);
            if (fn == 0 || fn.body == 0) { continue; }
            compiler_lower_parse_sugar_stmt(fn.body);
        }
    }

    if (g_all_globals != 0) {
        var gn: u64 = g_all_globals.len();
        for (var gi: u64 = 0; gi < gn; gi++) {
            var ginfo: *GlobalInfo = g_all_globals.get(gi);
            if (ginfo == 0 || ginfo.init_expr == 0) { continue; }
            ginfo.init_expr = (u64)compiler_lower_parse_sugar_expr((*AstNode)ginfo.init_expr);
        }
    }
    return 0;
}

func compiler_set_global_init_none(ginfo: *GlobalInfo) -> u64 {
    if (ginfo == 0) { return 0; }
    ginfo.init_kind = GLOBAL_INIT_NONE;
    ginfo.init_value = 0;
    ginfo.init_name_ptr = 0;
    ginfo.init_name_len = 0;
    return 0;
}

func compiler_set_global_init_value(ginfo: *GlobalInfo, value: u64) -> u64 {
    if (ginfo == 0) { return 0; }
    ginfo.init_kind = GLOBAL_INIT_VALUE;
    ginfo.init_value = value;
    ginfo.init_name_ptr = 0;
    ginfo.init_name_len = 0;
    return 0;
}

func compiler_set_global_init_const_name(ginfo: *GlobalInfo, name_ptr: u64, name_len: u64) -> u64 {
    if (ginfo == 0) { return 0; }
    ginfo.init_kind = GLOBAL_INIT_CONST;
    ginfo.init_value = 0;
    ginfo.init_name_ptr = name_ptr;
    ginfo.init_name_len = name_len;
    return 0;
}

func compiler_set_global_init_expr(ginfo: *GlobalInfo) -> u64 {
    if (ginfo == 0) { return 0; }
    ginfo.init_kind = GLOBAL_INIT_EXPR;
    ginfo.init_value = 0;
    ginfo.init_name_ptr = 0;
    ginfo.init_name_len = 0;
    return 0;
}

func compiler_set_global_init_runtime(ginfo: *GlobalInfo) -> u64 {
    if (ginfo == 0) { return 0; }
    ginfo.init_kind = GLOBAL_INIT_RUNTIME;
    ginfo.init_value = 0;
    ginfo.init_name_ptr = 0;
    ginfo.init_name_len = 0;
    return 0;
}

func compiler_finalize_single_global_init_kind(ginfo: *GlobalInfo) -> u64 {
    if (ginfo == 0 || ginfo.init_expr == 0) {
        compiler_set_global_init_none(ginfo);
        return 0;
    }

    var expr: *AstNode = (*AstNode)ginfo.init_expr;
    var kind: u64 = ast_kind(expr);

    if (kind == AST_GENERIC_STRUCT_LITERAL) {
        var lowered: *AstNode = compiler_lower_parse_sugar_generic_struct_literal((*AstGenericStructLiteral)expr);
        ginfo.init_expr = (u64)lowered;
        expr = lowered;
        kind = ast_kind(expr);
    }

    if (kind == AST_LITERAL) {
        var lit: *AstLiteral = (*AstLiteral)expr;
        compiler_set_global_init_value(ginfo, lit.value);
        return 0;
    }

    if (kind == AST_IDENT) {
        var id: *AstIdent = (*AstIdent)expr;
        var const_res: *ConstResult = (*ConstResult)compiler_find_const(id.name_ptr, id.name_len);
        if (const_res != 0 && const_res.found != 0) {
            compiler_set_global_init_value(ginfo, const_res.value);
        } else {
            compiler_set_global_init_const_name(ginfo, id.name_ptr, id.name_len);
        }
        return 0;
    }

    if (kind == AST_STRUCT_LITERAL) {
        if (ginfo.typeinfo_ptr == 0 || ginfo.typeinfo_ptr.type_kind != TYPE_STRUCT || ginfo.typeinfo_ptr.ptr_depth != 0) {
            emit_stderr("[ERROR] Global struct literal requires struct type\n");
            panic("Compiler error");
        }
        compiler_set_global_init_expr(ginfo);
        return 0;
    }

    if (kind == AST_CALL || kind == AST_METHOD_CALL || kind == AST_CALL_PTR || kind == AST_GENERIC_CALL || kind == AST_STATIC_GENERIC_CALL || kind == AST_NEW || kind == AST_STACK_CTOR) {
        compiler_set_global_init_runtime(ginfo);
        return 0;
    }

    emit_stderr("[ERROR] Unsupported global init expression\n");
    panic("Compiler error");
    return 0;
}

func compiler_finalize_global_init_kinds() -> u64 {
    if (g_all_globals == 0) { return 0; }
    var n: u64 = g_all_globals.len();
    for (var i: u64 = 0; i < n; i++) {
        compiler_finalize_single_global_init_kind(g_all_globals.get(i));
    }
    return 0;
}

func compiler_validate_direct_impl_call_name(name_ptr: u64, name_len: u64, origin: u64, owner_ptr: u64, owner_len: u64) -> u64 {
    if (origin != AST_CALL_ORIGIN_DIRECT) { return 0; }
    if (compiler_is_impl_method(name_ptr, name_len) == 0) { return 0; }
    if (owner_ptr != 0 && owner_len != 0) { return 0; }
    emit_stderr("[ERROR] Direct call to impl-lowered method is not allowed: ");
    emit_stderr_len(name_ptr, name_len);
    emit_stderr("\n");
    panic("Compiler error");
    return 0;
}

func compiler_validate_direct_impl_calls_args(args: *Vec<*AstNode>, owner_ptr: u64, owner_len: u64) -> u64 {
    if (args == 0) { return 0; }
    var n: u64 = args.len();
    for (var i: u64 = 0; i < n; i++) {
        compiler_validate_direct_impl_calls_expr(args.get(i), owner_ptr, owner_len);
    }
    return 0;
}

func compiler_validate_direct_impl_calls_expr(expr: *AstNode, owner_ptr: u64, owner_len: u64) -> u64 {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_CALL:
            var call: *AstCall = (*AstCall)expr;
            compiler_validate_direct_impl_call_name(call.name_ptr, call.name_len, call.origin, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_args(call.args_vec, owner_ptr, owner_len);
            break;
        case AST_GENERIC_CALL:
            var gcall: *AstGenericCall = (*AstGenericCall)expr;
            compiler_validate_direct_impl_call_name(gcall.name_ptr, gcall.name_len, gcall.origin, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_args(gcall.args_vec, owner_ptr, owner_len);
            break;
        case AST_STATIC_GENERIC_CALL:
            var sgc: *AstStaticGenericCall = (*AstStaticGenericCall)expr;
            compiler_validate_direct_impl_calls_args(sgc.args_vec, owner_ptr, owner_len);
            break;
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)expr;
            compiler_validate_direct_impl_calls_expr(cp.callee, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_args(cp.args_vec, owner_ptr, owner_len);
            break;
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)expr;
            compiler_validate_direct_impl_calls_expr(bin.left, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(bin.right, owner_ptr, owner_len);
            break;
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)expr;
            compiler_validate_direct_impl_calls_expr(un.operand, owner_ptr, owner_len);
            break;
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)expr;
            compiler_validate_direct_impl_calls_expr(ao.operand, owner_ptr, owner_len);
            break;
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)expr;
            compiler_validate_direct_impl_calls_expr(dr.operand, owner_ptr, owner_len);
            break;
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)expr;
            compiler_validate_direct_impl_calls_expr(dr8.operand, owner_ptr, owner_len);
            break;
        case AST_CAST:
            var cs: *AstCast = (*AstCast)expr;
            compiler_validate_direct_impl_calls_expr(cs.expr, owner_ptr, owner_len);
            break;
        case AST_MEMBER_ACCESS:
        case AST_SAFE_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)expr;
            compiler_validate_direct_impl_calls_expr(ma.object, owner_ptr, owner_len);
            break;
        case AST_METHOD_CALL:
        case AST_SAFE_METHOD_CALL:
            var mc: *AstMethodCall = (*AstMethodCall)expr;
            compiler_validate_direct_impl_calls_expr(mc.receiver, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_args(mc.args_vec, owner_ptr, owner_len);
            break;
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            compiler_validate_direct_impl_calls_expr(ix.base, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(ix.index, owner_ptr, owner_len);
            break;
        case AST_SLICE:
            var sl: *AstSlice = (*AstSlice)expr;
            compiler_validate_direct_impl_calls_expr(sl.ptr_expr, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(sl.len_expr, owner_ptr, owner_len);
            break;
        case AST_TRY:
            var tr: *AstTry = (*AstTry)expr;
            compiler_validate_direct_impl_calls_expr(tr.operand, owner_ptr, owner_len);
            break;
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)expr;
            compiler_validate_direct_impl_calls_expr(se.expr, owner_ptr, owner_len);
            break;
        case AST_STRUCT_LITERAL:
            var lit: *AstStructLiteral = (*AstStructLiteral)expr;
            compiler_validate_direct_impl_calls_args(lit.values_vec, owner_ptr, owner_len);
            break;
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)expr;
            compiler_validate_direct_impl_calls_args(gsl.values_vec, owner_ptr, owner_len);
            break;
        case AST_NEW:
            var nw: *AstNew = (*AstNew)expr;
            compiler_validate_direct_impl_calls_expr(nw.literal_expr, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_args(nw.ctor_args_vec, owner_ptr, owner_len);
            break;
        case AST_STACK_CTOR:
            var sc: *AstStackCtor = (*AstStackCtor)expr;
            compiler_validate_direct_impl_calls_args(sc.ctor_args_vec, owner_ptr, owner_len);
            break;
        default:
            break;
    }
    return 0;
}

func compiler_validate_direct_impl_calls_stmt(stmt: *AstNode, owner_ptr: u64, owner_len: u64) -> u64 {
    if (stmt == 0) { return 0; }
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)stmt;
            compiler_validate_direct_impl_calls_expr(r.expr, owner_ptr, owner_len);
            break;
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            compiler_validate_direct_impl_calls_expr(vd.init_expr, owner_ptr, owner_len);
            break;
        case AST_ASSIGN:
            var asn: *AstAssign = (*AstAssign)stmt;
            compiler_validate_direct_impl_calls_expr(asn.target, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(asn.value, owner_ptr, owner_len);
            break;
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)stmt;
            compiler_validate_direct_impl_calls_expr(es.expr, owner_ptr, owner_len);
            break;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            compiler_validate_direct_impl_calls_expr(ifs.cond, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_stmt(ifs.then_block, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_stmt(ifs.else_block, owner_ptr, owner_len);
            break;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            compiler_validate_direct_impl_calls_expr(wl.cond, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_stmt(wl.body, owner_ptr, owner_len);
            break;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            compiler_validate_direct_impl_calls_stmt(fr.init, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(fr.cond, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_expr(fr.update, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_stmt(fr.body, owner_ptr, owner_len);
            break;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            compiler_validate_direct_impl_calls_expr(sw.expr, owner_ptr, owner_len);
            if (sw.cases_vec != 0) {
                var cn: u64 = sw.cases_vec.len();
                for (var ci: u64 = 0; ci < cn; ci++) {
                    compiler_validate_direct_impl_calls_stmt(sw.cases_vec.get(ci), owner_ptr, owner_len);
                }
            }
            break;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            compiler_validate_direct_impl_calls_expr(cs.value, owner_ptr, owner_len);
            compiler_validate_direct_impl_calls_stmt(cs.body, owner_ptr, owner_len);
            break;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            if (blk.stmts_vec != 0) {
                var n: u64 = blk.stmts_vec.len();
                for (var i: u64 = 0; i < n; i++) {
                    compiler_validate_direct_impl_calls_stmt(blk.stmts_vec.get(i), owner_ptr, owner_len);
                }
            }
            break;
        case AST_DEFER:
            var df: *AstDefer = (*AstDefer)stmt;
            compiler_validate_direct_impl_calls_stmt(df.stmt, owner_ptr, owner_len);
            break;
        case AST_DELETE:
            var dl: *AstDelete = (*AstDelete)stmt;
            compiler_validate_direct_impl_calls_expr(dl.expr, owner_ptr, owner_len);
            break;
        default:
            break;
    }
    return 0;
}

func compiler_validate_direct_impl_calls() -> u64 {
    if (g_all_funcs != 0) {
        var fn_count: u64 = g_all_funcs.len();
        for (var i: u64 = 0; i < fn_count; i++) {
            var fn: *AstFunc = g_all_funcs.get(i);
            if (fn == 0 || fn.body == 0) { continue; }
            var owner_info: *NameInfo = compiler_get_impl_method_owner(fn.name_ptr, fn.name_len);
            var owner_ptr: u64 = 0;
            var owner_len: u64 = 0;
            if (owner_info != 0) {
                owner_ptr = owner_info.ptr;
                owner_len = owner_info.len;
            }
            compiler_validate_direct_impl_calls_stmt(fn.body, owner_ptr, owner_len);
        }
    }

    if (g_all_globals != 0) {
        var gn: u64 = g_all_globals.len();
        for (var gi: u64 = 0; gi < gn; gi++) {
            var ginfo: *GlobalInfo = g_all_globals.get(gi);
            if (ginfo == 0 || ginfo.init_expr == 0) { continue; }
            compiler_validate_direct_impl_calls_expr((*AstNode)ginfo.init_expr, 0, 0);
        }
    }
    return 0;
}

func compiler_is_super_expr(expr: *AstNode) -> u64 {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    if (kind == AST_SUPER) { return 1; }
    if (kind != AST_IDENT) { return 0; }
    var id: *AstIdent = (*AstIdent)expr;
    if (str_eq(id.name_ptr, id.name_len, "super", 5) != 0) { return 1; }
    return 0;
}

func compiler_super_require_member_form() -> u64 {
    emit_stderr("[ERROR] 'super' must be followed by '.' and a member name\n");
    panic("Compiler error");
    return 0;
}

func compiler_super_resolve_parent(owner_ptr: u64, owner_len: u64, parent_ptr_out: *u64, parent_len_out: *u64, parent_def_out: **AstStructDef) -> u64 {
    if (owner_ptr == 0 || owner_len == 0) {
        emit_stderr("[ERROR] 'super' is only allowed inside impl methods\n");
        panic("Compiler error");
    }

    var owner_def: *AstStructDef = get_struct_def(owner_ptr, owner_len);
    if (owner_def == 0) {
        emit_stderr("[ERROR] owner struct for super not found: ");
        emit_stderr_len(owner_ptr, owner_len);
        emit_stderr("\n");
        panic("Compiler error");
    }
    if (owner_def.parents_vec == 0 || owner_def.parents_vec.len() == 0) {
        emit_stderr("[ERROR] 'super' requires a parent struct\n");
        panic("Compiler error");
    }
    if (owner_def.parents_vec.len() != 1) {
        emit_stderr("[ERROR] ambiguous 'super' with multiple parents; use self.<Parent>member\n");
        panic("Compiler error");
    }

    var parent_desc: *ParentDesc = owner_def.parents_vec.get(0);
    var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
    *parent_ptr_out = parent_def.name_ptr;
    *parent_len_out = parent_def.name_len;
    *parent_def_out = parent_def;
    return 0;
}

func compiler_super_check_access(owner_ptr: u64, owner_len: u64, member_owner_ptr: u64, member_owner_len: u64, access: u64, member_ptr: u64, member_len: u64, kind_msg: u64) -> u64 {
    if (compiler_can_access_member_from(owner_ptr, owner_len, member_owner_ptr, member_owner_len, access) != 0) {
        return 1;
    }
    emit_stderr("[ERROR] super ");
    emit_stderr(kind_msg);
    emit_stderr(" access denied: ");
    emit_stderr_len(member_owner_ptr, member_owner_len);
    emit_stderr(".");
    emit_stderr_len(member_ptr, member_len);
    emit_stderr("\n");
    panic("Compiler error");
    return 0;
}

func compiler_lower_super_args(args: *Vec<*AstNode>, owner_ptr: u64, owner_len: u64) -> u64 {
    if (args == 0) { return 0; }
    var n: u64 = args.len();
    for (var i: u64 = 0; i < n; i++) {
        args.set(i, compiler_lower_super_expr(args.get(i), owner_ptr, owner_len));
    }
    return 0;
}

func compiler_lower_super_member_access_expr(ma: *AstMemberAccess, owner_ptr: u64, owner_len: u64, is_safe: u64) -> *AstNode {
    if (compiler_is_super_expr(ma.object) == 0) {
        ma.object = compiler_lower_super_expr(ma.object, owner_ptr, owner_len);
        return (*AstNode)ma;
    }

    if (is_safe != 0) {
        emit_stderr("[ERROR] safe navigation is not allowed with super\n");
        panic("Compiler error");
    }
    if (ma.parent_ptr != 0) {
        emit_stderr("[ERROR] super does not allow explicit parent scope; use super.member\n");
        panic("Compiler error");
    }

    var parent_ptr: u64 = 0;
    var parent_len: u64 = 0;
    var parent_def: *AstStructDef = 0;
    compiler_super_resolve_parent(owner_ptr, owner_len, &parent_ptr, &parent_len, &parent_def);

    var field_desc: *FieldDesc = _compiler_find_field_desc_unscoped(parent_def, ma.member_ptr, ma.member_len, 0);
    if (field_desc == 0) {
        emit_stderr("[ERROR] super field not found: ");
        emit_stderr_len(parent_ptr, parent_len);
        emit_stderr(".");
        emit_stderr_len(ma.member_ptr, ma.member_len);
        emit_stderr("\n");
        panic("Compiler error");
    }

    compiler_super_check_access(owner_ptr, owner_len, field_desc.owner_struct_ptr, field_desc.owner_struct_len, field_desc.access, ma.member_ptr, ma.member_len, "field");
    return (*AstNode)ast_member_access_scoped((*AstNode)ast_ident("self", 4), parent_ptr, parent_len, ma.member_ptr, ma.member_len);
}

func compiler_lower_super_method_call_expr(mc: *AstMethodCall, owner_ptr: u64, owner_len: u64, is_safe: u64) -> *AstNode {
    compiler_lower_super_args(mc.args_vec, owner_ptr, owner_len);
    if (compiler_is_super_expr(mc.receiver) == 0) {
        mc.receiver = compiler_lower_super_expr(mc.receiver, owner_ptr, owner_len);
        return (*AstNode)mc;
    }

    if (is_safe != 0) {
        emit_stderr("[ERROR] safe navigation is not allowed with super\n");
        panic("Compiler error");
    }

    var parent_ptr: u64 = 0;
    var parent_len: u64 = 0;
    var parent_def: *AstStructDef = 0;
    compiler_super_resolve_parent(owner_ptr, owner_len, &parent_ptr, &parent_len, &parent_def);

    var member_owner_ptr: u64 = 0;
    var member_owner_len: u64 = 0;
    var member_access: u64 = ACCESS_PUBLIC;
    var member_sig: *AstFunc = 0;
    if (compiler_find_method_decl_in_hierarchy(parent_ptr, parent_len, mc.method_ptr, mc.method_len, &member_owner_ptr, &member_owner_len, &member_access, &member_sig) == 0) {
        emit_stderr("[ERROR] super method not found: ");
        emit_stderr_len(parent_ptr, parent_len);
        emit_stderr(".");
        emit_stderr_len(mc.method_ptr, mc.method_len);
        emit_stderr("\n");
        panic("Compiler error");
    }

    compiler_super_check_access(owner_ptr, owner_len, member_owner_ptr, member_owner_len, member_access, mc.method_ptr, mc.method_len, "method");
    if (member_owner_ptr == 0 || member_owner_len == 0) {
        member_owner_ptr = parent_ptr;
        member_owner_len = parent_len;
    }

    var lowered_name: *NameInfo = compiler_build_method_name(member_owner_ptr, member_owner_len, mc.method_ptr, mc.method_len);
    var lowered_args: *Vec<*AstNode> = new Vec<*AstNode>(mc.args_vec.?len() + 1);
    lowered_args.push((*AstNode)ast_ident("self", 4));
    var arg_count: u64 = mc.args_vec.?len();
    for (var i: u64 = 0; i < arg_count; i++) {
        lowered_args.push(mc.args_vec.get(i));
    }
    return (*AstNode)ast_call_with_origin(lowered_name.ptr, lowered_name.len, lowered_args, AST_CALL_ORIGIN_MEMBER_LOWERED);
}

func compiler_lower_super_expr(expr: *AstNode, owner_ptr: u64, owner_len: u64) -> *AstNode {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_IDENT:
            var id: *AstIdent = (*AstIdent)expr;
            if (str_eq(id.name_ptr, id.name_len, "super", 5) != 0) {
                compiler_super_require_member_form();
            }
            return expr;
        case AST_SUPER:
            compiler_super_require_member_form();
            return expr;
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)expr;
            bin.left = compiler_lower_super_expr(bin.left, owner_ptr, owner_len);
            bin.right = compiler_lower_super_expr(bin.right, owner_ptr, owner_len);
            return expr;
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)expr;
            un.operand = compiler_lower_super_expr(un.operand, owner_ptr, owner_len);
            return expr;
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)expr;
            ao.operand = compiler_lower_super_expr(ao.operand, owner_ptr, owner_len);
            return expr;
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)expr;
            dr.operand = compiler_lower_super_expr(dr.operand, owner_ptr, owner_len);
            return expr;
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)expr;
            dr8.operand = compiler_lower_super_expr(dr8.operand, owner_ptr, owner_len);
            return expr;
        case AST_CAST:
            var cs: *AstCast = (*AstCast)expr;
            cs.expr = compiler_lower_super_expr(cs.expr, owner_ptr, owner_len);
            return expr;
        case AST_CALL:
            var call: *AstCall = (*AstCall)expr;
            compiler_lower_super_args(call.args_vec, owner_ptr, owner_len);
            return expr;
        case AST_GENERIC_CALL:
            var gcall: *AstGenericCall = (*AstGenericCall)expr;
            compiler_lower_super_args(gcall.args_vec, owner_ptr, owner_len);
            return expr;
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)expr;
            cp.callee = compiler_lower_super_expr(cp.callee, owner_ptr, owner_len);
            compiler_lower_super_args(cp.args_vec, owner_ptr, owner_len);
            return expr;
        case AST_METHOD_CALL:
            return compiler_lower_super_method_call_expr((*AstMethodCall)expr, owner_ptr, owner_len, 0);
        case AST_SAFE_METHOD_CALL:
            return compiler_lower_super_method_call_expr((*AstMethodCall)expr, owner_ptr, owner_len, 1);
        case AST_MEMBER_ACCESS:
            return compiler_lower_super_member_access_expr((*AstMemberAccess)expr, owner_ptr, owner_len, 0);
        case AST_SAFE_MEMBER_ACCESS:
            return compiler_lower_super_member_access_expr((*AstMemberAccess)expr, owner_ptr, owner_len, 1);
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            ix.base = compiler_lower_super_expr(ix.base, owner_ptr, owner_len);
            ix.index = compiler_lower_super_expr(ix.index, owner_ptr, owner_len);
            return expr;
        case AST_SLICE:
            var sl: *AstSlice = (*AstSlice)expr;
            sl.ptr_expr = compiler_lower_super_expr(sl.ptr_expr, owner_ptr, owner_len);
            sl.len_expr = compiler_lower_super_expr(sl.len_expr, owner_ptr, owner_len);
            return expr;
        case AST_TRY:
            var tr: *AstTry = (*AstTry)expr;
            tr.operand = compiler_lower_super_expr(tr.operand, owner_ptr, owner_len);
            return expr;
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)expr;
            se.expr = compiler_lower_super_expr(se.expr, owner_ptr, owner_len);
            return expr;
        case AST_STRUCT_LITERAL:
            var lit: *AstStructLiteral = (*AstStructLiteral)expr;
            compiler_lower_super_args(lit.values_vec, owner_ptr, owner_len);
            return expr;
        case AST_GENERIC_STRUCT_LITERAL:
            var gsl: *AstGenericStructLiteral = (*AstGenericStructLiteral)expr;
            compiler_lower_super_args(gsl.values_vec, owner_ptr, owner_len);
            return expr;
        case AST_NEW:
            var nw: *AstNew = (*AstNew)expr;
            nw.literal_expr = compiler_lower_super_expr(nw.literal_expr, owner_ptr, owner_len);
            compiler_lower_super_args(nw.ctor_args_vec, owner_ptr, owner_len);
            return expr;
        case AST_STACK_CTOR:
            var sc: *AstStackCtor = (*AstStackCtor)expr;
            compiler_lower_super_args(sc.ctor_args_vec, owner_ptr, owner_len);
            return expr;
        default:
            return expr;
    }
}

func compiler_lower_super_stmt(stmt: *AstNode, owner_ptr: u64, owner_len: u64) -> u64 {
    if (stmt == 0) { return 0; }
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)stmt;
            r.expr = compiler_lower_super_expr(r.expr, owner_ptr, owner_len);
            break;
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            vd.init_expr = compiler_lower_super_expr(vd.init_expr, owner_ptr, owner_len);
            break;
        case AST_ASSIGN:
            var asn: *AstAssign = (*AstAssign)stmt;
            asn.target = compiler_lower_super_expr(asn.target, owner_ptr, owner_len);
            asn.value = compiler_lower_super_expr(asn.value, owner_ptr, owner_len);
            break;
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)stmt;
            es.expr = compiler_lower_super_expr(es.expr, owner_ptr, owner_len);
            break;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            ifs.cond = compiler_lower_super_expr(ifs.cond, owner_ptr, owner_len);
            compiler_lower_super_stmt(ifs.then_block, owner_ptr, owner_len);
            compiler_lower_super_stmt(ifs.else_block, owner_ptr, owner_len);
            break;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            wl.cond = compiler_lower_super_expr(wl.cond, owner_ptr, owner_len);
            compiler_lower_super_stmt(wl.body, owner_ptr, owner_len);
            break;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            compiler_lower_super_stmt(fr.init, owner_ptr, owner_len);
            fr.cond = compiler_lower_super_expr(fr.cond, owner_ptr, owner_len);
            fr.update = compiler_lower_super_expr(fr.update, owner_ptr, owner_len);
            compiler_lower_super_stmt(fr.body, owner_ptr, owner_len);
            break;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            sw.expr = compiler_lower_super_expr(sw.expr, owner_ptr, owner_len);
            var cn: u64 = sw.cases_vec.?len();
            for (var ci: u64 = 0; ci < cn; ci++) {
                compiler_lower_super_stmt(sw.cases_vec.get(ci), owner_ptr, owner_len);
            }
            break;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            cs.value = compiler_lower_super_expr(cs.value, owner_ptr, owner_len);
            compiler_lower_super_stmt(cs.body, owner_ptr, owner_len);
            break;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            var n: u64 = blk.stmts_vec.?len();
            for (var i: u64 = 0; i < n; i++) {
                compiler_lower_super_stmt(blk.stmts_vec.get(i), owner_ptr, owner_len);
            }
            break;
        case AST_DEFER:
            var df: *AstDefer = (*AstDefer)stmt;
            compiler_lower_super_stmt(df.stmt, owner_ptr, owner_len);
            break;
        case AST_DELETE:
            var dl: *AstDelete = (*AstDelete)stmt;
            dl.expr = compiler_lower_super_expr(dl.expr, owner_ptr, owner_len);
            break;
        default:
            break;
    }
    return 0;
}

func compiler_resolve_super_references() -> u64 {
    if (g_all_funcs != 0) {
        var fn_count: u64 = g_all_funcs.len();
        for (var i: u64 = 0; i < fn_count; i++) {
            var fn: *AstFunc = g_all_funcs.get(i);
            if (fn == 0 || fn.body == 0) { continue; }
            var owner_info: *NameInfo = compiler_get_impl_method_owner(fn.name_ptr, fn.name_len);
            var owner_ptr: u64 = 0;
            var owner_len: u64 = 0;
            if (owner_info != 0) {
                owner_ptr = owner_info.ptr;
                owner_len = owner_info.len;
            }
            compiler_lower_super_stmt(fn.body, owner_ptr, owner_len);
        }
    }

    if (g_all_globals != 0) {
        var gn: u64 = g_all_globals.len();
        for (var gi: u64 = 0; gi < gn; gi++) {
            var ginfo: *GlobalInfo = g_all_globals.get(gi);
            if (ginfo == 0 || ginfo.init_expr == 0) { continue; }
            ginfo.init_expr = (u64)compiler_lower_super_expr((*AstNode)ginfo.init_expr, 0, 0);
        }
    }
    return 0;
}

func compiler_is_generic_template(fn: *AstFunc) -> u64 {
    if (fn == 0) { return false; }
    if (compiler_is_generic_func_ptr(fn) != 0) { return true; }
    if (fn.ret_type == TYPE_GENERIC) { return true; }

    var params: *Vec<*Param> = fn.params_vec;
    if (params == 0) { return false; }

    var n: u64 = params.len();
    for (var i: u64 = 0; i < n; i++) {
        var p: *Param = params.get(i);
        if (p.type_kind == TYPE_GENERIC) { return true; }
        if (p.elem_type_kind == TYPE_GENERIC) { return true; }
        if (p.array_len_is_param != 0) { return true; }
    }
    return false;
}

func compiler_monomorphize_generics_funcs() -> u64 {
    if (g_all_funcs == 0) { return 0; }
    for (var i: u64 = 0; i < g_all_funcs.len(); i++) {
        var fn: *AstFunc = g_all_funcs.get(i);
        if (compiler_is_generic_template(fn) != 0) { continue; }
        set_current_module_for_func(fn.name_ptr, fn.name_len);
        var symtab: *HashMap<u64, *TypeInfo> = generic_build_symtab_for_func(fn);
        generic_process_stmt(fn.body, symtab);
    }
    return 0;
}

func compiler_monomorphize_generics_globals() -> u64 {
    if (g_all_globals == 0) { return 0; }
    var gn: u64 = g_all_globals.len();
    for (var gi: u64 = 0; gi < gn; gi++) {
        var ginfo: *GlobalInfo = g_all_globals.get(gi);
        if (ginfo == 0 || ginfo.init_expr == 0) { continue; }
        set_current_module_for_global(ginfo.name_ptr, ginfo.name_len);
        var gsymtab: *HashMap<u64, *TypeInfo> = new HashMap<u64, *TypeInfo>(64);
        var init_node: *AstNode = (*AstNode)ginfo.init_expr;
        var new_init: *AstNode = generic_process_expr(init_node, gsymtab);
        ginfo.init_expr = (u64)new_init;
    }
    return 0;
}

func compiler_filter_out_generic_template_funcs() -> u64 {
    if (g_all_funcs == 0) { return 0; }
    var filtered_funcs: *Vec<*AstFunc> = new Vec<*AstFunc>(g_all_funcs.len());
    var total_funcs: u64 = g_all_funcs.len();
    for (var fi: u64 = 0; fi < total_funcs; fi++) {
        var fn2: *AstFunc = g_all_funcs.get(fi);
        if (compiler_is_generic_template(fn2) != 0) { continue; }
        filtered_funcs.push(fn2);
    }
    g_all_funcs = filtered_funcs;
    return 0;
}

func compiler_monomorphize_generics() -> u64 {
    if (g_generic_func_ptrs == 0 || g_generic_func_ptrs.len() == 0) {
        return 0;
    }
    compiler_monomorphize_generics_funcs();
    compiler_monomorphize_generics_globals();
    compiler_filter_out_generic_template_funcs();
    return 0;
}

// ============================================
// Driver helpers
// ============================================

func compiler_has_runtime_global_init() -> u64 {
    if (g_all_globals == 0) { return false; }
    var n: u64 = g_all_globals.len();
    for (var i: u64 = 0; i < n; i++) {
        var ginfo: *GlobalInfo = g_all_globals.get(i);
        if (ginfo.init_kind == GLOBAL_INIT_RUNTIME) { return true; }
    }
    return false;
}

func _compiler_make_self_param(struct_name_ptr: u64, struct_name_len: u64) -> *Param {
    var p: *Param = new Param();
    p.name_ptr = (u64)"self";
    p.name_len = 4;
    p.type_kind = TYPE_STRUCT;
    p.ptr_depth = 1;
    p.is_tagged = 0;
    p.struct_name_ptr = struct_name_ptr;
    p.struct_name_len = struct_name_len;
    p.tag_layout_ptr = 0;
    p.tag_layout_len = 0;
    p.elem_type_kind = 0;
    p.elem_ptr_depth = 0;
    p.array_len = 0;
    p.array_len_is_param = 0;
    p.array_len_param_ptr = 0;
    p.array_len_param_len = 0;
    return p;
}

func _compiler_build_constructor_name(struct_name_ptr: u64, struct_name_len: u64) -> *NameInfo {
    var method_ptr: u64 = (u64)"constructor";
    var method_len: u64 = 11;
    return compiler_build_method_name(struct_name_ptr, struct_name_len, method_ptr, method_len);
}

func compiler_build_method_name(struct_name_ptr: u64, struct_name_len: u64, method_ptr: u64, method_len: u64) -> *NameInfo {
    var base_len: u64 = compiler_find_generic_suffix_index(struct_name_ptr, struct_name_len);
    var suffix_len: u64 = struct_name_len - base_len;
    return compiler_build_method_name_base(struct_name_ptr, base_len, method_ptr, method_len, suffix_len);
}

func compiler_build_method_name_base(struct_name_ptr: u64, base_len: u64, method_ptr: u64, method_len: u64, suffix_len: u64) -> *NameInfo {
    return module_util_build_joined_name_with_suffix(struct_name_ptr, base_len, method_ptr, method_len, struct_name_ptr + base_len, suffix_len);
}

func compiler_build_scoped_name(base_ptr: u64, base_len: u64, member_ptr: u64, member_len: u64) -> *NameInfo {
    return module_util_build_joined_name(base_ptr, base_len, member_ptr, member_len);
}

// ============================================
// Trait Helpers and VTable Generation
// ============================================

func compiler_build_name_concat2(a_ptr: u64, a_len: u64, b_ptr: u64, b_len: u64) -> *NameInfo {
    return module_util_name_concat2(a_ptr, a_len, b_ptr, b_len);
}

func compiler_build_name_concat4(a_ptr: u64, a_len: u64, b_ptr: u64, b_len: u64, c_ptr: u64, c_len: u64, d_ptr: u64, d_len: u64) -> *NameInfo {
    return module_util_name_concat4(a_ptr, a_len, b_ptr, b_len, c_ptr, c_len, d_ptr, d_len);
}

func compiler_build_name_concat6(a_ptr: u64, a_len: u64, b_ptr: u64, b_len: u64, c_ptr: u64, c_len: u64, d_ptr: u64, d_len: u64, e_ptr: u64, e_len: u64, f_ptr: u64, f_len: u64) -> *NameInfo {
    return module_util_name_concat6(a_ptr, a_len, b_ptr, b_len, c_ptr, c_len, d_ptr, d_len, e_ptr, e_len, f_ptr, f_len);
}

func compiler_is_vptr_field_name(name_ptr: u64, name_len: u64) -> u64 {
    if (name_len < 7) { return false; }
    if (str_eq(name_ptr, 7, "__vptr_", 7) == 0) { return false; }
    return true;
}

func compiler_build_vptr_field_name(trait_ptr: u64, trait_len: u64) -> *NameInfo {
    return compiler_build_name_concat2("__vptr_", 7, trait_ptr, trait_len);
}

func compiler_build_vtable_struct_name(trait_ptr: u64, trait_len: u64) -> *NameInfo {
    return compiler_build_name_concat2("__VTable_", 9, trait_ptr, trait_len);
}

func compiler_build_vtable_global_name(trait_ptr: u64, trait_len: u64, struct_ptr: u64, struct_len: u64) -> *NameInfo {
    return compiler_build_name_concat4("__vtable_", 9, trait_ptr, trait_len, "__", 2, struct_ptr, struct_len);
}

func compiler_build_trait_thunk_name(trait_ptr: u64, trait_len: u64, struct_ptr: u64, struct_len: u64, method_ptr: u64, method_len: u64) -> *NameInfo {
    return compiler_build_name_concat6("__thunk_", 8, trait_ptr, trait_len, "__", 2, struct_ptr, struct_len, "__", 2, method_ptr, method_len);
}

func compiler_find_trait_impl(trait_ptr: u64, trait_len: u64, struct_ptr: u64, struct_len: u64) -> *TraitImpl {
    if (g_all_trait_impls_vec == 0) { return 0; }
    var n: u64 = g_all_trait_impls_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var ti: *TraitImpl = g_all_trait_impls_vec.get(i);
        if (str_eq(ti.trait_ptr, ti.trait_len, trait_ptr, trait_len) &&
            str_eq(ti.struct_ptr, ti.struct_len, struct_ptr, struct_len)) {
            return ti;
        }
    }
    return 0;
}

func compiler_find_trait_impl_method(impl_info: *TraitImpl, name_ptr: u64, name_len: u64) -> *TraitImplMethod {
    if (impl_info == 0 || impl_info.methods_vec == 0) { return 0; }
    var n: u64 = impl_info.methods_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var m: *TraitImplMethod = impl_info.methods_vec.get(i);
        if (str_eq(m.name_ptr, m.name_len, name_ptr, name_len)) { return m; }
    }
    return 0;
}

func compiler_find_trait_method_index(trait_ptr: u64, trait_len: u64, name_ptr: u64, name_len: u64) -> u64 {
    var td: *TraitDef = compiler_get_trait_def(trait_ptr, trait_len);
    if (td == 0 || td.methods_vec == 0) {
        if (TRAIT_LOOKUP_DEBUG != 0) {
            emit_stderr("[DEBUG] Trait def missing for: ");
            emit_stderr_len(trait_ptr, trait_len);
            emit_stderr("\n");
        }
        return COMPILER_NOT_FOUND_U64;
    }
    var n: u64 = td.methods_vec.len();
    for (var i: u64 = 0; i < n; i++) {
        var tm: *TraitMethod = td.methods_vec.get(i);
        if (str_eq(tm.name_ptr, tm.name_len, name_ptr, name_len)) { return tm.index; }
    }
    if (TRAIT_LOOKUP_LEGACY_M_PREFIX != 0 && name_len > 1) {
        var name_u8: []u8 = slice(name_ptr, name_len);
        if (name_u8[0] == 109) {
            var alt_ptr: u64 = name_ptr + 1;
            var alt_len: u64 = name_len - 1;
            for (var j: u64 = 0; j < n; j++) {
                var tm2: *TraitMethod = td.methods_vec.get(j);
                if (str_eq(tm2.name_ptr, tm2.name_len, alt_ptr, alt_len)) { return tm2.index; }
            }
        }
    }
    if (TRAIT_LOOKUP_DEBUG != 0) {
        emit_stderr("[DEBUG] Trait method lookup failed. Trait=");
        emit_stderr_len(trait_ptr, trait_len);
        emit_stderr(" name=");
        emit_stderr_len(name_ptr, name_len);
        emit_stderr("\n");
        for (var k: u64 = 0; k < n; k++) {
            var tm3: *TraitMethod = td.methods_vec.get(k);
            emit_stderr("[DEBUG] Trait method: ");
            emit_stderr_len(tm3.name_ptr, tm3.name_len);
            emit_stderr("\n");
        }
    }
    return COMPILER_NOT_FOUND_U64;
}

func compiler_sizeof_type(type_kind: u64, ptr_depth: u64, struct_name_ptr: u64, struct_name_len: u64) -> u64 {
    if (ptr_depth > 0) { return 8; }
    switch (type_kind) {
        case TYPE_U8: return 1;
        case TYPE_U16: return 2;
        case TYPE_U32: return 4;
        case TYPE_U64: return 8;
        case TYPE_I64: return 8;
        case TYPE_F64: return 8;
        case TYPE_SLICE: return 16;
        case TYPE_STRUCT:
            var sd: *AstStructDef = get_struct_def(struct_name_ptr, struct_name_len);
            if (sd == 0) { return 8; }
            return compiler_struct_size_internal(sd, 0);
        case TYPE_TRAIT:
            return 8;
    }
    return 8;
}

func compiler_sizeof_field_desc(field: *FieldDesc) -> u64 {
    if (field.bit_width > 0) {
        return (field.bit_width + 7) / 8;
    }
    if (field.type_kind == TYPE_ARRAY) {
        var elem_size: u64 = compiler_sizeof_type(field.elem_type_kind, field.elem_ptr_depth, field.struct_name_ptr, field.struct_name_len);
        return elem_size * field.array_len;
    }
    if (field.type_kind == TYPE_SLICE) { return 16; }
    return compiler_sizeof_type(field.type_kind, field.ptr_depth, field.struct_name_ptr, field.struct_name_len);
}

func compiler_struct_size_internal(struct_info: *AstStructDef, depth: u64) -> u64 {
    if (struct_info == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("TypeInfo error");
    }
    if (struct_info.is_packed == 1) {
        emit_stderr("[ERROR] packed struct cannot use trait vptr layout\n");
        panic("TypeInfo error");
    }
    var total_size: u64 = 0;
    if (struct_info.parents_vec != 0) {
        var parents: *Vec<*ParentDesc> = struct_info.parents_vec;
        var pn: u64 = parents.len();
        for (var pi: u64 = 0; pi < pn; pi++) {
            var parent_desc: *ParentDesc = parents.get(pi);
            var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "TypeInfo error");
            total_size = total_size + compiler_struct_size_internal(parent_def, depth + 1);
        }
    }
    var fields: *Vec<*FieldDesc> = struct_info.fields_vec;
    var num_fields: u64 = fields.len();
    for (var i: u64 = 0; i < num_fields; i++) {
        var field: *FieldDesc = fields.get(i);
        total_size = total_size + compiler_sizeof_field_desc(field);
    }
    return total_size;
}

func _compiler_struct_find_field_offset_hier(struct_def: *AstStructDef, field_ptr: u64, field_len: u64, depth: u64) -> u64 {
    if (struct_def == 0) { return COMPILER_NOT_FOUND_U64; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("TypeInfo error");
    }

    var parent_total: u64 = 0;
    if (struct_def.parents_vec != 0) {
        var parents0: *Vec<*ParentDesc> = struct_def.parents_vec;
        var pn0: u64 = parents0.len();
        for (var p0: u64 = 0; p0 < pn0; p0++) {
            var pd0: *ParentDesc = parents0.get(p0);
            var pdef0: *AstStructDef = compiler_resolve_parent_def(pd0, "TypeInfo error");
            parent_total = parent_total + compiler_struct_size_internal(pdef0, depth + 1);
        }
    }

    var fields: *Vec<*FieldDesc> = struct_def.fields_vec;
    var n: u64 = fields.len();
    var local_off: u64 = parent_total;
    for (var i: u64 = 0; i < n; i++) {
        var f: *FieldDesc = fields.get(i);
        if (str_eq(f.name_ptr, f.name_len, field_ptr, field_len)) {
            return local_off;
        }
        local_off = local_off + compiler_sizeof_field_desc(f);
    }

    if (struct_def.parents_vec == 0) { return COMPILER_NOT_FOUND_U64; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    var run_off: u64 = 0;
    var found_count: u64 = 0;
    var found_off: u64 = COMPILER_NOT_FOUND_U64;
    for (var pi: u64 = 0; pi < pn; pi++) {
        var pd: *ParentDesc = parents.get(pi);
        var pdef: *AstStructDef = compiler_resolve_parent_def(pd, "TypeInfo error");
        var cand_off: u64 = _compiler_struct_find_field_offset_hier(pdef, field_ptr, field_len, depth + 1);
        if (compiler_is_not_found_u64(cand_off) == 0) {
            found_count = found_count + 1;
            if (found_count == 1) {
                found_off = run_off + cand_off;
            }
        }
        run_off = run_off + compiler_struct_size_internal(pdef, depth + 1);
    }
    if (found_count > 1) {
        emit_stderr("[ERROR] Ambiguous field offset in inheritance: ");
        emit_stderr_len(struct_def.name_ptr, struct_def.name_len);
        emit_stderr(".");
        emit_stderr_len(field_ptr, field_len);
        emit_stderr("\n");
        panic("TypeInfo error");
    }
    return found_off;
}

func compiler_struct_find_field_offset(struct_def: *AstStructDef, field_ptr: u64, field_len: u64) -> u64 {
    return _compiler_struct_find_field_offset_hier(struct_def, field_ptr, field_len, 0);
}

func compiler_insert_vptr_field(struct_def: *AstStructDef, trait_ptr: u64, trait_len: u64, vtable_struct_ptr: u64, vtable_struct_len: u64) -> u64 {
    if (struct_def == 0) { return 0; }
    var vptr_name: *NameInfo = compiler_build_vptr_field_name(trait_ptr, trait_len);
    var existing_off: u64 = compiler_struct_find_field_offset(struct_def, vptr_name.ptr, vptr_name.len);
    if (compiler_is_not_found_u64(existing_off) == 0) {
        return 0;
    }
    if (struct_def.fields_vec == 0) {
        struct_def.fields_vec = new Vec<*FieldDesc>(1);
    }
    var fields: *Vec<*FieldDesc> = struct_def.fields_vec;
    var n: u64 = fields.len();
    for (var i: u64 = 0; i < n; i++) {
        var f: *FieldDesc = fields.get(i);
        if (str_eq(f.name_ptr, f.name_len, vptr_name.ptr, vptr_name.len)) { return 0; }
    }
    var new_field: *FieldDesc = new FieldDesc();
    new_field.name_ptr = vptr_name.ptr;
    new_field.name_len = vptr_name.len;
    new_field.type_kind = TYPE_STRUCT;
    new_field.struct_name_ptr = vtable_struct_ptr;
    new_field.struct_name_len = vtable_struct_len;
    new_field.ptr_depth = 1;
    new_field.is_tagged = 0;
    new_field.tag_layout_ptr = 0;
    new_field.tag_layout_len = 0;
    new_field.bit_width = 0;
    new_field.elem_type_kind = 0;
    new_field.elem_ptr_depth = 0;
    new_field.array_len = 0;
    new_field.access = ACCESS_PRIVATE;
    new_field.owner_struct_ptr = struct_def.name_ptr;
    new_field.owner_struct_len = struct_def.name_len;

    fields.push(new_field);
    return 0;
}

func compiler_clone_param(src: *Param) -> *Param {
    return module_util_clone_param(src);
}

func compiler_is_self_type(type_kind: u64, struct_name_ptr: u64, struct_name_len: u64) -> u64 {
    return module_util_is_self_type(type_kind, struct_name_ptr, struct_name_len);
}

func compiler_build_trait_thunk_func(thunk_name_ptr: u64, thunk_name_len: u64, trait_def: *TraitDef, trait_method: *TraitMethod, impl_method: *TraitImplMethod, struct_ptr: u64, struct_len: u64, vptr_offset: u64) -> *AstFunc {
    var trait_sig: *AstFunc = trait_method.sig;
    var params_in: *Vec<*Param> = trait_sig.params_vec;
    var param_count: u64 = params_in.len();
    var params_out: *Vec<*Param> = new Vec<*Param>(param_count);

    for (var i: u64 = 0; i < param_count; i++) {
        var src: *Param = params_in.get(i);
        var dst: *Param = compiler_clone_param(src);
        if (i == 0) {
            dst.type_kind = TYPE_TRAIT;
            dst.ptr_depth = 1;
            dst.is_tagged = 0;
            dst.struct_name_ptr = trait_def.name_ptr;
            dst.struct_name_len = trait_def.name_len;
            dst.tag_layout_ptr = 0;
            dst.tag_layout_len = 0;
        }
        params_out.push(dst);
    }

    var stmts: *Vec<*AstNode> = new Vec<*AstNode>(2);

    var self_param: *Param = params_out.get(0);
    var self_ident: *AstNode = (*AstNode)ast_ident(self_param.name_ptr, self_param.name_len);
    var cast_struct_ptr: u64 = struct_ptr;
    var cast_struct_len: u64 = struct_len;
    if (impl_method != 0 && impl_method.func_def != 0 && impl_method.func_def.params_vec != 0 && impl_method.func_def.params_vec.len() != 0) {
        var impl_self: *Param = impl_method.func_def.params_vec.get(0);
        if (impl_self.type_kind == TYPE_STRUCT && impl_self.struct_name_ptr != 0 && impl_self.struct_name_len != 0) {
            cast_struct_ptr = impl_self.struct_name_ptr;
            cast_struct_len = impl_self.struct_name_len;
        }
    }
    var casted: *AstNode = (*AstNode)ast_cast_ex(self_ident, TYPE_STRUCT, 1, 0, cast_struct_ptr, cast_struct_len, 0, 0);

    var call_args: *Vec<*AstNode> = new Vec<*AstNode>(param_count);
    call_args.push(casted);
    for (var j: u64 = 1; j < param_count; j++) {
        var p: *Param = params_out.get(j);
        call_args.push((*AstNode)ast_ident(p.name_ptr, p.name_len));
    }

    var call_node: *AstNode = (*AstNode)ast_call(impl_method.func_def.name_ptr, impl_method.func_def.name_len, call_args);
    if (trait_sig.ret_type == TYPE_VOID) {
        stmts.push((*AstNode)ast_expr_stmt(call_node));
    } else {
        stmts.push((*AstNode)ast_return(call_node));
    }

    var body: *AstNode = (*AstNode)ast_block(stmts);
    return ast_func_ex(thunk_name_ptr, thunk_name_len, params_out,
                       trait_sig.ret_type, trait_sig.ret_ptr_depth, trait_sig.ret_is_tagged,
                       trait_sig.ret_struct_name_ptr, trait_sig.ret_struct_name_len,
                       trait_sig.ret_tag_layout_ptr, trait_sig.ret_tag_layout_len, body);
}

func compiler_trait_method_error(prefix: u64, method_ptr: u64, method_len: u64) -> u64 {
    return module_util_trait_method_error(prefix, method_ptr, method_len);
}

func compiler_validate_struct_trait_inheritance(sd: *AstStructDef) -> u64 {
    if (sd == 0 || sd.traits_vec == 0) { return 0; }
    var tn: u64 = sd.traits_vec.len();
    for (var ti: u64 = 0; ti < tn; ti++) {
        var tref: *TraitRef = sd.traits_vec.get(ti);
        var tdef: *TraitDef = compiler_get_trait_def(tref.name_ptr, tref.name_len);
        if (tdef == 0) {
            emit_stderr("[ERROR] Trait not found for struct inheritance: ");
            emit_stderr_len(tref.name_ptr, tref.name_len);
            emit_stderr("\n");
            panic("Parse error");
        }
        var impl_info: *TraitImpl = compiler_find_trait_impl(tref.name_ptr, tref.name_len, sd.name_ptr, sd.name_len);
        if (impl_info == 0) {
            emit_stderr("[ERROR] Trait impl missing for struct inheritance: ");
            emit_stderr_len(sd.name_ptr, sd.name_len);
            emit_stderr(" -> ");
            emit_stderr_len(tref.name_ptr, tref.name_len);
            emit_stderr("\n");
            panic("Parse error");
        }
    }
    return 0;
}

func compiler_validate_all_struct_trait_inheritance() -> u64 {
    if (g_all_structs_vec == 0) { return 0; }
    var sn: u64 = g_all_structs_vec.len();
    for (var si: u64 = 0; si < sn; si++) {
        var sd: *AstStructDef = g_all_structs_vec.get(si);
        compiler_validate_struct_trait_inheritance(sd);
    }
    return 0;
}

func compiler_get_impl_trait_and_struct(impl_info: *TraitImpl, trait_def_out: **TraitDef, struct_def_out: **AstStructDef) -> u64 {
    var trait_def: *TraitDef = compiler_get_trait_def(impl_info.trait_ptr, impl_info.trait_len);
    if (trait_def == 0) {
        emit_stderr("[ERROR] Trait not found for impl: ");
        emit_stderr_len(impl_info.trait_ptr, impl_info.trait_len);
        emit_stderr("\n");
        panic("Parse error");
    }

    var struct_def: *AstStructDef = get_struct_def(impl_info.struct_ptr, impl_info.struct_len);
    if (struct_def == 0) {
        emit_stderr("[ERROR] Struct not found for trait impl: ");
        emit_stderr_len(impl_info.struct_ptr, impl_info.struct_len);
        emit_stderr("\n");
        panic("Parse error");
    }
    if (struct_def.is_packed == 1) {
        emit_stderr("[ERROR] packed struct cannot implement traits\n");
        panic("Parse error");
    }

    *trait_def_out = trait_def;
    *struct_def_out = struct_def;
    return 0;
}

func compiler_get_or_create_vtable_struct(trait_def: *TraitDef, trait_ptr: u64, trait_len: u64, vtable_name_ptr_out: *u64, vtable_name_len_out: *u64) -> *AstStructDef {
    var vtable_name: *NameInfo = compiler_build_vtable_struct_name(trait_ptr, trait_len);
    *vtable_name_ptr_out = vtable_name.ptr;
    *vtable_name_len_out = vtable_name.len;

    var vtable_struct_def: *AstStructDef = get_struct_def(vtable_name.ptr, vtable_name.len);
    if (vtable_struct_def != 0) { return vtable_struct_def; }

    var vtable_fields: *Vec<*FieldDesc> = new Vec<*FieldDesc>(trait_def.methods_vec.len());
    var mcount: u64 = trait_def.methods_vec.len();
    for (var mi: u64 = 0; mi < mcount; mi++) {
        var tm: *TraitMethod = trait_def.methods_vec.get(mi);
        var f: *FieldDesc = new FieldDesc();
        f.name_ptr = tm.name_ptr;
        f.name_len = tm.name_len;
        f.type_kind = TYPE_U64;
        f.ptr_depth = 0;
        f.is_tagged = 0;
        f.struct_name_ptr = 0;
        f.struct_name_len = 0;
        f.tag_layout_ptr = 0;
        f.tag_layout_len = 0;
        f.bit_width = 0;
        f.elem_type_kind = 0;
        f.elem_ptr_depth = 0;
        f.array_len = 0;
        f.access = ACCESS_PRIVATE;
        f.owner_struct_ptr = vtable_name.ptr;
        f.owner_struct_len = vtable_name.len;
        vtable_fields.push(f);
    }
    vtable_struct_def = ast_struct_def(vtable_name.ptr, vtable_name.len, 0, vtable_fields, 0, 0);
    register_struct_type(vtable_struct_def);
    return vtable_struct_def;
}

func compiler_get_trait_impl_vptr_offset(impl_info: *TraitImpl, struct_def: *AstStructDef, vtable_name_ptr: u64, vtable_name_len: u64) -> u64 {
    compiler_insert_vptr_field(struct_def, impl_info.trait_ptr, impl_info.trait_len, vtable_name_ptr, vtable_name_len);
    var vptr_name: *NameInfo = compiler_build_vptr_field_name(impl_info.trait_ptr, impl_info.trait_len);
    var vptr_offset: u64 = compiler_struct_find_field_offset(struct_def, vptr_name.ptr, vptr_name.len);
    if (vptr_offset == COMPILER_NOT_FOUND_U64) {
        emit_stderr("[ERROR] vptr field not found for trait impl: ");
        emit_stderr_len(impl_info.struct_ptr, impl_info.struct_len);
        emit_stderr("\n");
        panic("Parse error");
    }
    return vptr_offset;
}

func compiler_validate_trait_signature_method(trait_ptr: u64, trait_len: u64, trait_method: *TraitMethod, trait_sig: *AstFunc) -> u64 {
    if (trait_sig.params_vec == 0 || trait_sig.params_vec.len() == 0) {
        compiler_trait_method_error("[ERROR] Trait method must have self parameter: ", trait_method.name_ptr, trait_method.name_len);
    }
    var self_param: *Param = trait_sig.params_vec.get(0);
    var is_virtual_trait: u64 = compiler_is_virtual_trait_name(trait_ptr, trait_len);
    if (compiler_is_self_type(self_param.type_kind, self_param.struct_name_ptr, self_param.struct_name_len) == 0 || self_param.ptr_depth == 0) {
        compiler_trait_method_error("[ERROR] Trait method self must be *Self: ", trait_method.name_ptr, trait_method.name_len);
    }
    if (is_virtual_trait == 0 && trait_sig.ret_type == TYPE_STRUCT && trait_sig.ret_ptr_depth == 0) {
        compiler_trait_method_error("[ERROR] Trait method cannot return struct by value: ", trait_method.name_ptr, trait_method.name_len);
    }
    if (is_virtual_trait == 0 && compiler_is_self_type(trait_sig.ret_type, trait_sig.ret_struct_name_ptr, trait_sig.ret_struct_name_len) != 0) {
        compiler_trait_method_error("[ERROR] Trait method cannot return Self type: ", trait_method.name_ptr, trait_method.name_len);
    }
    return 0;
}

func compiler_validate_trait_impl_signature(trait_method: *TraitMethod, trait_sig: *AstFunc, impl_sig: *AstFunc, impl_info: *TraitImpl) -> u64 {
    if (impl_sig.params_vec.len() != trait_sig.params_vec.len()) {
        compiler_trait_method_error("[ERROR] Trait method param count mismatch: ", trait_method.name_ptr, trait_method.name_len);
    }

    var impl_self: *Param = impl_sig.params_vec.get(0);
    if (impl_self.ptr_depth == 0 || impl_self.type_kind != TYPE_STRUCT) {
        compiler_trait_method_error("[ERROR] Trait impl self must be struct pointer: ", trait_method.name_ptr, trait_method.name_len);
    }
    var is_virtual_trait: u64 = compiler_is_virtual_trait_name(impl_info.trait_ptr, impl_info.trait_len);
    if (is_virtual_trait == 0) {
        if (!(str_eq(impl_self.struct_name_ptr, impl_self.struct_name_len, impl_info.struct_ptr, impl_info.struct_len) ||
              compiler_is_self_type(impl_self.type_kind, impl_self.struct_name_ptr, impl_self.struct_name_len) != 0)) {
            compiler_trait_method_error("[ERROR] Trait impl self type mismatch: ", trait_method.name_ptr, trait_method.name_len);
        }
    } else {
        var self_ok: u64 = 0;
        if (str_eq(impl_self.struct_name_ptr, impl_self.struct_name_len, impl_info.struct_ptr, impl_info.struct_len) != 0) {
            self_ok = 1;
        } else if (compiler_struct_is_same_or_descendant(impl_info.struct_ptr, impl_info.struct_len, impl_self.struct_name_ptr, impl_self.struct_name_len) != 0) {
            self_ok = 1;
        }
        if (self_ok == 0) {
            compiler_trait_method_error("[ERROR] Virtual impl self type mismatch: ", trait_method.name_ptr, trait_method.name_len);
        }
    }

    for (var pi: u64 = 1; pi < trait_sig.params_vec.len(); pi++) {
        var tp: *Param = trait_sig.params_vec.get(pi);
        if (is_virtual_trait == 0 && compiler_is_self_type(tp.type_kind, tp.struct_name_ptr, tp.struct_name_len) != 0) {
            compiler_trait_method_error("[ERROR] Trait method params cannot use Self type: ", trait_method.name_ptr, trait_method.name_len);
        }
        var ip: *Param = impl_sig.params_vec.get(pi);
        if (tp.type_kind != ip.type_kind || tp.ptr_depth != ip.ptr_depth) {
            compiler_trait_method_error("[ERROR] Trait impl param type mismatch: ", trait_method.name_ptr, trait_method.name_len);
        }
        if (tp.type_kind == TYPE_STRUCT) {
            if (!str_eq(tp.struct_name_ptr, tp.struct_name_len, ip.struct_name_ptr, ip.struct_name_len)) {
                compiler_trait_method_error("[ERROR] Trait impl struct param mismatch: ", trait_method.name_ptr, trait_method.name_len);
            }
        }
    }

    if (trait_sig.ret_type != impl_sig.ret_type || trait_sig.ret_ptr_depth != impl_sig.ret_ptr_depth) {
        compiler_trait_method_error("[ERROR] Trait impl return type mismatch: ", trait_method.name_ptr, trait_method.name_len);
    }
    if (trait_sig.ret_type == TYPE_STRUCT) {
        if (!str_eq(trait_sig.ret_struct_name_ptr, trait_sig.ret_struct_name_len, impl_sig.ret_struct_name_ptr, impl_sig.ret_struct_name_len)) {
            compiler_trait_method_error("[ERROR] Trait impl return struct mismatch: ", trait_method.name_ptr, trait_method.name_len);
        }
    }
    return 0;
}

func compiler_register_trait_thunk(impl_info: *TraitImpl, trait_def: *TraitDef, trait_method: *TraitMethod, impl_method: *TraitImplMethod, vptr_offset: u64, values: *Vec<*AstNode>) -> u64 {
    var thunk_name: *NameInfo = compiler_build_trait_thunk_name(impl_info.trait_ptr, impl_info.trait_len, impl_info.struct_ptr, impl_info.struct_len, trait_method.name_ptr, trait_method.name_len);
    var thunk_fn: *AstFunc = compiler_build_trait_thunk_func(thunk_name.ptr, thunk_name.len, trait_def, trait_method, impl_method, impl_info.struct_ptr, impl_info.struct_len, vptr_offset);
    g_all_funcs.push(thunk_fn);
    g_all_func_sigs.push(thunk_fn);
    compiler_register_impl_method_ex(thunk_name.ptr, thunk_name.len, impl_info.struct_ptr, impl_info.struct_len, ACCESS_PUBLIC);
    compiler_register_impl_method_func(thunk_name.ptr, thunk_name.len, thunk_fn);
    compiler_invalidate_func_lookup();
    values.push((*AstNode)ast_ident(thunk_name.ptr, thunk_name.len));
    return 0;
}

func compiler_register_trait_vtable_global(impl_info: *TraitImpl, vtable_name_ptr: u64, vtable_name_len: u64, vtable_struct_def: *AstStructDef, values: *Vec<*AstNode>) -> u64 {
    var vtable_global: *NameInfo = compiler_build_vtable_global_name(impl_info.trait_ptr, impl_info.trait_len, impl_info.struct_ptr, impl_info.struct_len);
    impl_info.vtable_global_ptr = vtable_global.ptr;
    impl_info.vtable_global_len = vtable_global.len;

    var vtable_lit: *AstStructLiteral = ast_struct_literal(vtable_struct_def, values);
    var vtable_type: *TypeInfo = compiler_typeinfo_new(TYPE_STRUCT, 0);
    vtable_type.struct_name_ptr = vtable_name_ptr;
    vtable_type.struct_name_len = vtable_name_len;
    vtable_type.struct_def = vtable_struct_def;

    var ginfo: *GlobalInfo = new GlobalInfo();
    ginfo.name_ptr = vtable_global.ptr;
    ginfo.name_len = vtable_global.len;
    ginfo.typeinfo_ptr = vtable_type;
    ginfo.init_kind = GLOBAL_INIT_EXPR;
    ginfo.init_expr = (u64)vtable_lit;
    g_all_globals.push(ginfo);
    compiler_invalidate_global_lookup();
    return 0;
}

func compiler_build_trait_default_impl_func(impl_info: *TraitImpl, trait_method: *TraitMethod, trait_sig: *AstFunc) -> *AstFunc {
    if (impl_info == 0 || trait_method == 0 || trait_sig == 0 || trait_sig.body == 0) { return 0; }
    if (trait_sig.params_vec == 0 || trait_sig.params_vec.len() == 0) { return 0; }

    var fn_name: *NameInfo = compiler_build_method_name(impl_info.struct_ptr, impl_info.struct_len, trait_method.name_ptr, trait_method.name_len);
    var params_out: *Vec<*Param> = new Vec<*Param>(trait_sig.params_vec.len());
    var pn: u64 = trait_sig.params_vec.len();
    for (var i: u64 = 0; i < pn; i++) {
        var src: *Param = trait_sig.params_vec.get(i);
        var dst: *Param = compiler_clone_param(src);
        if (i == 0) {
            dst.type_kind = TYPE_STRUCT;
            dst.ptr_depth = 1;
            dst.is_tagged = 0;
            dst.struct_name_ptr = impl_info.struct_ptr;
            dst.struct_name_len = impl_info.struct_len;
            dst.tag_layout_ptr = 0;
            dst.tag_layout_len = 0;
        }
        params_out.push(dst);
    }

    return ast_func_ex(fn_name.ptr, fn_name.len, params_out,
                       trait_sig.ret_type, trait_sig.ret_ptr_depth, trait_sig.ret_is_tagged,
                       trait_sig.ret_struct_name_ptr, trait_sig.ret_struct_name_len,
                       trait_sig.ret_tag_layout_ptr, trait_sig.ret_tag_layout_len,
                       trait_sig.body);
}

func compiler_register_trait_default_impl_method(impl_info: *TraitImpl, trait_method: *TraitMethod, trait_sig: *AstFunc) -> *TraitImplMethod {
    var fn: *AstFunc = compiler_build_trait_default_impl_func(impl_info, trait_method, trait_sig);
    if (fn == 0) { return 0; }
    g_all_funcs.push(fn);
    g_all_func_sigs.push(fn);
    compiler_invalidate_func_lookup();
    compiler_register_impl_method_ex(fn.name_ptr, fn.name_len, impl_info.struct_ptr, impl_info.struct_len, ACCESS_PUBLIC);
    compiler_register_impl_method_func(fn.name_ptr, fn.name_len, fn);

    var impl_method: *TraitImplMethod = new TraitImplMethod();
    impl_method.name_ptr = trait_method.name_ptr;
    impl_method.name_len = trait_method.name_len;
    impl_method.func_def = fn;
    if (impl_info.methods_vec != 0) {
        impl_info.methods_vec.push(impl_method);
    }
    return impl_method;
}

func compiler_finalize_single_trait_impl(impl_info: *TraitImpl) -> u64 {
    var trait_def: *TraitDef = 0;
    var struct_def: *AstStructDef = 0;
    compiler_get_impl_trait_and_struct(impl_info, &trait_def, &struct_def);

    var vtable_name_ptr: u64 = 0;
    var vtable_name_len: u64 = 0;
    var vtable_struct_def: *AstStructDef = compiler_get_or_create_vtable_struct(trait_def, impl_info.trait_ptr, impl_info.trait_len, &vtable_name_ptr, &vtable_name_len);
    impl_info.vtable_struct_def = vtable_struct_def;

    var vptr_offset: u64 = compiler_get_trait_impl_vptr_offset(impl_info, struct_def, vtable_name_ptr, vtable_name_len);

    var method_count: u64 = trait_def.methods_vec.len();
    var values: *Vec<*AstNode> = new Vec<*AstNode>(method_count);
    for (var m: u64 = 0; m < method_count; m++) {
        var trait_method: *TraitMethod = trait_def.methods_vec.get(m);
        var trait_sig: *AstFunc = trait_method.sig;
        compiler_validate_trait_signature_method(impl_info.trait_ptr, impl_info.trait_len, trait_method, trait_sig);

        var impl_method: *TraitImplMethod = compiler_find_trait_impl_method(impl_info, trait_method.name_ptr, trait_method.name_len);
        if (impl_method == 0) {
            if (trait_sig.body != 0) {
                impl_method = compiler_register_trait_default_impl_method(impl_info, trait_method, trait_sig);
            }
        }
        if (impl_method == 0) {
            compiler_trait_method_error("[ERROR] Trait method not implemented: ", trait_method.name_ptr, trait_method.name_len);
        }

        var impl_sig: *AstFunc = impl_method.func_def;
        compiler_validate_trait_impl_signature(trait_method, trait_sig, impl_sig, impl_info);
        compiler_register_trait_thunk(impl_info, trait_def, trait_method, impl_method, vptr_offset, values);
    }

    compiler_register_trait_vtable_global(impl_info, vtable_name_ptr, vtable_name_len, vtable_struct_def, values);
    return 0;
}

func compiler_finalize_traits() -> u64 {
    compiler_validate_all_struct_trait_inheritance();
    if (g_all_trait_impls_vec == 0 || g_all_trait_impls_vec.len() == 0) { return 0; }

    var num_impls: u64 = g_all_trait_impls_vec.len();
    for (var ii: u64 = 0; ii < num_impls; ii++) {
        var impl_info: *TraitImpl = g_all_trait_impls_vec.get(ii);
        compiler_finalize_single_trait_impl(impl_info);
    }
    return 0;
}

func compiler_nameinfo_vec_push_unique(names: *Vec<*NameInfo>, item: *NameInfo) -> u64 {
    return module_util_nameinfo_vec_push_unique(names, item);
}

func compiler_collect_parent_abst_methods(parent_def: *AstStructDef, out: *Vec<*NameInfo>) -> u64 {
    if (parent_def == 0 || out == 0) { return 0; }
    compiler_collect_abst_methods_recursive(parent_def, out);

    var list: *Vec<*NameInfo> = compiler_get_abst_methods(parent_def.name_ptr, parent_def.name_len);
    if (list == 0) { return 0; }

    var n: u64 = list.len();
    for (var i: u64 = 0; i < n; i++) {
        compiler_nameinfo_vec_push_unique(out, list.get(i));
    }
    return 0;
}

func compiler_collect_abst_methods_recursive(struct_def: *AstStructDef, out: *Vec<*NameInfo>) -> u64 {
    if (struct_def == 0 || out == 0) { return 0; }
    if (struct_def.parents_vec != 0) {
        var pn: u64 = struct_def.parents_vec.len();
        for (var pi: u64 = 0; pi < pn; pi++) {
            var parent_desc: *ParentDesc = struct_def.parents_vec.get(pi);
            var parent_def: *AstStructDef = parent_desc.struct_def;
            if (parent_def == 0) {
                parent_def = get_struct_def(parent_desc.name_ptr, parent_desc.name_len);
            }
            if (parent_def != 0) {
                compiler_collect_parent_abst_methods(parent_def, out);
            }
        }
    }
    return 0;
}

func compiler_collect_self_and_parent_abst_methods(struct_def: *AstStructDef, out: *Vec<*NameInfo>) -> u64 {
    if (struct_def == 0 || out == 0) { return 0; }
    compiler_collect_abst_methods_recursive(struct_def, out);
    var own: *Vec<*NameInfo> = compiler_get_abst_methods(struct_def.name_ptr, struct_def.name_len);
    if (own == 0) { return 0; }
    var n: u64 = own.len();
    for (var i: u64 = 0; i < n; i++) {
        compiler_nameinfo_vec_push_unique(out, own.get(i));
    }
    return 0;
}

func _compiler_find_abst_decl_in_hierarchy(struct_def: *AstStructDef, method_ptr: u64, method_len: u64, depth: u64, owner_ptr_out: *u64, owner_len_out: *u64, access_out: *u64, sig_out: **AstFunc) -> u64 {
    if (struct_def == 0) { return 0; }
    if (depth > 64) {
        emit_stderr("[ERROR] Inheritance depth exceeded\n");
        panic("Compiler error");
    }

    if (compiler_is_abst_method(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len) != 0) {
        *owner_ptr_out = struct_def.name_ptr;
        *owner_len_out = struct_def.name_len;
        *access_out = compiler_get_abst_method_access(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
        *sig_out = compiler_get_abst_method_sig(struct_def.name_ptr, struct_def.name_len, method_ptr, method_len);
        return 1;
    }

    if (struct_def.parents_vec == 0) { return 0; }
    var parents: *Vec<*ParentDesc> = struct_def.parents_vec;
    var pn: u64 = parents.len();
    var found: u64 = 0;
    var owner_ptr_tmp: u64 = 0;
    var owner_len_tmp: u64 = 0;
    var access_tmp: u64 = ACCESS_PRIVATE;
    var sig_tmp: *AstFunc = 0;
    for (var i: u64 = 0; i < pn; i++) {
        var parent_desc: *ParentDesc = parents.get(i);
        var parent_def: *AstStructDef = compiler_resolve_parent_def(parent_desc, "Compiler error");
        var owner_ptr_c: u64 = 0;
        var owner_len_c: u64 = 0;
        var access_c: u64 = ACCESS_PRIVATE;
        var sig_c: *AstFunc = 0;
        if (_compiler_find_abst_decl_in_hierarchy(parent_def, method_ptr, method_len, depth + 1, &owner_ptr_c, &owner_len_c, &access_c, &sig_c) != 0) {
            found = found + 1;
            if (found == 1) {
                owner_ptr_tmp = owner_ptr_c;
                owner_len_tmp = owner_len_c;
                access_tmp = access_c;
                sig_tmp = sig_c;
            }
        }
    }
    if (found > 1) {
        emit_stderr("[ERROR] Ambiguous abstract method declaration in inheritance: ");
        emit_stderr_len(struct_def.name_ptr, struct_def.name_len);
        emit_stderr(".");
        emit_stderr_len(method_ptr, method_len);
        emit_stderr("\n");
        panic("Compiler error");
    }
    if (found == 0) { return 0; }

    *owner_ptr_out = owner_ptr_tmp;
    *owner_len_out = owner_len_tmp;
    *access_out = access_tmp;
    *sig_out = sig_tmp;
    return 1;
}

func compiler_find_abst_decl_in_hierarchy(struct_def: *AstStructDef, method_ptr: u64, method_len: u64, owner_ptr_out: *u64, owner_len_out: *u64, access_out: *u64, sig_out: **AstFunc) -> u64 {
    return _compiler_find_abst_decl_in_hierarchy(struct_def, method_ptr, method_len, 0, owner_ptr_out, owner_len_out, access_out, sig_out);
}

func compiler_build_virtual_trait_name(base_ptr: u64, base_len: u64) -> *NameInfo {
    return compiler_build_name_concat2("__Virt_", 7, base_ptr, base_len);
}

func compiler_build_virtual_trait_method_sig(abst_sig: *AstFunc, method_ptr: u64, method_len: u64) -> *AstFunc {
    if (abst_sig == 0 || abst_sig.params_vec == 0 || abst_sig.params_vec.len() == 0) {
        emit_stderr("[ERROR] abstract method requires self parameter for virtual dispatch: ");
        emit_stderr_len(method_ptr, method_len);
        emit_stderr("\n");
        panic("Parse error");
    }

    var params_out: *Vec<*Param> = new Vec<*Param>(abst_sig.params_vec.len());
    var pn: u64 = abst_sig.params_vec.len();
    for (var i: u64 = 0; i < pn; i++) {
        var src: *Param = abst_sig.params_vec.get(i);
        var dst: *Param = compiler_clone_param(src);
        if (i == 0) {
            dst.type_kind = TYPE_STRUCT;
            dst.ptr_depth = 1;
            dst.is_tagged = 0;
            dst.struct_name_ptr = (u64)"Self";
            dst.struct_name_len = 4;
            dst.tag_layout_ptr = 0;
            dst.tag_layout_len = 0;
        }
        params_out.push(dst);
    }

    return ast_func_ex(method_ptr, method_len, params_out,
                       abst_sig.ret_type, abst_sig.ret_ptr_depth, abst_sig.ret_is_tagged,
                       abst_sig.ret_struct_name_ptr, abst_sig.ret_struct_name_len,
                       abst_sig.ret_tag_layout_ptr, abst_sig.ret_tag_layout_len, 0);
}

func compiler_setup_single_virtual_dispatch_trait(base_def: *AstStructDef) -> u64 {
    if (base_def == 0) { return 0; }
    var own_abst: *Vec<*NameInfo> = compiler_get_abst_methods(base_def.name_ptr, base_def.name_len);
    if (own_abst == 0 || own_abst.len() == 0) { return 0; }

    var required: *Vec<*NameInfo> = new Vec<*NameInfo>(8);
    compiler_collect_self_and_parent_abst_methods(base_def, required);
    if (required.len() == 0) { return 0; }

    var trait_name: *NameInfo = compiler_build_virtual_trait_name(base_def.name_ptr, base_def.name_len);
    var trait_def: *TraitDef = compiler_get_trait_def(trait_name.ptr, trait_name.len);
    if (trait_def == 0) {
        var methods: *Vec<*TraitMethod> = new Vec<*TraitMethod>(required.len());
        var rn: u64 = required.len();
        for (var ri: u64 = 0; ri < rn; ri++) {
            var mi: *NameInfo = required.get(ri);
            var owner_ptr: u64 = 0;
            var owner_len: u64 = 0;
            var access: u64 = ACCESS_PRIVATE;
            var abst_sig: *AstFunc = 0;
            if (compiler_find_abst_decl_in_hierarchy(base_def, mi.ptr, mi.len, &owner_ptr, &owner_len, &access, &abst_sig) == 0 || abst_sig == 0) {
                emit_stderr("[ERROR] abstract signature missing for virtual dispatch: ");
                emit_stderr_len(base_def.name_ptr, base_def.name_len);
                emit_stderr(".");
                emit_stderr_len(mi.ptr, mi.len);
                emit_stderr("\n");
                panic("Parse error");
            }

            var tm: *TraitMethod = new TraitMethod();
            tm.name_ptr = mi.ptr;
            tm.name_len = mi.len;
            tm.sig = compiler_build_virtual_trait_method_sig(abst_sig, mi.ptr, mi.len);
            tm.index = methods.len();
            methods.push(tm);
        }

        trait_def = new TraitDef();
        trait_def.name_ptr = trait_name.ptr;
        trait_def.name_len = trait_name.len;
        trait_def.methods_vec = methods;
        compiler_register_trait_def(trait_def);
    }

    var vtable_name_ptr: u64 = 0;
    var vtable_name_len: u64 = 0;
    var vtable_struct_def: *AstStructDef = compiler_get_or_create_vtable_struct(trait_def, trait_name.ptr, trait_name.len, &vtable_name_ptr, &vtable_name_len);
    if (vtable_struct_def == 0) {
        emit_stderr("[ERROR] failed to build virtual vtable struct\n");
        panic("Parse error");
    }

    var sn: u64 = g_all_structs_vec.len();
    for (var si: u64 = 0; si < sn; si++) {
        var sd: *AstStructDef = g_all_structs_vec.get(si);
        if (sd == 0) { continue; }
        if (compiler_struct_is_same_or_descendant(sd.name_ptr, sd.name_len, base_def.name_ptr, base_def.name_len) == 0) { continue; }

        compiler_insert_vptr_field(sd, trait_name.ptr, trait_name.len, vtable_name_ptr, vtable_name_len);

        var rn2: u64 = required.len();
        for (var rj: u64 = 0; rj < rn2; rj++) {
            var mref: *NameInfo = required.get(rj);
            compiler_register_virtual_dispatch_trait(sd.name_ptr, sd.name_len, mref.ptr, mref.len, trait_name.ptr, trait_name.len);
        }

        if (compiler_find_trait_impl(trait_name.ptr, trait_name.len, sd.name_ptr, sd.name_len) != 0) { continue; }

        var impl_methods: *Vec<*TraitImplMethod> = new Vec<*TraitImplMethod>(required.len());
        var has_all_impls: u64 = 1;
        for (var rk: u64 = 0; rk < required.len(); rk++) {
            var mk: *NameInfo = required.get(rk);
            var fn: *AstFunc = compiler_find_method_func_in_hierarchy(sd, mk.ptr, mk.len);
            if (fn == 0) {
                has_all_impls = 0;
                break;
            }
            var tim: *TraitImplMethod = new TraitImplMethod();
            tim.name_ptr = mk.ptr;
            tim.name_len = mk.len;
            tim.func_def = fn;
            impl_methods.push(tim);
        }
        if (has_all_impls == 0) { continue; }

        var impl_info: *TraitImpl = new TraitImpl();
        impl_info.trait_ptr = trait_name.ptr;
        impl_info.trait_len = trait_name.len;
        impl_info.struct_ptr = sd.name_ptr;
        impl_info.struct_len = sd.name_len;
        impl_info.methods_vec = impl_methods;
        impl_info.vtable_struct_def = 0;
        impl_info.vtable_global_ptr = 0;
        impl_info.vtable_global_len = 0;
        compiler_register_trait_impl(impl_info);
    }
    return 0;
}

func compiler_setup_virtual_dispatch_traits() -> u64 {
    if (g_all_structs_vec == 0) { return 0; }
    var sn: u64 = g_all_structs_vec.len();
    for (var si: u64 = 0; si < sn; si++) {
        var sd: *AstStructDef = g_all_structs_vec.get(si);
        if (sd == 0) { continue; }
        compiler_setup_single_virtual_dispatch_trait(sd);
    }
    return 0;
}

func compiler_validate_child_implements_abst_method(sd: *AstStructDef, mi: *NameInfo) -> u64 {
    if (compiler_is_abst_method(sd.name_ptr, sd.name_len, mi.ptr, mi.len) != 0) {
        // Child remains abstract on this method; allowed.
        return 0;
    }
    var found_impl: *AstFunc = compiler_find_method_func_in_hierarchy(sd, mi.ptr, mi.len);
    if (found_impl == 0) {
        emit_stderr("[ERROR] Missing abstract method implementation in child: ");
        emit_stderr_len(sd.name_ptr, sd.name_len);
        emit_stderr(".");
        emit_stderr_len(mi.ptr, mi.len);
        emit_stderr("\n");
        panic("Parse error");
    }
    return 0;
}

func compiler_finalize_abst_methods() -> u64 {
    if (g_all_structs_vec == 0) { return 0; }
    var sn: u64 = g_all_structs_vec.len();
    for (var si: u64 = 0; si < sn; si++) {
        var sd: *AstStructDef = g_all_structs_vec.get(si);
        if (sd == 0 || sd.parents_vec == 0) { continue; }
        var required: *Vec<*NameInfo> = new Vec<*NameInfo>(8);
        compiler_collect_abst_methods_recursive(sd, required);
        var rn: u64 = required.len();
        for (var ri: u64 = 0; ri < rn; ri++) {
            var mi: *NameInfo = required.get(ri);
            compiler_validate_child_implements_abst_method(sd, mi);
        }
    }
    compiler_setup_virtual_dispatch_traits();
    return 0;
}

func compiler_insert_default_constructor(struct_name_ptr: u64, struct_name_len: u64) -> u64 {
    if (g_all_funcs == 0 || g_all_func_sigs == 0) { return 0; }
    var ctor_name: *NameInfo = _compiler_build_constructor_name(struct_name_ptr, struct_name_len);
    if (compiler_func_exists(ctor_name.ptr, ctor_name.len) != 0) { return 0; }

    var params: *Vec<*Param> = new Vec<*Param>(1);
    params.push(_compiler_make_self_param(struct_name_ptr, struct_name_len));
    var stmts: *Vec<*AstNode> = new Vec<*AstNode>(0);
    var body: *AstNode = (*AstNode)ast_block(stmts);
    var fn: *AstFunc = ast_func_ex(ctor_name.ptr, ctor_name.len, params, TYPE_VOID, 0, 0, 0, 0, 0, 0, body);
    g_all_funcs.push(fn);
    g_all_func_sigs.push(fn);
    compiler_invalidate_func_lookup();
    return 0;
}

func compiler_insert_default_constructors() -> u64 {
    var n: u64 = g_all_structs_vec.?len();
    if (n == 0) { return 0; }
    for (var i: u64 = 0; i < n; i++) {
        var def: *AstStructDef = g_all_structs_vec.get(i);
        if (def == 0) { continue; }
        if (compiler_is_generic_struct_def(def) != 0) { continue; }
        compiler_insert_default_constructor(def.name_ptr, def.name_len);
    }
    return 0;
}

func compiler_insert_global_init_func() -> u64 {
    if (compiler_has_runtime_global_init() == 0) { return 0; }
    if (compiler_func_exists("__global_init", 13)) { return 0; }

    var stmts: *Vec<*AstNode> = new Vec<*AstNode>(8);
    var n: u64 = g_all_globals.len();
    for (var i: u64 = 0; i < n; i++) {
        var ginfo: *GlobalInfo = g_all_globals.get(i);
        if (ginfo.init_kind != GLOBAL_INIT_RUNTIME) { continue; }
        var init_expr: *AstNode = (*AstNode)ginfo.init_expr;
        if (init_expr == 0) {
            emit_stderr("[ERROR] runtime global init expr missing\n");
            panic("Parse error");
        }
        var target: *AstNode = (*AstNode)ast_ident(ginfo.name_ptr, ginfo.name_len);
        var assign: *AstAssign = ast_assign(target, init_expr);
        stmts.push((*AstNode)assign);
    }

    if (stmts.len() == 0) { return 0; }

    var block: *AstNode = (*AstNode)ast_block(stmts);
    var params: *Vec<*Param> = new Vec<*Param>(0);
    var fn: *AstFunc = ast_func("__global_init", 13, params, TYPE_VOID, block);
    g_all_funcs.push(fn);
    g_all_func_sigs.push(fn);
    compiler_invalidate_func_lookup();
    return 0;
}

func setup_paths(filename: u64, filename_len: u64) -> u64 {
    return setup_paths_with_compiler(filename, filename_len);
}

func setup_paths_with_compiler(filename: u64, filename_len: u64) -> u64 {
    g_base_dir = path_dirname(filename, filename_len);
    g_base_dir_len = str_len(g_base_dir);

    // Find version directory by going up from base_dir
    // src: B/v3_XX/src -> up 1 = B/v3_XX
    // test: B/v3_XX/test/b -> up 2 = B/v3_XX
    var up_one: u64 = path_dirname(g_base_dir, g_base_dir_len);
    var up_one_len: u64 = str_len(up_one);
    var version_dir2: u64 = path_dirname(up_one, up_one_len);
    var version_dir_len2: u64 = str_len(version_dir2);
    var slash_config: u64 = "/config.ini";
    var config_path1: u64 = str_concat(up_one, up_one_len, slash_config, 11);
    var config_path2: u64 = str_concat(version_dir2, version_dir_len2, slash_config, 11);
    var version2: u64 = read_version_from_config(config_path1);

    if (version2 == 0) {
        version2 = read_version_from_config(config_path2);
    }

    if (version2 == 0) {
        emit_stderr("[ERROR] Could not determine compiler version from config.ini\n");
        emit_stderr("[ERROR] Checked: ");
        emit_stderr(config_path1);
        emit_stderr(" and ");
        emit_stderr(config_path2);
        emit_stderr("\n");
        panic("Path setup error");
    }

    // Build lib_dir path: prefer base_dir parent when filename is in src/
    var use_up_one: u64 = 0;
    if (g_base_dir_len >= 4) {
        var base_dir_u8: []u8 = slice(g_base_dir, g_base_dir_len);
        var c0: u64 = base_dir_u8[g_base_dir_len - 4];
        var c1: u64 = base_dir_u8[g_base_dir_len - 3];
        var c2: u64 = base_dir_u8[g_base_dir_len - 2];
        var c3: u64 = base_dir_u8[g_base_dir_len - 1];
        if (c0 == 47 && c1 == 115 && c2 == 114 && c3 == 99) { // "/src"
            use_up_one = 1;
        }
    }

    var lib_root: u64 = version_dir2;
    var lib_root_len: u64 = version_dir_len2;
    if (use_up_one != 0) {
        lib_root = up_one;
        lib_root_len = up_one_len;
    }

    var src_suffix2: u64 = "/src";
    g_lib_dir = str_concat(lib_root, lib_root_len, src_suffix2, 4);
    g_lib_dir_len = str_len(g_lib_dir);
    return 0;
}

func build_merged_program() -> *AstProgram {
    var dummy_imports: *Vec<u64> = new Vec<u64>(1);
    var merged_prog: *AstProgram = ast_program(g_all_funcs, g_all_consts, dummy_imports);
    merged_prog.globals_vec = g_all_globals;
    merged_prog.structs_vec = g_all_structs_vec;
    return merged_prog;
}

func get_func_sigs() -> *Vec<*AstFunc> {
    return g_all_func_sigs;
}
