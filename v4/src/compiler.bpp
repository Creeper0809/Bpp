// compiler.b - Compiler globals and driver helpers

import std.io;
import std.str;
import std.path;
import std.vec;
import std.hashmap;
import std.util;
import types;
import lexer;
import ast;
import parser.util;
import parser.decl;

// Local layout mirrors for sizeof (avoid sizeof on imported types during bootstrap).
struct NameInfoLocal { ptr: u64; len: u64; }
struct ExportEntryLocal { name_ptr: u64; name_len: u64; mangled_ptr: u64; mangled_len: u64; }
struct ConstResultLocal { found: u64; value: u64; }
struct StatLocal {
    pad0: u64;
    pad1: u64;
    pad2: u64;
    pad3: u64;
    pad4: u64;
    pad5: u64;
    size: u64;
}

const GENERIC_DEBUG = 0;

// ============================================
// Global Module State
// ============================================
var g_loaded_modules: *HashMap<u64, u64>;          // HashMap: path -> 1 (tracks fully loaded files)
var g_loaded_modules_pass1: *HashMap<u64, u64>;    // HashMap: path -> 1 (tracks pass1 signature scan)
var g_module_imports_pass1: *HashMap<u64, *Vec<*AstImport> >;    // HashMap: path -> imports vec from pass1
var g_module_imports_pass2: *HashMap<u64, *Vec<*AstImport> >;    // HashMap: path -> imports vec from pass2 (stable copies)
var g_loaded_modules_vec: *Vec<*NameInfo>;               // Vec<NameInfo> of fully loaded module paths
var g_all_funcs: *Vec<*AstFunc>;         // Vec of all function ASTs
var g_all_func_sigs: *Vec<*AstFunc>;     // Vec of all function signatures (pass 1)
var g_all_consts: *Vec<*AstConstDecl>;        // Vec of all const ASTs
var g_all_globals: *Vec<*GlobalInfo>;       // Vec of all global var info
var g_all_structs: *HashMap<u64, *AstStructDef>;       // HashMap: struct_name -> struct_def
var g_all_structs_vec: *Vec<*AstStructDef>;   // Vec of all struct_defs (for codegen)
var g_base_dir;          // Base directory for imports
var g_base_dir_len;
var g_lib_dir;           // Library root directory for compiler/runtime modules
var g_lib_dir_len;

// Generic templates and instantiations
var g_generic_funcs: *HashMap<u64, *GenericFuncTemplate>;          // HashMap: base name -> GenericFuncTemplate
var g_generic_funcs_mangled: *HashMap<u64, *GenericFuncTemplate>;  // HashMap: mangled name -> GenericFuncTemplate
var g_generic_func_ptrs: *Vec<*GenericFuncTemplate>;      // Vec of generic template pointers
var g_generic_instantiations: *HashMap<u64, u64>; // HashMap: mangled instantiation name -> 1
var g_generic_structs: *HashMap<u64, *GenericStructTemplate>;         // HashMap: base name -> GenericStructTemplate
var g_generic_struct_ptrs: *Vec<*GenericStructTemplate>;     // Vec of generic struct template pointers
var g_generic_struct_instantiations: *HashMap<u64, *AstStructDef>; // HashMap: instantiation name -> struct_def
var g_generic_struct_bindings: *HashMap<u64, *Vec<*GenericBinding> >; // HashMap: instantiation name -> bindings

var g_file_ptr;
var g_file_len;
var g_module_aliases: *HashMap<u64, *HashMap<u64, *NameInfo> >;     // HashMap: module_id -> alias_map
var g_prelude_aliases: *HashMap<u64, *NameInfo>;    // HashMap: alias -> [mangled_ptr, mangled_len]
var g_module_exports: *HashMap<u64, *Vec<*ExportEntry> >;     // HashMap: module_id -> exports vec
var g_func_module_map: *HashMap<u64, *NameInfo>;    // HashMap: mangled func -> module_id
var g_current_module_ptr;
var g_current_module_len;
var g_impl_prefix_ptr;
var g_impl_prefix_len;
var g_impl_methods: *HashMap<u64, u64>;       // HashMap: impl-lowered name -> 1

// Generic function template (48 bytes)
struct GenericFuncTemplate {
    name_ptr: u64;
    name_len: u64;
    mangled_ptr: u64;
    mangled_len: u64;
    params_vec: u64;
    func_sig: u64;
    func_def: u64;
}

// Generic struct template (24 bytes)
struct GenericStructTemplate {
    name_ptr: u64;
    name_len: u64;
    params_vec: u64;
    struct_def: u64;
}

// Generic binding (40 bytes)
struct GenericBinding {
    kind: u64;
    name_ptr: u64;
    name_len: u64;
    type_ptr: u64;
    value: u64;
}

// ============================================
// File Reading
// ============================================

func read_entire_file(path: u64) -> u64 {
    var fd: u64 = sys_open(path, 0, 0);
    if (fd < 0) { return 0; }

    var statbuf_raw: u64 = heap_alloc(144 * sizeof(u8));
    var statbuf: *StatLocal = (*StatLocal)statbuf_raw;
    sys_fstat(fd, statbuf_raw);
    var size: u64 = statbuf->size;

    var buf: u64 = heap_alloc((size + 1) * sizeof(u8));

    var total: u64 = 0;
    for (; total < size; ) {
        var n: u64 = sys_read(fd, buf + total, size - total);
        if (n <= 0) { break; }
        total = total + n;
    }

    *(*u8)(buf + total) = 0;

    sys_close(fd);

    g_file_ptr = buf;
    g_file_len = total;

    return buf;
}

func compiler_get_source_ptr() -> u64 {
    return g_file_ptr;
}

func compiler_get_source_len() -> u64 {
    return g_file_len;
}

// ============================================
// Config File Parsing
// ============================================

// Simple line-by-line config parser
func find_line_starting_with(content: u64, content_len: u64, prefix: u64, prefix_len: u64) -> u64 {
    for (var i: u64 = 0; i < content_len; ) {
        // Find start of current line
        var line_start: u64 = i;

        // Find end of line
        var line_end: u64 = i;
        for (; line_end < content_len; line_end++) {
            if (*(*u8)(content + line_end) == 10) { break; }
        }

        var line_len: u64 = line_end - line_start;

        // Check if line starts with prefix
        if (line_len >= prefix_len) {
            if (str_eq(content + line_start, prefix_len, prefix, prefix_len)) {
                // Found matching line, return pointer to start of value (after prefix)
                var value_start: u64 = line_start + prefix_len;
                var value_len: u64 = line_len - prefix_len;

                // Allocate and copy value
                var value: u64 = heap_alloc((value_len + 1) * sizeof(u8));
                for (var j: u64 = 0; j < value_len; j++) {
                    *(*u8)(value + j) = *(*u8)(content + value_start + j);
                }
                *(*u8)(value + value_len) = 0;
                return value;
            }
        }

        // Move to next line
        i = line_end + 1;
    }

    return 0;
}

// Read config.ini and extract VERSION value
func read_version_from_config(config_path: u64) -> u64 {
    var content: u64 = read_entire_file(config_path);
    if (content == 0) {
        return 0;
    }

    return find_line_starting_with(content, g_file_len, "VERSION=", 8);
}

// ============================================
// Module Id + Mangling Helpers
// ============================================

// Return 1 if s starts with prefix
func str_has_prefix(s: u64, s_len: u64, prefix: u64, prefix_len: u64) -> u64 {
    if (s_len < prefix_len) { return 0; }
    for (var i: u64 = 0; i < prefix_len; i++) {
        if (*(*u8)(s + i) != *(*u8)(prefix + i)) { return 0; }
    }
    return 1;
}

// Returns [ptr,len] struct (16 bytes)
func module_id_from_path(path: u64, path_len: u64) -> u64 {
    var start: u64 = 0;
    if (g_lib_dir != 0 && str_has_prefix(path, path_len, g_lib_dir, g_lib_dir_len)) {
        if (path_len > g_lib_dir_len && *(*u8)(path + g_lib_dir_len) == 47) {
            start = g_lib_dir_len + 1;
        }
    }
    if (start == 0 && g_base_dir != 0 && str_has_prefix(path, path_len, g_base_dir, g_base_dir_len)) {
        if (path_len > g_base_dir_len && *(*u8)(path + g_base_dir_len) == 47) {
            start = g_base_dir_len + 1;
        }
    }

    var end: u64 = path_len;
    if (end >= 4) {
        if (*(*u8)(path + end - 4) == 46 && *(*u8)(path + end - 3) == 98 && *(*u8)(path + end - 2) == 112 && *(*u8)(path + end - 1) == 112) {
            end = end - 4;
        }
    }
    if (end == path_len && end >= 2) {
        if (*(*u8)(path + end - 2) == 46 && *(*u8)(path + end - 1) == 98) {
            end = end - 2;
        }
    }

    var id_len: u64 = 0;
    if (end > start) { id_len = end - start; }
    var id_ptr: u64 = heap_alloc((id_len + 1) * sizeof(u8));
    for (var i: u64 = 0; i < id_len; i++) {
        *(*u8)(id_ptr + i) = *(*u8)(path + start + i);
    }
    *(*u8)(id_ptr + id_len) = 0;

    var out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    out->ptr = id_ptr;
    out->len = id_len;
    return (u64)out;
}

// Import path already normalized to "a/b" without extension
func module_id_from_import(path: u64, path_len: u64) -> u64 {
    var id_ptr: u64 = heap_alloc((path_len + 1) * sizeof(u8));
    for (var i: u64 = 0; i < path_len; i++) {
        *(*u8)(id_ptr + i) = *(*u8)(path + i);
    }
    *(*u8)(id_ptr + path_len) = 0;
    var out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    out->ptr = id_ptr;
    out->len = path_len;
    return (u64)out;
}

func module_prefix_from_id(id_ptr: u64, id_len: u64) -> u64 {
    var extra: u64 = 0;
    if (id_len > 0) {
        var c0: u64 = *(*u8)(id_ptr);
        if (c0 >= 48 && c0 <= 57) { extra = 1; }
    }

    var pref_ptr: u64 = heap_alloc((id_len + extra + 1) * sizeof(u8));
    if (extra == 1) {
        *(*u8)(pref_ptr) = 95;
    }
    for (var i: u64 = 0; i < id_len; i++) {
        var c: u64 = *(*u8)(id_ptr + i);
        if (c == 47 || c == 46) { c = 95; }
        *(*u8)(pref_ptr + extra + i) = c;
    }
    *(*u8)(pref_ptr + id_len + extra) = 0;
    var out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    out->ptr = pref_ptr;
    out->len = id_len + extra;
    return (u64)out;
}

// Returns [ptr,len]
func mangle_name(prefix_ptr: u64, prefix_len: u64, name_ptr: u64, name_len: u64) -> u64 {
    if (prefix_len == 0) {
        var out0: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
        out0->ptr = name_ptr;
        out0->len = name_len;
        return (u64)out0;
    }
    var sep_len: u64 = 2;
    var total: u64 = prefix_len + sep_len + name_len;
    var buf: u64 = heap_alloc((total + 1) * sizeof(u8));
    for (var i: u64 = 0; i < prefix_len; i++) {
        *(*u8)(buf + i) = *(*u8)(prefix_ptr + i);
    }
    *(*u8)(buf + prefix_len) = 95;
    *(*u8)(buf + prefix_len + 1) = 95;
    for (var j: u64 = 0; j < name_len; j++) {
        *(*u8)(buf + prefix_len + sep_len + j) = *(*u8)(name_ptr + j);
    }
    *(*u8)(buf + total) = 0;
    var out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    out->ptr = buf;
    out->len = total;
    return (u64)out;
}

// ============================================
// Module Loading
// ============================================

func file_exists(path: u64) -> u64 {
    var fd: u64 = sys_open(path, 0, 0);
    if (fd < 0) { return 0; }
    sys_close(fd);
    return 1;
}

func is_std_alias(module_path: u64, module_len: u64) -> u64 {
    if (str_eq(module_path, module_len, "io", 2)) { return 1; }
    if (str_eq(module_path, module_len, "util", 4)) { return 1; }
    if (str_eq(module_path, module_len, "vec", 3)) { return 1; }
    if (str_eq(module_path, module_len, "hashmap", 7)) { return 1; }
    return 0;
}

func std_alias_to_module_path(module_path: u64, module_len: u64) -> u64 {
    if (str_eq(module_path, module_len, "io", 2)) { return "std/io"; }
    if (str_eq(module_path, module_len, "util", 4)) { return "std/util"; }
    if (str_eq(module_path, module_len, "vec", 3)) { return "std/vec"; }
    if (str_eq(module_path, module_len, "hashmap", 7)) { return "std/hashmap"; }
    return 0;
}

func is_std_path(module_path: u64, module_len: u64) -> u64 {
    if (module_len < 4) { return 0; }
    if (*(*u8)module_path != 115) { return 0; }      // s
    if (*(*u8)(module_path + 1) != 116) { return 0; } // t
    if (*(*u8)(module_path + 2) != 100) { return 0; } // d
    if (*(*u8)(module_path + 3) != 47) { return 0; }  // /
    return 1;
}

func resolve_module_path(module_path: u64, module_len: u64) -> u64 {
    var eff_path: u64 = module_path;
    var eff_len: u64 = module_len;

    var prefer_lib: u64 = 0;

    if (is_std_alias(module_path, module_len)) {
        eff_path = std_alias_to_module_path(module_path, module_len);
        eff_len = str_len(eff_path);
        prefer_lib = 1;
    }

    if (is_std_path(eff_path, eff_len)) {
        prefer_lib = 1;
    }

    var ext: u64 = heap_alloc(3 * sizeof(u8));
    *(*u8)ext = 46;
    *(*u8)(ext + 1) = 98;
    *(*u8)(ext + 2) = 0;

    var with_ext: u64 = str_concat(eff_path, eff_len, ext, 2);
    var with_ext_len: u64 = eff_len + 2;

    var ext_bpp: u64 = heap_alloc(5 * sizeof(u8));
    *(*u8)ext_bpp = 46;
    *(*u8)(ext_bpp + 1) = 98;
    *(*u8)(ext_bpp + 2) = 112;
    *(*u8)(ext_bpp + 3) = 112;
    *(*u8)(ext_bpp + 4) = 0;

    var with_ext_bpp: u64 = str_concat(eff_path, eff_len, ext_bpp, 4);
    var with_ext_bpp_len: u64 = eff_len + 4;

    var slash: u64 = heap_alloc(sizeof(u8));
    *(*u8)slash = 47;

    var full1: u64;
    var full2: u64;
    if (prefer_lib) {
        full1 = str_concat3(g_lib_dir, g_lib_dir_len, slash, 1, with_ext_bpp, with_ext_bpp_len);
        if (file_exists(full1)) { return full1; }
        full2 = str_concat3(g_base_dir, g_base_dir_len, slash, 1, with_ext_bpp, with_ext_bpp_len);
        if (file_exists(full2)) { return full2; }

        full1 = str_concat3(g_lib_dir, g_lib_dir_len, slash, 1, with_ext, with_ext_len);
        if (file_exists(full1)) { return full1; }
        full2 = str_concat3(g_base_dir, g_base_dir_len, slash, 1, with_ext, with_ext_len);
        return full2;
    }

    full1 = str_concat3(g_base_dir, g_base_dir_len, slash, 1, with_ext_bpp, with_ext_bpp_len);
    if (file_exists(full1)) { return full1; }
    full2 = str_concat3(g_lib_dir, g_lib_dir_len, slash, 1, with_ext_bpp, with_ext_bpp_len);
    if (file_exists(full2)) { return full2; }

    full1 = str_concat3(g_base_dir, g_base_dir_len, slash, 1, with_ext, with_ext_len);
    if (file_exists(full1)) { return full1; }
    full2 = str_concat3(g_lib_dir, g_lib_dir_len, slash, 1, with_ext, with_ext_len);
    return full2;
}

// ============================================
// Import Alias + Export Table
// ============================================

// Export entry: [name_ptr, name_len, mangled_ptr, mangled_len] = 32 bytes
func module_exports_get(module_ptr: u64, module_len: u64) -> *Vec<*ExportEntry> {
    if (g_module_exports == 0) { return 0; }
    return ((*HashMap<u64, *Vec<*ExportEntry> >)g_module_exports)->get(module_ptr, module_len);
}

func module_exports_ensure(module_ptr: u64, module_len: u64) -> *Vec<*ExportEntry> {
    if (g_module_exports == 0) {
        g_module_exports = HashMap.new<u64, *Vec<*ExportEntry> >(64);
    }
    var existing: *Vec<*ExportEntry> = ((*HashMap<u64, *Vec<*ExportEntry> >)g_module_exports)->get(module_ptr, module_len);
    if (existing != 0) { return existing; }
    var vec: *Vec<*ExportEntry> = Vec.new<*ExportEntry>(32);
    ((*HashMap<u64, *Vec<*ExportEntry> >)g_module_exports)->put(module_ptr, module_len, vec);
    return vec;
}

func add_module_export(module_ptr: u64, module_len: u64, name_ptr: u64, name_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_ensure(module_ptr, module_len);
    var entry: *ExportEntry = (*ExportEntry)heap_alloc(sizeof(ExportEntryLocal));
    entry->name_ptr = name_ptr;
    entry->name_len = name_len;
    entry->mangled_ptr = mangled_ptr;
    entry->mangled_len = mangled_len;
    ((*Vec<*ExportEntry>)exports)->push(entry);
    return 0;
}

func module_aliases_get(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_module_aliases == 0) { return 0; }
    return ((*HashMap<u64, *HashMap<u64, *NameInfo> >)g_module_aliases)->get(module_ptr, module_len);
}

func module_aliases_ensure(module_ptr: u64, module_len: u64) -> *HashMap<u64, *NameInfo> {
    if (g_module_aliases == 0) {
        g_module_aliases = HashMap.new<u64, *HashMap<u64, *NameInfo> >(128);
    }
    var existing: *HashMap<u64, *NameInfo> = ((*HashMap<u64, *HashMap<u64, *NameInfo> >)g_module_aliases)->get(module_ptr, module_len);
    if (existing != 0) { return existing; }
    var map: *HashMap<u64, *NameInfo> = HashMap.new<u64, *NameInfo>(128);
    ((*HashMap<u64, *HashMap<u64, *NameInfo> >)g_module_aliases)->put(module_ptr, module_len, map);
    return map;
}

func add_import_alias(module_ptr: u64, module_len: u64, alias_ptr: u64, alias_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    var alias_map: *HashMap<u64, *NameInfo> = module_aliases_ensure(module_ptr, module_len);
    var existing: *NameInfo = ((*HashMap<u64, *NameInfo>)alias_map)->get(alias_ptr, alias_len);
    if (existing != 0) {
        var ex_ptr: u64 = existing->ptr;
        var ex_len: u64 = existing->len;
        if (ex_ptr == mangled_ptr && ex_len == mangled_len) {
            return 0;
        }
        emit_stderr("[ERROR] import alias conflict: ", 33);
        emit_stderr(alias_ptr, alias_len);
        emit_stderr_nl();
        panic("Parse error");
    }
    var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    info->ptr = mangled_ptr;
    info->len = mangled_len;
    ((*HashMap<u64, *NameInfo>)alias_map)->put(alias_ptr, alias_len, info);
    return 0;
}

func add_prelude_alias(alias_ptr: u64, alias_len: u64, mangled_ptr: u64, mangled_len: u64) -> u64 {
    if (g_prelude_aliases == 0) {
        g_prelude_aliases = HashMap.new<u64, *NameInfo>(128);
    }
    var existing: *NameInfo = ((*HashMap<u64, *NameInfo>)g_prelude_aliases)->get(alias_ptr, alias_len);
    if (existing != 0) {
        var ex_ptr: u64 = existing->ptr;
        var ex_len: u64 = existing->len;
        if (ex_ptr == mangled_ptr && ex_len == mangled_len) {
            return 0;
        }
        emit_stderr("[ERROR] prelude alias conflict: ", 34);
        emit_stderr(alias_ptr, alias_len);
        emit_stderr_nl();
        panic("Parse error");
    }
    var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    info->ptr = mangled_ptr;
    info->len = mangled_len;
    ((*HashMap<u64, *NameInfo>)g_prelude_aliases)->put(alias_ptr, alias_len, info);
    return 0;
}

func resolve_import_alias(module_ptr: u64, module_len: u64, name_ptr: u64, name_len: u64) -> *NameInfo {
    var alias_map: *HashMap<u64, *NameInfo> = module_aliases_get(module_ptr, module_len);
    if (alias_map == 0) { return 0; }
    return ((*HashMap<u64, *NameInfo>)alias_map)->get(name_ptr, name_len);
}

func resolve_prelude_alias(name_ptr: u64, name_len: u64) -> *NameInfo {
    if (g_prelude_aliases == 0) { return 0; }
    return ((*HashMap<u64, *NameInfo>)g_prelude_aliases)->get(name_ptr, name_len);
}

func compiler_func_exists(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_funcs == 0) { return 0; }
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: u64 = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        var resolved_info: *NameInfo = (*NameInfo)resolved;
        resolved_ptr = resolved_info->ptr;
        resolved_len = resolved_info->len;
    }
    var n: u64 = ((*Vec<*AstFunc>)g_all_funcs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var fn: *AstFunc = ((*Vec<*AstFunc>)g_all_funcs)->get(i);
        if (str_eq(fn->name_ptr, fn->name_len, resolved_ptr, resolved_len)) { return 1; }
    }
    return 0;
}

func compiler_get_func(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_funcs == 0) { return 0; }
    var resolved_ptr: u64 = name_ptr;
    var resolved_len: u64 = name_len;
    var resolved: u64 = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        var resolved_info: *NameInfo = (*NameInfo)resolved;
        resolved_ptr = resolved_info->ptr;
        resolved_len = resolved_info->len;
    }
    var n: u64 = ((*Vec<*AstFunc>)g_all_funcs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var fn: *AstFunc = ((*Vec<*AstFunc>)g_all_funcs)->get(i);
        if (str_eq(fn->name_ptr, fn->name_len, resolved_ptr, resolved_len)) { return (u64)fn; }
    }
    return 0;
}

func compiler_global_exists(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_globals == 0) { return 0; }
    var n: u64 = ((*Vec<*GlobalInfo>)g_all_globals)->len();
    for (var i: u64 = 0; i < n; i++) {
        var ginfo: *GlobalInfo = ((*Vec<*GlobalInfo>)g_all_globals)->get(i);
        if (str_eq(ginfo->name_ptr, ginfo->name_len, name_ptr, name_len)) { return 1; }
    }
    return 0;
}

func compiler_find_global_info(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_globals == 0) { return 0; }
    var n: u64 = ((*Vec<*GlobalInfo>)g_all_globals)->len();
    for (var i: u64 = 0; i < n; i++) {
        var ginfo: *GlobalInfo = ((*Vec<*GlobalInfo>)g_all_globals)->get(i);
        if (str_eq(ginfo->name_ptr, ginfo->name_len, name_ptr, name_len)) { return (u64)ginfo; }
    }
    return 0;
}

func compiler_set_all_globals(globals: u64) -> u64 {
    g_all_globals = (*Vec<*GlobalInfo>)globals;
    return 0;
}

func prelude_try_symbol(module_path: u64, module_len: u64, name_ptr: u64, name_len: u64) -> u64 {
    var mod_id: u64 = module_id_from_import(module_path, module_len);
    if (mod_id == 0) { return 0; }
    var mod_info: *NameInfo = (*NameInfo)mod_id;
    var mod_ptr: u64 = mod_info->ptr;
    var mod_len: u64 = mod_info->len;
    var exports: *Vec<*ExportEntry> = module_exports_get(mod_ptr, mod_len);
    if (exports == 0) { return 0; }

    var n: u64 = ((*Vec<*ExportEntry>)exports)->len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = ((*Vec<*ExportEntry>)exports)->get(i);
        var sym_ptr: u64 = e->name_ptr;
        var sym_len: u64 = e->name_len;
        if (str_eq(sym_ptr, sym_len, name_ptr, name_len) != 0) {
            var mangled_ptr: u64 = e->mangled_ptr;
            var mangled_len: u64 = e->mangled_len;
            add_prelude_alias(name_ptr, name_len, mangled_ptr, mangled_len);
            var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
            info->ptr = mangled_ptr;
            info->len = mangled_len;
            return (u64)info;
        }
    }
    return 0;
}

func load_std_prelude_modules() -> u64 {
    if (!load_module_by_name("std/io", 6)) { return 0; }
    if (!load_module_by_name("std/str", 7)) { return 0; }
    if (!load_module_by_name("std/char", 8)) { return 0; }
    if (!load_module_by_name("std/util", 8)) { return 0; }
    if (!load_module_by_name("std/vec", 7)) { return 0; }
    if (!load_module_by_name("std/hashmap", 11)) { return 0; }
    return 1;
}

func resolve_prelude_alias_lazy(name_ptr: u64, name_len: u64) -> u64 {
    var info: u64 = prelude_try_symbol("std/io", 6, name_ptr, name_len);
    if (info != 0) { return info; }
    info = prelude_try_symbol("std/str", 7, name_ptr, name_len);
    if (info != 0) { return info; }
    info = prelude_try_symbol("std/char", 8, name_ptr, name_len);
    if (info != 0) { return info; }
    info = prelude_try_symbol("std/util", 8, name_ptr, name_len);
    if (info != 0) { return info; }
    info = prelude_try_symbol("std/vec", 7, name_ptr, name_len);
    if (info != 0) { return info; }
    info = prelude_try_symbol("std/hashmap", 11, name_ptr, name_len);
    if (info != 0) { return info; }
    return 0;
}

func resolve_name(name_ptr: u64, name_len: u64) -> u64 {
    var suffix_idx: u64 = compiler_find_generic_suffix_index(name_ptr, name_len);
    if (suffix_idx < name_len) {
        var base_ptr: u64 = name_ptr;
        var base_len: u64 = suffix_idx;
        var resolved_base: u64 = resolve_name(base_ptr, base_len);
        if (resolved_base != 0) {
            var base_info: *NameInfo = (*NameInfo)resolved_base;
            var suffix_len: u64 = name_len - suffix_idx;
            var combined_len: u64 = base_info->len + suffix_len;
            var combined_ptr: u64 = heap_alloc((combined_len + 1) * sizeof(u8));
            for (var i: u64 = 0; i < base_info->len; i++) {
                *(*u8)(combined_ptr + i) = *(*u8)(base_info->ptr + i);
            }
            for (var j: u64 = 0; j < suffix_len; j++) {
                *(*u8)(combined_ptr + base_info->len + j) = *(*u8)(name_ptr + suffix_idx + j);
            }
            *(*u8)(combined_ptr + combined_len) = 0;
            var info_out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
            info_out->ptr = combined_ptr;
            info_out->len = combined_len;
            return (u64)info_out;
        }
    }
    if (g_current_module_ptr != 0 && g_current_module_len != 0) {
        var alias: *NameInfo = resolve_import_alias(g_current_module_ptr, g_current_module_len, name_ptr, name_len);
        if (alias != 0) { return (u64)alias; }
    }

    if (g_current_module_ptr != 0 && g_current_module_len != 0) {
        var exports: *Vec<*ExportEntry> = module_exports_get(g_current_module_ptr, g_current_module_len);
        if (exports != 0) {
            var n: u64 = ((*Vec<*ExportEntry>)exports)->len();
            for (var i: u64 = 0; i < n; i++) {
                var e: *ExportEntry = ((*Vec<*ExportEntry>)exports)->get(i);
                var n_ptr: u64 = e->name_ptr;
                var n_len: u64 = e->name_len;
                if (str_eq(n_ptr, n_len, name_ptr, name_len)) {
                    var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
                    info->ptr = e->mangled_ptr;
                    info->len = e->mangled_len;
                    return (u64)info;
                }
            }
        }
    }

    return 0;
}

// ============================================
// Const Lookup (SSA/Codegen Shared)
// ============================================

func compiler_find_const(name_ptr: u64, name_len: u64) -> u64 {
    var result: u64 = heap_alloc(sizeof(ConstResultLocal));
    var res: *ConstResult = (*ConstResult)result;
    res->found = 0;
    res->value = 0;

    if (g_all_consts == 0) { return result; }
    var n: u64 = ((*Vec<*AstConstDecl>)g_all_consts)->len();
    for (var i: u64 = 0; i < n; i++) {
        var c: *AstConstDecl = ((*Vec<*AstConstDecl>)g_all_consts)->get(i);
        if (str_eq(c->name_ptr, c->name_len, name_ptr, name_len)) {
            res->found = 1;
            res->value = c->value;
            return result;
        }
    }
    return result;
}

func import_all_from_module(importer_ptr: u64, importer_len: u64, module_ptr: u64, module_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return 0; }
    var n: u64 = ((*Vec<*ExportEntry>)exports)->len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = ((*Vec<*ExportEntry>)exports)->get(i);
        var name_ptr: u64 = e->name_ptr;
        var name_len: u64 = e->name_len;
        var mangled_ptr: u64 = e->mangled_ptr;
        var mangled_len: u64 = e->mangled_len;
        add_import_alias(importer_ptr, importer_len, name_ptr, name_len, mangled_ptr, mangled_len);
    }
    return 1;
}

func import_symbol_from_module(importer_ptr: u64, importer_len: u64, module_ptr: u64, module_len: u64, symbol_ptr: u64, symbol_len: u64, alias_ptr: u64, alias_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return 0; }
    var n: u64 = ((*Vec<*ExportEntry>)exports)->len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = ((*Vec<*ExportEntry>)exports)->get(i);
        var name_ptr: u64 = e->name_ptr;
        var name_len: u64 = e->name_len;
        if (str_eq(name_ptr, name_len, symbol_ptr, symbol_len)) {
            var mangled_ptr: u64 = e->mangled_ptr;
            var mangled_len: u64 = e->mangled_len;
            add_import_alias(importer_ptr, importer_len, alias_ptr, alias_len, mangled_ptr, mangled_len);
            return 1;
        }
    }
    emit_stderr("[ERROR] import symbol not found: ", 35);
    emit_stderr(symbol_ptr, symbol_len);
    emit_stderr_nl();
    panic("Parse error");
    return 0;
}

func prelude_import_all_from_module(module_ptr: u64, module_len: u64) -> u64 {
    var exports: *Vec<*ExportEntry> = module_exports_get(module_ptr, module_len);
    if (exports == 0) { return 0; }
    var n: u64 = ((*Vec<*ExportEntry>)exports)->len();
    for (var i: u64 = 0; i < n; i++) {
        var e: *ExportEntry = ((*Vec<*ExportEntry>)exports)->get(i);
        var name_ptr: u64 = e->name_ptr;
        var name_len: u64 = e->name_len;
        var mangled_ptr: u64 = e->mangled_ptr;
        var mangled_len: u64 = e->mangled_len;
        add_prelude_alias(name_ptr, name_len, mangled_ptr, mangled_len);
    }
    return 1;
}

// After all modules are loaded, resolve import aliases using stored import lists.
func finalize_import_aliases() -> u64 {
    if (g_loaded_modules_vec == 0) { return 0; }
    var n: u64 = ((*Vec<*NameInfo>)g_loaded_modules_vec)->len();
    for (var i: u64 = 0; i < n; i++) {
        var info: *NameInfo = ((*Vec<*NameInfo>)g_loaded_modules_vec)->get(i);
        var module_path: u64 = info->ptr;
        var module_path_len: u64 = info->len;
        var module_id: u64 = module_id_from_path(module_path, module_path_len);
        var module_info: *NameInfo = (*NameInfo)module_id;
        var module_ptr: u64 = module_info->ptr;
        var module_len: u64 = module_info->len;

        var imports: *Vec<*AstImport> = ((*HashMap<u64, *Vec<*AstImport> >)g_module_imports_pass2)->get(module_path, module_path_len);
        if (imports == 0) { continue; }
        var num_imports: u64 = ((*Vec<*AstImport>)imports)->len();
        for (var ii: u64 = 0; ii < num_imports; ii++) {
            var imp: *AstImport = ((*Vec<*AstImport>)imports)->get(ii);
            var imp_path: u64 = imp->path_ptr;
            var imp_len: u64 = imp->path_len;
            var imp_sym_ptr: u64 = imp->symbol_ptr;
            var imp_sym_len: u64 = imp->symbol_len;
            var imp_alias_ptr: u64 = imp->alias_ptr;
            var imp_alias_len: u64 = imp->alias_len;

            var imp_mod_id: u64 = module_id_from_import(imp_path, imp_len);
            var imp_mod_info: *NameInfo = (*NameInfo)imp_mod_id;
            var imp_mod_ptr: u64 = imp_mod_info->ptr;
            var imp_mod_len: u64 = imp_mod_info->len;

            if (imp_sym_ptr == 0) {
                import_all_from_module(module_ptr, module_len, imp_mod_ptr, imp_mod_len);
            } else {
                import_symbol_from_module(module_ptr, module_len, imp_mod_ptr, imp_mod_len, imp_sym_ptr, imp_sym_len, imp_alias_ptr, imp_alias_len);
            }
        }
    }
    return 1;
}

func set_current_module_for_func(func_ptr: u64, func_len: u64) -> u64 {
    g_current_module_ptr = 0;
    g_current_module_len = 0;
    if (g_func_module_map == 0) { return 0; }
    var info: *NameInfo = ((*HashMap<u64, *NameInfo>)g_func_module_map)->get(func_ptr, func_len);
    if (info != 0) {
        g_current_module_ptr = info->ptr;
        g_current_module_len = info->len;
    }
    return 0;
}

// ============================================
// Export Registration + Mangling
// ============================================

func register_module_exports(prog: u64, module_ptr: u64, module_len: u64, prefix_ptr: u64, prefix_len: u64) -> u64 {
    var prog_info: *AstProgram = (*AstProgram)prog;
    var funcs: *Vec<*AstFunc> = (*Vec<*AstFunc>)prog_info->funcs_vec;
    if (funcs != 0) {
        var num_funcs: u64 = ((*Vec<*AstFunc>)funcs)->len();
        for (var i: u64 = 0; i < num_funcs; i++) {
            var fn: *AstFunc = ((*Vec<*AstFunc>)funcs)->get(i);
            var orig_ptr: u64 = fn->name_ptr;
            var orig_len: u64 = fn->name_len;
            var mangled_ptr: u64 = orig_ptr;
            var mangled_len: u64 = orig_len;

            if (prefix_len != 0 && !(orig_len == 4 && str_eq(orig_ptr, orig_len, "main", 4))) {
                var m: u64 = mangle_name(prefix_ptr, prefix_len, orig_ptr, orig_len);
                var mangled_info: *NameInfo = (*NameInfo)m;
                mangled_ptr = mangled_info->ptr;
                mangled_len = mangled_info->len;
                fn->name_ptr = mangled_ptr;
                fn->name_len = mangled_len;
            }

            add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_ptr, mangled_len);

            if (g_func_module_map == 0) {
                g_func_module_map = HashMap.new<u64, *NameInfo>(128);
            }
            var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
            info->ptr = module_ptr;
            info->len = module_len;
            ((*HashMap<u64, *NameInfo>)g_func_module_map)->put(mangled_ptr, mangled_len, info);
        }
    }

    var consts: *Vec<*AstConstDecl> = (*Vec<*AstConstDecl>)prog_info->consts_vec;
    if (consts != 0) {
        var num_consts: u64 = ((*Vec<*AstConstDecl>)consts)->len();
        for (var ci: u64 = 0; ci < num_consts; ci++) {
            var c: *AstConstDecl = ((*Vec<*AstConstDecl>)consts)->get(ci);
            var orig_ptr: u64 = c->name_ptr;
            var orig_len: u64 = c->name_len;
            var mangled_ptr: u64 = orig_ptr;
            var mangled_len: u64 = orig_len;

            if (prefix_len != 0) {
                var m2: u64 = mangle_name(prefix_ptr, prefix_len, orig_ptr, orig_len);
                var mangled_info2: *NameInfo = (*NameInfo)m2;
                mangled_ptr = mangled_info2->ptr;
                mangled_len = mangled_info2->len;
                c->name_ptr = mangled_ptr;
                c->name_len = mangled_len;
            }
            add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_ptr, mangled_len);
        }
    }

    var globals: *Vec<*GlobalInfo> = (*Vec<*GlobalInfo>)prog_info->globals_vec;
    if (globals != 0) {
        var num_globals: u64 = ((*Vec<*GlobalInfo>)globals)->len();
        for (var gi: u64 = 0; gi < num_globals; gi++) {
            var ginfo: *GlobalInfo = ((*Vec<*GlobalInfo>)globals)->get(gi);
            var orig_ptr: u64 = ginfo->name_ptr;
            var orig_len: u64 = ginfo->name_len;
            var mangled_ptr: u64 = orig_ptr;
            var mangled_len: u64 = orig_len;

            if (prefix_len != 0) {
                var m3: u64 = mangle_name(prefix_ptr, prefix_len, orig_ptr, orig_len);
                var mangled_info3: *NameInfo = (*NameInfo)m3;
                mangled_ptr = mangled_info3->ptr;
                mangled_len = mangled_info3->len;
                ginfo->name_ptr = mangled_ptr;
                ginfo->name_len = mangled_len;
            }
            add_module_export(module_ptr, module_len, orig_ptr, orig_len, mangled_ptr, mangled_len);
        }
    }
    return 0;
}

func load_module_by_name(module_path: u64, module_len: u64) -> u64 {
    var resolved: u64 = resolve_module_path(module_path, module_len);
    var resolved_len: u64 = str_len(resolved);
    return load_module(resolved, resolved_len);
}

// Pass1: scan signatures and register generic templates before full parse.
func load_module_pass1(file_path: u64, file_path_len: u64) -> u64 {
    if (g_loaded_modules_pass1 == 0) { g_loaded_modules_pass1 = HashMap.new<u64, u64>(64); }
    if (((*HashMap<u64, u64>)g_loaded_modules_pass1)->has(file_path, file_path_len)) {
        return 1;
    }
    ((*HashMap<u64, u64>)g_loaded_modules_pass1)->put(file_path, file_path_len, 1);

    var content: u64 = read_entire_file(file_path);
    if (content == 0) {
        emit_stderr("[ERROR] Cannot open module: ", 29);
        for (var i: u64 = 0; i < file_path_len; i++) {
            emit_char(*(*u8)(file_path + i));
        }
        emit_nl();
        return 0;
    }

    var src: u64 = g_file_ptr;
    var slen: u64 = g_file_len;
    var tokens: u64 = lex_all(src, slen);

    // Pre-scan imports to ensure templates are registered before signature parse.
    var p0: u64 = parse_new(tokens);
    var imports_only: *Vec<*AstImport> = (*Vec<*AstImport>)parse_program_imports_only(p0);
    var imports_copy: *Vec<*AstImport> = Vec.new<*AstImport>(4);
    var num_imports: u64 = ((*Vec<*AstImport>)imports_only)->len();
    for (var ii: u64 = 0; ii < num_imports; ii++) {
        var imp: *AstImport = ((*Vec<*AstImport>)imports_only)->get(ii);
        var imp_path: u64 = imp->path_ptr;
        var imp_len: u64 = imp->path_len;
        var path_copy: u64 = heap_alloc((imp_len + 1) * sizeof(u8));
        for (var ci: u64 = 0; ci < imp_len; ci++) {
            *(*u8)(path_copy + ci) = *(*u8)(imp_path + ci);
        }
        *(*u8)(path_copy + imp_len) = 0;
        var imp_copy: *AstImport = (*AstImport)ast_import(path_copy, imp_len, 0, 0, 0, 0);
        ((*Vec<*AstImport>)imports_copy)->push(imp_copy);
    }
    if (g_module_imports_pass1 == 0) { g_module_imports_pass1 = HashMap.new<u64, *Vec<*AstImport> >(64); }
    ((*HashMap<u64, *Vec<*AstImport> >)g_module_imports_pass1)->put(file_path, file_path_len, imports_copy);

    for (var ii2: u64 = 0; ii2 < num_imports; ii2++) {
        var imp2: *AstImport = ((*Vec<*AstImport>)imports_copy)->get(ii2);
        var imp_path2: u64 = imp2->path_ptr;
        var imp_len2: u64 = imp2->path_len;
        var resolved: u64 = resolve_module_path(imp_path2, imp_len2);
        var resolved_len: u64 = str_len(resolved);
        if (!load_module_pass1(resolved, resolved_len)) {
            return 0;
        }
    }

    var p1: u64 = parse_new(tokens);
    var prog_sig: u64 = parse_program_pass1(p1);

    var prog_sig_info: *AstProgram = (*AstProgram)prog_sig;
    var sig_funcs: *Vec<*AstFunc> = (*Vec<*AstFunc>)prog_sig_info->funcs_vec;
    var num_sig_funcs: u64 = ((*Vec<*AstFunc>)sig_funcs)->len();
    for (var sfi: u64 = 0; sfi < num_sig_funcs; sfi++) {
        ((*Vec<*AstFunc>)g_all_func_sigs)->push(((*Vec<*AstFunc>)sig_funcs)->get(sfi));
    }
    return 1;
}

func load_std_prelude() -> u64 {
    if (!load_module_by_name("std/io", 6)) { return 0; }
    var id_io: u64 = module_id_from_import("std/io", 6);
    var io_info: *NameInfo = (*NameInfo)id_io;
    prelude_import_all_from_module(io_info->ptr, io_info->len);

    if (!load_module_by_name("std/str", 7)) { return 0; }
    var id_str: u64 = module_id_from_import("std/str", 7);
    var str_info: *NameInfo = (*NameInfo)id_str;
    prelude_import_all_from_module(str_info->ptr, str_info->len);

    if (!load_module_by_name("std/char", 8)) { return 0; }
    var id_char: u64 = module_id_from_import("std/char", 8);
    var char_info: *NameInfo = (*NameInfo)id_char;
    prelude_import_all_from_module(char_info->ptr, char_info->len);

    if (!load_module_by_name("std/util", 8)) { return 0; }
    var id_util: u64 = module_id_from_import("std/util", 8);
    var util_info: *NameInfo = (*NameInfo)id_util;
    prelude_import_all_from_module(util_info->ptr, util_info->len);

    if (!load_module_by_name("std/vec", 7)) { return 0; }
    var id_vec: u64 = module_id_from_import("std/vec", 7);
    var vec_info: *NameInfo = (*NameInfo)id_vec;
    prelude_import_all_from_module(vec_info->ptr, vec_info->len);

    if (!load_module_by_name("std/hashmap", 11)) { return 0; }
    var id_hm: u64 = module_id_from_import("std/hashmap", 11);
    var hm_info: *NameInfo = (*NameInfo)id_hm;
    prelude_import_all_from_module(hm_info->ptr, hm_info->len);
    return 1;
}

func load_module(file_path: u64, file_path_len: u64) -> u64 {
    if (((*HashMap<u64, u64>)g_loaded_modules)->has(file_path, file_path_len)) {
        return 1;
    }

    if (!load_module_pass1(file_path, file_path_len)) {
        return 0;
    }

    ((*HashMap<u64, u64>)g_loaded_modules)->put(file_path, file_path_len, 1);

    var path_copy2: u64 = heap_alloc((file_path_len + 1) * sizeof(u8));
    for (var pc: u64 = 0; pc < file_path_len; pc++) {
        *(*u8)(path_copy2 + pc) = *(*u8)(file_path + pc);
    }
    *(*u8)(path_copy2 + file_path_len) = 0;
    var path_info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    path_info->ptr = path_copy2;
    path_info->len = file_path_len;
    ((*Vec<*NameInfo>)g_loaded_modules_vec)->push(path_info);

    // Ensure imports are fully parsed before this module (type resolution).
    if (g_module_imports_pass1 != 0) {
        var imports_p1: *Vec<*AstImport> = ((*HashMap<u64, *Vec<*AstImport> >)g_module_imports_pass1)->get(file_path, file_path_len);
        if (imports_p1 != 0) {
            var num_imports_p1: u64 = ((*Vec<*AstImport>)imports_p1)->len();
            for (var pi: u64 = 0; pi < num_imports_p1; pi++) {
                var imp_p1: *AstImport = ((*Vec<*AstImport>)imports_p1)->get(pi);
                var imp_path_p1: u64 = imp_p1->path_ptr;
                var imp_len_p1: u64 = imp_p1->path_len;
                var resolved_p1: u64 = resolve_module_path(imp_path_p1, imp_len_p1);
                var resolved_len_p1: u64 = str_len(resolved_p1);
                if (!load_module(resolved_p1, resolved_len_p1)) {
                    return 0;
                }
            }
        }
    }

    var content: u64 = read_entire_file(file_path);
    if (content == 0) {
        emit_stderr("[ERROR] Cannot open module: ", 29);
        for (var i: u64 = 0; i< file_path_len;i++){
            emit_char(*(*u8)(file_path + i));
        }
        emit_nl();
        return 0;
    }

    var src: u64 = g_file_ptr;
    var slen: u64  = g_file_len;

    var tokens: u64 = lex_all(src, slen);
    
    // Module identity for mangling
    var module_id: u64 = module_id_from_path(file_path, file_path_len);
    var module_info: *NameInfo = (*NameInfo)module_id;
    var module_ptr: u64 = module_info->ptr;
    var module_len: u64 = module_info->len;
    var module_prefix: u64 = module_prefix_from_id(module_ptr, module_len);
    var prefix_info: *NameInfo = (*NameInfo)module_prefix;
    var prefix_ptr: u64 = prefix_info->ptr;
    var prefix_len: u64 = prefix_info->len;
    
    // Pass 1: collect function signatures only
    var p1: u64 = parse_new(tokens);
    var prog_sig: u64 = parse_program_pass1(p1);

    var prog_sig_info: *AstProgram = (*AstProgram)prog_sig;
    var sig_funcs: *Vec<*AstFunc> = (*Vec<*AstFunc>)prog_sig_info->funcs_vec;
    var num_sig_funcs: u64 = ((*Vec<*AstFunc>)sig_funcs)->len();
    for (var sfi: u64 = 0; sfi < num_sig_funcs; sfi++) {
        ((*Vec<*AstFunc>)g_all_func_sigs)->push(((*Vec<*AstFunc>)sig_funcs)->get(sfi));
    }

    // Pass 2: full parse with bodies
    var p: u64 = parse_new(tokens);
    var prog: u64 = parse_program(p);

    // Register exports + mangle symbols
    register_module_exports(prog, module_ptr, module_len, prefix_ptr, prefix_len);
    compiler_generic_update_mangled_names(prog);

    // Store imports for deferred alias resolution
    var prog_info2: *AstProgram = (*AstProgram)prog;
    var imports: *Vec<*AstImport> = (*Vec<*AstImport>)prog_info2->imports_vec;
    var imports_copy: *Vec<*AstImport> = Vec.new<*AstImport>(4);
    var num_imports: u64 = ((*Vec<*AstImport>)imports)->len();
    for (var ii: u64 = 0; ii < num_imports; ii++) {
        var imp: *AstImport = ((*Vec<*AstImport>)imports)->get(ii);
        var imp_path: u64 = imp->path_ptr;
        var imp_len: u64 = imp->path_len;
        var imp_sym_ptr: u64 = imp->symbol_ptr;
        var imp_sym_len: u64 = imp->symbol_len;
        var imp_alias_ptr: u64 = imp->alias_ptr;
        var imp_alias_len: u64 = imp->alias_len;

        var path_copy: u64 = heap_alloc((imp_len + 1) * sizeof(u8));
        for (var ci: u64 = 0; ci < imp_len; ci++) {
            *(*u8)(path_copy + ci) = *(*u8)(imp_path + ci);
        }
        *(*u8)(path_copy + imp_len) = 0;

        var sym_copy: u64 = 0;
        var alias_copy: u64 = 0;
        if (imp_sym_ptr != 0) {
            sym_copy = heap_alloc((imp_sym_len + 1) * sizeof(u8));
            for (var si: u64 = 0; si < imp_sym_len; si++) {
                *(*u8)(sym_copy + si) = *(*u8)(imp_sym_ptr + si);
            }
            *(*u8)(sym_copy + imp_sym_len) = 0;

            alias_copy = heap_alloc((imp_alias_len + 1) * sizeof(u8));
            for (var ai: u64 = 0; ai < imp_alias_len; ai++) {
                *(*u8)(alias_copy + ai) = *(*u8)(imp_alias_ptr + ai);
            }
            *(*u8)(alias_copy + imp_alias_len) = 0;
        }

        var imp_copy: *AstImport = (*AstImport)ast_import(path_copy, imp_len, sym_copy, imp_sym_len, alias_copy, imp_alias_len);
        ((*Vec<*AstImport>)imports_copy)->push(imp_copy);
    }
    ((*HashMap<u64, *Vec<*AstImport> >)g_module_imports_pass2)->put(file_path, file_path_len, imports_copy);

    // Add consts
    var consts: *Vec<*AstConstDecl> = (*Vec<*AstConstDecl>)prog_info2->consts_vec;
    var num_consts: u64  = ((*Vec<*AstConstDecl>)consts)->len();
    for (var ci: u64 = 0; ci < num_consts; ci++){
        ((*Vec<*AstConstDecl>)g_all_consts)->push(((*Vec<*AstConstDecl>)consts)->get(ci));
    }

    // Add funcs
    var funcs: *Vec<*AstFunc> = (*Vec<*AstFunc>)prog_info2->funcs_vec;
    var num_funcs: u64 = ((*Vec<*AstFunc>)funcs)->len();
    for (var fi: u64 = 0; fi < num_funcs; fi++){
        var fn: *AstFunc = ((*Vec<*AstFunc>)funcs)->get(fi);
        ((*Vec<*AstFunc>)g_all_funcs)->push(fn);
    }

    // Add globals
    var globals: *Vec<*GlobalInfo>  = (*Vec<*GlobalInfo>)prog_info2->globals_vec;
    if (globals != 0) {
        var num_globals: u64  = ((*Vec<*GlobalInfo>)globals)->len();
        for (var gi: u64 = 0; gi < num_globals; gi++){
            ((*Vec<*GlobalInfo>)g_all_globals)->push(((*Vec<*GlobalInfo>)globals)->get(gi));
        }
    }

    // Register structs
    var structs: *Vec<*AstStructDef> = (*Vec<*AstStructDef>)prog_info2->structs_vec;
    if (structs != 0) {
        var num_structs: u64 = ((*Vec<*AstStructDef>)structs)->len();
        for (var si: u64 = 0; si < num_structs; si++){
            var struct_info: *AstStructDef = ((*Vec<*AstStructDef>)structs)->get(si);
            var struct_name_ptr: u64 = struct_info->name_ptr;
            var struct_name_len: u64 = struct_info->name_len;
            ((*HashMap<u64, *AstStructDef>)g_all_structs)->put(struct_name_ptr, struct_name_len, struct_info);
        }
    }

    return 1;
}

// ============================================
// Helper functions for parser
// ============================================

// Check if a name is a registered struct type
func is_struct_type(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_structs == 0) { return 0; }
    var struct_def: *AstStructDef = ((*HashMap<u64, *AstStructDef>)g_all_structs)->get(name_ptr, name_len);
    if (struct_def == 0) { return 0; }
    return 1;
}

// Get struct definition by name
func get_struct_def(name_ptr: u64, name_len: u64) -> u64 {
    if (g_all_structs == 0) { return 0; }
    return (u64)((*HashMap<u64, *AstStructDef>)g_all_structs)->get(name_ptr, name_len);
}

// Register a struct type during parsing
func register_struct_type(struct_def: u64) -> u64 {
    if (g_all_structs == 0) {
        g_all_structs = HashMap.new<u64, *AstStructDef>(64);
    }
    if (g_all_structs_vec == 0) {
        g_all_structs_vec = Vec.new<*AstStructDef>(16);
    }
    var struct_info: *AstStructDef = (*AstStructDef)struct_def;
    var struct_name_ptr: u64 = struct_info->name_ptr;
    var struct_name_len: u64 = struct_info->name_len;
    ((*HashMap<u64, *AstStructDef>)g_all_structs)->put(struct_name_ptr, struct_name_len, struct_info);
    ((*Vec<*AstStructDef>)g_all_structs_vec)->push(struct_info);
}

// ============================================
// Generic Template Registry
// ============================================

func compiler_get_or_create_generic_template(name_ptr: u64, name_len: u64, params_vec: u64) -> u64 {
    if (g_generic_funcs == 0) { g_generic_funcs = HashMap.new<u64, *GenericFuncTemplate>(64); }
    var existing: *GenericFuncTemplate = ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->get(name_ptr, name_len);
    if (existing != 0) { return (u64)existing; }

    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)heap_alloc(sizeof(GenericFuncTemplate));
    tpl->name_ptr = name_ptr;
    tpl->name_len = name_len;
    tpl->mangled_ptr = 0;
    tpl->mangled_len = 0;
    tpl->params_vec = params_vec;
    tpl->func_sig = 0;
    tpl->func_def = 0;
    ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->put(name_ptr, name_len, tpl);
    return (u64)tpl;
}

func compiler_get_or_create_generic_struct_template(name_ptr: u64, name_len: u64, params_vec: u64) -> u64 {
    if (g_generic_structs == 0) { g_generic_structs = HashMap.new<u64, *GenericStructTemplate>(64); }
    var existing: *GenericStructTemplate = ((*HashMap<u64, *GenericStructTemplate>)g_generic_structs)->get(name_ptr, name_len);
    if (existing != 0) { return (u64)existing; }

    var tpl: *GenericStructTemplate = (*GenericStructTemplate)heap_alloc(sizeof(GenericStructTemplate));
    tpl->name_ptr = name_ptr;
    tpl->name_len = name_len;
    tpl->params_vec = params_vec;
    tpl->struct_def = 0;
    ((*HashMap<u64, *GenericStructTemplate>)g_generic_structs)->put(name_ptr, name_len, tpl);
    return (u64)tpl;
}

func compiler_register_generic_struct_def(name_ptr: u64, name_len: u64, params_vec: u64, struct_def: u64) -> u64 {
    if (params_vec == 0 || ((*Vec<*GenericParam>)params_vec)->len() == 0) { return 0; }
    var tpl_ptr: u64 = compiler_get_or_create_generic_struct_template(name_ptr, name_len, params_vec);
    var tpl: *GenericStructTemplate = (*GenericStructTemplate)tpl_ptr;
    tpl->params_vec = params_vec;
    tpl->struct_def = struct_def;
    if (g_generic_struct_ptrs == 0) { g_generic_struct_ptrs = Vec.new<*GenericStructTemplate>(16); }
    ((*Vec<*GenericStructTemplate>)g_generic_struct_ptrs)->push(tpl);
    if (GENERIC_DEBUG != 0) {
        emit_stderr("[DEBUG] generic struct template: ", 35);
        emit_stderr(name_ptr, name_len);
        emit_stderr("\n", 1);
    }
    return 0;
}

func compiler_register_generic_struct_sig(name_ptr: u64, name_len: u64, params_vec: u64) -> u64 {
    if (params_vec == 0 || ((*Vec<*GenericParam>)params_vec)->len() == 0) { return 0; }
    var tpl_ptr: u64 = compiler_get_or_create_generic_struct_template(name_ptr, name_len, params_vec);
    var tpl: *GenericStructTemplate = (*GenericStructTemplate)tpl_ptr;
    tpl->params_vec = params_vec;
    return 0;
}

func compiler_find_generic_struct_template_by_name(name_ptr: u64, name_len: u64) -> u64 {
    if (g_generic_structs == 0) { return 0; }
    return (u64)((*HashMap<u64, *GenericStructTemplate>)g_generic_structs)->get(name_ptr, name_len);
}

func compiler_is_generic_struct_def(struct_def: u64) -> u64 {
    if (g_generic_struct_ptrs == 0) { return 0; }
    var n: u64 = ((*Vec<*GenericStructTemplate>)g_generic_struct_ptrs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericStructTemplate = ((*Vec<*GenericStructTemplate>)g_generic_struct_ptrs)->get(i);
        if (tpl->struct_def == struct_def) { return 1; }
    }
    return 0;
}

func compiler_register_generic_struct_bindings(inst_ptr: u64, inst_len: u64, bindings: u64) -> u64 {
    if (inst_ptr == 0 || inst_len == 0 || bindings == 0) { return 0; }
    if (g_generic_struct_bindings == 0) { g_generic_struct_bindings = HashMap.new<u64, *Vec<*GenericBinding> >(64); }
    if (((*HashMap<u64, *Vec<*GenericBinding> >)g_generic_struct_bindings)->has(inst_ptr, inst_len) != 0) { return 0; }
    ((*HashMap<u64, *Vec<*GenericBinding> >)g_generic_struct_bindings)->put(inst_ptr, inst_len, (*Vec<*GenericBinding>)bindings);
    return 0;
}

func compiler_get_generic_struct_bindings(inst_ptr: u64, inst_len: u64) -> *Vec<*GenericBinding> {
    if (g_generic_struct_bindings == 0) { return 0; }
    return ((*HashMap<u64, *Vec<*GenericBinding> >)g_generic_struct_bindings)->get(inst_ptr, inst_len);
}

func compiler_find_generic_suffix_index(name_ptr: u64, name_len: u64) -> u64 {
    if (name_ptr == 0 || name_len < 5) { return name_len; }
    for (var i: u64 = 0; i + 5 <= name_len; i++) {
        if (str_eq(name_ptr + i, 5, "__G__", 5)) { return i; }
    }
    return name_len;
}

func compiler_register_generic_func_sig(name_ptr: u64, name_len: u64, params_vec: u64, func_sig: u64) -> u64 {
    if (params_vec == 0 || ((*Vec<*GenericParam>)params_vec)->len() == 0) { return 0; }
    var reg_name_ptr: u64 = name_ptr;
    var reg_name_len: u64 = name_len;
    if (g_impl_prefix_ptr != 0 && g_impl_prefix_len != 0) {
        var combined: *Vec<u64> = Vec.new<u64>(g_impl_prefix_len + name_len + 1);
        for (var i: u64 = 0; i < g_impl_prefix_len; i++) {
            ((*Vec<u64>)combined)->push(*(*u8)(g_impl_prefix_ptr + i));
        }
        ((*Vec<u64>)combined)->push(95);
        for (var j: u64 = 0; j < name_len; j++) {
            ((*Vec<u64>)combined)->push(*(*u8)(name_ptr + j));
        }
        var cname_len: u64 = ((*Vec<u64>)combined)->len();
        var cname_ptr: u64 = heap_alloc((cname_len + 1) * sizeof(u8));
        for (var k: u64 = 0; k < cname_len; k++) {
            *(*u8)(cname_ptr + k) = ((*Vec<u64>)combined)->get(k);
        }
        *(*u8)(cname_ptr + cname_len) = 0;
        reg_name_ptr = cname_ptr;
        reg_name_len = cname_len;
    }
    var tpl_ptr: u64 = compiler_get_or_create_generic_template(reg_name_ptr, reg_name_len, params_vec);
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    tpl->params_vec = params_vec;
    tpl->func_sig = func_sig;
    return 0;
}

func compiler_register_generic_func_def(name_ptr: u64, name_len: u64, params_vec: u64, func_def: u64) -> u64 {
    if (params_vec == 0 || ((*Vec<*GenericParam>)params_vec)->len() == 0) { return 0; }
    var reg_name_ptr: u64 = name_ptr;
    var reg_name_len: u64 = name_len;
    if (g_impl_prefix_ptr != 0 && g_impl_prefix_len != 0) {
        var combined: *Vec<u64> = Vec.new<u64>(g_impl_prefix_len + name_len + 1);
        for (var i: u64 = 0; i < g_impl_prefix_len; i++) {
            ((*Vec<u64>)combined)->push(*(*u8)(g_impl_prefix_ptr + i));
        }
        ((*Vec<u64>)combined)->push(95);
        for (var j: u64 = 0; j < name_len; j++) {
            ((*Vec<u64>)combined)->push(*(*u8)(name_ptr + j));
        }
        var cname_len: u64 = ((*Vec<u64>)combined)->len();
        var cname_ptr: u64 = heap_alloc((cname_len + 1) * sizeof(u8));
        for (var k: u64 = 0; k < cname_len; k++) {
            *(*u8)(cname_ptr + k) = ((*Vec<u64>)combined)->get(k);
        }
        *(*u8)(cname_ptr + cname_len) = 0;
        reg_name_ptr = cname_ptr;
        reg_name_len = cname_len;
    }
    var tpl_ptr: u64 = compiler_get_or_create_generic_template(reg_name_ptr, reg_name_len, params_vec);
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    tpl->params_vec = params_vec;
    tpl->func_def = func_def;
    if (g_generic_func_ptrs == 0) { g_generic_func_ptrs = Vec.new<*GenericFuncTemplate>(16); }
    ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->push(tpl);
    if (GENERIC_DEBUG != 0) {
        emit_stderr("[DEBUG] generic template: ", 28);
        emit_stderr(name_ptr, name_len);
        emit_stderr("\n", 1);
    }
    return 0;
}

func compiler_set_impl_prefix(prefix_ptr: u64, prefix_len: u64) -> u64 {
    g_impl_prefix_ptr = prefix_ptr;
    g_impl_prefix_len = prefix_len;
    return 0;
}

func compiler_clear_impl_prefix() -> u64 {
    g_impl_prefix_ptr = 0;
    g_impl_prefix_len = 0;
    return 0;
}

func compiler_is_impl_context() -> u64 {
    if (g_impl_prefix_ptr != 0 && g_impl_prefix_len != 0) { return 1; }
    return 0;
}

func compiler_register_impl_method(name_ptr: u64, name_len: u64) -> u64 {
    if (name_ptr == 0 || name_len == 0) { return 0; }
    if (g_impl_methods == 0) { g_impl_methods = HashMap.new<u64, u64>(64); }
    if (((*HashMap<u64, u64>)g_impl_methods)->has(name_ptr, name_len) != 0) { return 0; }
    ((*HashMap<u64, u64>)g_impl_methods)->put(name_ptr, name_len, 1);
    return 0;
}

func compiler_is_impl_method(name_ptr: u64, name_len: u64) -> u64 {
    if (g_impl_methods == 0) { return 0; }
    return ((*HashMap<u64, u64>)g_impl_methods)->has(name_ptr, name_len);
}

func compiler_is_generic_func_ptr(fn_ptr: u64) -> u64 {
    if (g_generic_func_ptrs == 0) { return 0; }
    var n: u64 = ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericFuncTemplate = ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->get(i);
        if (tpl->func_def == fn_ptr) { return 1; }
    }
    return 0;
}

func compiler_find_generic_template_by_name(name_ptr: u64, name_len: u64) -> u64 {
    if (g_generic_funcs == 0) { return 0; }
    return (u64)((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->get(name_ptr, name_len);
}

func compiler_find_generic_template_by_mangled(name_ptr: u64, name_len: u64) -> u64 {
    if (g_generic_funcs_mangled == 0) { return 0; }
    return (u64)((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs_mangled)->get(name_ptr, name_len);
}

func compiler_find_generic_template_by_func_ptr(fn_ptr: u64) -> u64 {
    if (g_generic_func_ptrs == 0) { return 0; }
    var n: u64 = ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var tpl: *GenericFuncTemplate = ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->get(i);
        if (tpl->func_def == fn_ptr) { return (u64)tpl; }
    }
    return 0;
}

func compiler_alias_generic_template_by_func_ptr(fn_ptr: u64, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var tpl_ptr: u64 = compiler_find_generic_template_by_func_ptr(fn_ptr);
    if (tpl_ptr == 0) { return 0; }
    if (g_generic_funcs == 0) { g_generic_funcs = HashMap.new<u64, *GenericFuncTemplate>(64); }
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->put(new_name_ptr, new_name_len, tpl);
    return 0;
}

func compiler_clone_generic_template_for_impl(fn_ptr: u64, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var tpl_ptr: u64 = compiler_find_generic_template_by_func_ptr(fn_ptr);
    if (tpl_ptr == 0) { return 0; }
    if (g_generic_funcs == 0) { g_generic_funcs = HashMap.new<u64, *GenericFuncTemplate>(64); }
    var existing: *GenericFuncTemplate = ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->get(new_name_ptr, new_name_len);
    if (existing != 0) { return 0; }

    var src: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)heap_alloc(sizeof(GenericFuncTemplate));
    tpl->name_ptr = new_name_ptr;
    tpl->name_len = new_name_len;
    tpl->mangled_ptr = 0;
    tpl->mangled_len = 0;
    tpl->params_vec = src->params_vec;
    tpl->func_sig = src->func_sig;
    tpl->func_def = src->func_def;
    ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs)->put(new_name_ptr, new_name_len, tpl);
    if (g_generic_func_ptrs == 0) { g_generic_func_ptrs = Vec.new<*GenericFuncTemplate>(16); }
    ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->push(tpl);
    return 0;
}

func compiler_generic_update_mangled_names(prog: u64) -> u64 {
    if (g_generic_func_ptrs == 0) { return 0; }
    var prog_info: *AstProgram = (*AstProgram)prog;
    var funcs: *Vec<*AstFunc> = (*Vec<*AstFunc>)prog_info->funcs_vec;
    if (funcs == 0) { return 0; }
    var n: u64 = ((*Vec<*AstFunc>)funcs)->len();
    for (var i: u64 = 0; i < n; i++) {
        var fn: *AstFunc = ((*Vec<*AstFunc>)funcs)->get(i);
        var tpl_ptr: u64 = compiler_find_generic_template_by_func_ptr((u64)fn);
        if (tpl_ptr == 0) { continue; }
        var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
        tpl->mangled_ptr = fn->name_ptr;
        tpl->mangled_len = fn->name_len;
        if (g_generic_funcs_mangled == 0) { g_generic_funcs_mangled = HashMap.new<u64, *GenericFuncTemplate>(64); }
        ((*HashMap<u64, *GenericFuncTemplate>)g_generic_funcs_mangled)->put(tpl->mangled_ptr, tpl->mangled_len, tpl);
    }
    return 0;
}

// ============================================
// Generic Monomorphization
// ============================================

// Append a string to a vec<u8>
func generic_vec_append_str(v: *Vec<u64>, s_ptr: u64, s_len: u64) -> u64 {
    for (var i: u64 = 0; i < s_len; i++) {
        ((*Vec<u64>)v)->push(*(*u8)(s_ptr + i));
    }
    return 0;
}

// Append unsigned integer to vec as decimal
func generic_vec_append_u64(v: *Vec<u64>, value: u64) -> u64 {
    if (value == 0) {
        ((*Vec<u64>)v)->push(48);
        return 0;
    }
    var buf: u64 = heap_alloc(32 * sizeof(u8));
    var idx: u64 = 0;
    var x: u64 = value;
    while (x > 0) {
        var digit: u64 = x % 10;
        *(*u8)(buf + idx) = 48 + digit;
        idx = idx + 1;
        x = x / 10;
    }
    for (var i: u64 = 0; i < idx; i++) {
        ((*Vec<u64>)v)->push(*(*u8)(buf + (idx - 1 - i)));
    }
    return 0;
}

func generic_type_equal(a: u64, b: u64) -> u64 {
    var ta: *TypeInfo = (*TypeInfo)a;
    var tb: *TypeInfo = (*TypeInfo)b;
    if (ta->type_kind != tb->type_kind) { return 0; }
    if (ta->ptr_depth != tb->ptr_depth) { return 0; }
    if (ta->type_kind == TYPE_STRUCT) {
        if (!str_eq(ta->struct_name_ptr, ta->struct_name_len, tb->struct_name_ptr, tb->struct_name_len)) { return 0; }
    }
    if (ta->type_kind == TYPE_ARRAY || ta->type_kind == TYPE_SLICE) {
        if (ta->elem_type_kind != tb->elem_type_kind) { return 0; }
        if (ta->elem_ptr_depth != tb->elem_ptr_depth) { return 0; }
        if (ta->type_kind == TYPE_ARRAY && ta->array_len != tb->array_len) { return 0; }
        if (!str_eq(ta->struct_name_ptr, ta->struct_name_len, tb->struct_name_ptr, tb->struct_name_len)) { return 0; }
    }
    return 1;
}

func generic_find_binding(bindings: u64, kind: u64, name_ptr: u64, name_len: u64) -> u64 {
    if (bindings == 0) { return 0; }
    var bindings_vec: *Vec<*GenericBinding> = (*Vec<*GenericBinding>)bindings;
    var n: u64 = ((*Vec<*GenericBinding>)bindings_vec)->len();
    for (var i: u64 = 0; i < n; i++) {
        var b: *GenericBinding = ((*Vec<*GenericBinding>)bindings_vec)->get(i);
        if (b->kind == kind && str_eq(b->name_ptr, b->name_len, name_ptr, name_len)) {
            return (u64)b;
        }
    }
    return 0;
}

func generic_bind_type(bindings: u64, name_ptr: u64, name_len: u64, type_ptr: u64) -> u64 {
    var existing: u64 = generic_find_binding(bindings, GENERIC_KIND_TYPE, name_ptr, name_len);
    if (existing != 0) {
        var b: *GenericBinding = (*GenericBinding)existing;
        if (!generic_type_equal(b->type_ptr, type_ptr)) {
            return 1;
        }
        return 0;
    }
    var nb: *GenericBinding = (*GenericBinding)heap_alloc(sizeof(GenericBinding));
    nb->kind = GENERIC_KIND_TYPE;
    nb->name_ptr = name_ptr;
    nb->name_len = name_len;
    nb->type_ptr = type_ptr;
    nb->value = 0;
    ((*Vec<*GenericBinding>)bindings)->push(nb);
    return 0;
}

func generic_bind_value(bindings: u64, name_ptr: u64, name_len: u64, value: u64) -> u64 {
    var existing: u64 = generic_find_binding(bindings, GENERIC_KIND_VALUE, name_ptr, name_len);
    if (existing != 0) {
        var b: *GenericBinding = (*GenericBinding)existing;
        if (b->value != value) {
            return 1;
        }
        return 0;
    }
    var nb: *GenericBinding = (*GenericBinding)heap_alloc(sizeof(GenericBinding));
    nb->kind = GENERIC_KIND_VALUE;
    nb->name_ptr = name_ptr;
    nb->name_len = name_len;
    nb->type_ptr = 0;
    nb->value = value;
    ((*Vec<*GenericBinding>)bindings)->push(nb);
    return 0;
}

func generic_make_typeinfo_from_fields(type_kind: u64, ptr_depth: u64, is_tagged: u64, struct_name_ptr: u64, struct_name_len: u64, tag_layout_ptr: u64, tag_layout_len: u64, elem_type_kind: u64, elem_ptr_depth: u64, array_len: u64, array_len_is_param: u64, array_len_param_ptr: u64, array_len_param_len: u64) -> u64 {
    var ti: *TypeInfo = (*TypeInfo)heap_alloc(sizeof(TypeInfo));
    ti->type_kind = type_kind;
    ti->ptr_depth = ptr_depth;
    ti->is_tagged = is_tagged;
    ti->struct_name_ptr = struct_name_ptr;
    ti->struct_name_len = struct_name_len;
    ti->tag_layout_ptr = tag_layout_ptr;
    ti->tag_layout_len = tag_layout_len;
    ti->struct_def = 0;
    ti->elem_type_kind = elem_type_kind;
    ti->elem_ptr_depth = elem_ptr_depth;
    ti->array_len = array_len;
    ti->array_len_is_param = array_len_is_param;
    ti->array_len_param_ptr = array_len_param_ptr;
    ti->array_len_param_len = array_len_param_len;
    return (u64)ti;
}

func generic_subst_typeinfo(type_kind: u64, ptr_depth: u64, is_tagged: u64, struct_name_ptr: u64, struct_name_len: u64, tag_layout_ptr: u64, tag_layout_len: u64, elem_type_kind: u64, elem_ptr_depth: u64, array_len: u64, array_len_is_param: u64, array_len_param_ptr: u64, array_len_param_len: u64, bindings: u64) -> u64 {
    if (type_kind == TYPE_GENERIC) {
        if (struct_name_ptr == 0 || struct_name_len == 0) {
            return generic_make_typeinfo_from_fields(TYPE_GENERIC, ptr_depth, is_tagged, struct_name_ptr, struct_name_len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
        }
        var b_ptr: u64 = generic_find_binding(bindings, GENERIC_KIND_TYPE, struct_name_ptr, struct_name_len);
        if (b_ptr == 0) {
            emit_stderr("[ERROR] Missing generic type binding: ", 43);
            emit_stderr(struct_name_ptr, struct_name_len);
            emit_stderr(" len=", 5);
            print_u64(struct_name_len);
            emit_stderr(" ptr=", 5);
            print_u64(struct_name_ptr);
            emit_stderr("\n", 1);
            panic("Parse error");
        }
        var b: *GenericBinding = (*GenericBinding)b_ptr;
        var bt: *TypeInfo = (*TypeInfo)b->type_ptr;
        var merged_ptr_depth: u64 = bt->ptr_depth + ptr_depth;
        return generic_make_typeinfo_from_fields(bt->type_kind, merged_ptr_depth, bt->is_tagged, bt->struct_name_ptr, bt->struct_name_len, bt->tag_layout_ptr, bt->tag_layout_len, bt->elem_type_kind, bt->elem_ptr_depth, bt->array_len, bt->array_len_is_param, bt->array_len_param_ptr, bt->array_len_param_len);
    }

    if (bindings != 0 && type_kind == TYPE_STRUCT && struct_name_ptr != 0 && struct_name_len != 0) {
        var b_ptr2: u64 = generic_find_binding(bindings, GENERIC_KIND_TYPE, struct_name_ptr, struct_name_len);
        if (b_ptr2 != 0) {
            var b2: *GenericBinding = (*GenericBinding)b_ptr2;
            var bt2: *TypeInfo = (*TypeInfo)b2->type_ptr;
            var merged_ptr_depth2: u64 = bt2->ptr_depth + ptr_depth;
            return generic_make_typeinfo_from_fields(bt2->type_kind, merged_ptr_depth2, bt2->is_tagged, bt2->struct_name_ptr, bt2->struct_name_len, bt2->tag_layout_ptr, bt2->tag_layout_len, bt2->elem_type_kind, bt2->elem_ptr_depth, bt2->array_len, bt2->array_len_is_param, bt2->array_len_param_ptr, bt2->array_len_param_len);
        }
    }

    if (type_kind == TYPE_STRUCT && bindings != 0) {
        var base_len: u64 = compiler_find_generic_suffix_index(struct_name_ptr, struct_name_len);
        if (base_len < struct_name_len) {
            var tpl_ptr: u64 = compiler_find_generic_struct_template_by_name(struct_name_ptr, base_len);
            if (tpl_ptr != 0) {
                var name_info: *NameInfo = (*NameInfo)generic_struct_get_or_create_instance(tpl_ptr, bindings);
                return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, name_info->ptr, name_info->len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
            }
        }
    }

    if (type_kind == TYPE_ARRAY || type_kind == TYPE_SLICE) {
        var new_array_len: u64 = array_len;
        var new_array_len_is_param: u64 = array_len_is_param;
        var new_array_param_ptr: u64 = array_len_param_ptr;
        var new_array_param_len: u64 = array_len_param_len;
        if (array_len_is_param != 0) {
            var vb_ptr: u64 = generic_find_binding(bindings, GENERIC_KIND_VALUE, array_len_param_ptr, array_len_param_len);
            if (vb_ptr == 0) {
                emit_stderr("[ERROR] Missing generic value binding\n", 40);
                panic("Parse error");
            }
            var vb: *GenericBinding = (*GenericBinding)vb_ptr;
            new_array_len = vb->value;
            new_array_len_is_param = 0;
            new_array_param_ptr = 0;
            new_array_param_len = 0;
        }

        var elem_sub: u64 = generic_subst_typeinfo(elem_type_kind, elem_ptr_depth, 0, struct_name_ptr, struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0, bindings);
        var et: *TypeInfo = (*TypeInfo)elem_sub;
        return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, et->struct_name_ptr, et->struct_name_len, tag_layout_ptr, tag_layout_len, et->type_kind, et->ptr_depth, new_array_len, new_array_len_is_param, new_array_param_ptr, new_array_param_len);
    }

    return generic_make_typeinfo_from_fields(type_kind, ptr_depth, is_tagged, struct_name_ptr, struct_name_len, tag_layout_ptr, tag_layout_len, elem_type_kind, elem_ptr_depth, array_len, array_len_is_param, array_len_param_ptr, array_len_param_len);
}

func generic_append_type_repr(v: *Vec<u64>, ti_ptr: u64) -> u64 {
    var ti: *TypeInfo = (*TypeInfo)ti_ptr;
    if (ti->ptr_depth > 0) {
        ((*Vec<u64>)v)->push(112); // 'p'
        generic_vec_append_u64(v, ti->ptr_depth);
        ((*Vec<u64>)v)->push(95); // '_'
        var base: u64 = generic_make_typeinfo_from_fields(ti->type_kind, 0, ti->is_tagged, ti->struct_name_ptr, ti->struct_name_len, ti->tag_layout_ptr, ti->tag_layout_len, ti->elem_type_kind, ti->elem_ptr_depth, ti->array_len, ti->array_len_is_param, ti->array_len_param_ptr, ti->array_len_param_len);
        return generic_append_type_repr(v, base);
    }

    switch (ti->type_kind) {
        case TYPE_U8: generic_vec_append_str(v, "u8", 2); break;
        case TYPE_U16: generic_vec_append_str(v, "u16", 3); break;
        case TYPE_U32: generic_vec_append_str(v, "u32", 3); break;
        case TYPE_U64: generic_vec_append_str(v, "u64", 3); break;
        case TYPE_I64: generic_vec_append_str(v, "i64", 3); break;
        case TYPE_STRUCT:
            ((*Vec<u64>)v)->push(83); // 'S'
            for (var i: u64 = 0; i < ti->struct_name_len; i++) {
                var c: u64 = *(*u8)(ti->struct_name_ptr + i);
                if (c == 47 || c == 46) { c = 95; }
                ((*Vec<u64>)v)->push(c);
            }
            break;
        case TYPE_ARRAY:
            ((*Vec<u64>)v)->push(97); // 'a'
            generic_vec_append_u64(v, ti->array_len);
            ((*Vec<u64>)v)->push(95);
            var elem: u64 = generic_make_typeinfo_from_fields(ti->elem_type_kind, ti->elem_ptr_depth, 0, ti->struct_name_ptr, ti->struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            generic_append_type_repr(v, elem);
            break;
        case TYPE_SLICE:
            ((*Vec<u64>)v)->push(115); // 's'
            ((*Vec<u64>)v)->push(95);
            var elem2: u64 = generic_make_typeinfo_from_fields(ti->elem_type_kind, ti->elem_ptr_depth, 0, ti->struct_name_ptr, ti->struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            generic_append_type_repr(v, elem2);
            break;
        default:
            generic_vec_append_str(v, "t", 1);
            break;
    }
    return 0;
}

func generic_mangle_name(base_ptr: u64, base_len: u64, params_vec: u64, bindings: u64) -> u64 {
    var v: *Vec<u64> = Vec.new<u64>(base_len + 32);
    generic_vec_append_str(v, base_ptr, base_len);
    generic_vec_append_str(v, "__G__", 5);
    var n: u64 = ((*Vec<u64>)params_vec)->len();
    for (var i: u64 = 0; i < n; i++) {
        var gp: *GenericParam = (*GenericParam)((*Vec<u64>)params_vec)->get(i);
        if (gp->kind == GENERIC_KIND_TYPE) {
            ((*Vec<u64>)v)->push(84); // 'T'
            ((*Vec<u64>)v)->push(95);
            var b_ptr: u64 = generic_find_binding(bindings, GENERIC_KIND_TYPE, gp->name_ptr, gp->name_len);
            if (b_ptr == 0) {
                emit_stderr("[ERROR] Missing generic type binding\n", 39);
                panic("Parse error");
            }
            var b: *GenericBinding = (*GenericBinding)b_ptr;
            generic_append_type_repr(v, b->type_ptr);
        } else {
            ((*Vec<u64>)v)->push(86); // 'V'
            ((*Vec<u64>)v)->push(95);
            var b2_ptr: u64 = generic_find_binding(bindings, GENERIC_KIND_VALUE, gp->name_ptr, gp->name_len);
            if (b2_ptr == 0) {
                emit_stderr("[ERROR] Missing generic value binding\n", 40);
                panic("Parse error");
            }
            var b2: *GenericBinding = (*GenericBinding)b2_ptr;
            generic_vec_append_u64(v, b2->value);
        }

        if (i + 1 < n) { ((*Vec<u64>)v)->push(95); }
    }

    var out_len: u64 = ((*Vec<u64>)v)->len();
    var out_ptr: u64 = heap_alloc((out_len + 1) * sizeof(u8));
    for (var i2: u64 = 0; i2 < out_len; i2++) {
        *(*u8)(out_ptr + i2) = ((*Vec<u64>)v)->get(i2);
    }
    *(*u8)(out_ptr + out_len) = 0;
    var info: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
    info->ptr = out_ptr;
    info->len = out_len;
    return (u64)info;
}

func generic_make_typeinfo_from_param(param: *Param) -> u64 {
    return generic_make_typeinfo_from_fields(param->type_kind, param->ptr_depth, param->is_tagged, param->struct_name_ptr, param->struct_name_len, param->tag_layout_ptr, param->tag_layout_len, param->elem_type_kind, param->elem_ptr_depth, param->array_len, param->array_len_is_param, param->array_len_param_ptr, param->array_len_param_len);
}

func generic_make_typeinfo_from_vardecl(decl: *AstVarDecl) -> u64 {
    return generic_make_typeinfo_from_fields(decl->type_kind, decl->ptr_depth, decl->is_tagged, decl->struct_name_ptr, decl->struct_name_len, decl->tag_layout_ptr, decl->tag_layout_len, decl->elem_type_kind, decl->elem_ptr_depth, decl->array_len, decl->array_len_is_param, decl->array_len_param_ptr, decl->array_len_param_len);
}

func generic_infer_from_typeinfo(tk: u64, pd: u64, sn_ptr: u64, sn_len: u64, elem_k: u64, elem_pd: u64, array_len: u64, array_len_is_param: u64, array_param_ptr: u64, array_param_len: u64, arg_ti_ptr: u64, bindings: u64) -> u64 {
    var arg_ti: *TypeInfo = (*TypeInfo)arg_ti_ptr;
    if (tk == TYPE_GENERIC) {
        if (arg_ti->ptr_depth < pd) {
            return 1;
        }
        var adj_ptr_depth: u64 = arg_ti->ptr_depth - pd;
        var bound: u64 = generic_make_typeinfo_from_fields(arg_ti->type_kind, adj_ptr_depth, arg_ti->is_tagged, arg_ti->struct_name_ptr, arg_ti->struct_name_len, arg_ti->tag_layout_ptr, arg_ti->tag_layout_len, arg_ti->elem_type_kind, arg_ti->elem_ptr_depth, arg_ti->array_len, arg_ti->array_len_is_param, arg_ti->array_len_param_ptr, arg_ti->array_len_param_len);
        if (generic_bind_type(bindings, sn_ptr, sn_len, bound) != 0) { return 1; }
        return 0;
    }

    if (tk == TYPE_ARRAY || tk == TYPE_SLICE) {
        if (arg_ti->type_kind != tk) {
            return 1;
        }
        if (array_len_is_param != 0) {
            if (generic_bind_value(bindings, array_param_ptr, array_param_len, arg_ti->array_len) != 0) { return 1; }
        } else if (tk == TYPE_ARRAY && array_len != arg_ti->array_len) {
            return 1;
        }

        return generic_infer_from_typeinfo(elem_k, elem_pd, sn_ptr, sn_len, 0, 0, 0, 0, 0, 0, generic_make_typeinfo_from_fields(arg_ti->elem_type_kind, arg_ti->elem_ptr_depth, 0, arg_ti->struct_name_ptr, arg_ti->struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0), bindings);
    }

    if (tk != arg_ti->type_kind || pd != arg_ti->ptr_depth) {
        return 1;
    }
    if (tk == TYPE_STRUCT && !str_eq(sn_ptr, sn_len, arg_ti->struct_name_ptr, arg_ti->struct_name_len)) {
        return 1;
    }
    return 0;
}

func generic_infer_expr_type(expr: u64, symtab: u64) -> u64 {
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_LITERAL:
            return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
        case AST_STRING:
            return generic_make_typeinfo_from_fields(TYPE_U8, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
        case AST_IDENT:
            var id: *AstIdent = (*AstIdent)expr;
            var t_ptr: u64 = 0;
            if (symtab != 0) {
                t_ptr = ((*HashMap<u64, u64>)symtab)->get(id->name_ptr, id->name_len);
            }
            if (t_ptr != 0) { return t_ptr; }
            // fallback: consts assumed u64
            return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
        case AST_ADDR_OF:
            var a: *AstAddrOf = (*AstAddrOf)expr;
            var base_t: *TypeInfo = (*TypeInfo)generic_infer_expr_type(a->operand, symtab);
            return generic_make_typeinfo_from_fields(base_t->type_kind, base_t->ptr_depth + 1, base_t->is_tagged, base_t->struct_name_ptr, base_t->struct_name_len, base_t->tag_layout_ptr, base_t->tag_layout_len, base_t->elem_type_kind, base_t->elem_ptr_depth, base_t->array_len, base_t->array_len_is_param, base_t->array_len_param_ptr, base_t->array_len_param_len);
        case AST_DEREF:
            var d: *AstDeref = (*AstDeref)expr;
            var dt: *TypeInfo = (*TypeInfo)generic_infer_expr_type(d->operand, symtab);
            if (dt->ptr_depth == 0) { return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0); }
            return generic_make_typeinfo_from_fields(dt->type_kind, dt->ptr_depth - 1, dt->is_tagged, dt->struct_name_ptr, dt->struct_name_len, dt->tag_layout_ptr, dt->tag_layout_len, dt->elem_type_kind, dt->elem_ptr_depth, dt->array_len, dt->array_len_is_param, dt->array_len_param_ptr, dt->array_len_param_len);
        case AST_CAST:
            var c: *AstCast = (*AstCast)expr;
            return generic_make_typeinfo_from_fields(c->target_type, c->target_ptr_depth, c->target_is_tagged, c->struct_name_ptr, c->struct_name_len, c->tag_layout_ptr, c->tag_layout_len, 0, 0, 0, 0, 0, 0);
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            var bt: *TypeInfo = (*TypeInfo)generic_infer_expr_type(ix->base, symtab);
            if (bt->type_kind == TYPE_ARRAY || bt->type_kind == TYPE_SLICE) {
                return generic_make_typeinfo_from_fields(bt->elem_type_kind, bt->elem_ptr_depth, 0, bt->struct_name_ptr, bt->struct_name_len, 0, 0, 0, 0, 0, 0, 0, 0);
            }
            if (bt->ptr_depth > 0) {
                return generic_make_typeinfo_from_fields(bt->type_kind, bt->ptr_depth - 1, bt->is_tagged, bt->struct_name_ptr, bt->struct_name_len, bt->tag_layout_ptr, bt->tag_layout_len, bt->elem_type_kind, bt->elem_ptr_depth, bt->array_len, bt->array_len_is_param, bt->array_len_param_ptr, bt->array_len_param_len);
            }
            return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
        case AST_CALL:
            // Try function signatures
            var call: *AstCall = (*AstCall)expr;
            var resolved_ptr: u64 = call->name_ptr;
            var resolved_len: u64 = call->name_len;
            var resolved: u64 = resolve_name(call->name_ptr, call->name_len);
            if (resolved != 0) {
                var info: *NameInfo = (*NameInfo)resolved;
                resolved_ptr = info->ptr;
                resolved_len = info->len;
            }
            if (g_all_func_sigs != 0) {
                var n: u64 = ((*Vec<*AstFunc>)g_all_func_sigs)->len();
                for (var i2: u64 = 0; i2 < n; i2++) {
                    var fn: *AstFunc = ((*Vec<*AstFunc>)g_all_func_sigs)->get(i2);
                    if (str_eq(fn->name_ptr, fn->name_len, resolved_ptr, resolved_len)) {
                        return generic_make_typeinfo_from_fields(fn->ret_type, fn->ret_ptr_depth, fn->ret_is_tagged, fn->ret_struct_name_ptr, fn->ret_struct_name_len, fn->ret_tag_layout_ptr, fn->ret_tag_layout_len, 0, 0, 0, 0, 0, 0);
                    }
                }
            }
            return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
        default:
            return generic_make_typeinfo_from_fields(TYPE_U64, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
    }
}

func generic_collect_var_decls(stmt: u64, symtab: u64) -> u64 {
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            var t_ptr: u64 = generic_make_typeinfo_from_vardecl(vd);
            ((*HashMap<u64, u64>)symtab)->put(vd->name_ptr, vd->name_len, t_ptr);
            break;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            var stmts: u64 = blk->stmts_vec;
            var n: u64 = ((*Vec<u64>)stmts)->len();
            for (var i: u64 = 0; i < n; i++) {
                generic_collect_var_decls(((*Vec<u64>)stmts)->get(i), symtab);
            }
            break;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            if (ifs->then_block != 0) { generic_collect_var_decls(ifs->then_block, symtab); }
            if (ifs->else_block != 0) { generic_collect_var_decls(ifs->else_block, symtab); }
            break;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            if (wl->body != 0) { generic_collect_var_decls(wl->body, symtab); }
            break;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            if (fr->init != 0) { generic_collect_var_decls(fr->init, symtab); }
            if (fr->body != 0) { generic_collect_var_decls(fr->body, symtab); }
            break;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            var cases: u64 = sw->cases_vec;
            var cn: u64 = ((*Vec<u64>)cases)->len();
            for (var j: u64 = 0; j < cn; j++) {
                generic_collect_var_decls(((*Vec<u64>)cases)->get(j), symtab);
            }
            break;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            if (cs->body != 0) { generic_collect_var_decls(cs->body, symtab); }
            break;
        default:
            break;
    }
    return 0;
}

func generic_build_symtab_for_func(fn_ptr: u64) -> u64 {
    var symtab: *HashMap<u64, u64> = HashMap.new<u64, u64>(64);
    var fn: *AstFunc = (*AstFunc)fn_ptr;
    var params: u64 = fn->params_vec;
    if (params != 0) {
        var n: u64 = ((*Vec<u64>)params)->len();
        for (var i: u64 = 0; i < n; i++) {
            var p: *Param = (*Param)((*Vec<u64>)params)->get(i);
            var t_ptr: u64 = generic_make_typeinfo_from_param(p);
            ((*HashMap<u64, u64>)symtab)->put(p->name_ptr, p->name_len, t_ptr);
        }
    }
    if (fn->body != 0) {
        generic_collect_var_decls(fn->body, (u64)symtab);
    }
    return (u64)symtab;
}

func generic_clone_args_vec(args: u64, bindings: u64) -> u64 {
    var out: *Vec<u64> = Vec.new<u64>(8);
    var n: u64 = ((*Vec<u64>)args)->len();
    for (var i: u64 = 0; i < n; i++) {
        var expr: u64 = ((*Vec<u64>)args)->get(i);
        ((*Vec<u64>)out)->push(generic_clone_expr(expr, bindings));
    }
    return (u64)out;
}

func generic_clone_expr(node: u64, bindings: u64) -> u64 {
    if (node == 0) { return 0; }
    var kind: u64 = ast_kind(node);
    switch (kind) {
        case AST_LITERAL:
            var lit: *AstLiteral = (*AstLiteral)node;
            return ast_literal(lit->value);
        case AST_IDENT:
            var id: *AstIdent = (*AstIdent)node;
            return ast_ident(id->name_ptr, id->name_len);
        case AST_STRING:
            var st: *AstString = (*AstString)node;
            return ast_string(st->str_ptr, st->str_len);
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)node;
            return ast_binary(bin->op, generic_clone_expr(bin->left, bindings), generic_clone_expr(bin->right, bindings));
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)node;
            return ast_unary(un->op, generic_clone_expr(un->operand, bindings));
        case AST_CALL:
            var call: *AstCall = (*AstCall)node;
            var args: u64 = generic_clone_args_vec(call->args_vec, bindings);
            return ast_call(call->name_ptr, call->name_len, args);
        case AST_GENERIC_CALL:
            var gc: *AstGenericCall = (*AstGenericCall)node;
            var gargs: *Vec<u64> = Vec.new<u64>(4);
            var gn: u64 = ((*Vec<u64>)gc->generic_args_vec)->len();
            for (var i: u64 = 0; i < gn; i++) {
                var ga: *GenericArg = (*GenericArg)((*Vec<u64>)gc->generic_args_vec)->get(i);
                var ng: *GenericArg = (*GenericArg)heap_alloc(sizeof(GenericArg));
                ng->kind = ga->kind;
                ng->value = ga->value;
                if (ga->kind == GENERIC_KIND_TYPE) {
                    var ti: *TypeInfo = (*TypeInfo)ga->type_ptr;
                    if (ti->type_kind == TYPE_GENERIC) {
                        var chk: u64 = generic_find_binding(bindings, GENERIC_KIND_TYPE, ti->struct_name_ptr, ti->struct_name_len);
                        if (chk == 0) {
                            emit_stderr("[DEBUG] Missing binding for generic arg in call: ", 56);
                            emit_stderr(gc->name_ptr, gc->name_len);
                            emit_stderr(" arg=", 5);
                            emit_stderr(ti->struct_name_ptr, ti->struct_name_len);
                            emit_stderr("\n", 1);
                        }
                    }
                    var subst: u64 = generic_subst_typeinfo(ti->type_kind, ti->ptr_depth, ti->is_tagged, ti->struct_name_ptr, ti->struct_name_len, ti->tag_layout_ptr, ti->tag_layout_len, ti->elem_type_kind, ti->elem_ptr_depth, ti->array_len, ti->array_len_is_param, ti->array_len_param_ptr, ti->array_len_param_len, bindings);
                    ng->type_ptr = subst;
                } else {
                    ng->type_ptr = 0;
                }
                ((*Vec<u64>)gargs)->push((u64)ng);
            }
            var args_g: u64 = generic_clone_args_vec(gc->args_vec, bindings);
            return ast_generic_call(gc->name_ptr, gc->name_len, (u64)gargs, args_g);
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)node;
            return ast_addr_of(generic_clone_expr(ao->operand, bindings));
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)node;
            return ast_deref(generic_clone_expr(dr->operand, bindings));
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)node;
            return ast_deref8(generic_clone_expr(dr8->operand, bindings));
        case AST_CAST:
            var cs: *AstCast = (*AstCast)node;
            var sub: u64 = generic_subst_typeinfo(cs->target_type, cs->target_ptr_depth, cs->target_is_tagged, cs->struct_name_ptr, cs->struct_name_len, cs->tag_layout_ptr, cs->tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
            var ti: *TypeInfo = (*TypeInfo)sub;
            return ast_cast_ex(generic_clone_expr(cs->expr, bindings), ti->type_kind, ti->ptr_depth, ti->is_tagged, ti->struct_name_ptr, ti->struct_name_len, ti->tag_layout_ptr, ti->tag_layout_len);
        case AST_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)node;
            return ast_member_access(generic_clone_expr(ma->object, bindings), ma->member_ptr, ma->member_len);
        case AST_METHOD_CALL:
            var mc: *AstMethodCall = (*AstMethodCall)node;
            var args2: u64 = generic_clone_args_vec(mc->args_vec, bindings);
            return ast_method_call(generic_clone_expr(mc->receiver, bindings), mc->method_ptr, mc->method_len, args2);
        case AST_STRUCT_LITERAL:
            var sl: *AstStructLiteral = (*AstStructLiteral)node;
            var vals: u64 = generic_clone_args_vec(sl->values_vec, bindings);
            return ast_struct_literal(sl->struct_def, vals);
        case AST_SIZEOF:
            var sz: *AstSizeof = (*AstSizeof)node;
            var sub2: u64 = generic_subst_typeinfo(sz->type_kind, sz->ptr_depth, 0, sz->struct_name_ptr, sz->struct_name_len, 0, 0, sz->elem_type_kind, sz->elem_ptr_depth, sz->array_len, sz->array_len_is_param, sz->array_len_param_ptr, sz->array_len_param_len, bindings);
            var ti2: *TypeInfo = (*TypeInfo)sub2;
            return ast_sizeof(ti2->type_kind, ti2->ptr_depth, ti2->struct_name_ptr, ti2->struct_name_len, ti2->elem_type_kind, ti2->elem_ptr_depth, ti2->array_len, ti2->array_len_is_param, ti2->array_len_param_ptr, ti2->array_len_param_len);
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)node;
            return ast_sizeof_expr(generic_clone_expr(se->expr, bindings));
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)node;
            return ast_index(generic_clone_expr(ix->base, bindings), generic_clone_expr(ix->index, bindings));
        case AST_SLICE:
            var sl2: *AstSlice = (*AstSlice)node;
            return ast_slice(generic_clone_expr(sl2->ptr_expr, bindings), generic_clone_expr(sl2->len_expr, bindings));
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)node;
            var args3: u64 = generic_clone_args_vec(cp->args_vec, bindings);
            return ast_call_ptr(generic_clone_expr(cp->callee, bindings), args3);
        default:
            return node;
    }
}

func generic_clone_stmt(node: u64, bindings: u64) -> u64 {
    if (node == 0) { return 0; }
    var kind: u64 = ast_kind(node);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)node;
            return ast_return(generic_clone_expr(r->expr, bindings));
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)node;
            var sub: u64 = generic_subst_typeinfo(vd->type_kind, vd->ptr_depth, vd->is_tagged, vd->struct_name_ptr, vd->struct_name_len, vd->tag_layout_ptr, vd->tag_layout_len, vd->elem_type_kind, vd->elem_ptr_depth, vd->array_len, vd->array_len_is_param, vd->array_len_param_ptr, vd->array_len_param_len, bindings);
            var ti: *TypeInfo = (*TypeInfo)sub;
            var decl: *AstVarDecl = (*AstVarDecl)ast_var_decl(vd->name_ptr, vd->name_len, ti->type_kind, ti->ptr_depth, generic_clone_expr(vd->init_expr, bindings));
            decl->is_tagged = ti->is_tagged;
            decl->struct_name_ptr = ti->struct_name_ptr;
            decl->struct_name_len = ti->struct_name_len;
            decl->tag_layout_ptr = ti->tag_layout_ptr;
            decl->tag_layout_len = ti->tag_layout_len;
            decl->elem_type_kind = ti->elem_type_kind;
            decl->elem_ptr_depth = ti->elem_ptr_depth;
            decl->array_len = ti->array_len;
            decl->array_len_is_param = ti->array_len_is_param;
            decl->array_len_param_ptr = ti->array_len_param_ptr;
            decl->array_len_param_len = ti->array_len_param_len;
            return (u64)decl;
        case AST_ASSIGN:
            var assign_node: *AstAssign = (*AstAssign)node;
            return ast_assign(generic_clone_expr(assign_node->target, bindings), generic_clone_expr(assign_node->value, bindings));
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)node;
            return ast_expr_stmt(generic_clone_expr(es->expr, bindings));
        case AST_IF:
            var ifs: *AstIf = (*AstIf)node;
            return ast_if(generic_clone_expr(ifs->cond, bindings), generic_clone_stmt(ifs->then_block, bindings), generic_clone_stmt(ifs->else_block, bindings));
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)node;
            return ast_while(generic_clone_expr(wl->cond, bindings), generic_clone_stmt(wl->body, bindings));
        case AST_FOR:
            var fr: *AstFor = (*AstFor)node;
            return ast_for(generic_clone_stmt(fr->init, bindings), generic_clone_expr(fr->cond, bindings), generic_clone_expr(fr->update, bindings), generic_clone_stmt(fr->body, bindings));
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)node;
            var cases_out: *Vec<u64> = Vec.new<u64>(4);
            var cn: u64 = ((*Vec<u64>)sw->cases_vec)->len();
            for (var i: u64 = 0; i < cn; i++) {
                ((*Vec<u64>)cases_out)->push(generic_clone_stmt(((*Vec<u64>)sw->cases_vec)->get(i), bindings));
            }
            return ast_switch(generic_clone_expr(sw->expr, bindings), (u64)cases_out);
        case AST_CASE:
            var cs: *AstCase = (*AstCase)node;
            return ast_case(cs->value, generic_clone_stmt(cs->body, bindings), cs->is_default);
        case AST_BREAK:
            return ast_break();
        case AST_CONTINUE:
            return ast_continue();
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)node;
            var stmts_out: *Vec<u64> = Vec.new<u64>(8);
            var n: u64 = ((*Vec<u64>)blk->stmts_vec)->len();
            for (var i: u64 = 0; i < n; i++) {
                ((*Vec<u64>)stmts_out)->push(generic_clone_stmt(((*Vec<u64>)blk->stmts_vec)->get(i), bindings));
            }
            return ast_block((u64)stmts_out);
        case AST_ASM:
            var asmnode: *AstAsm = (*AstAsm)node;
            var text_vec: *Vec<u64> = Vec.new<u64>(16);
            var tn: u64 = ((*Vec<u64>)asmnode->text_vec)->len();
            for (var i2: u64 = 0; i2 < tn; i2++) { ((*Vec<u64>)text_vec)->push(((*Vec<u64>)asmnode->text_vec)->get(i2)); }
            return ast_asm((u64)text_vec);
        default:
            return node;
    }
}

func generic_clone_func(template_ptr: u64, bindings: u64, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var fn: *AstFunc = (*AstFunc)template_ptr;
    var params_out: *Vec<u64> = Vec.new<u64>(8);
    var pn: u64 = ((*Vec<u64>)fn->params_vec)->len();
    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = (*Param)((*Vec<u64>)fn->params_vec)->get(i);
        var sub: u64 = generic_subst_typeinfo(p->type_kind, p->ptr_depth, p->is_tagged, p->struct_name_ptr, p->struct_name_len, p->tag_layout_ptr, p->tag_layout_len, p->elem_type_kind, p->elem_ptr_depth, p->array_len, p->array_len_is_param, p->array_len_param_ptr, p->array_len_param_len, bindings);
        var ti: *TypeInfo = (*TypeInfo)sub;
        var np: *Param = (*Param)heap_alloc(sizeof(Param));
        np->name_ptr = p->name_ptr;
        np->name_len = p->name_len;
        np->type_kind = ti->type_kind;
        np->ptr_depth = ti->ptr_depth;
        np->is_tagged = ti->is_tagged;
        np->struct_name_ptr = ti->struct_name_ptr;
        np->struct_name_len = ti->struct_name_len;
        np->tag_layout_ptr = ti->tag_layout_ptr;
        np->tag_layout_len = ti->tag_layout_len;
        np->elem_type_kind = ti->elem_type_kind;
        np->elem_ptr_depth = ti->elem_ptr_depth;
        np->array_len = ti->array_len;
        np->array_len_is_param = ti->array_len_is_param;
        np->array_len_param_ptr = ti->array_len_param_ptr;
        np->array_len_param_len = ti->array_len_param_len;
        ((*Vec<u64>)params_out)->push((u64)np);
    }

    var ret_sub: u64 = generic_subst_typeinfo(fn->ret_type, fn->ret_ptr_depth, fn->ret_is_tagged, fn->ret_struct_name_ptr, fn->ret_struct_name_len, fn->ret_tag_layout_ptr, fn->ret_tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
    var rt: *TypeInfo = (*TypeInfo)ret_sub;
    var body_out: u64 = generic_clone_stmt(fn->body, bindings);
    return ast_func_ex(new_name_ptr, new_name_len, (u64)params_out, rt->type_kind, rt->ptr_depth, rt->is_tagged, rt->struct_name_ptr, rt->struct_name_len, rt->tag_layout_ptr, rt->tag_layout_len, body_out);
}

func generic_clone_func_sig(template_ptr: u64, bindings: u64, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var fn: *AstFunc = (*AstFunc)template_ptr;
    var params_out: *Vec<u64> = Vec.new<u64>(8);
    var pn: u64 = ((*Vec<u64>)fn->params_vec)->len();
    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = (*Param)((*Vec<u64>)fn->params_vec)->get(i);
        var sub: u64 = generic_subst_typeinfo(p->type_kind, p->ptr_depth, p->is_tagged, p->struct_name_ptr, p->struct_name_len, p->tag_layout_ptr, p->tag_layout_len, p->elem_type_kind, p->elem_ptr_depth, p->array_len, p->array_len_is_param, p->array_len_param_ptr, p->array_len_param_len, bindings);
        var ti: *TypeInfo = (*TypeInfo)sub;
        var np: *Param = (*Param)heap_alloc(sizeof(Param));
        np->name_ptr = p->name_ptr;
        np->name_len = p->name_len;
        np->type_kind = ti->type_kind;
        np->ptr_depth = ti->ptr_depth;
        np->is_tagged = ti->is_tagged;
        np->struct_name_ptr = ti->struct_name_ptr;
        np->struct_name_len = ti->struct_name_len;
        np->tag_layout_ptr = ti->tag_layout_ptr;
        np->tag_layout_len = ti->tag_layout_len;
        np->elem_type_kind = ti->elem_type_kind;
        np->elem_ptr_depth = ti->elem_ptr_depth;
        np->array_len = ti->array_len;
        np->array_len_is_param = ti->array_len_is_param;
        np->array_len_param_ptr = ti->array_len_param_ptr;
        np->array_len_param_len = ti->array_len_param_len;
        ((*Vec<u64>)params_out)->push((u64)np);
    }

    var ret_sub: u64 = generic_subst_typeinfo(fn->ret_type, fn->ret_ptr_depth, fn->ret_is_tagged, fn->ret_struct_name_ptr, fn->ret_struct_name_len, fn->ret_tag_layout_ptr, fn->ret_tag_layout_len, 0, 0, 0, 0, 0, 0, bindings);
    var rt: *TypeInfo = (*TypeInfo)ret_sub;
    return ast_func_ex(new_name_ptr, new_name_len, (u64)params_out, rt->type_kind, rt->ptr_depth, rt->is_tagged, rt->struct_name_ptr, rt->struct_name_len, rt->tag_layout_ptr, rt->tag_layout_len, 0);
}

func generic_get_or_create_instance(tpl_ptr: u64, bindings: u64) -> u64 {
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    var base_ptr: u64 = tpl->mangled_ptr;
    var base_len: u64 = tpl->mangled_len;
    if (base_ptr == 0) {
        base_ptr = tpl->name_ptr;
        base_len = tpl->name_len;
    }

    var name_info: *NameInfo = (*NameInfo)generic_mangle_name(base_ptr, base_len, tpl->params_vec, bindings);
    var inst_ptr: u64 = name_info->ptr;
    var inst_len: u64 = name_info->len;

    if (g_generic_instantiations != 0) {
        var exists: u64 = ((*HashMap<u64, u64>)g_generic_instantiations)->get(inst_ptr, inst_len);
        if (exists != 0) { return (u64)name_info; }
    }

    var def_src: u64 = tpl->func_def;
    if (def_src == 0 && tpl->func_sig != 0) { def_src = tpl->func_sig; }
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic function body\n", 43);
        panic("Parse error");
    }

    var fn_new: u64 = generic_clone_func(def_src, bindings, inst_ptr, inst_len);
    var sig_new: u64 = generic_clone_func_sig(def_src, bindings, inst_ptr, inst_len);
    ((*Vec<*AstFunc>)g_all_funcs)->push((*AstFunc)fn_new);
    ((*Vec<*AstFunc>)g_all_func_sigs)->push((*AstFunc)sig_new);

    if (g_generic_instantiations == 0) { g_generic_instantiations = HashMap.new<u64, u64>(64); }
    ((*HashMap<u64, u64>)g_generic_instantiations)->put(inst_ptr, inst_len, 1);

    // Map new function to template module
    if (g_func_module_map != 0 && tpl->mangled_ptr != 0) {
        var mod_info: *NameInfo = ((*HashMap<u64, *NameInfo>)g_func_module_map)->get(tpl->mangled_ptr, tpl->mangled_len);
        if (mod_info != 0) {
            ((*HashMap<u64, *NameInfo>)g_func_module_map)->put(inst_ptr, inst_len, mod_info);
        }
    }

    return (u64)name_info;
}

func generic_build_bindings_for_explicit(tpl_ptr: u64, gargs: u64) -> u64 {
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    var bindings: *Vec<*GenericBinding> = Vec.new<*GenericBinding>(4);
    var pn: u64 = ((*Vec<u64>)tpl->params_vec)->len();
    var an: u64 = ((*Vec<u64>)gargs)->len();
    if (pn != an) {
        emit_stderr("[ERROR] Generic argument count mismatch\n", 46);
        panic("Parse error");
    }
    for (var i: u64 = 0; i < pn; i++) {
        var gp: *GenericParam = (*GenericParam)((*Vec<u64>)tpl->params_vec)->get(i);
        var ga: *GenericArg = (*GenericArg)((*Vec<u64>)gargs)->get(i);
        if (gp->kind == GENERIC_KIND_TYPE) {
            if (ga->kind != GENERIC_KIND_TYPE) {
                emit_stderr("[ERROR] Expected type generic argument\n", 47);
                panic("Parse error");
            }
            if (generic_bind_type((u64)bindings, gp->name_ptr, gp->name_len, ga->type_ptr) != 0) {
                emit_stderr("[ERROR] Conflicting generic type bindings\n", 45);
                panic("Parse error");
            }
        } else {
            if (ga->kind != GENERIC_KIND_VALUE) {
                emit_stderr("[ERROR] Expected value generic argument\n", 48);
                panic("Parse error");
            }
            if (generic_bind_value((u64)bindings, gp->name_ptr, gp->name_len, ga->value) != 0) {
                emit_stderr("[ERROR] Conflicting generic value bindings\n", 46);
                panic("Parse error");
            }
        }
    }
    return (u64)bindings;
}

func generic_build_bindings_for_struct(tpl_ptr: u64, gargs: u64) -> u64 {
    var tpl: *GenericStructTemplate = (*GenericStructTemplate)tpl_ptr;
    var bindings: *Vec<*GenericBinding> = Vec.new<*GenericBinding>(4);
    var pn: u64 = ((*Vec<u64>)tpl->params_vec)->len();
    var an: u64 = ((*Vec<u64>)gargs)->len();
    if (pn != an) {
        emit_stderr("[ERROR] Generic argument count mismatch\n", 46);
        panic("Parse error");
    }
    for (var i: u64 = 0; i < pn; i++) {
        var gp: *GenericParam = (*GenericParam)((*Vec<u64>)tpl->params_vec)->get(i);
        var ga: *GenericArg = (*GenericArg)((*Vec<u64>)gargs)->get(i);
        if (gp->kind == GENERIC_KIND_TYPE) {
            if (ga->kind != GENERIC_KIND_TYPE) {
                emit_stderr("[ERROR] Expected type generic argument\n", 47);
                panic("Parse error");
            }
            if (generic_bind_type((u64)bindings, gp->name_ptr, gp->name_len, ga->type_ptr) != 0) {
                emit_stderr("[ERROR] Conflicting generic type bindings\n", 45);
                panic("Parse error");
            }
        } else {
            if (ga->kind != GENERIC_KIND_VALUE) {
                emit_stderr("[ERROR] Expected value generic argument\n", 48);
                panic("Parse error");
            }
            if (generic_bind_value((u64)bindings, gp->name_ptr, gp->name_len, ga->value) != 0) {
                emit_stderr("[ERROR] Conflicting generic value bindings\n", 46);
                panic("Parse error");
            }
        }
    }
    return (u64)bindings;
}

func generic_clone_struct(template_def: u64, bindings: u64, new_name_ptr: u64, new_name_len: u64) -> u64 {
    var sd: *AstStructDef = (*AstStructDef)template_def;
    var fields_in: u64 = sd->fields_vec;
    var fields_out: *Vec<u64> = Vec.new<u64>(((*Vec<u64>)fields_in)->len());
    var n: u64 = ((*Vec<u64>)fields_in)->len();
    for (var i: u64 = 0; i < n; i++) {
        var f: *FieldDesc = (*FieldDesc)((*Vec<u64>)fields_in)->get(i);
        var sub: u64 = generic_subst_typeinfo(f->type_kind, f->ptr_depth, f->is_tagged, f->struct_name_ptr, f->struct_name_len, f->tag_layout_ptr, f->tag_layout_len, f->elem_type_kind, f->elem_ptr_depth, f->array_len, 0, 0, 0, bindings);
        var ti: *TypeInfo = (*TypeInfo)sub;
        var nf: *FieldDesc = (*FieldDesc)heap_alloc(sizeof(FieldDesc));
        nf->name_ptr = f->name_ptr;
        nf->name_len = f->name_len;
        nf->type_kind = ti->type_kind;
        nf->struct_name_ptr = ti->struct_name_ptr;
        nf->struct_name_len = ti->struct_name_len;
        nf->ptr_depth = ti->ptr_depth;
        nf->is_tagged = ti->is_tagged;
        nf->tag_layout_ptr = ti->tag_layout_ptr;
        nf->tag_layout_len = ti->tag_layout_len;
        nf->bit_width = f->bit_width;
        nf->elem_type_kind = ti->elem_type_kind;
        nf->elem_ptr_depth = ti->elem_ptr_depth;
        nf->array_len = ti->array_len;
        ((*Vec<u64>)fields_out)->push((u64)nf);
    }
    return ast_struct_def(new_name_ptr, new_name_len, (u64)fields_out, sd->is_packed);
}

func generic_struct_get_or_create_instance(tpl_ptr: u64, bindings: u64) -> u64 {
    var tpl: *GenericStructTemplate = (*GenericStructTemplate)tpl_ptr;
    var name_info: *NameInfo = (*NameInfo)generic_mangle_name(tpl->name_ptr, tpl->name_len, tpl->params_vec, bindings);
    var inst_ptr: u64 = name_info->ptr;
    var inst_len: u64 = name_info->len;

    if (g_generic_struct_instantiations != 0) {
        var exists: *AstStructDef = ((*HashMap<u64, *AstStructDef>)g_generic_struct_instantiations)->get(inst_ptr, inst_len);
        if (exists != 0) {
            compiler_register_generic_struct_bindings(inst_ptr, inst_len, bindings);
            return (u64)name_info;
        }
    }

    var def_src: u64 = tpl->struct_def;
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic struct template\n", 46);
        panic("Parse error");
    }

    var struct_new: u64 = generic_clone_struct(def_src, bindings, inst_ptr, inst_len);
    register_struct_type(struct_new);

    if (g_generic_struct_instantiations == 0) { g_generic_struct_instantiations = HashMap.new<u64, *AstStructDef>(64); }
    ((*HashMap<u64, *AstStructDef>)g_generic_struct_instantiations)->put(inst_ptr, inst_len, (*AstStructDef)struct_new);
    compiler_register_generic_struct_bindings(inst_ptr, inst_len, bindings);
    return (u64)name_info;
}

func generic_build_bindings_by_infer(tpl_ptr: u64, call_args: u64, symtab: u64) -> u64 {
    var tpl: *GenericFuncTemplate = (*GenericFuncTemplate)tpl_ptr;
    var bindings: *Vec<*GenericBinding> = Vec.new<*GenericBinding>(4);
    var def_src: u64 = tpl->func_def;
    if (def_src == 0) { def_src = tpl->func_sig; }
    if (def_src == 0) {
        emit_stderr("[ERROR] Missing generic signature for inference\n", 51);
        panic("Parse error");
    }
    var fn: *AstFunc = (*AstFunc)def_src;
    var params: u64 = fn->params_vec;
    var pn: u64 = ((*Vec<u64>)params)->len();
    var an: u64 = ((*Vec<u64>)call_args)->len();
    if (pn != an) {
        emit_stderr("[ERROR] Generic inference arg count mismatch\n", 49);
        panic("Parse error");
    }

    for (var i: u64 = 0; i < pn; i++) {
        var p: *Param = (*Param)((*Vec<u64>)params)->get(i);
        var arg_expr: u64 = ((*Vec<u64>)call_args)->get(i);
        var arg_ti: u64 = generic_infer_expr_type(arg_expr, symtab);
        if (generic_infer_from_typeinfo(p->type_kind, p->ptr_depth, p->struct_name_ptr, p->struct_name_len, p->elem_type_kind, p->elem_ptr_depth, p->array_len, p->array_len_is_param, p->array_len_param_ptr, p->array_len_param_len, arg_ti, (u64)bindings) != 0) {
            return 0;
        }
    }

    return (u64)bindings;
}

func generic_resolve_template(name_ptr: u64, name_len: u64) -> u64 {
    var tpl: u64 = compiler_find_generic_template_by_name(name_ptr, name_len);
    if (tpl != 0) {
        var t: *GenericFuncTemplate = (*GenericFuncTemplate)tpl;
        if (t->params_vec != 0 && ((*Vec<u64>)t->params_vec)->len() > 0) { return tpl; }
    }
    var resolved: u64 = resolve_name(name_ptr, name_len);
    if (resolved != 0) {
        var info: *NameInfo = (*NameInfo)resolved;
        tpl = compiler_find_generic_template_by_mangled(info->ptr, info->len);
        if (tpl != 0) {
            var t2: *GenericFuncTemplate = (*GenericFuncTemplate)tpl;
            if (t2->params_vec != 0 && ((*Vec<u64>)t2->params_vec)->len() > 0) { return tpl; }
            tpl = 0;
        }
    }
    return tpl;
}

func generic_process_expr(expr: u64, symtab: u64) -> u64 {
    if (expr == 0) { return 0; }
    var kind: u64 = ast_kind(expr);
    switch (kind) {
        case AST_GENERIC_CALL:
            var gc: *AstGenericCall = (*AstGenericCall)expr;
            var tpl_ptr: u64 = generic_resolve_template(gc->name_ptr, gc->name_len);
            if (tpl_ptr == 0) {
                emit_stderr("[ERROR] Generic function not found\n", 36);
                panic("Parse error");
            }
            var bindings: u64 = generic_build_bindings_for_explicit(tpl_ptr, gc->generic_args_vec);
            var name_info: *NameInfo = (*NameInfo)generic_get_or_create_instance(tpl_ptr, bindings);
            var args_new: *Vec<u64> = Vec.new<u64>(4);
            var an: u64 = ((*Vec<u64>)gc->args_vec)->len();
            for (var i: u64 = 0; i < an; i++) {
                ((*Vec<u64>)args_new)->push(generic_process_expr(((*Vec<u64>)gc->args_vec)->get(i), symtab));
            }
            return ast_call(name_info->ptr, name_info->len, (u64)args_new);
        case AST_CALL:
            var call: *AstCall = (*AstCall)expr;
            var tpl_ptr2: u64 = generic_resolve_template(call->name_ptr, call->name_len);
            if (tpl_ptr2 == 0) {
                var args_new2: *Vec<u64> = Vec.new<u64>(4);
                var an2: u64 = ((*Vec<u64>)call->args_vec)->len();
                for (var i2: u64 = 0; i2 < an2; i2++) {
                    ((*Vec<u64>)args_new2)->push(generic_process_expr(((*Vec<u64>)call->args_vec)->get(i2), symtab));
                }
                return ast_call(call->name_ptr, call->name_len, (u64)args_new2);
            }
            var bindings2: u64 = generic_build_bindings_by_infer(tpl_ptr2, call->args_vec, symtab);
            if (bindings2 == 0) {
                var args_fallback: *Vec<u64> = Vec.new<u64>(4);
                var an_fallback: u64 = ((*Vec<u64>)call->args_vec)->len();
                for (var ifb: u64 = 0; ifb < an_fallback; ifb++) {
                    ((*Vec<u64>)args_fallback)->push(generic_process_expr(((*Vec<u64>)call->args_vec)->get(ifb), symtab));
                }
                return ast_call(call->name_ptr, call->name_len, (u64)args_fallback);
            }
            var name_info2: *NameInfo = (*NameInfo)generic_get_or_create_instance(tpl_ptr2, bindings2);
            var args_new3: *Vec<u64> = Vec.new<u64>(4);
            var an3: u64 = ((*Vec<u64>)call->args_vec)->len();
            for (var i3: u64 = 0; i3 < an3; i3++) {
                ((*Vec<u64>)args_new3)->push(generic_process_expr(((*Vec<u64>)call->args_vec)->get(i3), symtab));
            }
            return ast_call(name_info2->ptr, name_info2->len, (u64)args_new3);
        case AST_BINARY:
            var bin: *AstBinary = (*AstBinary)expr;
            bin->left = generic_process_expr(bin->left, symtab);
            bin->right = generic_process_expr(bin->right, symtab);
            return expr;
        case AST_UNARY:
            var un: *AstUnary = (*AstUnary)expr;
            un->operand = generic_process_expr(un->operand, symtab);
            return expr;
        case AST_ADDR_OF:
            var ao: *AstAddrOf = (*AstAddrOf)expr;
            ao->operand = generic_process_expr(ao->operand, symtab);
            return expr;
        case AST_DEREF:
            var dr: *AstDeref = (*AstDeref)expr;
            dr->operand = generic_process_expr(dr->operand, symtab);
            return expr;
        case AST_DEREF8:
            var dr8: *AstDeref8 = (*AstDeref8)expr;
            dr8->operand = generic_process_expr(dr8->operand, symtab);
            return expr;
        case AST_CAST:
            var cs: *AstCast = (*AstCast)expr;
            cs->expr = generic_process_expr(cs->expr, symtab);
            return expr;
        case AST_MEMBER_ACCESS:
            var ma: *AstMemberAccess = (*AstMemberAccess)expr;
            ma->object = generic_process_expr(ma->object, symtab);
            return expr;
        case AST_METHOD_CALL:
            var mc: *AstMethodCall = (*AstMethodCall)expr;
            mc->receiver = generic_process_expr(mc->receiver, symtab);
            var args: *Vec<u64> = Vec.new<u64>(4);
            var an4: u64 = ((*Vec<u64>)mc->args_vec)->len();
            for (var i4: u64 = 0; i4 < an4; i4++) {
                ((*Vec<u64>)args)->push(generic_process_expr(((*Vec<u64>)mc->args_vec)->get(i4), symtab));
            }
            mc->args_vec = (u64)args;
            var recv_ti_ptr: u64 = generic_infer_expr_type(mc->receiver, symtab);
            if (recv_ti_ptr != 0) {
                var recv_ti: *TypeInfo = (*TypeInfo)recv_ti_ptr;
                if (recv_ti->type_kind == TYPE_STRUCT) {
                    var struct_ptr: u64 = recv_ti->struct_name_ptr;
                    var struct_len: u64 = recv_ti->struct_name_len;
                    var base_len: u64 = compiler_find_generic_suffix_index(struct_ptr, struct_len);
                    var full_len2: u64 = base_len + 1 + mc->method_len;
                    var full_ptr2: u64 = heap_alloc((full_len2 + 1) * sizeof(u8));
                    for (var bi: u64 = 0; bi < base_len; bi++) {
                        *(*u8)(full_ptr2 + bi) = *(*u8)(struct_ptr + bi);
                    }
                    *(*u8)(full_ptr2 + base_len) = 95;
                    for (var mj: u64 = 0; mj < mc->method_len; mj++) {
                        *(*u8)(full_ptr2 + base_len + 1 + mj) = *(*u8)(mc->method_ptr + mj);
                    }
                    *(*u8)(full_ptr2 + full_len2) = 0;

                    var tpl_ptr3: u64 = compiler_find_generic_template_by_name(full_ptr2, full_len2);
                    if (tpl_ptr3 != 0) {
                        var bindings3: u64 = 0;
                        if (base_len != struct_len) {
                            bindings3 = (u64)compiler_get_generic_struct_bindings(struct_ptr, struct_len);
                        }
                        if (bindings3 == 0) {
                            var infer_args: *Vec<u64> = Vec.new<u64>(an4 + 1);
                            ((*Vec<u64>)infer_args)->push(ast_addr_of(mc->receiver));
                            for (var ai: u64 = 0; ai < an4; ai++) {
                                ((*Vec<u64>)infer_args)->push(((*Vec<u64>)args)->get(ai));
                            }
                            bindings3 = generic_build_bindings_by_infer(tpl_ptr3, (u64)infer_args, symtab);
                        }
                        if (bindings3 != 0) {
                            generic_get_or_create_instance(tpl_ptr3, bindings3);
                        }
                    }
                }
            }
            return expr;
        case AST_INDEX:
            var ix: *AstIndex = (*AstIndex)expr;
            ix->base = generic_process_expr(ix->base, symtab);
            ix->index = generic_process_expr(ix->index, symtab);
            return expr;
        case AST_SLICE:
            var sl: *AstSlice = (*AstSlice)expr;
            sl->ptr_expr = generic_process_expr(sl->ptr_expr, symtab);
            sl->len_expr = generic_process_expr(sl->len_expr, symtab);
            return expr;
        case AST_CALL_PTR:
            var cp: *AstCallPtr = (*AstCallPtr)expr;
            cp->callee = generic_process_expr(cp->callee, symtab);
            var args5: *Vec<u64> = Vec.new<u64>(4);
            var an5: u64 = ((*Vec<u64>)cp->args_vec)->len();
            for (var i5: u64 = 0; i5 < an5; i5++) {
                ((*Vec<u64>)args5)->push(generic_process_expr(((*Vec<u64>)cp->args_vec)->get(i5), symtab));
            }
            cp->args_vec = (u64)args5;
            return expr;
        case AST_SIZEOF_EXPR:
            var se: *AstSizeofExpr = (*AstSizeofExpr)expr;
            se->expr = generic_process_expr(se->expr, symtab);
            return expr;
        default:
            return expr;
    }
}

func generic_process_stmt(stmt: u64, symtab: u64) -> u64 {
    if (stmt == 0) { return 0; }
    var kind: u64 = ast_kind(stmt);
    switch (kind) {
        case AST_RETURN:
            var r: *AstReturn = (*AstReturn)stmt;
            r->expr = generic_process_expr(r->expr, symtab);
            return stmt;
        case AST_VAR_DECL:
            var vd: *AstVarDecl = (*AstVarDecl)stmt;
            vd->init_expr = generic_process_expr(vd->init_expr, symtab);
            return stmt;
        case AST_ASSIGN:
            var assign_stmt: *AstAssign = (*AstAssign)stmt;
            assign_stmt->target = generic_process_expr(assign_stmt->target, symtab);
            assign_stmt->value = generic_process_expr(assign_stmt->value, symtab);
            return stmt;
        case AST_EXPR_STMT:
            var es: *AstExprStmt = (*AstExprStmt)stmt;
            es->expr = generic_process_expr(es->expr, symtab);
            return stmt;
        case AST_IF:
            var ifs: *AstIf = (*AstIf)stmt;
            ifs->cond = generic_process_expr(ifs->cond, symtab);
            generic_process_stmt(ifs->then_block, symtab);
            generic_process_stmt(ifs->else_block, symtab);
            return stmt;
        case AST_WHILE:
            var wl: *AstWhile = (*AstWhile)stmt;
            wl->cond = generic_process_expr(wl->cond, symtab);
            generic_process_stmt(wl->body, symtab);
            return stmt;
        case AST_FOR:
            var fr: *AstFor = (*AstFor)stmt;
            generic_process_stmt(fr->init, symtab);
            fr->cond = generic_process_expr(fr->cond, symtab);
            fr->update = generic_process_expr(fr->update, symtab);
            generic_process_stmt(fr->body, symtab);
            return stmt;
        case AST_SWITCH:
            var sw: *AstSwitch = (*AstSwitch)stmt;
            sw->expr = generic_process_expr(sw->expr, symtab);
            var cases: u64 = sw->cases_vec;
            var cn: u64 = ((*Vec<u64>)cases)->len();
            for (var i: u64 = 0; i < cn; i++) {
                generic_process_stmt(((*Vec<u64>)cases)->get(i), symtab);
            }
            return stmt;
        case AST_CASE:
            var cs: *AstCase = (*AstCase)stmt;
            generic_process_stmt(cs->body, symtab);
            return stmt;
        case AST_BLOCK:
            var blk: *AstBlock = (*AstBlock)stmt;
            var stmts: u64 = blk->stmts_vec;
            var n: u64 = ((*Vec<u64>)stmts)->len();
            for (var i2: u64 = 0; i2 < n; i2++) {
                generic_process_stmt(((*Vec<u64>)stmts)->get(i2), symtab);
            }
            return stmt;
        default:
            return stmt;
    }
}

func compiler_is_generic_template(fn_ptr: u64) -> u64 {
    if (compiler_is_generic_func_ptr(fn_ptr) != 0) { return 1; }
    var fn: *AstFunc = (*AstFunc)fn_ptr;
    if (fn->ret_type == TYPE_GENERIC) { return 1; }

    var params: u64 = fn->params_vec;
    if (params == 0) { return 0; }

    var n: u64 = ((*Vec<u64>)params)->len();
    for (var i: u64 = 0; i < n; i++) {
        var p: *Param = (*Param)((*Vec<u64>)params)->get(i);
        if (p->type_kind == TYPE_GENERIC) { return 1; }
        if (p->elem_type_kind == TYPE_GENERIC) { return 1; }
        if (p->array_len_is_param != 0) { return 1; }
    }
    return 0;
}

func compiler_monomorphize_generics() -> u64 {
    if (g_generic_func_ptrs == 0 || ((*Vec<*GenericFuncTemplate>)g_generic_func_ptrs)->len() == 0) {
        return 0;
    }
    if (g_all_funcs == 0) { return 0; }
    for (var i2: u64 = 0; i2 < ((*Vec<*AstFunc>)g_all_funcs)->len(); i2++) {
        var fn_ptr: u64 = (u64)((*Vec<*AstFunc>)g_all_funcs)->get(i2);
        if (compiler_is_generic_template(fn_ptr) != 0) { continue; }
        var fn: *AstFunc = (*AstFunc)fn_ptr;
        var symtab: u64 = generic_build_symtab_for_func(fn_ptr);
        generic_process_stmt(fn->body, symtab);
    }

    var filtered_funcs: *Vec<*AstFunc> = Vec.new<*AstFunc>(((*Vec<*AstFunc>)g_all_funcs)->len());
    var total_funcs: u64 = ((*Vec<*AstFunc>)g_all_funcs)->len();
    for (var fi: u64 = 0; fi < total_funcs; fi++) {
        var fn_ptr2: u64 = (u64)((*Vec<*AstFunc>)g_all_funcs)->get(fi);
        if (compiler_is_generic_template(fn_ptr2) != 0) { continue; }
        ((*Vec<*AstFunc>)filtered_funcs)->push((*AstFunc)fn_ptr2);
    }
    g_all_funcs = filtered_funcs;
    return 0;
}

// ============================================
// Driver helpers
// ============================================

func init_compiler_globals() -> u64 {
    g_loaded_modules = HashMap.new<u64, u64>(64);
    g_loaded_modules_pass1 = HashMap.new<u64, u64>(64);
    g_module_imports_pass1 = HashMap.new<u64, *Vec<*AstImport> >(64);
    g_module_imports_pass2 = HashMap.new<u64, *Vec<*AstImport> >(64);
    g_loaded_modules_vec = Vec.new<*NameInfo>(64);
    g_all_funcs = Vec.new<*AstFunc>(64);
    g_all_func_sigs = Vec.new<*AstFunc>(64);
    g_all_consts = Vec.new<*AstConstDecl>(128);
    g_all_globals = Vec.new<*GlobalInfo>(64);
    g_all_structs = HashMap.new<u64, *AstStructDef>(64);
    g_all_structs_vec = Vec.new<*AstStructDef>(16);
    g_generic_funcs = HashMap.new<u64, *GenericFuncTemplate>(64);
    g_generic_funcs_mangled = HashMap.new<u64, *GenericFuncTemplate>(64);
    g_generic_func_ptrs = Vec.new<*GenericFuncTemplate>(16);
    g_generic_instantiations = HashMap.new<u64, u64>(64);
    g_generic_structs = HashMap.new<u64, *GenericStructTemplate>(64);
    g_generic_struct_ptrs = Vec.new<*GenericStructTemplate>(16);
    g_generic_struct_instantiations = HashMap.new<u64, *AstStructDef>(64);
    g_generic_struct_bindings = HashMap.new<u64, *Vec<*GenericBinding> >(64);
    g_module_aliases = HashMap.new<u64, *HashMap<u64, *NameInfo> >(128);
    g_prelude_aliases = HashMap.new<u64, *NameInfo>(128);
    g_module_exports = HashMap.new<u64, *Vec<*ExportEntry> >(64);
    g_func_module_map = HashMap.new<u64, *NameInfo>(128);
    g_current_module_ptr = 0;
    g_current_module_len = 0;
    g_impl_prefix_ptr = 0;
    g_impl_prefix_len = 0;
    g_impl_methods = HashMap.new<u64, u64>(64);
    return 0;
}

func setup_paths(filename: u64, filename_len: u64) -> u64 {
    return setup_paths_with_compiler(0, 0, filename, filename_len);
}

func extract_version_from_compiler_path(path: u64, path_len: u64) -> u64 {
    if (path == 0 || path_len == 0) { return 0; }
    var base: u64 = path_basename_noext(path, path_len);
    var base_len: u64 = str_len(base);
    var needle: u64 = "_stage";
    for (var i: u64 = 0; i + 6 <= base_len; i++) {
        if (str_eq(base + i, 6, needle, 6)) {
            var vlen: u64 = i;
            if (vlen == 0) { return 0; }
            var vptr: u64 = heap_alloc((vlen + 1) * sizeof(u8));
            str_copy(vptr, base, vlen);
            *(*u8)(vptr + vlen) = 0;
            var out: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
            out->ptr = vptr;
            out->len = vlen;
            return (u64)out;
        }
    }
    // Fallback: accept direct version name (e.g., v3_20.out -> v3_20)
    if (base_len >= 2) {
        var c0: u64 = *(*u8)base;
        var c1: u64 = *(*u8)(base + 1);
        if (c0 == 118 && c1 >= 48 && c1 <= 57) { // 'v' + digit
            var vptr2: u64 = heap_alloc((base_len + 1) * sizeof(u8));
            str_copy(vptr2, base, base_len);
            *(*u8)(vptr2 + base_len) = 0;
            var out2: *NameInfo = (*NameInfo)heap_alloc(sizeof(NameInfoLocal));
            out2->ptr = vptr2;
            out2->len = base_len;
            return (u64)out2;
        }
    }
    return 0;
}

func setup_paths_with_compiler(compiler_path: u64, compiler_len: u64, filename: u64, filename_len: u64) -> u64 {
    g_base_dir = path_dirname(filename, filename_len);
    g_base_dir_len = str_len(g_base_dir);

    var version_info: u64 = extract_version_from_compiler_path(compiler_path, compiler_len);
    if (version_info != 0) {
        var version_name: *NameInfo = (*NameInfo)version_info;
        var version_ptr: u64 = version_name->ptr;
        var version_len: u64 = version_name->len;
        var compiler_dir: u64 = path_dirname(compiler_path, compiler_len);
        var compiler_dir_len: u64 = str_len(compiler_dir);
        var project_root: u64 = path_dirname(compiler_dir, compiler_dir_len);
        var project_root_len: u64 = str_len(project_root);
        var slash: u64 = "/";
        var version_dir: u64 = 0;
        if (version_len == 6 && str_eq(version_ptr, version_len, "bpp_v1", 6)) {
            var bpp_path: u64 = "bpp/v1";
            version_dir = str_concat3(project_root, project_root_len, slash, 1, bpp_path, 6);
        } else {
            version_dir = str_concat3(project_root, project_root_len, slash, 1, version_ptr, version_len);
        }
        var version_dir_len: u64 = str_len(version_dir);
        var src_suffix: u64 = "/src";
        g_lib_dir = str_concat(version_dir, version_dir_len, src_suffix, 4);
        g_lib_dir_len = str_len(g_lib_dir);
        return 0;
    }

    // Find version directory by going up from base_dir
    // src: B/v3_XX/src -> up 1 = B/v3_XX
    // test: B/v3_XX/test/b -> up 2 = B/v3_XX
    var up_one: u64 = path_dirname(g_base_dir, g_base_dir_len);
    var up_one_len: u64 = str_len(up_one);
    var version_dir2: u64 = path_dirname(up_one, up_one_len);
    var version_dir_len2: u64 = str_len(version_dir2);
    var slash_config: u64 = "/config.ini";
    var config_path1: u64 = str_concat(up_one, up_one_len, slash_config, 11);
    var version2: u64 = read_version_from_config(config_path1);

    if (version2 == 0) {
        var config_path2: u64 = str_concat(version_dir2, version_dir_len2, slash_config, 11);
        version2 = read_version_from_config(config_path2);
    }

    if (version2 == 0) {
        version2 = "v3_15";  // Fallback to hardcoded default
    }

    // Build lib_dir path: prefer base_dir parent when filename is in src/
    var use_up_one: u64 = 0;
    if (g_base_dir_len >= 4) {
        var c0: u64 = *(*u8)(g_base_dir + g_base_dir_len - 4);
        var c1: u64 = *(*u8)(g_base_dir + g_base_dir_len - 3);
        var c2: u64 = *(*u8)(g_base_dir + g_base_dir_len - 2);
        var c3: u64 = *(*u8)(g_base_dir + g_base_dir_len - 1);
        if (c0 == 47 && c1 == 115 && c2 == 114 && c3 == 99) { // "/src"
            use_up_one = 1;
        }
    }

    var lib_root: u64 = version_dir2;
    var lib_root_len: u64 = version_dir_len2;
    if (use_up_one != 0) {
        lib_root = up_one;
        lib_root_len = up_one_len;
    }

    var src_suffix2: u64 = "/src";
    g_lib_dir = str_concat(lib_root, lib_root_len, src_suffix2, 4);
    g_lib_dir_len = str_len(g_lib_dir);
    return 0;
}

func build_merged_program() -> u64 {
    var dummy_imports: *Vec<u64> = Vec.new<u64>(1);
    var merged_prog: u64 = ast_program((u64)g_all_funcs, (u64)g_all_consts, (u64)dummy_imports);
    var merged_info: *AstProgram = (*AstProgram)merged_prog;
    merged_info->globals_vec = (u64)g_all_globals;
    merged_info->structs_vec = (u64)g_all_structs_vec;
    return merged_prog;
}

func get_func_sigs() -> u64 {
    return (u64)g_all_func_sigs;
}
